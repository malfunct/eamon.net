 1  REM THE WONDERFUL WORLD OF
 2  REM         EAMON
 3  REM 
 4  REM A COMPUTERIZED FANTASY
 5  REM ROLE-PLAYING SYSTEM
 6  REM 
 7  REM RULES AND CORE PROGRAMS
 8  REM BY DONALD BROWN

 9  REM REV. 9 SEP 95

 10  REM EAMON COLOR.PIC BY
 11  REM FRANK KUNZE 1993

 100  TEXT : HOME : PRINT : PRINT "EAMON ADVENTURER'S GUILD": PRINT "7625 HAWKHAVEN DR.": PRINT "CLEMMONS, NC 27012": PRINT "(910) 766-7490": FOR I = 1 TO 500: NEXT : PRINT : PRINT : PRINT 
 110 D$ =  CHR$ (4): PRINT D$"BLOADEAMON.PIC": POKE  - 16304,0: POKE  - 16297,0: POKE  - 16302,0
 120  PRINT D$"OPENSNAPPY,L64": PRINT D$"READSNAPPY,R0": INPUT NS: PRINT D$
 130  PRINT D$"OPENCHAR.EXT,L256": PRINT D$"READCHAR.EXT,R0": INPUT NC: PRINT D$
 140  PRINT "   YOU ARE IN THE OUTER CHAMBER OF THE": PRINT "HALL OF THE GUILD OF FREE ADVENTURERS.": PRINT "MANY MEN AND WOMEN ARE GUZZLING BEER"
 150  PRINT "AND THERE IS LOUD SINGING AND LAUGHTER.": PRINT : PRINT "   ON THE NORTH SIDE OF THE CHAMBER IS"
 160  PRINT "A CUBBYHOLE WITH A DESK.  OVER THE DESK": PRINT "IS A SIGN WHICH SAYS 'REGISTER HERE": PRINT "OR ELSE!'": PRINT 
 170  PRINT : PRINT : PRINT "DO YOU GO OVER TO THE DESK OR JOIN THE": PRINT "   MEN DRINKING THE BEER?": PRINT : PRINT "   (HIT 'D' FOR DESK OR 'M' FOR MEN)   ";
 180  FOR X = 1 TO 1000:Z =  RND (1): IF  PEEK ( - 16384) <  > 155 AND  PEEK ( - 16384) <  > 141 THEN  NEXT 
 190  POKE 49168,0: TEXT :R =  INT ( RND (1) * NS + 1): PRINT D$"READSNAPPY,R"R: INPUT CM$:C1$ =  LEFT$ (CM$,23):C2$ =  MID$ (CM$,24): PRINT D$"CLOSESNAPPY"
 200  GET A$: ON A$ = "D" GOTO 230: IF A$ <  > "M" THEN 200
 220  PRINT A$: PRINT : PRINT "   AS YOU GO OVER TO THE MEN, YOU FEEL": PRINT "A SWORD BEING THRUST THROUGH YOUR BACK": PRINT "AND YOU HEAR SOMEONE SAY, 'YOU REALLY": PRINT "MUST LEARN TO FOLLOW DIRECTIONS!'": PRINT D$"CLOSE": END 
 230  PRINT A$: PRINT : PRINT : PRINT "   YOU ARE GREETED THERE BY A BURLY": PRINT "IRISHMAN WHO LOOKS AT YOU WITH A SCOWL": PRINT "AND ASKS YOU, 'WHAT'S YOUR NAME?'": PRINT : PRINT 
 235  PRINT "(TYPE ** FOR LIST OF CHARACTERS ON DISK)"
 240  INPUT "YOU GIVE HIM YOUR NAME (TYPE IT IN NOW)        ";NA$
 245  ON NA$ <  > "**" GOTO 250: PRINT : PRINT "CHARACTERS ON DISK:": PRINT : FOR X = 1 TO NC: PRINT D$"READ CHAR.EXT,R"X: INPUT NA$: IF  LEN (NA$) > 19 THEN NA$ =  LEFT$ (NA$,19)
 247  PRINT NA$; SPC( 20 -  LEN (NA$)): NEXT : PRINT D$: PRINT : PRINT : PRINT "HIT ANY KEY...";: GET A$: GOTO 230
 250  PRINT : IF NA$ = "" OR  LEN (NA$) < 2 OR  LEFT$ (NA$,1) < "A" OR  LEFT$ (NA$,1) > "Z" THEN  GOSUB 400: GOTO 240
 260  IF  LEN (NA$) > 20 THEN  PRINT "   HE SCOWLS AT YOU AND SAYS, 'YER": PRINT "NAME'S TOO LONG FER ME BOOK.  I CANNA": PRINT "USE MORE THAN TWENTY LETTERS.": PRINT : GOTO 240
 270  PRINT "  HE STARTS LOOKING THROUGH HIS BOOK,": PRINT : PRINT "MUTTERING ABOUT ";C1$: PRINT : PRINT C2$: PRINT 
 280  FOR R = 1 TO NC: PRINT D$"READCHAR.EXT,R"R: INPUT A$: IF A$ = NA$ THEN 340
 290  NEXT : PRINT D$: PRINT "   HE EVENTUALLY LOOKS AT YOU AND SAYS,": PRINT "'YER NAME'S NA IN HERE. HAVE YE GIVEN": PRINT "IT TO ME ARIGHT?'": PRINT : PRINT "HOW DO YOU ANSWER (HIT 'Y' OR 'N') ";
 300  GET A$: IF A$ = "N" THEN  PRINT A$: PRINT : GOSUB 400: GOTO 240
 310  IF A$ <  > "Y" THEN 300
 320  PRINT A$: PRINT : PRINT "   HE HITS HIS FOREHEAD AND SAYS, 'AH,": PRINT "YE MUST BE NEW HERE!  WELL, WAIT JUST": PRINT "A MINUTE AND I'LL BRING SOMEONE OUT TO": PRINT "TAKE CARE OF YE.'": PRINT 
 330  GOSUB 360: PRINT D$"-NEW.CHARACTERS"
 340  PRINT D$: PRINT : PRINT "   HE LOOKS UP AND SAYS, 'AH, HERE YE": PRINT "BE!  WELL, GO AND HAVE FUN IN THE HALL!'": PRINT : FOR X = 1 TO 1000: NEXT 
 350  GOSUB 360: PRINT D$"-MAIN.HALL"
 360  PRINT D$"OPENTHE.ADVENTURER": PRINT D$"WRITETHE.ADVENTURER": PRINT NA$: PRINT R: PRINT D$"CLOSE": RETURN 
 400  REM 

NO-NAME RTN

 410 CN = CN + 1: IF CN > 1 THEN 450
 420  PRINT " HE PULLS OUT A SWORD AND BEGINS TO": PRINT "SHARPEN IT, SAYING 'YE'D BEST BE GIVIN'": PRINT "ME YER NAME LADDIE, IF YE KNOW WOTS": PRINT "GOOD FER YE!!!"
 430  RETURN 
 450  IF CN = 2 THEN  PRINT "I'VE 'BOUT HAD ME FILL O' YER SICK": PRINT "SENSA 'UMOR!!": PRINT "NOW GIMME YER NAME!!": RETURN 
 460  IF CN = 3 THEN  PRINT "THE MAN CUTS ONE OF YOUR FINGERS OFF!!": PRINT "HE THEN EATS IT!!!!": PRINT "THEN HE SAYS 'ARE YE READY T' TALK NOW?": RETURN 
 470  IF CN < 13 THEN  PRINT "THE MAN CUTS OFF ANOTHER FINGER!!!": PRINT "HE EATS THIS ONE TOO!!": RETURN 
 480  PRINT "THE MAN STARTS SLOWLY, 'WELL YE BE ": PRINT "OUTTA FINGERS!'": PRINT "THE MAN THEN SPINS AROUND AND RUNS YOU": PRINT "THROUGH WITH A SPEED YOU HAVE NEVER": PRINT "SEEN BEFORE! (AND NEVER WILL AGAIN.)": END 

 1  REM BUILD EAMON DATABASE
 2  REM   BY FRANK KUNZE

 4  REM REV. 27 SEP 94

 10 D$ =  CHR$ (4): HOME : VTAB 6: PRINT "  INSERT EAMON ADVENTURE DISKETTE AND": GOSUB 9600: IF A$ = "1" OR A$ = "2" THEN  PRINT D$"PREFIX,D"A$
 15  PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT A$: PRINT D$"PREFIX"A$
 20  REM 

VERSION?

 25  GOSUB 9000: ONERR  GOTO 40
 30 DR = 1: PRINT D$"OPENEAMON.NAME": PRINT D$"READEAMON.NAME": INPUT NA$: INPUT ND: INPUT MP: PRINT D$"CLOSE": POKE 216,0: IF MP < 7 THEN 50
 35 MP = 7:LD = 242: GOTO 55
 40  PRINT D$"CLOSE": IF  PEEK (222) = 5 THEN 50
 45  PRINT "FOUND ERROR #" PEEK (222): PRINT : PRINT "IN " PEEK (218) +  PEEK (219) * 256: END 
 50  POKE 216,0:MP = 6:LD = 256:LA = 128:LM = 128:LR = 64:LN = 64: IF ND < 6 THEN ND = 6
 55  PRINT D$"OPENEAMON.DESC": PRINT D$"READEAMON.DESC": INPUT NR,NA,NE,NM: IF MP > 6 THEN  PRINT : INPUT LR,LM,LA
 60  PRINT D$"CLOSE": PRINT D$"PREFIX": INPUT P1$: HOME : PRINT "IF YOU  WANT  TO BUILD  THE FILES  ON A DIFFERENT  DISK THEN INSERT  THAT DISK  NOW ---"
 65  GOSUB 9600: PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$
 70  REM 

TEST VAR SET

 75  ONERR  GOTO 85
 80 X$ = A$(11): GOTO 90
 85  ON  PEEK (222) <  > 107 GOTO 45: POKE 216,0:SE = 1: GOTO 100
 90  POKE 216,0:SE = 2
 100  REM 

MENU

 110  HOME : PRINT "WORKING ON: ";: INVERSE : PRINT NA$;: NORMAL : PRINT 
 120  VTAB 4: PRINT " 1.  BUILD ARTIFACT FILE": PRINT : PRINT " 2.  BUILD MONSTER FILE": PRINT : PRINT " 3.  CHANGE FILE LENGTHS (LA="LA",LM="LM")": PRINT 
 130  PRINT " 4.  CATALOG DISK": PRINT : PRINT " 5.  CREATE A FAKE DESC FILE": PRINT : PRINT " 6.  QUIT": PRINT : PRINT  SPC( 10)"ENTER 1-6 [_]"; CHR$ (8); CHR$ (8);
 140  GET A$:M =  VAL (A$): ON M < 1 OR M > 6 GOTO 140: PRINT M: ON M GOTO 1000,2000,3000,4000,5000: END 
 1000  REM 

ARTIFACTS

 1010  HOME : INPUT "BUILD 'EAMON.ARTIFACTS'? (Y/N) ";A$: ON A$ <  > "Y" GOTO 100: ONERR  GOTO 1030
 1020 A$ = "EAMON.ARTIFACTS": PRINT D$"VERIFY"A$: GOTO 8000
 1030  ON SE GOTO 9300,9400: STOP 
 2000  REM 

MONSTERS

 2010  HOME : INPUT "BUILD 'EAMON.MONSTERS'? (Y/N)  ";A$: ON A$ <  > "Y" GOTO 100: ONERR  GOTO 2030
 2020 A$ = "EAMON.MONSTERS": PRINT D$"VERIFY"A$: GOTO 8000
 2030  ON SE GOTO 9100,9200: STOP 
 3000  REM 

LENGTHS

 3010  IF MP < 7 THEN  PRINT : PRINT  CHR$ (7)"INVALID OPTION": FOR A = 1 TO 1500: NEXT : GOTO 100
 3020  HOME : PRINT "LENGTH OF 'EAMON.ARTIFACTS' --": PRINT : PRINT "<RETURN> = "LA;: INPUT " : ";A$:M =  VAL (A$): IF A$ = "" THEN 3040
 3030  PRINT : PRINT "CHANGE THE LENGTH TO "M: PRINT "  ARE YOU SURE? (Y/N) ";: GET A$: PRINT A$: ON A$ <  > "Y" GOTO 100:LA = M
 3040  PRINT : PRINT "LENGTH OF 'EAMON.MONSTERS' --": PRINT : PRINT "<RETURN> = "LM;: INPUT " : ";A$:M =  VAL (A$): IF A$ = "" THEN M = LM
 3050  PRINT : PRINT "CHANGE TO "M: PRINT "  SURE? (Y/N) ";: GET A$: PRINT A$: ON A$ <  > "Y" GOTO 3060:LM = M
 3060  PRINT : PRINT "LA="LA,"LM="LM: PRINT : INPUT "UPDATE 'EAMON.DESC'? (Y/N) : ";A$: ON A$ <  > "Y" GOTO 100: PRINT : PRINT "MAKE SURE THE DISKETTE WITH 'EAMON.DESC'": PRINT "IS IN THE DISK DRIVE": GOSUB 9600
 3070  PRINT D$"PREFIX/": PRINT D$"OPENEAMON.DESC": PRINT D$"WRITEEAMON.DESC": PRINT NR: PRINT NA: PRINT NE: PRINT NM: PRINT LR: PRINT LM: PRINT LA: PRINT D$"CLOSE"
 3080  HOME : PRINT "DONE.": PRINT : PRINT "NOW RE-INSERT YOUR WORK DISK": GOSUB 9600: PRINT D$"PREFIX"P$: GOTO 100
 4000  REM 

CATALOG

 4010  HOME : PRINT D$"CAT": GOSUB 9600: GOTO 100
 5000  REM 

MAKE FAKE DESC

 5010  HOME : INPUT "CREATE A FAKE 'EAMON.DESC'? (Y/N) ";A$: ON A$ <  > "Y" GOTO 100: ONERR  GOTO 5030
 5020 A$ = "EAMON.DESC": PRINT D$"VERIFY"A$: GOTO 8000
 5030  POKE 216,0: PRINT D$"OPENEAMON.DESC": PRINT D$"WRITEEAMON.DESC": PRINT NR: PRINT NA: PRINT NE: PRINT NM: IF MP > 6 THEN  PRINT LR: PRINT LA: PRINT LM
 5040  PRINT D$"CLOSE": GOTO 100
 8000  POKE 216,0: PRINT : PRINT  CHR$ (7)"ERROR -- ": PRINT "THERE ALREADY IS AN": PRINT "'"A$"' FILE.": FOR A = 1 TO 3000: NEXT : GOTO 100
 9000  REM 

LOAD DATA

 9010  ONERR  GOTO 9050
 9020  PRINT D$"RESTOREFAST.START":D$ =  CHR$ (4): POKE 216,0:ND = 0:MP = 0: RETURN 
 9050  ON  PEEK (222) <  > 6 GOTO 45: PRINT "ERROR -- FILE 'FAST.START' IS MISSING": END 
 9100  REM 

WRITE MON 6

 9120  POKE 216,0: PRINT D$"OPENEAMON.MONSTERS,L"LM: FOR M = 1 TO NM: PRINT D$"WRITEEAMON.MONSTERS,R"M: PRINT MN$(M): FOR A = 1 TO 12: PRINT MD%(M,A): NEXT : NEXT : PRINT D$"CLOSE": GOTO 9500
 9200  REM 

WRITE MON 7

 9220  POKE 216,0: PRINT D$"OPENEAMON.MONSTERS,L"LM: FOR M = 1 TO NM: PRINT D$"WRITEEAMON.MONSTERS,R"M: PRINT M$(M): FOR A = 1 TO 11 + (MP < 7): PRINT M%(M,A): NEXT : NEXT : PRINT D$"CLOSE": GOTO 9500
 9300  REM 

WRITE ART 6

 9320  POKE 216,0: PRINT D$"OPENEAMON.ARTIFACTS,L"LA: FOR A = 1 TO NA: PRINT D$"WRITEEAMON.ARTIFACTS,R"A: PRINT AN$(A): FOR M = 1 TO 8: PRINT AD%(A,M): NEXT : NEXT : GOTO 9500
 9400  REM 

WRITE ART 7

 9420  POKE 216,0: PRINT D$"OPENEAMON.ARTIFACTS,L"LA: FOR A = 1 TO NA: PRINT D$"WRITEEAMON.ARTIFACTS,R"A: PRINT A$(A): FOR M = 1 TO 8: PRINT A%(A,M): NEXT : NEXT 
 9500  PRINT D$"CLOSE": PRINT : PRINT "DONE.": FOR A = 1 TO 1500: NEXT : GOTO 100
 9600  REM 

GET A$

 9610  POKE 49168,0: PRINT : PRINT "  PRESS ANY KEY TO CONTINUE ... ";: GET A$: PRINT : RETURN 

 1  REM        EAMON ADVENTURE #19
 2  REM  ->      DEATH  TRAP    <-
 3  REM 
 4  REM       BY JOHN NELSON
 5  REM 
 7  REM REV. 12-04-84


 8  REM 

EAMON ADVENTURER'S GUILD
7625 HAWKHAVEN DR.
CLEMMONS, NC 27012


 10  ONERR  GOTO 50
 20 DK$ =  CHR$ (4): PRINT DK$"VERIFY GAME.PTRS": GOTO 31000
 50  POKE 216,0: GOTO 1000
 60  IF V$ = "WEAR" THEN  POP : GOTO 22000
 70  IF V$ = "REMOVE" THEN  POP : GOTO 22500
 90  RETURN 
 100  REM  ///MAIN LOOP. GIVE ROOM DESC AND GET COMMAND
 110  PRINT 
 120  IF SPD THEN SPD = SPD - 1: IF  NOT SPD THEN MD%(0,2) = MD%(0,2) / 2:MD%(0,10) = MD%(0,10) - 2 * MD%(0,2): IF  RND (1) > .8 THEN  PRINT "YOUR SPEED SPELL HAS JUST EXPIRED!": PRINT 
 130  IF V%(ROOM) THEN  PRINT DK$;"READ EAMON.ROOM NAMES,R";ROOM: INPUT A$: PRINT DK$: PRINT "YOU ARE ": PRINT " ";A$: PRINT 
 140  IF  NOT V%(ROOM) THEN  PRINT DK$;"READ EAMON.DESC,R";ROOM: INPUT A$: PRINT DK$: PRINT A$: PRINT :V%(ROOM) = 1
 145  IF CL% THEN CL% = 0: PRINT "THE REFLECTION IN THE MIRROR STEPS OUT": PRINT "OF THE MIRROR. IT IS AN EXACT DUPLICATE": PRINT "OF YOU.";: PRINT  MID$ ("HISHER",(SEX$ = "F") * 3 + 1,3);" NAME IS ";MN$(NM);"."
 148  IF MB AND RO = 42 THEN  PRINT "THE MIRROR IS SMASHED TO PIECES."
 150  FOR M = 1 TO NM: IF MD%(M,5) = ROOM AND MD%(M,15) THEN  PRINT MN$(M);" IS HERE.": PRINT 
 160  IF MD%(M,5) = ROOM AND  NOT MD%(M,15) THEN  PRINT DK$;"READ EAMON.DESC,R";M + 300: INPUT A$: PRINT DK$: PRINT A$:MD%(M,15) = 1: PRINT 
 165  NEXT M: IF MD%(37,5) = RO AND  NOT RM THEN  PRINT : PRINT "YOU HAVE TURNED TO STONE. YOU ARE DEAD.": PRINT :DIE = 1: GOTO 2000
 170  FOR A = 1 TO NZ: IF AD%(A,4) = ROOM AND AD%(A,9) THEN  PRINT "YOU SEE ";AN$(A)
 180  IF AD%(A,4) = ROOM AND  NOT AD%(A,9) THEN  PRINT DK$;"READ EAMON.DESC,R";A + 100: INPUT A$: PRINT DK$: PRINT A$:AD%(A,9) = 1: PRINT 
 190  NEXT A: FOR A = A TO NA: IF AD%(A,4) = ROOM THEN  PRINT "YOUR ";AN$(A) + " IS HERE."
 200  NEXT A:V%(ROOM) = 1
 205  IF MR% > MM% THEN MR% = 0: PRINT : PRINT "THE MAGIC RING ON YOUR FINGER VANISHED!": PRINT "THE FORCE FIELD IS GONE!!":AD%(3,4) = 0:MD%(0,8) = AC
 210  PRINT : INPUT "  YOUR COMMAND?";A$
 220  IF  LEFT$ (A$,1) = " " THEN A$ =  MID$ (A$,2): GOTO 220
 230  IF A$ = "" THEN A$ = CZ$: VTAB  PEEK (37): HTAB 16: PRINT A$
 240 CZ$ = A$: FOR A = 2 TO  LEN (A$): IF  MID$ (A$,A,1) <  > " " THEN  NEXT 
 250 V$ =  LEFT$ (A$,A - 1):S$ =  MID$ (A$,A + 1)
 260  IF  LEFT$ (S$,1) = " " THEN S$ =  MID$ (S$,2): GOTO 260
 265  GOSUB 60
 270  FOR C = 1 TO NC: IF C$(C) <  > V$ AND C$(C) <  > S$ THEN  NEXT : PRINT : PRINT "HUH? I ONLY UNDERSTAND THESE COMMANDS--     ";: FOR C = 1 TO NC: PRINT C$(C); SPC( 20 -  LEN (C$(C)));: NEXT : PRINT : PRINT : GOTO 210
 280  IF C$(C) = S$ THEN S$ = V$:V$ = C$(C)
 285 C = C - 9: IF C < 2 THEN C = 1
 290  ON C GOTO 3000,4000,4000,5000,5000,6000,6000,7000,8000,8000,8000,9000,10000,10000,11000,12000,13000,14000,15000,15000,16000,17000
 295  ON C - 22 GOTO 18000,19000,20000,21000,30000
 300  REM ///COMMANDS RETURN TO HERE
 310  IF  NOT NBTL THEN 500
 320  FOR M = 1 TO NM: IF MD%(M,5) <  > ROOM THEN 490
 330 M2 = MD%(M,14) - (MD%(M,14) = 2):MR = FD%(M2) / TD%(M2) +  INT (41 *  RND (1) - 20)
 340  IF MD%(M,4) < MR THEN  PRINT : PRINT MN$(M);" FLEES OUT AN EXIT.": PRINT : GOSUB 8500:MD%(M,5) = R2:M2 = MD%(M,14):TD%(M2) = TD%(M2) - MD%(M,1):FD%(M2) = FD%(M2) - MD%(M,13): GOTO 490
 345  IF MD%(M,14) = 2 THEN 490
 350  IF MD%(M,14) = 3 THEN 390
 360 OF = M: IF TD%(3) = MD%(0,1) THEN DF = 0: GOSUB 7500: GOTO 490
 370  FOR M2 = 1 TO NM: IF MD%(M2,5) = ROOM AND MD%(M2,14) = 3 AND  RND (1) < .25 THEN DF = M2: GOSUB 7500:M2 = 200: NEXT M2: GOTO 490
 380  NEXT M2:DF = 0: GOSUB 7500: GOTO 490
 390 OF = M
 400  FOR M2 = 1 TO NM: IF MD%(M2,5) = ROOM AND MD%(M2,14) = 1 THEN DF = M2: GOSUB 7500:M2 = 200: NEXT M2: GOTO 490
 410  NEXT M2
 490 NBTL = (FD%(1) < TD%(1)): IF NBTL THEN  NEXT M
 500  REM 
 510  IF MD%(16,5) = ROOM AND RO < 51 AND RO > 10 AND  RND (1) > .88 THEN  GOTO 910
 900  GOTO 100
 910  PRINT : PRINT "YOU HEAR A CRACKLE OF ENERGY AND SEE A": PRINT "FLASH OF LIGHT!! ": PRINT "LOCKJAW HAS TRANSPORTED YOU SOMEWHERE!!":R2 =  INT ( RND (1) * (40) + 10): GOTO 3500
 1000  REM ///INITIALIZE DATA
 1010  PRINT DK$;"OPEN EAMON.DESC": PRINT DK$;"READ EAMON.DESC": INPUT NR,NZ,NE,NM: PRINT DK$;"CLOSE":NA = NZ
 1020  PRINT DK$;"OPEN EAMON.ARTIFACTS,L128": DIM AN$(NA + 4),AD%(NA + 4,9): FOR A = 1 TO NA: PRINT DK$;"READ EAMON.ARTIFACTS,R";A: INPUT AN$(A): FOR A2 = 1 TO 4: INPUT AD%(A,A2): NEXT A2: IF AD%(A,2) > 1 THEN  FOR A2 = 5 TO 8: INPUT AD%(A,A2): NEXT A2
 1030  NEXT A: PRINT DK$;"OPEN EAMON.MONSTERS,L128": DIM MN$(NM + 30),MD%(NM + 30,15): FOR A = 1 TO NM: PRINT DK$;"READ EAMON.MONSTERS,R";A: INPUT MN$(A): FOR A2 = 1 TO 12: INPUT MD%(A,A2): NEXT A2,A
 1035 MD%(31,14) = 2: PRINT DK$"CLOSE"
 1040  PRINT DK$;"OPEN FRESH MEAT": PRINT DK$;"READ FRESH MEAT": INPUT REC: INPUT MN$(0),MD%(0,1),MD%(0,2),CH: FOR A = 1 TO 4: INPUT SA%(A): NEXT : FOR A = 1 TO 5: INPUT WA%(A): NEXT : INPUT AE,SEX$,GOLD,BANK,AC
 1050 NW = 15: DIM WN$(NW),WT%(NW),WO%(NW),WD%(NW),WP%(NW): FOR A = 1 TO 4: INPUT WN$(A),WT%(A),WO%(A),WD%(A),WS%(A): NEXT : PRINT DK$;"CLOSE"
 1055  PRINT DK$;"DELETE FRESH MEAT"
 1060  PRINT DK$;"OPEN EAMON.DESC,L256": PRINT DK$;"OPEN EAMON.ROOM NAMES,L64": PRINT DK$;"OPEN EAMON.ROOMS,L64"
 1070  FOR W2 = 1 TO 4: IF WN$(W2) <  > "NONE" THEN NA = NA + 1:AN$(NA) = WN$(W2):AD%(NA,2) = 2:AD%(NA,3) = 2:AD%(NA,4) =  - 1:AD%(NA,5) = WO%(W2):AD%(NA,6) = WT%(W2):AD%(NA,7) = WD%(W2):AD%(NA,8) = WS%(W2): NEXT :W2 = 4
 1080 WZ = W2:WT = W * WZ
 1090  FOR W2 = 1 TO WZ: FOR A = 1 TO NA - WZ: IF AN$(A) = WN$(W2) THEN AN$(A) = AN$(A) + "#": FOR A = 0 TO 0: NEXT : GOTO 1090
 1100  NEXT A,W2:EA = 0:MD%(0,8) = 0:A2 =  INT (AC / 2): IF A2 * 2 <  > AC THEN MD%(0,8) = 1:EA =  - 5
 1110  IF A2 THEN MD%(0,8) = MD%(0,8) + A2:EA = EA - A2 * 10: IF A2 = 3 THEN MD%(0,8) = MD%(0,8) + 2:EA = EA - 30
 1120  IF NA > NZ THEN MD%(0,10) = (EA + AE) * ( - EA > AE) + WO%(1) + WA%(WT%(1)) + 2 * MD%(0,2):MD%(0,7) = 0:MD%(0,9) = NZ + 1:MD%(0,11) = WD%(1):MD%(0,12) = WS%(1):MD%(0,14) = 3
 1130  READ NC: DIM C$(NC): FOR C = 1 TO NC: READ C$(C): NEXT 
 1140  FOR S = 1 TO 4:S2%(S) = SA%(S): NEXT : DIM V%(96):ROOM = 1
 1150 MM% =  INT ( RND (1) * 120) + 50
 1160 A = 0: FOR W = 1 TO 4:A = A + WS%(W) * WD%(W): NEXT : IF A > 50 THEN 1250
 1170  IF MD%(0,1) + MD%(0,2) + CH < 57 THEN MD%(17,5) =  INT ( RND (1) * 20 + 14):MD%(17,3) = 100
 1175  GOTO 1300
 1250  REM  /// BRING IN MORE MONSTERS (THE ADVENTURER IS LOADED FOR BEAR)
 1260 B = 0
 1270  FOR M = 1 TO NM: IF MD%(M,5) = 0 AND A / 4 > (MD%(M,1) + B) / 2 THEN MD%(M,5) =  INT ( RND (1) * 40 + 10):B = B + MD%(M,1)
 1280  NEXT 
 1300  REM  READY TO GO
 1900  GOTO 100
 1910  DATA   36
 1920  DATA  N,S,E,W,U,D,NE,NW,SE,SW,GET,TAKE,DROP,PUT,LOOK,EXAMINE,ATTACK,FLEE,RETREAT,ESCAPE,GIVE,INVENTORY,I,BLAST,HEAL,POWER,SPEED,SMILE,WAVE,SAY,READY
 1930  DATA   THROW,READ,DIG,OPEN,SAVE
 2000  REM ///DONE WITH GAME
 2010  PRINT : PRINT "(HIT ANY KEY TO CONTINUE)   ";: POKE  - 16368,0: GET A$: PRINT 
 2020  PRINT DK$;"CLOSE": IF SPD THEN MD%(0,2) = MD%(0,2) / 2
 2030  IF DIE THEN 2500
 2040  FOR W = 1 TO 4:WN$(W) = "NONE": NEXT :W2 = 1: FOR A = 1 TO NA: IF AD%(A,2) > 1 AND AD%(A,4) =  - 1 THEN WN$(W2) = AN$(A):WP%(W2) = A:W2 = W2 + 1:AD%(A,4) = 0
 2050  NEXT A: IF W2 < 6 THEN 2300
 2060  HOME : VTAB 5: PRINT "AS YOU START TO ENTER THE MAIN HALL,": PRINT "LORD WILLIAM MISSLEFIRE APPEARS AND": PRINT "TELLS YOU, 'YOU HAVE TOO MANY WEAPONS": PRINT "TO KEEP THEM ALL--4 IS THE LEGAL LIMIT.": PRINT : PRINT "YOUR WEAPONS ARE--"
 2070  FOR W = 1 TO W2 - 1: PRINT  SPC( 4);W;"--";WN$(W): NEXT : PRINT : PRINT "ENTER THE NUMBER OF THE WEAPON TO SELL     (1-";W2 - 1;") "
 2080  VTAB  PEEK (37): HTAB 23: INPUT A$:W5 =  VAL (A$): IF W5 < 1 OR W5 > W2 - 1 OR W5 <  >  INT (W5) THEN 2080
 2090 AD%(WP%(W5),4) =  - 1:WN$(W5) = WN$(W2 - 1):WP%(W5) = WP%(W2 - 1):W2 = W2 - 1: IF W2 > 5 THEN 2060
 2300  FOR W = 1 TO 4: IF WP%(W) THEN WT%(W) = AD%(WP%(W),6):WO%(W) = AD%(WP%(W),5):WD%(W) = AD%(WP%(W),7):WS%(W) = AD%(WP%(W),8)
 2310  NEXT W: HOME : VTAB 5: PRINT "AS YOU DELIVER YOUR TREASURES TO SAM": PRINT "SLICKER, THE LOCAL BUYER FOR SUCH": PRINT "THINGS, HE EXAMINES YOUR GOODS AND": PRINT "PAYS YOU ";
 2320 TP = 0: FOR A = 1 TO NA: IF AD%(A,4) <  >  - 1 THEN 2350
 2330  IF AD%(A,2) <  > 0 THEN AD%(A,1) = AD%(A,1) * CH / 10
 2340 TP = TP + AD%(A,1)
 2350  NEXT A: PRINT TP;" GOLD PIECES.": PRINT 
 2360 GOLD = GOLD + TP: PRINT : PRINT "(HIT ANY KEY TO CONTINUE)  ";: GET A$: PRINT 
 2370 A = 0: FOR X = 1 TO NR:A = A + V%(X): NEXT : IF A < NR * .8 THEN  PRINT : PRINT "YOU DID NOT EXPLORE THE CAVE THOROUGHLY": PRINT "YOU DO NOT WIN THE 20000 GP BET.": GOTO 2410
 2400  HOME : PRINT "SOME OF YOUR FRIENDS HELPED YOU COLLECT": PRINT "YOUR BET, SINCE BLACK BART WAS SOMEWHAT": PRINT "RELUCTANT TO PAY.":GOLD = GOLD + 20000
 2410  PRINT : PRINT "PRESS ANY KEY TO CONTINUE": GET A$: PRINT 
 2500  ONERR  GOTO 2500
 2510  HOME : VTAB 5: PRINT "(INSERT EAMON MASTER DISKETTE, THEN": PRINT "   HIT THE 'C' KEY)  ";: POKE  - 16368,0
 2520  GET A$: IF A$ <  > "C" THEN 2520
 2530  PRINT A$
 2540  IF DIE THEN  PRINT DK$;"OPEN THE ADVENTURER,S6,D1": PRINT DK$;"DELETE THE ADVENTURER": GOTO 2900
 2550  PRINT DK$;"OPEN CHARACTERS,L150,S6,D1": PRINT DK$;"WRITE CHARACTERS,R";REC: PRINT MN$(0): PRINT MD%(0,1): PRINT MD%(0,2): PRINT CH: FOR A = 1 TO 4: PRINT SA%(A): NEXT 
 2560  FOR A = 1 TO 5: PRINT WA%(A): NEXT : PRINT AE: PRINT SEX$: PRINT GOLD: PRINT BANK: PRINT AC: FOR A = 1 TO 4: PRINT WN$(A): PRINT WT%(A): PRINT WO%(A): PRINT WD%(A): PRINT WS%(A): NEXT 
 2570  PRINT DK$;"OPEN THE ADVENTURER": PRINT DK$;"WRITE THE ADVENTURER": PRINT MN$(0): PRINT REC: PRINT DK$;"CLOSE"
 2900  PRINT DK$;"RUN MAIN HALL,S6,D1"
 3000  REM ///MOVE COMMAND
 3005  IF MR% AND MM% > 1000 THEN NBTL = 0
 3010  IF NBTL THEN  PRINT : PRINT "YOU CAN'T DO THAT WITH UNFRIENDLIES        ABOUT!": PRINT : GOTO 100
 3015  IF  LEN (V$) < 2 THEN V$ = V$ + " ": GOTO 3015
 3020  FOR D = 1 TO 20 STEP 2: IF  LEFT$ (V$,2) <  >  MID$ ("N S E W U D NENWSESW",D,2) THEN  NEXT 
 3025 D =  INT (D / 2 + .5)
 3030  PRINT DK$;"READ EAMON.ROOMS,R";ROOM: FOR D2 = 1 TO D: INPUT R2: NEXT : PRINT DK$
 3040  GOSUB 3100: IF R2 > 0 THEN 3500
 3050  REM ///SPECIAL MOVES
 3060  IF R2 =  - 99 THEN  PRINT : PRINT "YOU SUCCESSFULLY RIDE OFF INTO THE": PRINT "  SUNSET.": GOTO 2000
 3070  IF  NOT R2 THEN 3490
 3080  IF R2 < 0 THEN 3490
 3090  GOTO 3500
 3100  IF (R2 = 9 AND ROOM = 51) OR (R2 = 51 AND ROOM = 9) THEN 3200
 3105  IF RO = 10 AND BE AND D = 1 THEN R2 = 9: RETURN 
 3108  IF RO = 66 AND D = 2 AND L6 THEN R2 = 9: RETURN 
 3110  IF R2 = 4 AND  NOT UP AND ROOM = 3 THEN  PRINT : PRINT "THE ROPE HAS NOT BEEN FASTENED.": POP : GOTO 100
 3130  IF R2 = 42 THEN 3250
 3140  IF R2 = 38 AND RO = 13 AND L0 = 0 THEN  POP : GOTO 3490
 3150  IF R2 =  - 52 AND L1 THEN R2 =  - R2: RETURN 
 3160  IF R2 =  - 61 AND L3 THEN R2 =  - R2: RETURN 
 3170  IF R2 =  - 62 AND L2 THEN R2 =  - R2: RETURN 
 3190  RETURN 
 3200  IF DC < 3 THEN  POP : GOTO 3490
 3240  RETURN 
 3250  REM  /// CLONE RTN
 3252  IF MB THEN  RETURN 
 3255 NM = NM + 1:CL% = 1:MD%(NM,15) = 1:SW% = 0:CL = CL + 1
 3256 Y = 0: FOR X = 1 TO NA: IF AD%(X,2) > 1 AND AD%(X,5) =  - 1 AND (AD%(X,7) * AD%(X,8)) > Y THEN Y = AD%(X,7) * AD%(X,8):SW% = X
 3257  NEXT X
 3258 MW% = MD%(0,9): IF MD%(0,9) =  - 1 THEN MW% = 0:MD%(NM,10) = 80:MD%(NM,11) = 1:MD%(NM,12) = 8
 3260 MN$(NM) = "": FOR X =  LEN (MN$(0)) TO 1 STEP  - 1:MN$(NM) = MN$(NM) +  MID$ (MN$(0),X,1): NEXT :MD%(NM,1) = MD%(0,1):MD%(NM,2) = MD%(0,2):MD%(NM,8) = AC
 3265  IF MD%(0,9) > 0 THEN MD%(NM,10) = 2 * MD%(NM,2) + WA%(AD%(MD%(0,9),6)) + AD%(MD%(0,9),5)
 3270  IF MD%(0,9) > 0 THEN MD%(NM,11) = AD%(MD%(0,9),7):MD%(NM,12) = AD%(MD%(0,9),8)
 3273  IF MD%(0,9) < 0 THEN MD%(NM,11) = 1:MD%(NM,12) = 6
 3275 MD%(NM,6) =  - 1: IF MR% THEN MD%(NM,8) = 100
 3280 MD%(NM,5) = 42
 3290 MD%(NM,4) =  INT ( RND (1) * 80)
 3350  RETURN 
 3490  PRINT : PRINT "YOU CAN'T GO THAT WAY!":V%(ROOM) = 0: GOTO 100
 3500 R3 = ROOM:ROOM = R2: GOSUB 3600
 3590  GOTO 100
 3600  REM ///CHECK REACTIONS ON MONSTERS
 3610 TD%(1) = 0:TD%(3) = MD%(0,1):FD%(1) = 0:FD%(3) = MD%(0,13)
 3620  FOR M = 1 TO NM: IF MD%(M,5) = R3 THEN  IF MD%(M,14) = 3 OR (MD%(M,14) = 1 AND 200 *  RND (1) < MD%(M,4)) THEN MD%(M,5) = ROOM: GOTO 3670
 3630  IF MD%(M,5) <  > ROOM THEN 3900
 3640  IF MD%(M,14) THEN 3670
 3650 FR = MD%(M,3): IF FR AND FR <  > 100 THEN FR = FR +  INT ((CH - 10) / 2)
 3660 MD%(M,14) = 1: IF FR > (100 *  RND (1)) THEN MD%(M,14) = 2: IF FR > (100 *  RND (1)) THEN MD%(M,14) = 3
 3670 A = MD%(M,14):FD%(A) = FD%(A) + MD%(M,13):TD%(A) = TD%(A) + MD%(M,1)
 3900  NEXT M:NBTL = (TD%(1) > 0): RETURN 
 4000  REM ///GET COMMAND
 4010  GOSUB 4900: IF V$ = "TAKE" THEN 4500
 4020  IF S$ = "ALL" THEN 4160
 4030  IF (S$ = "PILL" OR S$ = "WHITE PILL") AND V$ = "TAKE" THEN  GOTO 4300
 4040  IF S$ = "RING" THEN S$ = "MAGIC RING"
 4050  IF S$ = "GOLDEN BOW" THEN S$ = "MAGIC BOW"
 4120  FOR A = 1 TO NA: IF AN$(A) <  > S$ OR AD%(A,4) <  > ROOM THEN  NEXT : PRINT : PRINT "I SEE NO ";S$;" HERE!": PRINT : GOTO 100
 4130  GOSUB 4200: IF WT + AD%(A,3) > 10 * MD%(0,1) THEN  PRINT : PRINT "IT IS TOO HEAVY FOR YOU.": PRINT : GOTO 100
 4140  PRINT : PRINT "GOT IT.":AD%(A,4) =  - 1:WT = WT + AD%(A,3): PRINT : IF AD%(A,2) < 2 OR MD%(0,9) <  >  - 1 THEN 300
 4150  GOTO 17000
 4160  FOR A = 1 TO NA: IF AD%(A,4) <  > ROOM THEN 4190
 4170  GOSUB 4200: IF WT + AD%(A,3) > 10 * MD%(0,1) THEN  PRINT AN$(A);" IS TOO HEAVY": GOTO 4190
 4180  PRINT AN$(A) + " TAKEN.":AD%(A,4) =  - 1:WT = WT + AD%(A,3)
 4190  NEXT : PRINT : GOTO 300
 4200  REM ///INSERT HERE SPECIAL EFFECTS OF PICKING SOMETHING UP
 4210  IF A = 14 AND MD%(31,5) = ROOM THEN  PRINT MN$(31);" WON'T LET YOU PICK UP ";AN$(14);".": POP :MD%(31,14) = 1: GOSUB 3600: GOTO 300
 4220  IF A = 33 AND MD%(38,5) = 0 AND MD%(38,13) = 0 THEN  PRINT : PRINT "ONE OF THE GOLD COINS JUMPS UP AND": PRINT "  BITES YOU!!":MD%(38,5) = RO: GOSUB 3600: POP :D = 2:S = 4:DF = 0:OF = 38: GOSUB 7635: GOTO 100
 4299  RETURN 
 4300  IF AD%(19,4) <  >  - 1 THEN  PRINT : PRINT "YOU DON'T HAVE A ";S$: GOTO 100
 4310  PRINT : PRINT "YOU HAVE TAKEN THE ";S$:AD%(19,4) = 0: IF MD%(0,13) > 0 THEN  PRINT "ALL YOUR WOUNDS HAVE SUDDENLY HEALED.":MD%(0,13) = 0: GOTO 300
 4320  PRINT "NOTHING HAPPENED.": GOTO 300
 4500  REM  /// TAKE OFF RING
 4510  IF S$ = "OFF RING" OR S$ = "RING OFF" OR S$ = "OFF MAGIC RING" OR S$ = "MAGIC RING OFF" THEN 4550
 4520  GOTO 4020
 4550 S$ = "MAGIC RING": GOTO 22500
 4900  REM ///ROUTINE TO GET S$ IF NOT PREVIOUSLY SPECIFIED.
 4905  IF S$ = "" THEN  PRINT : PRINT C$(C + 9);" WHAT";: INPUT S$: GOTO 4900
 4910  IF S$ = "PILL" THEN S$ = "WHITE PILL"
 4915  IF S$ = "ROD" THEN S$ = "PLATINUM ROD"
 4920  IF S$ = "BRASS BOTTLE" THEN S$ = "BOTTLE"
 4990  RETURN 
 5000  REM ///DROP COMMAND
 5010  IF V$ = "PUT" THEN 5200
 5020  IF S$ = "ALL" THEN 5100
 5030  FOR A = 1 TO NA: IF AN$(A) <  > S$ OR AD%(A,4) <  >  - 1 THEN  NEXT : PRINT : PRINT "YOU AREN'T CARRYING A ";S$: PRINT : GOTO 100
 5040 WT = WT - AD%(A,3):AD%(A,4) = ROOM: PRINT AN$(A);" DROPPED.": PRINT 
 5050  IF WT > 10 * MD%(0,1) THEN  PRINT "YOU SUDDENLY FIND YOU CANNOT CARRY ALL  OF THE ITEMS YOU ARE CARRYING, AND THEY ALL FALL TO THE GROUND.": PRINT : GOTO 5100
 5060  IF MD%(0,9) > 0 THEN  IF AD%(MD%(0,9),4) <  >  - 1 THEN MD%(0,9) =  - 1
 5070  GOTO 300
 5100  FOR A = 1 TO NA: IF AD%(A,4) =  - 1 THEN  PRINT AN$(A);" DROPPED.":AD%(A,4) = ROOM
 5110  NEXT A:WT = 0: GOTO 5060
 5200  REM  /// PUT RTN
 5210  GOSUB 4900
 5220  IF S$ = "ON MAGIC RING" OR S$ = "MAGIC RING ON" OR S$ = "ON RING" OR S$ = "RING ON" THEN 5500
 5230  GOTO 5020
 5500  REM  /// PUT ON RING
 5510 S$ = "MAGIC RING": GOTO 22000
 6000  REM ///LOOK COMMAND
 6010 LK = 0: GOSUB 6500
 6015  IF LK THEN 300
 6020 LK = 0: FOR A = 1 TO NZ: IF AN$(A) = S$ AND (AD%(A,4) = ROOM OR AD%(A,4) =  - 1) THEN  PRINT DK$;"READ EAMON.DESC,R";A + 100: INPUT A$: PRINT DK$: PRINT : PRINT A$: PRINT :LK = 1
 6030  NEXT A: IF LK THEN 300
 6040  FOR A = 1 TO NM: IF MN$(A) = S$ AND MD%(A,5) = ROOM THEN  PRINT DK$;"READ EAMON.DESC,R";A + 300: INPUT A$: PRINT DK$: PRINT : PRINT A$: PRINT :LK = 1
 6050  NEXT A: IF LK THEN 300
 6060  IF RO = 13 THEN L0 = 1: PRINT : PRINT "YOU SEE A DARK HOLE TO THE SOUTH!!": GOTO 300
 6070  IF RO = 31 THEN  PRINT : PRINT "THE EAST WALL SEEMS TO BE MADE OF PAPER.": GOTO 300
 6080  IF RO = 26 THEN L1 = 1: PRINT : PRINT "YOU FOUND A SECRET PASSAGE TO THE EAST!": GOTO 300
 6090  IF RO = 66 THEN L6 = 1: PRINT : PRINT "YOU SEE A STRANGE MIST TO THE SOUTH.": GOTO 300
 6490 V%(ROOM) = 0: GOTO 300
 6500  IF (S$ = "PILL" OR S$ = "WHITE PILL") AND AD%(19,4) =  - 1 THEN  PRINT : PRINT "THE PILL HAS A SMALL 'RX' SYMBOL ON IT.":LK = 1: RETURN 
 6510  IF S$ = "BOOK" AND AD%(24,4) =  - 1 THEN  POP : GOTO 19000
 6520  IF S$ = "GOLD COINS" AND AD%(33,4) = RO AND MD%(38,5) = 0 AND MD%(38,13) = 0 THEN  PRINT : PRINT "ONE OF THE COINS IS REALLY A BUG WITH": PRINT " A GOLDEN SHELL. HE'S ALSO POISONOUS!":MD%(38,5) = RO: GOSUB 3600
 6590  RETURN 
 6990 V%(ROOM) = 0: GOTO 300
 7000  REM ///ATTACK COMMAND
 7010  GOSUB 4900
 7020  IF S$ = "MIRROR" AND ROOM = 42 THEN  PRINT :MB = 1: PRINT "THE MIRROR BREAKS.":AD%(25,4) = RO:NM = NM - CL: IF CL > 0 THEN  PRINT "THE CLONES CRUMBLE.": GOSUB 3600:CL = 0: GOTO 300
 7030  IF S$ = "MIRROR" THEN 300
 7040  IF  LEFT$ (S$,4) = "WOLF" THEN  FOR X = 23 TO 28: IF MD%(X,5) <  > ROOM THEN  NEXT : GOTO 7280
 7050  IF S$ = "WOLF" THEN S$ = MN$(X)
 7060  IF S$ = "ENTRANCE" AND MD%(0,9) = 20 AND ROOM = 10 THEN  PRINT : PRINT "YOU BLASTED THE ENTRANCE OPEN!!":BE = 1: GOTO 300
 7280  FOR M = 1 TO NM: IF S$ <  > MN$(M) OR MD%(M,5) <  > ROOM THEN  NEXT : PRINT : PRINT "ATTACK WHO?": PRINT : GOTO 100
 7290  IF MD%(0,9) =  - 1 THEN  PRINT : PRINT "YOU HAVE NO WEAPON READY!": PRINT : GOTO 100
 7300 OF = 0:DF = M: GOSUB 7500: IF  NOT HIT THEN 7360
 7310  IF INC THEN W2 = AD%(MD%(0,9),6):WA%(W2) = WA%(W2) + 2
 7320  IF  - EA <  = AE THEN 300
 7330  IF  INT (100 *  RND (1) + 1) <  = MD%(0,10) THEN 7360
 7340 A = 2: IF EA + AE =  - 1 THEN A = 1
 7350 AE = AE + A:MD%(0,10) = MD%(0,10) + A
 7360  IF MD%(DF,14) = 1 OR DF = 0 THEN 300
 7370 MD%(DF,3) = MD%(DF,3) / 2: FOR M = 1 TO NM: IF MD%(M,5) = ROOM AND MD%(M,14) > 1 THEN MD%(M,14) = 0
 7380  NEXT M:R3 = ROOM: GOSUB 3600: GOTO 300
 7400  REM ///MONSTER TRIES TO PICK UP WEAPON
 7410  IF MD%(OF,10) = 0 THEN  RETURN 
 7420  FOR A = 1 TO NA: IF AD%(A,4) <  > ROOM OR AD%(A,2) < 2 THEN  NEXT : RETURN 
 7430  IF A = 14 AND OF <  > 31 AND MD%(31,5) = ROOM THEN  NEXT : RETURN 
 7440  PRINT MN$(OF);" PICKS UP ";AN$(A): PRINT :AD%(A,4) = 0:MD%(OF,9) = A:MD%(OF,10) = MD%(OF,10) + AD%(A,5):MD%(OF,4) = MD%(OF,4) * 2: RETURN 
 7500 HIT = 0: IF MD%(OF,9) =  - 1 THEN 7400
 7505  PRINT MN$(OF);" ATTACKS ";MN$(DF): PRINT " --";: IF DF = 17 AND MD%(OF,9) > 0 THEN  GOTO 7570
 7508 RL =  INT (100 *  RND (1) + 1):HIT = 0: IF (RL < 5 OR RL < MD%(OF,10) - MD%(DF,7)) AND RL < 96 THEN HIT = 1
 7510  IF HIT THEN 7600
 7520  IF RL < 97 THEN  PRINT "A MISS.": RETURN 
 7525  PRINT "A FUMBLE!":FR =  INT (100 *  RND (1) + 1): IF FR <  = 35 + 40 * ( NOT MD%(OF,9)) THEN  PRINT "  FUMBLE RECOVERED.": PRINT : RETURN 
 7530  IF FR < 76 THEN  PRINT "  WEAPON DROPPED!":AD%(MD%(OF,9),4) = ROOM:MD%(OF,10) = MD%(OF,10) - AD%(MD%(OF,9),5):MD%(OF,9) =  - 1:MD%(OF,4) = MD%(OF,4) / 2: PRINT : RETURN 
 7535  IF FR > 95 THEN 7550
 7540  PRINT "  WEAPON BROKEN!":AD%(MD%(OF,9),4) = 0:MD%(OF,10) = MD%(OF,10) - AD%(MD%(OF,9),5):MD%(OF,9) =  - 1:MD%(OF,4) = MD%(OF,4) / 2: IF  RND (1) > .5 THEN  PRINT : RETURN 
 7545  PRINT "  BROKEN WEAPON HURTS USER!"
 7550  IF FR = 100 THEN 7560
 7555 D = MD%(OF,11):S = MD%(OF,12):DF = OF:A = 1: GOTO 7635
 7560 D = MD%(OF,11) * 2:S = MD%(OF,12):DF = OF:A = 0: GOTO 7635
 7570  IF  RND (1) < .93 THEN 7508
 7575  PRINT MN$(17);" TAKES ";AN$(MD%(OF,9));" AND EATS IT!!": PRINT  CHR$ (34);MN$(17);" LIKES ";AN$(MD%(OF,9));"! - IT'S CHEWY!!"; CHR$ (34):AD%(MD%(OF,9),4) = 0:MD%(OF,9) =  - 1: RETURN 
 7600 INC = ( INT (100 *  RND (1) + 1) > MD%(OF,10)):MD%(OF,10) = MD%(OF,10) + 2 * INC
 7605 D = MD%(OF,11):S = MD%(OF,12):A = 1: IF RL > 5 THEN  PRINT "A HIT!": GOTO 7635
 7610  PRINT "A CRITICAL HIT!":R2 =  INT (100 *  RND (1) + 1): IF R2 < 51 THEN A = 0: GOTO 7635
 7615  IF R2 < 86 THEN S = 1.5 * S: GOTO 7635
 7620  IF R2 < 96 THEN D = 2 * D: GOTO 7635
 7625  IF R2 < 100 THEN D = 3 * D: GOTO 7635
 7630  GOTO 7700
 7635 D2 = 0: FOR D3 = 1 TO D:D2 = D2 +  INT (S *  RND (1) + 1): NEXT 
 7636  IF (DF = 5 AND MD%(OF,9) = 10) OR (DF = 13 AND MD%(OF,9) = 27) THEN MD%(DF,13) = MD%(DF,1): GOTO 7700
 7638  IF MD%(DF,8) <  > 100 THEN D2 = D2 - A * MD%(DF,8): IF D2 < 1 THEN  PRINT "   BLOW BOUNCES OFF !!": PRINT : RETURN 
 7639  IF MD%(DF,8) = 100 THEN  PRINT "   BLOW BOUNCES OFF MAGIC FORCE FIELD!": IF DF = 0 THEN MR% = MR% + D2
 7640  IF MD%(DF,8) = 100 THEN  RETURN 
 7642 MD%(DF,13) = MD%(DF,13) + D2: IF MD%(DF,13) >  = MD%(DF,1) THEN 7700
 7645 A = MD%(DF,14):FD%(A) = FD%(A) + D2
 7650  PRINT : PRINT MN$(DF);: ON  INT (MD%(DF,13) * 5 / MD%(DF,1) + 1) GOTO 7655,7660,7665,7670,7675
 7655  PRINT " IS STILL IN GOOD": PRINT "  SHAPE.": RETURN 
 7660  PRINT " IS HURTING.": RETURN 
 7665  PRINT " IS IN PAIN.": RETURN 
 7670  PRINT " IS VERY BADLY INJURED.": RETURN 
 7675  PRINT " IS AT DEATH'S DOOR,": PRINT "  KNOCKING LOUDLY.": RETURN 
 7700  IF MD%(DF,2) < 50 THEN  PRINT MN$(DF);" IS DEAD!": PRINT 
 7720 A = MD%(DF,14):FD%(A) = FD%(A) + MD%(DF,1) - MD%(DF,13) + D2:MD%(DF,5) = 0
 7730  IF MD%(DF,6) > 0 THEN A2 = DF + NZ - NM + CL:AD%(A2,4) = ROOM
 7735  IF MD%(DF,6) < 0 THEN  PRINT : PRINT "THE BODY DISINTEGRATES.":CL = CL - 1: IF DF = NM OR CL = 0 THEN NM = NM - 1
 7740  IF MD%(DF,9) > 0 THEN AD%(MD%(DF,9),4) = ROOM:MD%(DF,10) = MD%(DF,10) - AD%(MD%(DF,9),5):MD%(DF,9) =  - 1:MD%(DF,4) = MD%(DF,4) / 2
 7760  IF DF = 0 THEN DIE = 1: GOTO 2000
 7770  IF DF = 5 AND MD%(OF,9) <  > 10 THEN 7910
 7780  IF DF = 5 THEN  PRINT : PRINT "AS THE WEREWOLF DIES, A CHAIN FALLS ": PRINT "OFF HIS NECK.":AD%(26,4) = ROOM: GOSUB 3600
 7790  IF DF = 36 THEN MD%(DF,13) = 0: PRINT "THE GENIE REENTERS HIS BOTTLE."
 7800  IF DF = 13 AND MD%(OF,9) <  > 27 THEN  PRINT MN$(13);" STANDS UP!":MD%(DF,13) = 5:MD%(DF,5) = ROOM:AD%(A2,4) = 0: GOSUB 3600
 7900  PRINT : RETURN 
 7910  PRINT "YOU HEAR A LOW GROWL, FOLLOWED BY A": PRINT "LOUD SNARL AS THE WEREWOLF COMES BACK ": PRINT "TO LIFE!!"
 7920 MD%(DF,5) = ROOM:MD%(DF,13) = 0:AD%(39,4) = 0: GOSUB 3600: PRINT : RETURN 
 8000  REM ///FLEE COMMAND
 8010  IF  NOT NBTL THEN  PRINT : PRINT "THERE'S NOTHING TO FLEE FROM!": PRINT : GOTO 100
 8020 D$ = ""
 8040  GOSUB 8500: IF R2 = 0 THEN  PRINT : PRINT "THERE'S NO PLACE TO GO!!": GOTO 100
 8490  GOTO 3500
 8500  PRINT DK$;"READ EAMON.ROOMS,R";ROOM: FOR D = 1 TO 10: INPUT DR%(D):D$ = D$ +  STR$ (DR%(D)): NEXT : PRINT DK$
 8510  IF D$ = "0000000000" THEN R2 = 0: RETURN 
 8520  IF ROOM = 31 AND L1 = 0 THEN R2 = 30: RETURN 
 8530  IF RO = 13 AND L0 <  > 1 THEN R2 = 12: RETURN 
 8580 R2 = DR%(10 *  RND (1) + 1): IF R2 < 1 THEN 8580
 8590  RETURN 
 9000  REM ///GIVE COMMAND
 9010 A2 = 1
 9020  FOR M = 1 TO NM: IF  LEFT$ (S$, LEN (MN$(M))) = MN$(M) AND MD%(M,5) = ROOM THEN 9050
 9030  NEXT : IF A2 = 1 THEN A2 = 2: PRINT : INPUT "WHO TO GIVE TO? ";S$: GOTO 9020
 9040  PRINT : PRINT "NOBODY HERE BY THAT NAME!": PRINT : GOTO 100
 9050 S$ =  MID$ (S$, LEN (MN$(M)) + 1)
 9060  IF  LEFT$ (S$,1) = " " THEN S$ =  MID$ (S$,2): GOTO 9060
 9065  IF  VAL (S$) > 0 THEN 9500
 9070  FOR A = 1 TO NA: IF  LEFT$ (S$, LEN (AN$(A))) = AN$(A) AND AD%(A,4) =  - 1 THEN 9095
 9080  NEXT : IF A2 = 2 THEN A2 = 3: PRINT : INPUT "WHAT TO GIVE? ";S$: GOTO 9065
 9090  PRINT : PRINT "YOU AREN'T CARRYING A ";S$;"!": PRINT : GOTO 100
 9095  IF A = MD%(0,9) THEN MD%(0,9) =  - 1
 9100  PRINT : PRINT "OKAY.":AD%(A,4) = 0:MD%(M,14) = 0:MD%(M,3) = MD%(M,3) * (1 + AD%(A,1) / 100)
 9110  IF MD%(M,10) <  > 0 AND MD%(M,9) =  - 1 THEN MD%(M,9) = A:MD%(M,10) = MD%(M,9) + AD%(A,5):MD%(M,4) = 2 * MD%(M,4)
 9120 R3 = ROOM: GOSUB 3600: GOTO 5050
 9500  IF  VAL (S$) > GOLD THEN  PRINT : PRINT "YOU AREN'T CARRYING THAT MUCH GOLD OF      YOUR OWN!": PRINT : GOTO 300
 9510 GOLD =  INT (GOLD -  VAL (S$)):MD%(M,14) = 0:MD%(M,3) = MD%(M,3) * (1 +  VAL (S$) / 100): GOTO 9120
 10000  REM ///INVENTORY COMMAND
 10010  PRINT : PRINT "YOU ARE CARRYING THE FOLLOWING--":A0 = 0: FOR A = 1 TO NA: IF AD%(A,4) =  - 1 THEN  PRINT " ";AN$(A); LEFT$ (" (READY WEAPON)",1 + 20 * (A = MD%(0,9))):A0 = A0 + 1: IF A0 = 15 THEN  PRINT : PRINT "(HIT ANY KEY TO CONTINUE)";: GET A$: PRINT :A0 = 0
 10020  NEXT : GOTO 300
 11000  REM ///BLAST SPELL
 11010  GOSUB 4900
 11470  FOR M = 1 TO NM: IF S$ <  > MN$(M) OR MD%(M,5) <  > ROOM THEN  NEXT : PRINT : PRINT "YOU CAN'T BLAST ";S$;"!": PRINT : GOTO 100
 11480 S = 1: GOSUB 11500: IF  NOT SUC THEN 300
 11490  PRINT : PRINT "DIRECT HIT!": PRINT :S = 10:D = 3:DF = M:A = 0: GOSUB 7635: PRINT : GOTO 300
 11500 SUC = 0: IF SA%(S) THEN RL =  INT (100 *  RND (1) + 1):SUC = (((RL < S2%(S)) OR (RL < 5)) AND (RL < 95)): IF RL = 100 THEN 11600
 11510  IF  NOT SUC THEN  PRINT : PRINT "NOTHING HAPPENED.": RETURN 
 11520 RL =  INT (100 *  RND (1) + 1): IF RL > S2%(S) THEN S2%(S) = S2%(S) + 2:SA%(S) = SA%(S) + 2
 11530 S2%(S) =  INT (S2%(S) * .9)
 11540  RETURN 
 11600  PRINT : PRINT "THE STRAIN OF ATTEMPTING TO CAST THIS      SPELL OVERLOADS YOUR BRAIN AND YOU      FORGET IT COMPLETELY.": PRINT :SA%(S) = 0: RETURN 
 12000  REM ///HEAL SPELL
 12010 S = 2: GOSUB 11500: IF  NOT SUC THEN 300
 12020  PRINT : IF MD%(0,13) THEN  PRINT "SOME OF YOUR WOUNDS SEEM TO CLEAR UP.": PRINT 
 12030 MD%(0,13) = MD%(0,13) -  INT (10 *  RND (1) + 1): IF MD%(0,13) < 0 THEN MD%(0,13) = 0
 12040 DF = 0: GOSUB 7650: GOTO 300
 13000  REM ///POWER SPELL--RE-WRITE THIS SECTION FOR YOUR OWN SPECIAL PLOTS
 13010 S = 4: GOSUB 11500: IF  NOT SUC THEN 300
 13020 RA = 0: IF MD%(0,13) > MD%(0,1) * .65 AND  RND (1) > .7 THEN  PRINT : PRINT "ALL YOUR WOUNDS HAVE HEALED.":MD%(0,13) = 0: GOTO 300
 13025  IF  RND (1) < .85 THEN  GOTO 13200
 13030  PRINT : PRINT "YOU HEAR A BOOMING VOICE SAY:": PRINT " 'DO YOU SURRENDER?' ": PRINT "INPUT Y OR N :";
 13040  GET A$: IF A$ <  > "Y" AND A$ <  > "N" AND A$ <  > "T" THEN 13040
 13050  IF A$ = "Y" THEN  PRINT : PRINT "YOU CAN NOT BE BLAMED. MANY OF THE BEST": PRINT "WARRIORS CAN'T MAKE IT THRU THIS CAVE.": GOTO 2000
 13060  IF A$ = "T" THEN  PRINT : PRINT "YOU HEAR A BOOMING VOICE SAY:": PRINT "  'YOU ARE WISE BEYOND YOUR YEARS.'": PRINT " 'YOU SHALL SURVIVE'":MD%(0,8) = 100:MM% = 32500:MR% = 1: GOTO 300
 13200  IF  RND (0) < .45 THEN  GOTO 13300
 13205  PRINT : PRINT "YOUR BODY RADIATES A SUDDEN FLASH OF ": PRINT "INTENSE ENERGY!! IT STRIKES EVERYTHING": PRINT "IN THE ROOM!"
 13210 D = 3:S = 4:OF = 0: FOR DF = 1 TO NM
 13220  IF MD%(DF,5) = ROOM THEN  PRINT "IT STRIKES ";MN$(DF): GOSUB 7635
 13230  NEXT : GOTO 300
 13300  REM  /// REST OF POWER SPELLS
 13305  IF  RND (1) < .6 THEN 13400
 13310  PRINT : PRINT "A BLINDING FLASH OF LIGHT APPEARS!!": PRINT "AND THEN VANISHES.": FOR X = 1 TO 4:SA%(X) = 80:S2%(X) = 80: NEXT : GOTO 300
 13400  IF AD%(28,4) = RO THEN  PRINT : PRINT "THE STATUE COMES TO LIFE!":MD%(39,5) = RO:AD%(28,4) = 0: GOSUB 3600: GOTO 300
 13950  PRINT : PRINT "NOTHING HAPPENED.": GOTO 300
 14000  REM ///SPEED SPELL
 14010 S = 3: GOSUB 11500: IF  NOT SUC THEN 300
 14020  IF  NOT SPD THEN MD%(0,10) = MD%(0,10) + 2 * MD%(0,2):MD%(0,2) = 2 * MD%(0,2)
 14030 SPD = SPD +  INT (25 *  RND (1) + 10): PRINT : PRINT "YOU CAN FEEL THE NEW AGILITY FLOWING": PRINT "   THROUGH YOU!": PRINT : GOTO 300
 15000  REM ///SMILE COMMAND
 15010  PRINT : FOR M = 1 TO NM: IF MD%(M,5) <  > ROOM THEN 15100
 15020  ON MD%(M,14) GOTO 15030,15040,15050
 15030  PRINT MN$(M);" GROWLS AT YOU": GOTO 15100
 15040  PRINT MN$(M);" IGNORES YOU": GOTO 15100
 15050  PRINT MN$(M);" ";V$;"S BACK": GOTO 15100
 15100  NEXT M: PRINT : GOTO 300
 16000  REM ///SAY COMMAND
 16010  GOSUB 4900
 16030  IF S$ = "BLAST" THEN V$ = S$:S$ = "": GOTO 11000
 16040  IF S$ = "HEAL" THEN 12000
 16050  IF S$ = "POWER" THEN 13000
 16060  IF S$ = "SPEED" THEN 14000
 16080  IF  LEFT$ (S$,4) = "ROOM" THEN R2 =  VAL ( MID$ (S$,5)): IF R2 <  = NR AND R2 > 0 THEN  GOTO 3500
 16900  PRINT : PRINT "OKAY, '";S$;"'": PRINT : GOTO 300
 17000  REM ///READY A WEAPON.
 17010  GOSUB 4900: IF S$ = "MIRROR" THEN 17030
 17020  FOR A = 1 TO NA: IF AN$(A) <  > S$ OR AD%(A,4) <  >  - 1 THEN  NEXT : PRINT : PRINT "YOU AREN'T CARRYING IT!": PRINT : GOTO 100
 17030  IF (S$ = "MIRROR" OR S$ = "BROKEN MIRROR") AND AD%(25,4) =  - 1 THEN RM = 1: PRINT : PRINT "O.K. YOU ARE VIEWING THROUGH THE MIRROR": GOTO 100
 17930  IF AD%(A,2) < 2 THEN  PRINT : PRINT "THAT ISN'T A WEAPON!": PRINT : GOTO 100
 17980 MD%(0,10) = (EA + AE) * ( - EA > AE) + AD%(A,5) + WA%(AD%(A,6)) + 2 * MD%(0,2):MD%(0,9) = A:MD%(0,11) = AD%(A,7):MD%(0,12) = AD%(A,8)
 17990  PRINT : PRINT "READIED.": PRINT : GOTO 300
 18000  REM /// THROW COMMAND
 18010  GOSUB 4900: IF S$ = "ROPE" THEN 18100
 18020  FOR A = 1 TO NA: IF S$ <  > AN$(A) THEN  NEXT : PRINT : PRINT "YOU AREN'T CARRYING ";S$: GOTO 100
 18030  IF AD%(A,3) > 1 THEN 18200
 18050  IF AD%(A,4) <  >  - 1 THEN  PRINT : PRINT "YOU AREN'T CARRYING ";S$: GOTO 100
 18060  PRINT : PRINT S$;" THROWN.":AD%(A,4) = ROOM: IF NBTL THEN  PRINT " --A MISS.": GOTO 300
 18070  GOTO 300
 18100  IF AD%(6,4) <  >  - 1 THEN  PRINT : PRINT "YOU AREN'T CARRYING A ROPE.": GOTO 100
 18105 M = 0
 18110  IF ROOM > 5 THEN 18060
 18120  PRINT : PRINT "ROPE THROWN.": IF  RND (1) * 24 > MD%(0,2) THEN  PRINT "YOU MISSED THE ROCK OUT CROP.":M = 1
 18130  IF  RND (1) > .9 AND MD%(0,1) <  RND (1) * 48 THEN  PRINT : PRINT "YOU HAVE LOST YOUR GRIP ON THE ROPE": PRINT "SUPPORTING YOUR BODY, AND PLUNGED": PRINT "TO YOUR DEATH":DIE = 1: GOTO 2000
 18140  IF M THEN  GOTO 300
 18150  PRINT : PRINT "THE ROPE CATCHES THE ROCK.":UP = 1: GOTO 210
 18200  REM  /// THROW WEAPON
 18210  PRINT : PRINT "THROWING WEAPONS IS NOT ALLOWED. ": PRINT "YOU MIGHT HURT SOMEBODY!!": GOTO 300
 18900  GOTO 300
 19000  REM /// READ COMMAND
 19010  GOSUB 4900: IF (S$ = "DOOR" OR S$ = "WARNING") AND RO = 64 THEN  PRINT : PRINT  CHR$ (34);"BEFRUAU AUN VAUN MEDUSA NIC GEH"; CHR$ (34): GOTO 100
 19020  IF S$ = "BOOK" AND AD%(24,4) =  - 1 THEN  PRINT : PRINT "THE COVER OF THE BOOK SAYS:": INVERSE : PRINT "BOOK OF EVIL": NORMAL : PRINT "THE BOOK SEEMS TO BE SEALED SHUT.": GOTO 100
 19030  IF  LEFT$ (S$,5) = "HYROG" AND RO = 53 THEN  PRINT "YOU CAN'T READ THEM.": GOTO 100
 19900  GOTO 300
 20000  REM /// DIG COMMAND
 20020  IF AD%(5,4) <  >  - 1 THEN 20800
 20030  IF ROOM = 36 OR ROOM = 37 OR ROOM = 51 THEN 20100
 20040  IF ROOM = 68 OR ROOM = 71 THEN  PRINT : PRINT "YOU FOUND NOTHING.": GOTO 100
 20090  GOTO 20900
 20100  IF ROOM = 36 AND AD%(7,4) = 0 THEN  PRINT : PRINT "YOU FOUND A BOX BURIED IN THE GROUND.":AD%(7,4) = ROOM: GOTO 100
 20110  IF ROOM = 51 THEN 20200
 20120  IF ROOM = 37 AND AD%(8,4) = 0 THEN  PRINT : PRINT "YOU FOUND SOMETHING!":AD%(8,4) = ROOM: GOTO 100
 20130  IF ROOM = 71 AND AD%(25,4) = 0 THEN  PRINT "YOU FOUND SOMETHING.":AD%(25,4) = RO: GOTO 100
 20200  PRINT "WHICH WAY DO YOU WANT TO DIG?";: GET DR$: IF  LEN (DR$) < 2 THEN DR$ = DR$ + " "
 20210  FOR D = 1 TO 10 STEP 2: IF DR$ <  >  MID$ ("N S E W U D NENWSESW",D,2) THEN  NEXT : PRINT "INVALID DIRECTION.": GOTO 20200
 20220  IF D <  > 7 THEN  PRINT : PRINT "YOU ONLY GOT A COUPLE OF FEET AND": PRINT "STRUCK SOLID ROCK.": GOTO 100
 20230 DC = DC + 1: IF DC = 3 THEN  PRINT : PRINT "YOU HAVE BROKEN OUT TO DAYLIGHT!!": GOTO 100
 20240  PRINT : PRINT "YOU GOT A COUPLE OF FEET DUG OUT.": GOTO 100
 20800  PRINT : PRINT "THE GROUND IS TOO HARD TO DIG WITH": PRINT "YOUR FINGERS.": GOTO 300
 20900  PRINT : PRINT "THE WALLS AND FLOOR ARE SOLID ROCK.": GOTO 300
 21000  REM /// OPEN COMMAND
 21010  GOSUB 4900
 21020  IF S$ = "BOX" THEN 21100
 21030  IF S$ = "BOTTLE" THEN 21200
 21040  IF S$ = "BOOK" THEN 21300
 21050  IF  RIGHT$ (S$,4) = "DOOR" THEN 21400
 21090  PRINT : PRINT "I DON'T KNOW WHAT YOU MEAN.": GOTO 100
 21100  IF AD%(7,4) = ROOM OR AD%(7,4) =  - 1 THEN 21120
 21110  PRINT : PRINT "I SEE NO BOX HERE.": GOTO 100
 21120  IF AD%(9,4) = 0 THEN AD%(9,4) = ROOM: PRINT : PRINT "YOU HAVE FOUND SOMETHING INSIDE.": GOTO 100
 21130  GOTO 100
 21200  REM  >>> OPENING BOTTLE
 21210  IF MD%(36,5) <  = 0 AND MD%(36,13) = 0 AND AD%(70,4) =  - 1 THEN 21250
 21220  IF AD%(70,4) <  >  - 1 THEN  PRINT : PRINT "I DON'T KNOW WHAT YOU'RE TALKING ABOUT!": GOTO 100
 21230  PRINT "THE BOTTLE IS ALREADY OPEN.": GOTO 100
 21250  PRINT : PRINT "AS YOU OPEN THE BOTTLE, A PUFF OF SMOKE": PRINT "RISES FROM IT AND FORMS INTO A POWERFUL": PRINT "GENIE!":MD%(36,5) = RO: GOSUB 3600: GOTO 100
 21300  IF OB THEN  PRINT "O.K. THE BOOK IS OPEN.": GOTO 100
 21310  PRINT : PRINT "AS YOU OPEN THE BOOK, THE PICTURE OF": PRINT "A PACK OF MONSTERS GREETS YOU. - THE ": PRINT "MONSTERS IN THE PICTURE GROW UNTIL THEY": PRINT "REACH FULL SIZE AND THEN THEY STEP OUT": PRINT "OF THE BOOK!"
 21320 OB = 1:MD%(7,5) = RO: PRINT :MD%(13,5) = RO:MD%(35,5) = RO:MD%(12,5) = RO: GOSUB 3600: GOTO 100
 21400  REM  >>> OPEN DOOR
 21410  IF RO = 60 AND S$ = "DOOR" THEN 21450
 21420  IF RO = 64 THEN  PRINT "IT IS DARK IN THERE, BUT SOMETHING SEEMSTO BE MOVING.": GOTO 100
 21430  IF RO = 60 AND ( LEFT$ (S$,2) = "OA" OR  LEFT$ (S$,2) = "NO") THEN L3 = 1: GOTO 21485
 21440  IF RO = 60 AND ( LEFT$ (S$,2) = "HI" OR  LEFT$ (S$,2) = "EA") THEN L2 = 1: GOTO 21485
 21450  PRINT "EAST OR NORTH DOOR?";
 21460  GET A$: IF A$ <  > "E" AND A$ <  > "N" THEN 21460
 21470  PRINT A$: IF A$ = "E" THEN L2 = 1: GOTO 21485
 21480 L3 = 1
 21485  PRINT : PRINT "THE DOOR IS OPEN.": GOTO 100
 21490  GOTO 21090
 22000  REM  /// WEAR COMMAND
 22010  GOSUB 4900
 22020  IF S$ = "MAGIC RING" OR S$ = "RING" THEN 22100
 22030  PRINT : PRINT "ARE YOU KIDDING?": GOTO 210
 22100  IF AD%(3,4) <  >  - 1 THEN  PRINT : PRINT "YOU DON'T HAVE ANY ";S$;".": GOTO 100
 22110 MR% = 1: PRINT : PRINT "MAGIC RING DONNED.": PRINT : PRINT "YOU FEEL A PROTECTIVE FIELD": PRINT "ENVELOPE YOU.":MD%(0,8) = 100
 22120  GOTO 300
 22500  IF MR% THEN  PRINT : PRINT "MAGIC RING HAS BEEN REMOVED.":MD%(0,8) = AC:MM% = MM% - MR%:MR% = 0: GOTO 300
 30000  REM   // SAVE GAME
 30010  PRINT : PRINT "DO YOU WANT TO SAVE THIS GAME? ": PRINT "(Y/N) :";
 30020  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 30020
 30030  PRINT A$: IF A$ = "N" THEN 100
 30040  PRINT DK$;"CLOSE":X =  FRE (0)
 30050  PRINT DK$"BSAVE GAME.PTRS,A$69,L8"
 30060  PRINT DK$"BSAVE GAME.SVAR,A"; PEEK (105) +  PEEK (106) * 256;",L"; PEEK (109) +  PEEK (110) * 256 -  PEEK (105) -  PEEK (106) * 256 + 1
 30070  PRINT DK$"BSAVE GAME.STR,A"; PEEK (111) +  PEEK (112) * 256;",L"; PEEK (115) +  PEEK (116) * 256 -  PEEK (111) -  PEEK (112) * 256 + 1
 30080  PRINT : PRINT "THE GAME IS NOW SAVED. BOOT ON THIS": PRINT "  DISKETTE TO RESTART THE GAME."
 30090  END 
 31000  REM   /// RESTART
 31005  POKE 216,0
 31010  PRINT : PRINT "DO YOU WANT TO RESTART THIS GAME?": INPUT "  Y OR N:";A$: IF A$ = "N" THEN 1000
 31020  PRINT  CHR$ (4);"BLOAD GAME.PTRS": PRINT  CHR$ (4);"BLOAD GAME.SVAR": PRINT  CHR$ (4);"BLOAD GAME.STR"
 31030  PRINT DK$;"DELETE GAME.PTRS": PRINT DK$;"DELETE GAME.SVAR": PRINT DK$;"DELETE GAME.STR"
 31060  PRINT DK$;"OPEN EAMON.DESC,L256": PRINT DK$;"OPEN EAMON.ROOMS,L64": PRINT DK$;"OPEN EAMON.ROOM NAMES,L64"
 31080  GOTO 100

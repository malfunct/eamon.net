 1  REM TEXT ARTIFACT EDITOR
 2  REM   TA10
 3  REM  6/21/87
 4  REM 


 5  HOME : INPUT "INSERT DUNGEON DISK & HIT RETURN";A$
 7  POKE 768,104: POKE 769,168: POKE 770,104: POKE 771,166: POKE 772,223: POKE 773,154: POKE 774,72: POKE 775,152: POKE 776,72: POKE 777,96
 10 D$ =  CHR$ (4):DQ$ =  CHR$ (34)
 20  PRINT D$;"OPEN TEXT.ARTIFACTS,L170": PRINT D$;"OPEN TEXT.DESC,L40"
 50  PRINT D$
 100  HOME : VTAB 5
 105 I$ = "":I2$ = ""
 110  PRINT "INPUT YOUR CHOICE:"
 120  PRINT : PRINT : PRINT "1. ADD ARTIFACT": PRINT : PRINT "2. EDIT ARTIFACT": PRINT : PRINT "3. LIST": PRINT : PRINT "4. EXIT"
 130  GET A$: PRINT A$:A =  VAL (A$): IF (A < 1 OR A > 4) AND A$ <  >  CHR$ (3) THEN 100
 140  IF A$ =  CHR$ (3) THEN  END 
 150  ON A GOTO 1000,2000,4000,3000
 600  REM ///ROUTINE TO INPUT I$, HAVING BEEN PASSED I2$
 605 I$ = "":IS = 0
 610  PRINT I2$;: CALL  - 958: IF  LEN (I2$) THEN  FOR A = 1 TO  LEN (I2$): PRINT  CHR$ (8);: NEXT 
 615  GET A$: IF A$ =  CHR$ (13) THEN  CALL  - 958: PRINT : GOTO 700
 618  IF A$ =  CHR$ (17) AND LK THEN  POP : POP : GOTO 900
 620  IF (A$ >  CHR$ (31) OR A$ =  CHR$ (9) OR (A$ =  CHR$ (21) AND  NOT  LEN (I2$))) AND  LEN (I$) +  LEN (I2$) > MX THEN  PRINT  CHR$ (7);: GOTO 615
 622  IF A$ >  CHR$ (31) AND IS THEN  PRINT A$;:I$ = I$ + A$: GOTO 610
 625  IF A$ >  CHR$ (31) THEN  PRINT A$;:I$ = I$ + A$:I2$ =  MID$ (I2$,2): GOTO 615
 630  IF A$ =  CHR$ (5) THEN  PRINT I2$;:I$ = I$ + I2$:I2$ = "": GOTO 615
 635  IF A$ =  CHR$ (2) AND  LEN (I$) THEN  FOR A = 1 TO  LEN (I$): PRINT  CHR$ (8);: NEXT :I2$ = I$ + I2$:I$ = "": GOTO 615
 640  IF A$ =  CHR$ (4) THEN I2$ =  MID$ (I2$,2): GOTO 610
 642  IF A$ =  CHR$ (9) AND CI THEN IS =  NOT IS:H =  PEEK (36):V =  PEEK (37): HTAB 39: VTAB 1: INVERSE : PRINT  MID$ (" I",IS + 1,1): NORMAL : HTAB H + 1: VTAB V + 1: GOTO 615
 645  IF A$ =  CHR$ (9) THEN I2$ = " " + I2$: GOTO 610
 650  IF A$ =  CHR$ (21) AND  LEN (I2$) THEN A$ =  LEFT$ (I2$,1): GOTO 625
 655  IF A$ =  CHR$ (21) THEN A$ = " ": GOTO 625
 660  IF A$ =  CHR$ (8) AND  LEN (I$) > 1 THEN I2$ =  RIGHT$ (I$,1) + I2$:I$ =  LEFT$ (I$, LEN (I$) - 1): PRINT A$;: GOTO 615
 665  IF A$ =  CHR$ (8) AND  LEN (I$) THEN I2$ = I$ + I2$: PRINT A$;: GOTO 600
 670  IF A$ =  CHR$ (8) THEN 600
 675  IF A$ =  CHR$ (27) THEN  PRINT I2$:I$ = I$ + I2$: GOTO 700
 680  GOTO 615
 700  IF  LEN (I$) > MX + 1 THEN I$ =  LEFT$ (I$,MX + 1)
 710  RETURN 
 800  HOME : INPUT "ROOM NO. ";R$:R =  VAL (R$): IF R = 0 THEN  POP : GOTO 100
 810  ONERR  GOTO 850
 820  PRINT D$;"READ TEXT.ARTIFACTS,R";R
 830  INPUT NT: FOR I = 1 TO NT: INPUT TA$(I): NEXT 
 840  PRINT D$: POKE 216,0: RETURN 
 850  PRINT D$: CALL 768: POKE 216,0:NT = 0: PRINT D$;"OPEN TEXT.ARTIFACTS,L170": RETURN 
 900  PRINT D$;"WRITE TEXT.ARTIFACTS,R"R
 905  PRINT NT
 910  FOR I = 1 TO NT: PRINT TA$(I): NEXT 
 920  PRINT D$: GOTO 100
 1000  REM ADD ARTIFACT
 1010 ZZ = 1: GOSUB 800:FL = 1
 1020  HOME : IF NT THEN  FOR I = 1 TO NT: PRINT  SPC( 1 + (I < 10));I;". ";TA$(I): NEXT 
 1025  IF NT = 10 THEN  PRINT : PRINT "THE FILE FOR ROOM #";R;"IS FULL.  HIT  ANY KEY TO CONTINUE.": GET A$: PRINT A$: ON FL GOTO 100,900
 1030  PRINT : PRINT "INPUT ARTIFACT #";NT + 1;" (MAX LEN 14 CHAR)": PRINT :MX = 14:I$ = "": GOSUB 600: IF I$ = "" THEN  ON FL GOTO 100,900
 1040 NT = NT + 1:TA$(NT) = I$:FL = 2
 1050  PRINT : PRINT "INPUT ARTIFACT DESC. (MAX 37 CHAR)": PRINT :MX = 37:I$ = "": GOSUB 600
 1055  IF  LEFT$ (I$,1) <  > DQ$ THEN I$ = DQ$ + I$
 1060  PRINT D$;"WRITE TEXT.DESC,R"R * 10 + NT: PRINT I$
 1070  PRINT D$: GOTO 1020
 2000  REM EDIT ARTIFACT
 2010 ZZ = 2: GOSUB 800:FL = 1
 2020  HOME : FOR I = 1 TO NT: PRINT  SPC( 1 + (I < 10));I;". ";TA$(I): NEXT 
 2030  PRINT : INPUT "INPUT # OF ARTIFACT TO EDIT:";A$:AZ =  VAL (A$): IF AZ = 0 THEN  ON FL GOTO 100,900
 2040  PRINT : PRINT "INPUT ARTIFACT #";AZ;" (MAX LEN 14 CHAR)": PRINT :I2$ = TA$(AZ):I$ = "":MX = 14: GOSUB 600:TA$(AZ) = I$
 2045  PRINT D$;"READ TEXT.DESC,R"R * 10 + AZ: INPUT I2$: PRINT D$
 2050  PRINT : PRINT "INPUT ARTIFACT DESC. (MAX 37 CHAR)": PRINT :MX = 37:I$ = "": GOSUB 600
 2055  IF  LEFT$ (I$,1) <  > DQ$ THEN I$ = DQ$ + I$
 2060  PRINT D$;"WRITE TEXT.DESC,R"R * 10 + AZ
 2070  PRINT I$
 2080  PRINT D$:FL = 2
 2090  GOTO 2020
 3000  REM EXIT
 3010  PRINT D$;"CLOSE"
 3020  END 
 4000  REM LIST
 4005  REM PRINT:PRINT"NOT IMPLEMENTED":GETA$:PRINTA$:GOTO100
 4010  HOME : VTAB 5
 4020  PRINT "OUTPUT TO PRINTER?": INPUT P$
 4030  PRINT : PRINT "STARTING #: ";: INPUT BE$:BE =  VAL (BE$): IF BE$ = "" THEN BE = 1
 4040  PRINT : PRINT "ENDING #: ";: INPUT EN$:EN =  VAL (EN$): IF EN$ = "" THEN 4040
 4050  IF P$ = "Y" THEN  PRINT D$;"PR#1"
 4055  PRINT  TAB( 33)"TEXT ARTIFACTS"
 4060  FOR I = BE TO EN: PRINT : PRINT "ROOM #";I;":"
 4070  PRINT D$;"READ TEXT.ARTIFACTS, R";I: INPUT NT: FOR J = 1 TO NT: INPUT TA$(J): NEXT 
 4075  FOR J = 1 TO NT: PRINT D$;"READ TEXT.DESC,R";I * 10 + J
 4080  INPUT A$
 4090  PRINT D$
 4100  PRINT J;".  ";TA$(J),: IF P$ <  > "Y" THEN  PRINT : PRINT "     ";
 4105  PRINT A$: IF P$ <  > "Y" THEN  PRINT 
 4110  NEXT : NEXT 
 4120  PRINT D$;"PR#0"
 4130  PRINT : INPUT "HIT RETURN TO CONTINUE";A$
 4140  GOTO 100

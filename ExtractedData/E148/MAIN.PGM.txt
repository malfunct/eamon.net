 1  REM 

JOURNEY TO JOTUNHEIM
    - BY -
TOM ZUCHOWSKI

7625 HAWKHAVEN DR.
CLEMMONS,NC 27012
(910) 766-7490

 6  GOTO 40000: REM MP68 11/14/93
 40  IF  NOT LT THEN LF = 0: GOTO 63
 41  RETURN 
 45  PRINT D$"FRE": PRINT D$"READEAMON.DESC,R"R: INPUT A$: PRINT D$
 49 LL =  INT (( LEN (A$) + CL - 1) / CL):L = L + LL: GOSUB 58: PRINT A$:L = L + LL * (L = 0)
 51  PRINT 
 52 L = L + 1: GOTO 58
 53  PRINT :L = L + 2: GOTO 58
 54  PRINT 
 56 L = L + 3: PRINT 
 58  IF L < 23 THEN  RETURN 
 59  PRINT "      (PRESS ANY KEY TO CONTINUE)     ";: GET Q$: PRINT : VTAB  PEEK (37): FOR L = 1 TO 39: PRINT " ";: NEXT : PRINT : VTAB  PEEK (37):L = 0: RETURN 
 60 B$ = " WHO ": GOTO 63
 61 B$ = " WHO OR WHAT ": GOTO 63
 62 B$ = " WHAT "
 63  IF  NOT LF AND  NOT LT THEN  PRINT : PRINT "YOU CAN'T FIND ANYTHING.": POP : GOTO 305
 64  IF S$ = "" THEN  PRINT : PRINT C$(C);B$;: INPUT S$: IF S$ = "" THEN  POP : GOTO 510
 65  RETURN 
 70  PRINT : PRINT "NONE HERE BY THAT NAME!": GOTO 510
 100  GOTO 500: REM JUMP TO NEW 'YOU SEE' #
 135  PRINT A$;
 300  REM 

PICK FOE

 305  GOSUB 56
 310  IF  NOT T%(1) THEN 530
 320  GOSUB 51: FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 500
 340  IF M = 9 THEN  IF  NOT TB AND GG < 1 THEN  PRINT : PRINT "THJALFI STEPS BACK TO BETTER ENJOY YOUR SWORDPLAY.": GOSUB 56:TB = 1
 345  IF M = 9 THEN  NEXT 
 350 OF = M:AF = M%(M,14) * (M%(M,3) =  > 0) + (M%(M,3) =  - 6): ON AF + 1 GOTO 490,360,490,400
 360 DF = 0:AF = 3: IF M%(M,2) = 1 THEN M$(M) =  LEFT$ (M$(M), LEN (M$(M)) - 1): FOR I = 1 TO  INT ((M%(M,1) - M%(M,13) + 14) / 15)
 380  GOSUB 7500: IF M%(M,2) THEN  NEXT :M$(M) = M$(M) + "S"
 390  GOTO 490
 400  FOR M2 = 1 TO NM: IF M%(M2,5) = R1 AND M%(M2,14) = 1 THEN DF = M2:M2 = 999
 410  NEXT 
 430 AF = 1: GOSUB 7500
 490  IF  NOT T%(1) THEN M = 999
 495  NEXT : IF DI THEN 3100
 499  GOTO 530
 500  REM 

YOU SEE:

 510  GOSUB 56: GOTO 530
 520  GOSUB 51
 530  GOTO 900
 540  IF AG = 3 THEN VC = VC + 1: IF  NOT GG THEN  IF VC > 4 AND R1 <  > 9 THEN R = 217: GOSUB 45:GG = .5: FOR M = 7 TO 8: IF M%(M,5) = 9 THEN M%(M,5) = 0: NEXT 
 610 LT = LP OR R%(R1,7): IF  NOT LT THEN  PRINT "IT'S TOO DARK TO SEE.": GOSUB 53: GOTO 2010
 620  PRINT D$"READ EAMON.ROOM.NAME,R"R1: INPUT A$: PRINT D$: PRINT "YOU ARE "A$: GOSUB 56: IF R1 = 6 THEN R%(R1,0) = 1
 630  IF  NOT R%(R1,0) OR VB THEN R%(R1,0) = 1:R = R1: GOSUB 45
 640 RM = 0: FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 665
 645  IF M%(M,15) THEN  NEXT : GOTO 665
 647  IF M = 9 THEN  IF R1 > 14 THEN R = 279 + (R1 > 21): GOSUB 45:M%(M,15) = 1:TF = (R1 > 21): GOTO 655
 648  IF M = 20 THEN  IF P(4) > 1 THEN R = 296: GOSUB 45:M%(M,15) = 1:P(4) = 3: NEXT 
 650 M%(M,15) = 1:R = M + 300: GOSUB 45: IF M = 6 AND  NOT HD THEN R = 215: GOSUB 45:HD = 1: IF AG = 3 AND R1 <  > 9 THEN R = 281: GOSUB 45
 651  IF M = 9 THEN  IF R1 = 7 AND  NOT TI THEN R = 282: GOSUB 45:TI = 1
 653  IF M = 16 AND P(7) = 2 THEN  PRINT "SHE LOOKS OVER ";XE + 1;"00 YEARS OLD!": GOSUB 54
 654  IF M = 5 THEN  IF P(5) AND  NOT T(5) THEN  PRINT "HE IS HOLDING A DRINKING HORN.": GOSUB 54
 655  IF P(2) = 2 AND R1 = 17 THEN  PRINT "HE IS SOAKING WET!": GOSUB 54
 657  IF C <  > 8 AND (M <  > 9 OR R1 < 15) THEN  IF A%(20,4) AND  RND (1) > .7 THEN RM = M
 660  NEXT 
 665  IF RM THEN A$ = M$(RM): IF M%(RM,2) = 1 THEN A$ = "A " +  LEFT$ (A$, LEN (A$) - 1)
 670  IF RM THEN  PRINT : PRINT A$;" ADMIRES YOUR RED BOOTS!": GOSUB 56
 675  FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 710
 680  IF M = 1 THEN A =  INT ((M%(M,1) - M%(M,13) + 14) / 15): PRINT A;" ";: IF A = 1 THEN  PRINT "ROBBER IS HERE.": GOSUB 52: GOTO 700
 690  PRINT M$(M);" ";VB$(M%(M,2));" HERE.": GOSUB 52
 700  NEXT 
 710  GOSUB 51
 720 A2 = 0: FOR A = 1 TO NZ: IF A%(A,4) <  > R1 THEN  NEXT : GOTO 760
 730  IF  NOT A%(A,9) THEN A2 = A2 + 1:AZ%(A2) = A
 740  IF A%(A,9) THEN  PRINT "YOU SEE ";A$(A);".": GOSUB 52
 750  NEXT 
 760  FOR A = A TO N: IF A%(A,4) = R1 THEN  PRINT "YOUR ";A$(A) + " IS HERE.": GOSUB 52
 770  NEXT : GOSUB 51
 780  IF A2 = 0 THEN 810
 790  FOR A = 1 TO A2:A%(AZ%(A),9) = 1:R = AZ%(A) + 100: GOSUB 45
 800  NEXT 
 810  IF T%(1) THEN 2010
 820  IF OG = 1 OR GG = 1 OR GT = 1 OR DH = 1 OR SC = 1 THEN 900
 830  GOTO 2010
 900  IF AL = 1 THEN  IF R1 > 23 AND R1 < 28 THEN R =  INT ( RND (1) * 3) * 6 + 1: PRINT : PRINT "SOMEONE HOOTS, 'HEY, LOOKIT THE "; MID$ ("WEENIESHRIMPWIMP",R,6);"!": PRINT "THEY ALL RAISE THEIR WEAPONS!":L = L + 1: GOSUB 56:AL = 2
 910  IF T(8) > 1 THEN  IF R1 < 31 THEN R = 289 + (R1 > 22): GOSUB 45:M%(0,13) = M%(0,13) +  INT ( RND (1) * 10):DF = 0: ON (M%(0,1) > M%(0,13)) + 1 GOSUB 7700,7650
 990  ON R1 GOTO 2000,2000,1060,1080,1100,1120,1140,1160,1180,2000,2000,2000,2000,1280,1300,1320,1340,1360,1380,1400,1420,1440,1460,1480,1500,1520,1540,1560,1580,1600,2000,1640,1660,1680,1700
 1060  IF M%(2,5) <  > R1 THEN 2000
 1062 OM = OM + 1: IF OM = 2 THEN R = 205: GOSUB 45
 1065  IF PW THEN PW = 0: IF M%(2,5) = R1 THEN  PRINT : PRINT "HE SMILES, AND SAYS, 'AYE, YOU'LL DO.'": GOSUB 56:OP = 2
 1072  IF OM = 3 THEN R = 207: GOSUB 45
 1074  IF OM <  > 10 AND OP <  > 2 THEN 2000
 1076  IF OP = 1 THEN  PRINT : PRINT "THE MAN SNORTS WITH CONTEMPT, LOOKS UP, AND SPURS HIS ODD HORSE INTO MOTION."
 1078  IF OP = 2 THEN OP = 3:R = 204:M%(2,5) = 0: GOSUB 45
 1079  GOTO 2000
 1080  IF  NOT OW THEN R = 206: GOSUB 45:OW = 1: GOTO 2000
 1099  GOTO 2000
 1100  IF  NOT T%(1) THEN R = 209: GOSUB 45:DI = 1: FOR I = 1 TO 1000: NEXT : GOSUB 31000: GOTO 3100
 1119  GOTO 2000
 1120  GOTO 2000
 1140  IF VC > 30 AND R3 = 9 THEN VC = 34
 1159  GOTO 2000
 1160  IF  NOT PB THEN  IF  RND (1) > .5 THEN R = 244: GOSUB 45:PB = 1
 1179  GOTO 2000
 1180  IF AG <  > 3 THEN 2000
 1182  IF GG < 1 THEN GG = 1: GOTO 2000
 1185  IF GG = 1 THEN  FOR I = 4 TO 6:M%(I,5) = 9: NEXT :M%(7,5) = 0:M%(8,5) = 0:GG = 2: PRINT "SUDDENLY ALL FIGHTING IN THE HALL STOPS.":OG = 1: GOTO 2000
 1190  IF OG = 1 THEN OG = 2: IF  NOT EP OR  NOT DM THEN R = 218: GOSUB 45: GOTO 2010
 1195  IF OG < 3 AND EP AND DM THEN  FOR R = 222 TO 228: GOSUB 45: NEXT :A%(8,4) = 14:OG = 3:M%(5,5) = 15:M%(6,5) = 15:M%(9,5) = 15:M%(4,5) = 0
 1199  GOTO 2000
 1280  IF OG = 3 THEN R%(R1,2) = 15
 1299  GOTO 2000
 1300  IF GT = 1 THEN  FOR R = 230 TO 233: GOSUB 45: NEXT :R = 279: GOSUB 45:GT = 2:TB = 1: GOTO 305
 1310  IF  NOT GT THEN GT = 1:M%(5,14) = 3:M%(6,14) = 3:M%(6,5) = 15
 1319  GOTO 2000
 1320  IF  NOT P(1) AND  NOT T(1) THEN P(1) = 1
 1321  IF GV AND  NOT P(2) AND  NOT T(2) THEN P(2) = 1
 1325  IF SG < 4 OR GV THEN 1330
 1327 R = 236: GOSUB 45:GV = 1:R%(R1,1) = 20:A%(9,4) = 0:M%(10,5) = R1:M%(10,14) = 3
 1328  IF  NOT T(1) THEN R = 237: GOSUB 45: GOTO 500
 1329 R = 292: GOSUB 45
 1330  IF C = 7 THEN  IF  NOT WB AND ( NOT SG OR GV) AND  RND (1) > .3 THEN R = 245: GOSUB 45:WB = 1
 1339  GOTO 2000
 1340  IF  NOT P(2) THEN P(2) = 1
 1341  IF  NOT P(1) THEN P(1) = 1
 1342  IF T(1) THEN P(1) = 0
 1343  IF T(2) THEN P(2) = 0
 1350 SG = SG + 1: IF SG = 4 THEN  PRINT : GOSUB 53:R = 235: GOSUB 45:M%(10,5) = 16:M%(10,14) = 3
 1352  IF SG = 1 AND T(1) THEN R = 295: GOSUB 45: GOTO 2000
 1355  IF SG = 1 THEN R = 238: GOSUB 45
 1359  GOTO 2000
 1360  IF  NOT P(1) AND  NOT P(2) THEN P(1) = 1
 1379  GOTO 2000
 1380  IF  NOT P(1) AND  NOT P(2) THEN P(1) = 1
 1399  GOTO 2000
 1400  IF C = 7 THEN  IF  NOT WB AND  RND (1) > .3 THEN R = 245: GOSUB 45:WB = 1
 1419  GOTO 2000
 1420  IF C = 7 THEN  IF  NOT WB AND  RND (1) > .3 THEN R = 245: GOSUB 45:WB = 1
 1439  GOTO 2000
 1440  IF HK = 1 THEN  PRINT : PRINT "HEIMDALL DRAWS HIS GREAT SWORD, 'HEAD'.": GOSUB 56:HK = 2
 1445  IF  NOT HK THEN HK = 1
 1459  GOTO 2000
 1460  IF TA = 0 THEN R = 251: GOSUB 45:TA = 1
 1470  IF  NOT TF THEN M%(9,15) = 0:TF = 1
 1479  GOTO 2000
 1480  IF SC = 1 THEN  FOR R = 248 TO 250: GOSUB 45: NEXT :SC = 2
 1485  IF  NOT SC THEN SC = 1
 1499  GOTO 2000
 1500  IF EC = 1 THEN R = 252: GOSUB 45:R = 318: GOSUB 45: FOR R = 253 TO 254: GOSUB 45: NEXT :EC = 2:M%(18,5) = R1:M%(18,15) = 1: GOTO 310
 1505  IF EC AND  NOT P(3) THEN P(3) = 1
 1510  IF  NOT EC THEN EC = 1
 1519  GOTO 2000
 1520 M%(19,5) = 0: IF RC = 2 THEN RC = 3: GOTO 2000
 1522  IF RC = 3 AND P(4) THEN  FOR R = 257 TO 258: GOSUB 45: NEXT 
 1525  IF RC = 3 THEN RC = 4:M%(9,5) = R1: IF P(4) THEN 310
 1530  IF RC = 1 THEN RC = 2:R = 255: GOSUB 45:R = 320: GOSUB 45:R = 256: GOSUB 45:M%(20,5) = R1:M%(20,15) = 1:M%(9,5) = 0: GOTO 310
 1535  IF  NOT RC THEN RC = 1
 1536  IF  NOT P(4) THEN P(4) = 1: IF RC > 1 THEN 1520
 1539  GOTO 2000
 1540 M%(20,5) = 0: IF DH = 3 THEN DH = 4: IF P(5) THEN R = 267: GOSUB 45
 1542  IF DH = 2 THEN DH = 3: IF P(5) THEN  FOR R = 265 TO 266: GOSUB 45: NEXT 
 1544  IF DH = 1 THEN  FOR R = 262 TO 264: GOSUB 45: NEXT :DH = 2
 1546  IF  NOT DH THEN DH = 1
 1548  IF  NOT P(5) THEN P(5) = 1: IF DH > 2 THEN 1540
 1550  IF  NOT SE THEN 2000
 1552  IF SE = 2 AND M%(10,5) = 0 THEN SE = 3: IF A%(19,4) =  - 1 THEN  PRINT : PRINT "HEIMDALL REMARKS,  'WELL DONE!   RETURN MY SWORD NOW.'":L = L + 1: GOSUB 54: GOTO 2000
 1554  IF SE = 3 THEN SE = 4:R = 268: GOSUB 45
 1555  IF SE = 1 THEN M%(10,11) =  INT (M%(0,1) / 6):M%(10,3) = 0:M%(10,14) = 1: GOSUB 3600:R = 234: GOSUB 45: FOR I = NZ + 1 TO NA:SE = SE + (A%(I,7) * A%(I,8) > 25): NEXT : IF SE > 1 THEN SE = 2
 1557  IF SE = 1 THEN SE = 2:R = 294: GOSUB 45: INPUT "WILL YOU BORROW IT?";A$: IF  LEFT$ (A$,1) = "Y" THEN  PRINT : PRINT "HE GIVES IT TO YOU.":L = L + 2:A%(19,4) =  - 1:A = 19: GOTO 17100
 1559  GOTO 2000
 1560  IF CS = 3 THEN CS = 4: IF P(6) THEN R = 271: GOSUB 45
 1565  IF CS = 2 THEN CS = 3: IF P(6) THEN R = 270: GOSUB 45
 1570  IF CS = 1 THEN R = 269: GOSUB 45:CS = 2:M%(21,5) = R1:M%(10,5) = 0:M%(21,14) = 2:M%(21,15) = 1:R = 321: GOSUB 45
 1572  IF  NOT CS THEN CS = 1
 1575  IF  NOT P(6) THEN P(6) = 1: IF CS > 2 THEN 1560
 1579  GOTO 2000
 1580  IF WE = 4 THEN WE = 5:T(7) = 1: IF P(7) THEN R = 278: GOSUB 45:R = 293: GOSUB 45
 1585  IF WE = 3 THEN WE = 4:R = 277: GOSUB 45
 1590  IF WE = 2 THEN WE = 3:R = 276: GOSUB 45
 1595  IF WE = 1 THEN  FOR R = 274 TO 275: GOSUB 45: NEXT :WE = 2:A%(11,4) = 0
 1596  IF  NOT WE THEN WE = 1
 1597  IF  NOT P(7) THEN P(7) = 1
 1599  GOTO 2000
 1600 M%(15,14) = 2:M%(16,5) = 0: IF  NOT P(8) THEN P(8) = 1
 1602  IF P(8) = 2 THEN  IF  NOT NU THEN M%(17,5) = 0:M%(24,5) = R1:M%(24,15) = 1:A%(21,4) = 0:A%(22,4) = 530:NU = 1
 1604  IF P(8) = 3 THEN T%(1) = 0: IF NU = 1 THEN M%(24,5) = 0:M%(25,5) = R1:M%(25,15) = 1:A%(22,4) = 0:A%(23,4) = 530:NU = 2
 1610  IF T(8) = 2 THEN  FOR R = 287 TO 288: GOSUB 45: NEXT :T(8) = 3: FOR M = 7 TO NM::M%(M,5) = 0: NEXT 
 1615  IF T(8) = 1 THEN  FOR R = 283 TO 286: GOSUB 45: NEXT :T(8) = 2:A%(23,4) =  - 6:A = 23:M = 5: GOSUB 33400:M%(6,5) = 0
 1619  GOTO 2000
 1640  IF DG = 2 THEN  PRINT : PRINT "THEY GRAB YOU AND PULL YOUR LEGS OFF!":DI = 1: GOTO 31000
 1650  IF DG = 1 THEN  PRINT : PRINT "THE DRUNK GRABS YOUR SWORD ARM!":DG = 2:R%(R1,4) =  - 47: GOSUB 56
 1655  IF  NOT DG THEN DG = 1
 1659  GOTO 2000
 1660  IF WG = 2 THEN  PRINT : PRINT " THEY GRAB YOU,  AND THEN THEY TEAR YOU INTO BITE-SIZED PIECES!":DI = 1: GOTO 31000
 1670  IF WG = 1 THEN  PRINT : PRINT "THE WRESTLERS THROW YOU TO THE GROUND!":WG = 2:R%(R1,3) =  - 47: GOSUB 56
 1675  IF  NOT WG THEN WG = 1
 1679  GOTO 2000
 1680 R = 289: GOSUB 45
 1700  GOTO 2000
 2000  GOTO 540
 2005  REM 

CMD

 2010  INPUT "  YOUR COMMAND?";A$:L = 0:LF = 1: IF A$ =  CHR$ (6) THEN 60000
 2020  IF  LEFT$ (A$,1) = " " THEN A$ =  MID$ (A$,2): GOTO 2020
 2030  IF A$ = "" THEN A$ = CZ$: VTAB  PEEK (37): HTAB 16: PRINT A$
 2040  PRINT : IF A$ = "" THEN 2120
 2050 CZ$ = A$: FOR A = 2 TO  LEN (A$): IF  MID$ (A$,A,1) <  > " " THEN  NEXT 
 2060 V$ =  LEFT$ (A$,A - 1):S$ =  MID$ (A$,A + 1)
 2070  IF  LEFT$ (S$,1) = " " THEN S$ =  MID$ (S$,2): GOTO 2070
 2080  FOR A = 1 TO 10: IF  LEFT$ (C$(A), LEN (V$)) = V$ THEN C = A:A = 999: NEXT : GOTO 2140
 2090  NEXT :F = 0: FOR A = 7 TO NC: IF C$(A) = V$ THEN C = A: GOTO 2140
 2100  IF  LEFT$ (C$(A), LEN (V$)) = V$ THEN F = F + 1:C = A: IF F > 1 THEN A = 999: NEXT : GOTO 2120
 2110  NEXT : IF F THEN 2140
 2120  PRINT : PRINT "YOUR PARDON. I KNOW BUT THESE COMMANDS:": PRINT : FOR C = 1 TO NC / 2 + 1: PRINT C$(C); SPC( 20 -  LEN (C$(C)));C$(C + NC / 2 + 1): NEXT : PRINT : PRINT : GOTO 2010
 2130  IF C$(C) = S$ THEN S$ = V$:V$ = C$(C)
 2140  ON C < 7 GOTO 3000: ON C - 6 GOTO 6110,6000,7000,10000,13000,4000,5000,8000,9000,15000,23000,17000,19000,20000,21000,22000,22000,24000,14000,11000,28000,34000,34000
 3000  REM 

MOVE

 3010 R2 = 0: IF R1 = 5 THEN  PRINT : PRINT "THE ROBBERS BLOCK YOUR PATH!": GOTO 510
 3015  IF R1 > 6 THEN  IF M%(7,5) = R1 OR M%(8,5) = R1 THEN  PRINT : PRINT M$(7 + (M%(8,5) = R1));" BLOCKS YOUR WAY!": GOTO 305
 3020 B$ =  LEFT$ (V$,1): FOR D = 1 TO 6: IF B$ = C$(D) THEN R2 = R%(R1,D)
 3040  NEXT 
 3070  IF  NOT R2 THEN  PRINT : PRINT "YOU CAN'T GO THAT WAY.": GOTO 510
 3075  IF R2 = R1 - 1 THEN  IF  NOT T(8) AND  NOT TP AND R1 > 24 AND M%(9,5) = R1 THEN  PRINT : PRINT "THJALFI PALES, SEEING THAT YOU ARE ABOUT TO LEAVE THE SAFETY OF THOR'S SIDE.":R = 246:L = L + 3: GOSUB 45:TP = 1: GOTO 300
 3077  IF R1 = 22 THEN  IF T(8) AND R2 = 21 THEN R2 =  - 99
 3080  IF R1 = 3 THEN  IF M%(2,5) = R1 AND OP < 2 THEN OP = 1: PRINT : PRINT "THE MAN SAYS, 'HOLD, YOUNGLING! YOU MAY NOT LEAVE UNTIL YOU SHOW ME YOUR POWER!":L = L + 1: GOTO 305
 3081  IF R1 = 3 THEN  IF M%(2,5) = R1 AND OP > 1 THEN R = 204:M%(2,5) = 0: GOSUB 45
 3082  IF R2 =  - 96 THEN  PRINT : PRINT "NAY.   YOU MUST CONTINUE THIS ADVENTURE THAT YOU HAVE UNDERTAKEN.": GOSUB 56: GOTO 300
 3083  IF R2 =  - 97 THEN  PRINT : PRINT "ARE YOU CRAZY!?": GOTO 305
 3084  IF R2 =  - 15 THEN R = 221: GOSUB 45: GOTO 300
 3085  IF R2 =  - 17 THEN R2 = 17: IF GV THEN R2 = 20
 3086  IF R2 =  - 19 THEN R2 = 19: PRINT : INPUT "WHICH PASSAGE? (1-4): ";A$:A =  VAL (A$): IF A < 1 OR A > 4 THEN 300
 3088  IF R2 =  - 23 THEN R2 = 23: IF HK < 3 OR M%(5,5) <  > R1 THEN R2 =  - 23: PRINT : PRINT "THE DOOR IS CLOSED!": GOTO 305
 3090  IF R2 =  - 16 THEN R2 = 16: IF  NOT T(2) THEN  PRINT : PRINT "THOR WON'T VENTURE OUT INTO THE RAIN!":L = L + 1: GOTO 305
 3092  IF R2 >  - 34 AND R2 <  - 30 THEN R2 =  ABS (R2): IF M%(5,5) = R1 AND  NOT TC THEN R = 247: GOSUB 45:TC = 1: GOTO 310
 3094  IF R2 >  - 31 AND R2 <  - 25 THEN  GOTO 16000
 3096  IF R2 =  - 47 THEN  PRINT : PRINT "THE "; MID$ ("WRESTLERDRUNK",1 + (R1 = 32) * 8,8);" HOLDS YOU FAST!": GOTO 305
 3097  IF R2 =  - 98 AND T(8) > 1 THEN R2 = 34: IF T(8) = 2 THEN  FOR R = 287 TO 288: GOSUB 45: NEXT 
 3098  IF R2 =  - 98 AND T(8) < 2 THEN R = 247: GOSUB 45: GOTO 310
 3099  IF R2 =  - 99 THEN R = 291: GOSUB 45: PRINT : PRINT :L = 60: GOSUB 58: GOTO 31000
 3100  REM 
GO

 3102  IF A%(19,4) =  - 1 THEN  GOSUB 33500: PRINT : PRINT "HEIMDALL CRIES, 'A PURSE OF GOLD TO THE ONE WHO KILLS THE THIEF!'":L = L + 1: GOSUB 56
 3105 PW = 0: FOR I = 1 TO 10:P(I) = P(I) * (R1 > 15 AND R1 < 20): NEXT 
 3110 TB = GT:R3 = R1:R1 = R2: IF R2 < 31 THEN  GOSUB 3600
 3130 NL = R%(R1,0):LT = NL: IF A%(LS,4) =  - 1 OR A%(LS,4) = R1 THEN LT = NL OR LS
 3290  GOTO 500
 3600  REM 

ENEMY? SBR.

 3610 T%(1) = 0:T%(3) = M%(0,1):F%(1) = 0:F%(3) = M%(0,13)
 3615  IF R1 < 23 OR R3 <  > R1 + 1 THEN 3620
 3616  FOR M = 1 TO NM: IF M%(M,5) = R1 THEN M%(M,3) = 0:M%(M,14) = 1:A = 1:AL = 1: GOTO 3650
 3617  NEXT : RETURN 
 3620  FOR M = 1 TO NM
 3622  IF AL THEN  IF R1 > 23 AND M%(M,5) = R3 THEN M%(M,3) =  - 3:M%(M,14) = 2
 3625  IF  NOT DI AND R1 <  > R3 AND M%(M,5) = R3 THEN  IF M%(M,14) = 3 OR M%(M,14) = 1 AND 300 *  RND (1) < M%(M,1) THEN M%(M,5) = R1
 3630  IF M%(M,5) <  > R1 THEN  NEXT :DI = 0:AL = (M%(6,5) <  > R1): RETURN 
 3640  IF  NOT M%(M,14) THEN M%(M,14) = 3 - 2 *  NOT M%(M,3)
 3645 A = M%(M,14) * (M%(M,3) =  > 0) + (M%(M,3) =  - 6)
 3650 F%(A) = F%(A) + M%(M,13):T%(A) = T%(A) + M%(M,1)
 3660  NEXT :DI = 0:AL = (M%(6,5) <  > R1): RETURN 
 4000  REM 

GET

 4020 LF = 0: GOSUB 62: IF S$ = "ALL" THEN 4160
 4030 D = N: GOSUB 4804: PRINT : IF F > 500 OR  NOT F THEN 33010
 4040  IF A%(A,4) =  - 1 THEN  PRINT : PRINT "YOU'VE ALREADY GOT IT!": GOTO 305
 4060  IF A%(A,3) > 900 THEN  PRINT : PRINT "DON'T BE ABSURD.": GOTO 305
 4070  IF WT + A%(A,3) > 10 * M%(0,1) THEN  PRINT : PRINT "IT IS TOO HEAVY FOR YOU.": GOTO 305
 4073  IF A > 16 AND A%(A,4) <  > R1 THEN M =  - A%(A,4) - 1: GOTO 14090
 4075  IF A = 1 THEN R = 214 + GH: GOSUB 45:M%(6,5) = R1:GH = 5: IF  NOT A(3) THEN  GOSUB 26000
 4076  IF A = 1 THEN 520
 4080  PRINT "GOT IT.":A%(A,4) =  - 1:SZ = SZ + A%(A,0):WT = WT + A%(A,3): GOSUB 56: IF (A%(A,2) = 2 OR A%(A,2) = 3) AND M%(0,9) =  - 1 THEN 17100
 4110  GOTO 310
 4160  GOSUB 51:M = 0: FOR A = 1 TO N: IF A%(A,4) <  > R1 THEN  NEXT : GOTO 305
 4170  IF WT + A%(A,3) > 10 * M%(0,1) THEN  PRINT A$(A);" IS TOO HEAVY": GOSUB 52: GOTO 4185
 4180  PRINT A$(A) + " TAKEN.": GOSUB 52:A%(A,4) =  - 1:WT = WT + A%(A,3): IF  NOT A%(A,2) THEN M = 1
 4185  NEXT 
 4190  GOTO 310
 4600  REM 

ART. NAME SUB.

 4605  IF S$ = "" THEN  GOSUB 62
 4606 AR$ = " ":A$ =  LEFT$ (S$,1): IF A$ = "A" OR A$ = "E" OR A$ = "I" OR A$ = "O" OR A$ = "U" THEN AR$ = "N "
 4607  IF  RIGHT$ (S$,1) = "S" THEN AR$ = "NY "
 4610 A = 0:S =  LEN (S$): IF R1 = 9 THEN  IF S$ =  LEFT$ ("WOODEN PLATTERS",S) OR S$ =  LEFT$ ("PLATTERS",S) THEN S$ = "PLATTER OF PORK"
 4612  IF R1 = 9 THEN  IF S$ = "FLAGONS" THEN S$ = "FLAGON"
 4614  IF R1 = 8 THEN  IF  LEFT$ (S$,1) = "P" THEN S$ = "TREE"
 4616  IF R1 = 11 THEN  IF  LEFT$ (S$,2) = "PA" THEN S$ = "SERPENT"
 4620  IF R1 = 12 THEN  IF S$ =  LEFT$ ("GJALLERHORN",S) THEN S$ = "HORN"
 4622  IF R1 = 9 THEN  IF S$ =  LEFT$ ("TUNS",S) THEN S$ = "CASKS"
 4624  IF R1 = 8 THEN  IF S$ =  LEFT$ ("NIFFLEHEIM",S) THEN S$ = "JOTUN"
 4626  IF R1 = 8 THEN  IF S$ =  LEFT$ ("ASGARD",S) THEN  PRINT : PRINT "LOOK AROUND YOU, INSTEAD!": GOSUB 56:F = 0: POP : RETURN 
 4628  IF S$ =  LEFT$ ("PORK",S) THEN S$ = "PLATTER OF PORK"
 4630  IF S$ =  LEFT$ ("MEAD",S) THEN S$ = "FLAGON"
 4632  IF R1 = 30 THEN  IF S$ =  LEFT$ ("THIGHBONE",S) THEN S$ = "BONE":S = 4
 4634  IF R1 = 30 THEN  IF S$ =  LEFT$ ("BONE",S) AND A%(23,4) THEN S$ = "MJOLLNIR"
 4636  IF R1 = 25 THEN  IF S$ =  LEFT$ ("GLOW",S) THEN  PRINT : PRINT "YOU CAN'T FIND IT!":F = 0: POP : POP : GOTO 305
 4638  IF R1 = 3 THEN  IF S$ =  LEFT$ ("HAT",S) THEN S$ = "CLOAK"
 4640  IF R1 = 9 THEN  IF S$ =  LEFT$ ("THRALLS",S) THEN  PRINT : PRINT "THEY DO KEEP BUSY!": GOSUB 56:F = 0: POP : RETURN 
 4690  IF A > 0 THEN F = 200: POP 
 4695  RETURN 
 4700  REM 

MON SRCH SBR.

 4710 B =  LEN (S$):F = 0: FOR X = 1 TO NM: IF  LEFT$ (M$(X),B) = S$ THEN  GOSUB 4780
 4720  NEXT : IF F = 200 THEN  RETURN 
 4730  FOR X = 1 TO NM: IF  RIGHT$ (M$(X),B) = S$ THEN  GOSUB 4780
 4750  NEXT : RETURN 
 4780  IF M%(X,5) <  > R1 THEN  RETURN 
 4782 F = F + 1: IF F = 1 THEN M = X
 4784  IF S$ = M$(X) THEN M = X:X = 999:F = 200
 4786  RETURN 
 4800  REM 

ART SRCH

 4801 AR =  - 1
 4802 RB =  - 99:G1 =  - 1:G2 =  - 1:HA =  - 1:WH =  - 1:EM =  - 1:D = N: GOSUB 4810: IF F > 500 OR  NOT F THEN  PRINT : PRINT "YOU AREN'T CARRYING A";AR$;S$: GOSUB 56: POP : GOTO 520
 4803  RETURN 
 4804 HA =  - 1
 4805 RB =  - 99:G1 =  - 3 - (3 * (R1 > 6)):G2 =  - 7:WH = R1:EM = R1 + 200:AR = R1 + 500
 4810  GOSUB 4600
 4812 F2 = 0:FO = 0:B =  LEN (S$):F = 0: FOR X = 1 TO D: IF  LEFT$ (A$(X),B) = S$ THEN  GOSUB 4880
 4820  NEXT : IF F = 200 THEN  RETURN 
 4830  FOR X = 1 TO D: IF  RIGHT$ (A$(X),B) = S$ THEN  GOSUB 4880
 4840  NEXT : IF F = 200 THEN  RETURN 
 4850 FO = F: FOR I = 1 TO NT%(R1): IF  LEFT$ (TA$(R1,I),B) = S$ OR  RIGHT$ (TA$(R1,I),B) = S$ THEN F = F + 500 + I: IF TA$(R1,I) = S$ THEN F = 500 + I:I = 99:FO = 0
 4855  NEXT : IF FO AND F <  > FO AND I < 100 THEN F = 2
 4860  IF F > 1 AND F < 500 OR F > 1000 THEN  PRINT : PRINT "WHICH ";S$;" DO YOU WANT?": GOSUB 56:F = 0:F2 = 1
 4870  RETURN 
 4880 T = A%(X,4): IF T = RB OR T = G1 OR T = G2 OR T = WH OR T = HA OR T = EM OR T = AR THEN F = F + 1:A = X: IF S$ = A$(X) THEN X = 999:F = 200
 4885  RETURN 
 5000  REM 

DROP

 5010  GOSUB 62: IF S$ = "ALL" THEN 5100
 5030  GOSUB 4801: IF A = 20 THEN 9080
 5040 WT = WT - A%(A,3):A%(A,4) = R1: PRINT : PRINT A$(A);" DROPPED.": GOSUB 56
 5050  IF A = M%(0,9) THEN M%(0,9) =  - 1
 5070  GOTO 310
 5100  FOR A = 1 TO N: IF A%(A,4) =  - 1 THEN  PRINT A$(A);" DROPPED.":A%(A,4) = R1: GOSUB 52
 5110  NEXT :WT = 0:M%(0,9) =  - 1: GOTO 310
 6000  REM 

LOOK

 6010  GOSUB 40: IF S$ = "" THEN 6110
 6020 D = NZ: GOSUB 4804: IF  NOT F THEN 6040
 6021  IF A = 23 THEN P(8) = 4: PRINT : PRINT "SURT SAYS, 'HEY...'": GOSUB 54
 6022  IF F > 500 THEN  PRINT D$"FRE": PRINT D$"READTEXT.DESC,R"R1 * 10 + F - 500: INPUT A$: PRINT D$: GOSUB 49: GOTO 310
 6024  IF A%(A,4) = 200 + R1 THEN A%(A,4) = R1
 6030 A%(A,9) = 1:R = A + 100: GOSUB 45
 6031 X = 0: IF A = 6 THEN A(1) = 1:X = 1
 6032  IF A = 7 THEN A(2) = 1:X = 1
 6033  IF A = 1 THEN A(3) = 1:X = 1
 6034 AG = A(1) + A(2) + A(3): IF  NOT X THEN 305
 6035  IF AG = 1 THEN  PRINT : PRINT "YOU FEEL A STIRRING OF PURPOSE!"
 6036  IF AG = 2 THEN  PRINT : PRINT "YOUR PURPOSE GROWS STRONGER!"
 6037  IF AG = 3 THEN  PRINT : PRINT "YOUR PURPOSE IS COMPLETE! YOU WILL SOON BE CALLED!":L = L + 1
 6039  GOTO 305
 6040  GOSUB 4700: IF M = 21 THEN  IF F AND P(6) = 2 THEN M%(21,5) = 0:A%(11,4) = R1:P(6) = 3
 6042  IF M = 19 THEN  IF F AND P(3) > 1 THEN P(3) = 3
 6050  IF F THEN M%(M,15) = 0: GOTO 310
 6110  IF R1 <  > 27 OR S$ = "" THEN 6200
 6120  IF S$ <  >  LEFT$ ("HORN", LEN (S$)) AND S$ <  >  LEFT$ ("DRINKING HORN", LEN (S$)) THEN 6200
 6130  IF P(5) > 1 THEN P(5) = 3:R = 273: GOSUB 45: GOTO 305
 6140  PRINT : PRINT "YOU SEE NOTHING SPECIAL.": GOTO 305
 6200 R%(R1,0) =  NOT R%(R1,0): GOTO 310
 7000  REM 

ATTACK

 7100  IF M%(0,9) =  - 1 THEN  PRINT : PRINT "YOU HAVE NO WEAPON READY!": GOTO 510
 7110 LF = 0: GOSUB 61: GOSUB 4700: IF F THEN 7180
 7150 HA = R1:D = N: GOSUB 4805: IF F2 THEN  GOTO 310
 7155  IF A <  > 10 OR HK = 3 THEN  PRINT : PRINT "WHY WOULD YOU DO THAT?": GOTO 305
 7160  IF HK = 2 AND M%(6,5) = R1 THEN  FOR R = 241 TO 243: GOSUB 45: NEXT :HK = 3: GOTO 310
 7170  PRINT : PRINT "WHAM!!  YOU HIT THE ";A$(A);"!": GOTO 305
 7180  IF M > 6 THEN 7195
 7185  IF  NOT TW AND M > 2 AND M%(9,5) = R1 THEN R = 229: GOSUB 45:TW = 1: GOTO 310
 7190  IF TW = 1 AND M > 2 THEN  GOSUB 33500:TW = 2
 7195  IF M = 9 AND GJ = 1 THEN  GOSUB 33500
 7197  IF M = 9 THEN  IF R1 > 14 AND  NOT GJ THEN A$ = "THRALL!":GJ = 1: GOTO 33700
 7198  IF M = 10 THEN  IF SE = 1 OR SE = 2 THEN 7290
 7200  IF M > 9 THEN  IF  NOT TG AND R1 > 23 AND M%(9,5) = R1 THEN R = 246: GOSUB 45:TG = 1: GOTO 310
 7203  IF  NOT T%(1) THEN  IF M = 10 AND GW OR M = 15 AND GS THEN  GOSUB 33500
 7205  IF M = 10 AND  NOT GW THEN A$ = "GUIDE!":GW = 1: GOTO 33700
 7208  IF M = 15 AND  NOT GS THEN A$ = "HOST!":GS = 1: GOTO 33700
 7210  IF  NOT T%(1) THEN  IF TG = 1 AND M > 9 THEN M2 = M:M%(M,3) = 0:M%(M,14) = 1: GOSUB 3600:M = M2
 7220  IF M > 9 THEN M%(M,14) = 1:M2 = M: GOSUB 3600:M = M2
 7290 AF = 1:OF = 0:DF = M: IF M = 1 THEN M$(1) = "A ROBBER": GOSUB 7500:M$(1) = "ROBBERS"
 7292  IF M = 2 AND R1 = 3 THEN  FOR R = 201 TO 203: GOSUB 45: NEXT :A%(M%(0,9),4) = R1:M%(0,9) =  - 1:OM = 3: GOTO 500
 7295  IF DF > 1 THEN  GOSUB 7500
 7300  IF  NOT HIT THEN 7360
 7310  IF INC THEN W2 = A%(M%(0,9),6):WA%(W2) = WA%(W2) + 2
 7320  IF  - EA <  = AE THEN 300
 7330  IF  INT (100 *  RND (1) + 1) <  = M%(0,10) THEN 7360
 7340 A = 2: IF EA + AE =  - 1 THEN A = 1
 7350 AE = AE + A:M%(0,10) = M%(0,10) + A
 7360  IF M%(DF,14) = 1 OR DF = 0 OR M%(DF,3) < 0 THEN 300
 7370 M%(DF,3) = M%(DF,3) / 2: FOR M = 1 TO NM: IF M%(M,5) = R1 AND M%(M,14) > 1 THEN M%(M,14) = 0
 7380  NEXT :R3 = R1: GOSUB 3600: GOTO 310
 7400  REM 

P/U WEAP SBR.

 7410 A2 = 0: FOR A = 1 TO N: IF A%(A,4) = R1 THEN  IF A%(A,2) = 2 OR A%(A,2) = 3 THEN  IF A%(A,7) * A%(A,8) > A%(A2,7) * A%(A2,8) THEN A2 = A
 7425  NEXT 
 7430  IF A2 > 0 THEN  PRINT M$(OF);" PICKS UP ";A$(A2);".": GOSUB 52:A%(A2,4) =  - OF - 1:M = OF:A = A2: GOSUB 33400
 7440  RETURN 
 7500  REM 

DO BATTLE SBR.

 7505 HIT = 0: IF M%(OF,9) =  - 1 THEN 7400
 7508 A = A%(M%(OF,9),6): PRINT M$(OF);
 7510  PRINT " ";BV$(A, INT ( RND (1) * 3));" AT ";M$(DF): GOSUB 52:RL =  INT (100 *  RND (1) + 1): IF (RL < 5 OR RL < M%(OF,10) - M%(DF,7)) AND RL < 96 THEN HIT = 1: GOTO 7600
 7515  IF OF <  > 7 AND OF <  > 8 AND M%(OF,9) = 0 THEN RL = 96
 7520  IF RL < 97 THEN  PRINT " --";RB$(A, INT ( RND (1) * 2));"!": GOTO 53
 7525 A = M%(OF,9): PRINT " --";A$(A);" FUMBLED--";:FR =  INT (100 *  RND (1) + 1): IF FR <  = 50 THEN  PRINT "-BUT RECOVERED!": GOTO 53
 7527  IF FR > 75 OR OF <  > 0 AND OF <  > 7 AND OF <  > 8 THEN 7535
 7528  PRINT "AND DROPPED!": IF OF = 7 OR OF = 8 THEN 7850
 7530 A%(A,4) = R1:M%(OF,10) = M%(OF,10) - A%(A,5):M%(OF,9) =  - 1:M%(OF,4) = M%(OF,4) / 2
 7531  GOTO 53
 7535  IF FR > 95 THEN  PRINT "-";M$(OF);" STRUCK!": GOTO 7550
 7540  IF A > 0 THEN A%(A,7) = A%(A,7) - (A%(A,7) > 1): IF A%(A,7) = 1 THEN A%(A,8) = A%(A,8) - 1:M%(OF,11) = A%(A,7):M%(OF,12) = A%(A,8): IF A%(A,8) > 0 THEN  PRINT "-AND DAMAGED!"
 7541  IF A%(A,8) = 0 THEN  PRINT "-AND BROKEN!":L = L + 1: IF A > 0 THEN M%(OF,10) = M%(OF,10) - A%(A,5):M%(OF,9) =  - 1:M%(OF,4) = M%(OF,4) / 2
 7542  IF OF = 7 OR OF = 8 THEN 7850
 7543  IF  RND (1) > .5 THEN 53
 7545  PRINT : PRINT " --FUMBLED ";A$(A);" STRIKES ";M$(OF);"!": GOSUB 56
 7550 D = M%(OF,11):S = M%(OF,12):DT = DF:DF = OF:A = 1: IF FR = 100 THEN D = D * 2:A = 0
 7560  GOSUB 7635:DF = DT: RETURN 
 7600 INC = ( INT (100 *  RND (1) + 1) > M%(OF,10)):M%(OF,10) = M%(OF,10) + 2 * INC:D = M%(OF,11):S = M%(OF,12)
 7605 A = 1: PRINT  MID$ (" ** ++",(DF > 0) * 3 + 1,3);: IF RL > 5 THEN  PRINT "A HIT!": GOTO 7635
 7610  PRINT "WELL STRUCK!":R2 =  INT (100 *  RND (1) + 1): IF R2 < 51 THEN A = 0: GOTO 7635
 7615  IF R2 < 86 THEN S = 1.5 * S: GOTO 7635
 7620  IF R2 < 96 THEN D = 2 * D: GOTO 7635
 7625  IF R2 < 100 THEN D = 3 * D: GOTO 7635
 7630 D2 = M%(DF,1): GOTO 7638
 7635 D2 = 0: FOR D3 = 1 TO D:D2 = D2 +  INT (S *  RND (1) + 1): NEXT :D2 = D2 - A * M%(DF,8): IF D2 < 1 THEN  PRINT " --REBOUNDS OFF ARMOUR!": GOTO 56
 7638  IF DF = 1 THEN D1 = M%(1,1) - M%(1,13) -  INT ((M%(1,1) - M%(1,13)) / 15) * 15:D1 = D1 + 15 * (D1 = 0):D3 = D1 - D2: IF D2 > D1 THEN D2 = D1:D3 = 0
 7640 F%(AF) = F%(AF) + D2:M%(DF,13) = M%(DF,13) + D2: IF M%(DF,13) >  = M%(DF,1) AND DF <  > 1 THEN 7700
 7647  IF DF = 1 THEN  PRINT " --A ROBBER IS ";: ON (15 - D3) / 3 + 1 GOTO 7655,7660,7665,7670,7675: PRINT "DEAD!": GOSUB 56:M%(DF,5) = M%(DF,5) * (M%(DF,13) < M%(DF,1)): GOTO 7710
 7650 VB = M%(DF,2) * (DF > 0): PRINT " --";M$(DF);" ";VB$(VB);" ";: ON  INT (M%(DF,13) * 5 / M%(DF,1) + 2 - (M%(DF,13) = 0)) GOTO 7652,7655,7660,7665,7670,7675
 7652  PRINT "IN PERFECT HEALTH.": GOTO 56
 7655  PRINT "OK.": GOTO 56
 7660  PRINT "HURT.": GOTO 56
 7665  PRINT "BADLY HURT.": GOTO 56
 7670  PRINT "GRAVELY INJURED.": GOTO 56
 7675  PRINT "AT DEATH'S DOOR.": GOTO 56
 7700  PRINT " --";M$(DF);" ";VB$(VB);" DEAD!": GOSUB 56:F%(AF) = F%(AF) + M%(DF,1) - M%(DF,13):M%(DF,5) = 0
 7705  IF DF = 0 THEN DI = 1: GOTO 31000
 7710  IF F%(1) =  > T%(1) THEN T%(1) = 0
 7720  IF R1 < 15 THEN  IF DF = 7 OR DF = 8 OR DF = 9 THEN 7800
 7730  FOR AJ = 1 TO N: IF A%(AJ,4) =  - DF - 1 THEN A%(AJ,4) = R1
 7740  NEXT : IF M%(DF,9) > 0 THEN A%(M%(DF,9),4) = R1:M%(DF,10) = M%(DF,10) - A%(M%(DF,9),5):M%(DF,9) =  - 1:M%(DF,4) = M%(DF,4) / 2
 7790  RETURN 
 7800 R = 220: GOSUB 45
 7805 M%(DF,13) = 0: IF DF = 9 THEN  RETURN 
 7807 M%(DF,1) = 16 + 20 *  RND (1):M%(DF,15) = 0:VM = VM + 1: IF VM > 6 THEN  RETURN 
 7810 M$(DF) = VM$(VM):M%(DF,5) = 7 +  RND (1) * 3: IF M%(DF,5) = R1 OR M%(DF,5) = 8 THEN 7810
 7820  RETURN 
 7850  PRINT : PRINT "HE STOPS, SHOCKED, EYES ON HIS WEAPON.  'AWW......' HE STOMPS AWAY, SWEARING ANDKICKING AT THE  GROUND.":M%(OF,5) = 0:T%(1) = T%(1) - M%(OF,1):DF = OF: GOTO 7805
 8000  REM 

FLEE

 8004  IF T(8) > 1 AND R1 < 30 THEN R2 = (R1 - 1) * (R1 <  > 22) + (R1 = 22) *  - 99: GOTO 1099
 8005  IF R1 > 30 AND R1 < 34 THEN 8100
 8007  IF R1 = 30 AND T(8) > 1 THEN R2 =  - 98: GOTO 3097
 8008  IF R1 = 34 THEN R2 =  - 99: GOTO 3099
 8010  IF  NOT T%(1) THEN  PRINT : PRINT "THERE'S NOTHING TO FLEE FROM!": GOTO 305
 8020  IF M%(1,5) = R1 THEN 3000
 8030  IF S$ = "" THEN 8060
 8040  IF S$ <  > "" THEN  FOR D = 1 TO 6: IF S$ = C$(D) THEN R2 = R%(R1,D):D = 99
 8050  NEXT : GOTO 8070
 8060 R2 = R%(R1, INT (6 *  RND (1) + 1)): IF R2 < 1 OR R2 > 900 THEN 8060
 8070  GOTO 3100
 8100  FOR D = 1 TO 6: IF R%(R1,D) THEN R2 = R%(R1,D)
 8110  NEXT : GOTO 3096
 9000  REM 

GIVE

 9010  GOSUB 40: IF S$ = "" THEN 9040
 9020  FOR X = 1 TO  LEN (S$): IF  MID$ (S$,X,4) = " TO " AND X > 2 THEN B$ =  MID$ (S$,X + 4):A$ =  LEFT$ (S$,X - 1):X = 999: NEXT : GOTO 9050
 9030  NEXT 
 9040  GOSUB 62:A$ = S$: INPUT "TO WHOM?";B$: GOSUB 56: IF B$ = "" THEN 520
 9050 S$ = B$: GOSUB 4700: IF  NOT F THEN 70
 9060 S$ = A$: IF  VAL (A$) > 0 THEN 9500
 9070  GOSUB 4801
 9080  IF A = 20 THEN  PRINT : PRINT "DON'T BE ABSURD!   YOU CAN'T RUN AROUND IN THIS FROZEN COUNTRY WITH BARE FEET!":L = L + 2: GOTO 305
 9090  PRINT : PRINT "OKAY.": GOSUB 56: IF A = M%(0,9) THEN M%(0,9) =  - 1
 9110 A%(A,4) =  - M - 1: IF M%(M,10) <  > 0 AND M%(M,9) =  - 1 THEN  GOSUB 33400
 9120 R3 = R1: GOSUB 3600: GOTO 5050
 9500  IF  VAL (S$) > GO THEN  PRINT : PRINT "YOU AREN'T CARRYING THAT MUCH GOLD!": GOTO 305
 9540 GO =  INT (GO -  VAL (S$)):M%(M,0) = M%(M,0) +  INT ( VAL (S$)):M%(M,14) = 0: IF M%(M,3) < 100 THEN M%(M,3) = M%(M,3) * (1 +  VAL (S$) / 100)
 9550  GOTO 9120
 10000  REM 

INVEN

 10005  IF  NOT LT THEN LF = 0: GOSUB 63
 10010  IF S$ = "" THEN M = 0:X =  - 1:W =  - 99: GOTO 10110
 10020  GOSUB 4700: IF  NOT F THEN 70
 10030  IF M%(M,14) = 1 THEN  FOR M = M TO M: GOTO 15020
 10040 X =  - M - 1:W = X:D = 0
 10110  PRINT : PRINT M$(M);" IS CARRYING THE FOLLOWING:": GOSUB 56: FOR A = 1 TO N: IF A%(A,4) <  > X AND A%(A,4) <  > W THEN  NEXT : GOTO 10150
 10120 D = 1: PRINT " ";A$(A);: IF A = M%(M,9) THEN  PRINT " (READY WEAPON)";
 10130  IF A%(A,4) =  - 99 THEN  PRINT " (BEING WORN)";
 10132  IF A <  > 3 THEN 10140
 10133 A$ = "": IF DM = 1 THEN A$ = " (2/3 FULL)"
 10134  IF DM = 2 THEN A$ = " (1/3 FULL)"
 10135  PRINT A$;
 10140  GOSUB 51: NEXT 
 10150  IF  NOT M THEN  PRINT "  ";GO;" GOLD PIECES.": GOSUB 52
 10152  IF D AND M THEN  GOSUB 51
 10153  IF  NOT D THEN  PRINT : PRINT "NOTHING": GOSUB 56
 10155  IF  NOT M THEN  GOSUB 51:DF = M: GOSUB 7650
 10160  GOTO 310
 11000  REM  

TELL

 11010  GOSUB 40: IF S$ = "" THEN 11040
 11020  FOR X = 1 TO  LEN (S$): IF  MID$ (S$,X,7) = " ABOUT " AND X > 2 THEN B$ =  LEFT$ (S$,X - 1):A$ =  MID$ (S$,X + 7):X = 999: NEXT : GOTO 11050
 11030  NEXT 
 11040  GOSUB 60:B$ = S$: INPUT "ABOUT WHAT?";A$: GOSUB 56: IF A$ = "" THEN 520
 11050 S$ = B$: GOSUB 4700: IF  NOT F THEN 70
 11055 F = 0: FOR X = 1 TO  LEN (A$): IF  MID$ (A$,X,1) = " " THEN F = 1
 11057  NEXT : IF F THEN 11800
 11060  IF M <  > 5 AND M <  > 6 THEN 11900
 11070  IF R1 < 16 THEN 11900
 11080  IF R1 > 20 THEN 11200
 11100 T$(1) = "GLOVE":T$(2) = "SHELTER":T$(3) = "RAIN":T$(4) = "WET":T$(5) = "SOAKED":NT = 3 * (R1 > 16) + 2: GOSUB 11850
 11110  IF  NOT (F) OR P(1) <  > 2 AND P(2) <  > 2 THEN 11900
 11115  IF F < 3 AND T(1) THEN 11950
 11120  IF F < 3 THEN T(1) = 1:P(1) = 0: IF R1 > 16 THEN P(2) = 2
 11125  IF F > 2 AND T(2) THEN 11950
 11130  IF F > 2 THEN T(2) = 1:P(2) = 0
 11140  PRINT : PRINT M$(M);" LOOKS ABOUT WITH KEEN INTEREST.": PRINT "'AYE', HE ANSWERS, 'WELL DONE, LAD.'":L = L + 1: GOTO 305
 11150  GOTO 11900
 11200  ON R1 - 20 GOTO 11900,11900,11900,11900,11300,11400,11500,11600,11700,11750
 11210  GOTO 11900
 11300 T$(1) = "CONTEST":T$(2) = "FLAME":T$(3) = "FIRE":T$(4) = "LOGI":NT = 4: GOSUB 11850
 11305  IF T(3) = 1 THEN 11950
 11310  IF  NOT (F) OR P(3) <  > 3 THEN 11900
 11320  IF M = 6 THEN 11870
 11340  PRINT "SKRYMIR NOTES YOUR WHISPERED CONFERENCE": GOSUB 53: FOR R = 259 TO 260: GOSUB 45: NEXT :T(3) = 1: GOTO 310
 11400 T$(2) = "HUGI":T$(3) = "SHADOW":NT = 3: GOSUB 11850
 11410  IF T(4) = 1 THEN 11950
 11420  IF  NOT (F) OR P(4) <  > 3 THEN 11900
 11430  IF M = 6 THEN 11870
 11440  PRINT "SKRYMIR LOOKS AT YOU WITH IRRITATION": GOSUB 53:R = 261: GOSUB 45:T(4) = 1: GOTO 310
 11500 T$(2) = "HORN":T$(3) = "LAKE":NT = 3: GOSUB 11850
 11510  IF T(5) = 1 THEN 11950
 11520  IF  NOT (F) OR P(5) <  > 3 THEN 11900
 11530  IF M = 6 THEN 11870
 11540 T(5) = 1:M%(15,14) = 3:SE = 1: GOTO 310
 11600 T$(2) = "CAT":T$(3) = "SNAKE":T$(4) = "MIDGUARD":T$(5) = "SERPENT":T$(6) = "GREY":NT = 6: GOSUB 11850
 11610  IF T(6) = 1 THEN 11950
 11620  IF  NOT (F) OR P(6) <  > 3 THEN 11900
 11630  IF M = 6 THEN 11870
 11640 R = 272: GOSUB 45:T(6) = 1: GOTO 310
 11700 T$(2) = "ELLI":NT = 2: GOSUB 11850
 11710  IF T(7) = 1 THEN 11950
 11720  IF  NOT (F) THEN 11900
 11725  IF M = 6 AND P(7) <  > 2 THEN 11900
 11730  IF M = 6 THEN 11870
 11740  PRINT : PRINT "THOR IS BUSY!": GOTO 305
 11750 T$(1) = "MJOLLNIR":T$(2) = "HAMMER":T$(3) = "BONE":NT = 3: GOSUB 11850
 11752  IF  NOT (F) THEN 11900
 11755  IF P(8) = 2 OR P(8) = 3 THEN  PRINT : PRINT "HE SAYS, 'I DO NOT SEE IT...MAKE SURE!'": GOTO 305
 11760  IF M = 6 THEN 11870
 11770  IF P(8) = 4 THEN T(8) = 1:T%(1) = 0: GOTO 310
 11780  GOTO 11900
 11800  PRINT : PRINT M$(M);" GIVE";: IF M%(M,2) = 0 THEN  PRINT "S";
 11810  PRINT " YOU A BLANK LOOK.": PRINT : PRINT "(USE SINGLE-WORD OBJECTS)":L = L + 1: GOTO 305
 11850 F = 0: FOR I = 1 TO NT:S =  LEN (A$): IF A$ =  LEFT$ (T$(I),S) OR A$ =  RIGHT$ (T$(I),S) THEN F = I
 11860  NEXT : RETURN 
 11870  PRINT : PRINT "HEIMDALL MURMURS IN YOUR EAR, 'QUIETLY, LAD!  TELL THOR WHAT YOU CAN SEE.'":L = L + 1: GOTO 305
 11900  PRINT : PRINT M$(M);: IF M%(M,2) THEN  PRINT " IGNORE YOU.": GOTO 305
 11902  IF M = 9 THEN  PRINT " SHIVERS AND LOOKS AWAY.": GOTO 305
 11905  ON  RND (1) * 3 + 1 GOTO 11910,11920,11930
 11910  PRINT " GRUNTS.": GOTO 305
 11920  PRINT " LOOKS BORED.": GOTO 305
 11930  PRINT " YAWNS.": GOTO 305
 11950  PRINT : PRINT M$(M);" NODS.": GOTO 305
 13000  REM 

POWER

 13010 PW = 0: IF R1 > 6 THEN 13100
 13020 PW = 1: IF  NOT A%(20,4) THEN  PRINT : PRINT "YOUR BOOTS TURN BRIGHT, SHINY RED!":A%(20,4) =  - 99: GOTO 305
 13030 PW =  INT ( RND (1) * 2 + 1): ON PW GOTO 13040,13050
 13040  PRINT : PRINT "AN IMMENSE PEAL OF THUNDER SOUNDS IN THE DISTANCE!":L = L + 1: GOTO 305
 13050  PRINT : PRINT "YOU ARE DELUGED BY A RAIN SQUALL!": GOTO 305
 13100  IF R1 > 15 AND R1 < 20 OR R1 > 23 THEN 13200
 13110  PRINT : PRINT "NOTHING HAPPENS.": GOTO 305
 13120  IF M%(9,5) = R1 AND R1 > 7 AND R1 < 15 THEN R = 239: GOSUB 45
 13130  GOTO 310
 13200  PRINT "YOUR VISION CLEARS";: IF R1 = 30 AND P(8) = 1 OR R1 = 29 THEN  PRINT " A LITTLE";
 13205  PRINT "...": GOSUB 53: IF R1 = 30 THEN 13400
 13210  FOR I = 1 TO 10: IF P(I) THEN PW = I:I = 99:P(PW) = 2
 13220  NEXT : ON PW GOTO 13310,13320,13330,300,300,300,13340
 13230  PRINT "...BUT YOU SEE NOTHING UNUSUAL.": GOSUB 53: GOTO 310
 13310 R = 240: GOSUB 45
 13320  IF SG THEN  PRINT : PRINT "YOU SUDDENLY REALIZE THAT YOU ARE SOAKED TO THE SKIN!": GOSUB 56:P(2) = 2
 13325  GOTO 310
 13330  PRINT : PRINT "THERE IS A BRIGHT GLOW IN THE ROOM...":M%(18,5) = 0:M%(19,5) = R1:M%(19,15) = 1: GOTO 305
 13340 XE = XE + 1
 13400  IF P(8) = 2 THEN P(8) = 3: GOTO 300
 13410 P(8) = 2: GOTO 300
 14000  REM 

REQUEST

 14010  GOSUB 40: IF S$ = "" THEN 14040
 14020  FOR X = 1 TO  LEN (S$): IF  MID$ (S$,X,6) = " FROM " AND X > 2 THEN A$ =  LEFT$ (S$,X - 1):B$ =  MID$ (S$,X + 6):X = 999: NEXT : GOTO 14050
 14030  NEXT 
 14040  GOSUB 62:A$ = S$: INPUT "FROM WHOM?";B$: GOSUB 56: IF B$ = "" THEN 520
 14050 S$ = B$: GOSUB 4700: IF  NOT F THEN 70
 14060  IF M%(M,14) <  > 3 THEN  FOR M = M TO M: GOTO 15020
 14070 S$ = A$:HA =  - M - 1:WH = HA:EM = HA:D = N: GOSUB 4810: IF  NOT F THEN  PRINT : PRINT M$(M);" DOESN'T HAVE IT.": GOTO 510
 14080  IF A < 17 THEN  PRINT : PRINT "OKAY.": GOSUB 56:A%(A,4) =  - 1: GOTO 5050
 14090  PRINT : PRINT M$(M);" REFUSES!": GOSUB 56: IF M%(M,14) > 1 THEN  GOSUB 26000: GOTO 310
 15000  REM 

SMILE

 15005  IF R1 = 5 THEN  PRINT "THE ROBBERS JEER AT YOU.": GOSUB 52: GOTO 15040
 15010  GOSUB 40: GOSUB 51: FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 15040
 15020  PRINT M$(M);: ON M%(M,14) GOSUB 15050,15060,15080
 15030  GOSUB 52: NEXT 
 15040  GOSUB 51: IF SE = 2 AND  NOT SM THEN SM = 1: GOTO 100
 15045  GOTO 310
 15050  PRINT " GROWL";: IF  NOT M%(M,2) THEN  PRINT "S";
 15055  PRINT " AT YOU!": RETURN 
 15060  IF M > 10 AND M < 15 THEN  PRINT " IGNORE YOU.": RETURN 
 15065  IF M = 2 THEN  PRINT " STARES AT YOU.": RETURN 
 15070  ON M%(M,3) + 7 + ( RND (1) > .5) GOTO 15071,15072,15073,15074,15075,15076
 15071  PRINT " GRINS LIKE AN IDIOT": RETURN 
 15072  PRINT " STARES AT YOU.": RETURN 
 15073  PRINT " SMIRKS AT YOU.": RETURN 
 15074  PRINT " SNEERS AT YOU.": RETURN 
 15075  PRINT " GRUNTS.": RETURN 
 15076  PRINT " IGNORES YOU.": RETURN 
 15080  PRINT " SMILES BACK.": RETURN 
 16000  REM 

CONTESTS

 16020  ON  ABS (R2) - 25 GOTO 16100,16200,16300,16400,16500
 16100  IF T(3) = 1 THEN R2 = 26: GOTO 3100
 16110  PRINT : PRINT "THE GIANTS BLOCK YOUR PATH!": GOTO 305
 16200  IF T(4) = 1 THEN R2 = 27: GOTO 3100
 16210  PRINT : PRINT "THE TROLLS BLOCK YOUR PATH!": GOTO 305
 16300  IF T(5) = 1 AND M%(10,5) = 0 THEN R2 = 28: GOTO 3100
 16305  IF M%(10,5) THEN  PRINT : PRINT "SKRYMIR BLOCKS YOUR PATH!": GOTO 305
 16310  PRINT : PRINT "THE GIANTS BLOCK YOUR PATH!": GOTO 305
 16400  IF T(6) = 1 THEN R2 = 29: GOTO 3100
 16410  PRINT : PRINT "SURT BLOCKS YOUR PATH!": GOTO 305
 16500  IF T(7) = 1 THEN R2 = 30: GOTO 3100
 16510  IF WE < 2 THEN 16410
 16520  PRINT : PRINT "THE WRESTLING MATCH BLOCKS YOUR PATH!": GOTO 305
 17000  REM 

READY

 17020  GOSUB 62: GOSUB 4801
 17030  IF A%(A,2) < 2 OR A%(A,2) > 3 THEN  PRINT : PRINT A$(A);" ISN'T A WEAPON!": GOTO 305
 17100 M%(0,10) = (EA + AE) * ( - EA > AE) + A%(A,5) + WA%(A%(A,6)) + 2 * M%(0,2):M%(0,9) = A:M%(0,11) = A%(A,7):M%(0,12) = A%(A,8)
 17110  PRINT : PRINT "READIED."
 17115  IF SE = 2 THEN 100
 17120  GOTO 305
 19000  REM 

LIGHT

 19010 LF = 0: GOSUB 62:D = N: GOSUB 4804: PRINT : IF  NOT F THEN 33010
 19020  GOTO 33020
 20000  REM 

OPEN

 20010  GOSUB 40:D = N: GOSUB 4805: IF F THEN  IF A = 10 AND HK < 3 THEN  PRINT : PRINT "IT'S BARRED FROM WITHIN!": GOTO 305
 20020  IF A = 10 AND HK = 3 THEN  PRINT : PRINT "IT'S OPEN ALREADY!": GOTO 305
 20090  PRINT : PRINT "IT DOESN'T OPEN.": GOTO 305
 21000  REM 

PUT

 21010  PRINT : INPUT "PUT WHAT? ";A$: IF A$ = "" THEN 305
 21020  INPUT "INTO WHAT? ";B$: IF B$ = "" THEN 305
 21150  PRINT : PRINT "YOU CAN'T PUT ";A$;" INTO ";B$: GOTO 305
 22000  REM 

EAT & DRINK

 22010 AR =  - 1: IF R1 = 9 THEN AR = 500 + R1
 22015  GOSUB 4802: PRINT : IF  NOT F THEN 33000
 22020  IF A = 2 AND C = 22 OR A = 4 AND C = 23 THEN  PRINT "DON'T MAKE A PIG OF YOURSELF!": GOTO 305
 22030 F = 0: IF A = 3 AND C = 22 THEN F = 1:DM = DM + 1: IF DM = 3 THEN A%(A,4) = 0:A%(16,4) =  - 1:A%(16,9) = 1
 22040  IF A = 5 AND C = 23 THEN F = 1:EP = 1:A%(A,4) = 0
 22045  IF A = 16 THEN  PRINT : PRINT "IT'S EMPTY!": GOTO 305
 22050  IF F AND R1 < 15 THEN  PRINT "IT TASTES VERY GOOD, INDEED!": PRINT "YOU FEEL WONDERFUL!": GOSUB 54
 22055  IF F THEN  PRINT : PRINT "YOUR WOUNDS ARE ALL HEALED!":M%(0,13) = 0: GOTO 305
 22060  IF  NOT F THEN  PRINT "THAT'S NOT WHAT YOU DO WITH IT."
 22100  GOTO 305
 23000  REM 

READ

 23010  PRINT : PRINT "TRY EXAMINING IT.": GOTO 305
 24000  REM 

FREE

 24010 LF = 0: GOSUB 62:HA = R1:D = N: GOSUB 4805: IF  NOT F THEN 70
 24050  GOTO 310
 26000  PRINT "HE ADDS,  'I WILL PERMIT YOU TO EXAMINE IT, IF YOU WISH!'": GOSUB 54: RETURN 
 28000  REM 

JUMPSTART

 28005  IF R1 > 1 THEN  PRINT : PRINT "JMP ONLY WORKS IN THE FIRST ROOM!": GOTO 305
 28007  IF JP THEN  PRINT : PRINT "NO GUESSING!": GOTO 305
 28010 L = 0: PRINT : PRINT : PRINT "YOU MAY JUMP AHEAD TO UTGARD CASTLE"
 28040  PRINT : INPUT "DO YOU WISH TO JUMP AHEAD? ";A$: IF A$ <  > "Y" THEN  PRINT : PRINT : GOTO 310
 28080  PRINT : PRINT "HOW MANY N. PASSAGES IN THE ODD SHELTER?"
 28100  INPUT "YOUR ANSWER:";AN$: IF AN$ <  > "" THEN JP = 1: IF AN$ <  > "4" AND AN$ <  > "FOUR" THEN  PRINT : PRINT "WRONG ANSWER!": PRINT : GOTO 310
 28110  IF AN$ = "" THEN 310
 28800 A%(20,4) =  - 99:R1 = 22:M%(5,5) = R1:M%(6,5) = R1:M%(9,5) = R1:M%(10,5) = R1:M%(5,14) = 3:M%(6,14) = 3:M%(10,14) = 3:M%(5,15) = 1:M%(6,15) = 1:M%(9,15) = 1:M%(10,15) = 1: PRINT :GG = 2:OG = 2:R2 = R1:A%(3,4) =  - 1:DM = 1:A%(3,9) = 1: GOTO 3100
 31000  REM 

DONE

 31010  POKE  - 16368,0: PRINT : PRINT 
 31012  IF DI AND OF > 2 AND OF < 7 THEN DI = 2
 31015  IF DI THEN  PRINT : PRINT "YOU HAVE DIED!": IF DI = 1 AND R1 > 4 AND R1 < 15 THEN 31600
 31020  FOR I = 1 TO 2000: NEXT : PRINT D$"CLOSE": PRINT D$"CHAINEND.PROG"
 31599  REM 

NOT YET!
 31600 L = 0: FOR I = 1 TO 23: FOR J = 6 * I TO 140: POKE  - 16368,0: NEXT : PRINT : NEXT :R = 210: SPEED= 100: GOSUB 45: SPEED= 255: FOR I = 1 TO 2000: NEXT : IF R1 = 5 THEN  FOR R = 211 TO 213: GOSUB 45: NEXT 
 31605  IF R1 <  > 5 THEN R = 216: GOSUB 45
 31610 T%(1) = 0:M%(0,13) = 0:R2 = 7: RETURN 
 33000  REM 

MISC. SUBR.

 33010  PRINT : PRINT "YOU CAN'T ";C$(C);" THE ";S$: GOTO 510
 33020  PRINT : PRINT "YOU CAN'T ";C$(C);" THE ";A$(A): GOTO 305
 33400  IF (A%(A,2) = 2 OR A%(A,2) = 3) THEN M%(M,9) = A:M%(M,10) = M%(M,10) + A%(A,5):M%(M,4) = 2 + M%(M,4)
 33410  RETURN 
 33500 M2 = M: FOR I = 1 TO NM:M%(I,3) = 0:M%(I,14) = 1: NEXT : GOSUB 3600:M = M2: RETURN 
 33700  IF M%(6,5) = R1 THEN  PRINT : PRINT "THOR CUFFS YOU.  'NAY! DO NOT ATTACK OUR ";A$: GOTO 305
 33710  GOTO 7180
 34000  REM 

VERBOSE/BRIEF
 34010 VB =  NOT VB: GOTO 520
 40000  REM 

INIT
 40005  IF D$ <  >  CHR$ (4) THEN  PRINT  CHR$ (4)"-STARTUP"
 40010  PRINT D$"OPENEAMON.ROOM.NAME,L64": PRINT D$"OPENTEXT.DESC,L40": PRINT D$"OPENEAMON.DESC,L256":R1 = R1 + (R1 = 0):R2 = R1:L = 50: GOSUB 58: GOTO 3100
 60000  FOR I = 1 TO NT%(R1): PRINT TA$(R1,I),;: NEXT : PRINT : PRINT "EMB: ";: FOR A = 1 TO NZ: IF A%(A,4) = 200 + R1 OR A%(A,4) = 500 + R1 THEN  PRINT A$(A),;
 60010  NEXT : PRINT : PRINT : GOTO 2010

 1  REM THE WONDERFUL WORLD OF
 2  REM         EAMON
 3  REM  CHARACTER GENERATOR
 4  REM   AND INSTRUCTIONS
 5  REM    BY DONALD BROWN

 6  REM REV. 9 SEP 95

 10 D$ =  CHR$ (4): DEF  FN R(X) =  INT (X *  RND (1) + 1): DEF  FN A(X) =  FN R(X) +  FN R(X) +  FN R(X)
 40  PRINT D$"OPENTHE.ADVENTURER": PRINT D$"READTHE.ADVENTURER": INPUT NA$: PRINT D$"CLOSE"
 50  PRINT : PRINT "THE IRISHMAN SAYS, 'FIRST I MUST KNOW": PRINT "WHETHER YE BE MALE OR FEMALE. WHICH ARE": PRINT "YE  (HIT 'M' FOR MALE, 'F' FOR FEMALE)?";
 60  GET SE$: IF SE$ = "M" THEN 80
 70  IF SE$ <  > "F" THEN 60
 80  PRINT SE$: PRINT 
 90  PRINT : PRINT "THE IRISHMAN WALKS AWAY AND IN WALKS A": PRINT "TALL MAN OF POSSIBLY ELVISH DESCENT.": PRINT : PRINT "HE STUDIES YOU FOR A MOMENT AND SAYS,": PRINT "'HERE IS A BOOKLET OF INSTRUCTION FOR"
 100  PRINT "YOU TO READ, AND YOUR PRIME ATTRIBUTES": PRINT "ARE--": PRINT 
 110 HD =  FN A(8):AG =  FN A(8):CH =  FN A(8)
 120  PRINT "   HARDINESS--";HD: PRINT "   AGILITY  --";AG: PRINT "   CHARISMA --";CH;"'": PRINT 
 125  IF HD < 10 OR AG < 10 OR HD + AG + CH < 39 THEN  GOSUB 600
 130  PRINT "(HIT 'R' TO READ INSTRUCTIONS, 'T' TO      GIVE THEM BACK)";
 140  GET A$: IF A$ = "T" THEN 500
 150  IF A$ <  > "R" THEN 140
 200  PRINT : HOME : PRINT "YOU READ THE INSTRUCTIONS AND THEY SAY--": INVERSE : PRINT "  INFORMATION ABOUT THE WORLD OF EAMON  ": NORMAL 
 210  PRINT : PRINT "YOU WILL HAVE TO BUY A WEAPON. YOUR": PRINT "CHANCE TO HIT WITH IT WILL BE THE": PRINT "WEAPON COMPLEXITY, PLUS YOUR ABILITY": PRINT "IN THAT CLASS, PLUS TWICE YOUR AGILITY.": PRINT 
 220  PRINT "THE FIVE CLASSES OF WEAPONS (AND YOUR": PRINT "CURRENT ABILITIES WITH EACH) ARE--": PRINT "   CLUB/MACE       20%": PRINT "   SPEAR           10%": PRINT "   AXE              5%": PRINT "   SWORD            0%": PRINT "   BOW            -10%": PRINT 
 230  PRINT "EVERY TIME YOU SCORE A HIT IN BATTLE,": PRINT "YOUR ABILITY IN THE WEAPON CLASS MAY GO": PRINT "UP BY 2%, IF A RANDOM NUMBER FROM 1-100": PRINT "IS LESS THAN YOUR CHANCE TO MISS!": PRINT 
 240  PRINT "(HIT ANY KEY TO CONTINUE)";: GET A$: PRINT : HOME 
 250  PRINT "THERE ARE FOUR ARMOR TYPES, AND YOU MAY": PRINT "ALSO CARRY A SHIELD IF YOU DO NOT USE": PRINT "A TWO-HANDED WEAPON.  THESE PROTECTIONS": PRINT "WILL ABSORB HITS PLACED UPON YOU(ALMOST": PRINT "ALWAYS!) BUT THEY LOWER YOUR CHANCE TO"
 260  PRINT "HIT. THE PROTECTIONS ARE--": PRINT : PRINT "ARMOR"; TAB( 15);"HITS PROTECT  ODDS ADJUST":: PRINT " NONE"; TAB( 18);"0"; TAB( 35);"- 0%": PRINT " LEATHER"; TAB( 18);1; TAB( 35);"-10%": PRINT " CHAIN"; TAB( 18);2; TAB( 35);"-20%"
 270  PRINT " PLATE"; TAB( 18);5; TAB( 35);"-60%": PRINT " SHIELD"; TAB( 18);1; TAB( 35);"- 5%": PRINT : PRINT : PRINT "YOU WILL DEVELOP AN ARMOUR EXPERTISE,": PRINT "WHICH WILL GO UP WHEN YOU HIT A BLOW": PRINT "WEARING ARMOUR AND YOUR EXPERTISE IS": PRINT "LESS THAN THE ARMOUR YOU ARE WEARING."
 280  PRINT "NO MATTER HOW HIGH YOUR ARMOUR EXPERTISEIS, HOWEVER, THE NET EFFECT OF ARMOUR": PRINT "WILL NEVER INCREASE YOUR CHANCE TO HIT.": PRINT 
 290  PRINT "(HIT ANY KEY TO CONTINUE)   ";: GET A$: PRINT : HOME 
 300  PRINT : PRINT "YOU CAN CARRY WEIGHTS UP TO TEN TIMES": PRINT "YOUR HARDINESS, OR ";HD * 10;" GRONDS.": PRINT "(A MEASURE OF WEIGHT, ONE GROND=10 DOS)": PRINT 
 310  PRINT "ADDITIONALLY, YOUR HARDINESS TELLS HOW": PRINT "MANY POINTS OF DAMAGE YOU CAN SURVIVE.": PRINT "THEREFORE, YOU CAN BE HIT WITH ";HD: PRINT "1-POINT BLOWS BEFORE YOU DIE.": PRINT 
 320  PRINT "HOWEVER, YOU WILL NOT BE TOLD HOW MANY": PRINT "BLOWS YOU HAVE TAKEN.  YOU WILL BE": PRINT "MERELY TOLD THINGS SUCH AS": PRINT : PRINT "   'WOW!  THAT ONE HURT!'": PRINT "OR 'YOU DON'T FEEL VERY WELL'": PRINT 
 330  PRINT "(HIT ANY KEY TO CONTINUE)   ";: GET A$: PRINT : HOME 
 340  PRINT "YOUR CHARISMA (";CH;") AFFECTS HOW CITIZENS": PRINT "OF EAMON REACT TO YOU.  YOU AFFECT A": PRINT "MONSTER'S FRIENDLINESS RATING BY YOUR": PRINT "CHARISMA LESS TEN, DIFFERENCE TIMES TWO": PRINT "(";(CH - 10) * 2;"%)": PRINT 
 350  PRINT "YOU START OFF WITH 200 GOLD PIECES,": PRINT "WHICH YOU WILL WANT TO SPEND ON SUPPLIESFOR YOUR FIRST ADVENTURE.  YOU WILL GET": PRINT "A LOWER PRICE FOR ITEMS IF YOUR CHARISMAIS HIGH.": PRINT 
 360  PRINT "AFTER YOU BEGIN TO ACCUMULATE WEALTH,": PRINT "YOU MAY WANT TO PUT SOME OF YOUR MONEY": PRINT "INTO THE BANK, WHERE IT CANNOT BE": PRINT "STOLEN.  HOWEVER, IT IS A GOOD IDEA TO": PRINT "CARRY SOME GOLD WITH YOU FOR USE IN"
 370  PRINT "BARGAINING AND RANSOM SITUATIONS.": PRINT : PRINT "(HIT ANY KEY TO CONTINUE)  ";: GET A$: PRINT : HOME 
 380  PRINT "YOU MAY ALSO HIRE A WIZARD TO TEACH YOU": PRINT "SOME MAGIC SPELLS. THERE ARE FOUR": PRINT "SPELLS YOU MAY LEARN.": PRINT : PRINT "BLAST--HURT YOUR ENEMIES FROM A DISTANCEHEAL --REMOVE DAMAGE FROM YOUR BODY.": PRINT "SPEED--DOUBLE YOUR DEXTERITY FOR A TIME"
 390  PRINT "POWER--DOES SOMETHING WEIRD.  THE EXACT": PRINT "       EFFECT IS UNPREDICTABLE.": PRINT 
 400  PRINT : PRINT "OTHER TYPES OF MAGIC MAY WORK IN VARIOUSADVENTURES, AND ITEMS MAY HAVE SPECIAL": PRINT "PROPERTIES.  HOWEVER, THESE WILL NOT": PRINT "WORK IN OTHER ADVENTURES THAN WHERE THEYWERE FOUND.  THUS, IT IS BEST (AND YOU"
 410  PRINT "HAVE NO CHOICE BUT TO) SELL ALL ITEMS": PRINT "FOUND IN ADVENTURES, EXCEPT FOR WEAPONS": PRINT "AND ARMOUR.": PRINT 
 420  PRINT "(HIT ANY KEY TO CONTINUE)   ";: GET A$
 500  PRINT : HOME : PRINT "THE MAN BEHIND THE DESK TAKES BACK THE": PRINT "INSTRUCTIONS AND SAYS, 'IT IS NOW TIME": PRINT "FOR YOU TO START YOUR LIFE.'  HE MAKES": PRINT "AN ODD SIGN WITH HIS HAND AND SAYS,": PRINT "'LIVE LONG AND PROSPER.'": PRINT 
 510  PRINT "YOU NOW WANDER INTO THE MAIN HALL."
 520  PRINT D$"OPENCHAR.EXT,L256": PRINT D$"READCHAR.EXT,R0": INPUT NC: FOR R = 1 TO NC: PRINT D$"READCHAR.EXT,R"R: INPUT A$: IF A$ <  > "" THEN  NEXT : PRINT D$"WRITECHAR.EXT,R0": PRINT NC + 1:R = NC + 1
 530  PRINT D$"WRITECHAR.EXT,R"R: PRINT NA$
 540  PRINT HD: PRINT AG: PRINT CH: FOR X = 1 TO 4: PRINT "0": NEXT 
 550  PRINT "5": PRINT "-10": PRINT "20": PRINT "10": PRINT "0": PRINT "0": PRINT SE$: PRINT "200": PRINT "0": PRINT "0"
 560  FOR W = 1 TO 4: PRINT "NONE": PRINT "0": PRINT "0": PRINT "0,0": NEXT 
 570  PRINT D$"CLOSE"
 580  PRINT D$"OPENTHE.ADVENTURER": PRINT D$"WRITETHE.ADVENTURER": PRINT NA$: PRINT R: PRINT D$"CLOSE"
 590  PRINT D$"-MAIN.HALL"
 595  END 
 600  REM  /// SUICIDE ROUTINE 
 610  PRINT : PRINT "YOUR CHARACTER IS SUCH A POOR EXCUSE": PRINT : PRINT "FOR AN ADVENTURER THAT WE WILL ALLOW": PRINT : PRINT "YOU TO COMMIT SUICIDE. ENTER 'Y' TO": PRINT : PRINT "COMMIT SUICIDE, ELSE 'N'";
 620  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 620
 630  PRINT : PRINT : IF A$ = "N" THEN  RETURN 
 640  PRINT "WE RESSURRECT YOU AGAIN AND YOUR PRIME": PRINT : PRINT "ATTRIBUTES ARE --": PRINT : POP : GOTO 110

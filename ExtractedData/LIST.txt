 1  REM 

EAMON UNIVERSAL DUNGEON LIST
BY FRANK KUNZE 1994

 2  REM REV. 25 NOV 94/23 DEC 02

 5  GOTO 50000
 10 D$ =  CHR$ (4):CL$ = D$ + "CLOSE":O$ = D$ + "OPENEAMON.":W$ = D$ + "WRITEEAMON.":R$ = D$ + "READEAMON.": TEXT : HOME : VTAB 5: HTAB 11: PRINT "EAMON DUNGEON LIST": HTAB 15: PRINT "UNIVERSAL": VTAB 10
 15 B$ = D$ + "PREFIX": FOR X = 1 TO 7000: NEXT 
 25  ONERR  GOTO 40000
 26 A$ = D$ + "VERIFYEAMON.": PRINT A$"ROOMS": PRINT A$"ARTIFACTS": PRINT A$"MONSTERS": POKE 216,0: ONERR  GOTO 45
 35  PRINT O$"NAME": PRINT R$"NAME": INPUT AN$: INPUT ND: INPUT MP: PRINT CL$: POKE 216,0: IF MP < 7 THEN 50
 40 MP = 7:LD = 242: GOTO 55
 45  PRINT CL$: IF  PEEK (222) <  > 5 THEN  PRINT "ERROR " PEEK (222)" IN " PEEK (218) +  PEEK (219) * 256: PRINT D$"PREFIX/": END 
 47  POKE 216,0:MP = 5
 50 LD = 256:LA = 128:LM = 128:LR = 64:LN = 64: IF ND < 6 THEN ND = 6:MP = 5
 55  PRINT O$"DESC,L"LD: PRINT R$"DESC,R0": INPUT NR,NA,NE,NM: IF MP >  = 7 THEN  PRINT : INPUT LR,LM,LA
 60  PRINT D$: GOSUB 35000: GOSUB 300: GOTO 100
 75  PRINT  SPC( (CP -  LEN (AN$)) / 2)AN$: PRINT :L = L + 2: RETURN 
 80  ON  NOT PT GOTO 97: IF PT THEN  PRINT D$"PR#"PS: PRINT  CHR$ (9)"80N": RETURN 
 88 L = L + 1: GOTO 90
 89  PRINT :L = L + 1
 90  ON L < 20 + (35 * (PT > 0)) GOTO 97:L = 0: IF PT THEN  PRINT FF$: RETURN 
 92  POKE 49168,0: PRINT "HIT ANY KEY...";: GET A$: PRINT : PRINT : PRINT  CHR$ (12): HOME : IF A$ =  CHR$ (27) THEN  POP : GOTO 100
 97  RETURN 
 100  REM 

MAIN ROUTINE

 110 L = 0: HOME : PRINT  SPC( 11)"EAMON DUNGEON LIST": PRINT : PRINT  SPC( (40 -  LEN (AN$)) / 2);: INVERSE : PRINT AN$: NORMAL 
 120  PRINT : PRINT "SELECT ACTION DESIRED:": PRINT : PRINT "  1. LIST ROOMS": PRINT "  2. LIST ARTIFACTS": PRINT "  3. LIST EFFECTS"
 130  PRINT "  4. LIST MONSTERS": PRINT "  5. TOGGLE PRINTER (NOW " MID$ ("OFFON",PT * 3 + 1,3)")": PRINT "  6. TOGGLE DESC. (NOW " MID$ ("OFFON",DS * 3 + 1,3)")"
 140  PRINT "  7. SWITCH TO " MID$ ("4080",(CP = 40) * 2 + 1,2)" COLUMNS": PRINT "  8. QUIT"
 150  PRINT : INPUT "INPUT YOUR CHOICE (1-7):";CH: PRINT FF$: HOME : ON CH GOTO 1000,2000,3000,4000,5000,6000,7000,8000: GOTO 100
 300  REM 

LOAD DATA

 310  ON RM GOTO 97:RM = 1: PRINT O$"ROOMS,L"LR: IF MP >  = 7 THEN  FOR A = 1 TO NR: PRINT R$"ROOMS,R"A: INPUT R$(A): FOR A2 = 1 TO ND + 1: INPUT R%(A,A2): NEXT : PRINT D$: NEXT : GOTO 340
 320  FOR A = 1 TO NR: PRINT R$"ROOMS,R"A: FOR A2 = 1 TO ND + (MP >  = 6): INPUT R%(A,A2): NEXT 
 330  NEXT : PRINT D$: PRINT O$"ROOM.NAME,L"LN: FOR A = 1 TO NR: PRINT R$"ROOM.NAME,R"A: INPUT R$(A): NEXT : PRINT CL$"EAMON.ROOM.NAME"
 340  PRINT CL$"EAMON.ROOMS"
 370  PRINT O$"ARTIFACTS,L"LA: FOR A = 1 TO NA: PRINT R$"ARTIFACTS,R"A: INPUT A$(A): FOR A2 = 1 TO 4: INPUT A%(A,A2): NEXT : IF A%(A,2) > 1 OR MP >  = 6 THEN  FOR A2 = 5 TO 8: INPUT A%(A,A2): NEXT 
 380  NEXT : PRINT CL$"EAMON.ARTIFACTS"
 400  PRINT O$"MONSTERS,L"LM: FOR A = 1 TO NM: PRINT R$"MONSTERS,R"A: INPUT M$(A): FOR A2 = 1 TO MF: INPUT M%(A,A2): NEXT : NEXT : PRINT CL$"EAMON.MONSTERS": RETURN 
 1000  REM 

LIST ROOMS

 1010  INPUT "BEGINNING WITH # (1): ";BE$: IF BE$ = "" THEN BE$ = "1": VTAB  PEEK (37): HTAB 23: POKE HT,23: PRINT BE$
 1020  PRINT : PRINT "ENDING WITH # ("NR;: INPUT "): ";EN$: IF EN$ = "" THEN EN$ =  STR$ (NR): VTAB  PEEK (37): HTAB 21: POKE HT,21: PRINT EN$
 1030 BE =  VAL (BE$):EN =  VAL (EN$): IF BE > NR OR BE < 1 THEN BE = 1
 1040  IF EN > NR OR EN < BE THEN EN = NR
 1050  GOSUB 80: GOSUB 75: FOR R = BE TO EN: PRINT "ROOM #"R": "R$(R): GOSUB 88: IF DS THEN  PRINT R$"DESC,R"R: INPUT A$: PRINT D$: GOSUB 9000
 1060  FOR D = 1 TO ND:X = R%(R,D): PRINT "   "D$(D)": "; SPC( (X >  = 0) + ( ABS (X) < 10));X; SPC( 5)
 1070  IF X =  - 99 THEN  PRINT "[EXIT]";: GOTO 1110
 1080  IF MP < 7 THEN  IF MP >  = 6 THEN  IF X < 0 AND  ABS (X) <  = NR THEN  PRINT "["R$( ABS (X))"]";
 1090  IF MP >  = 6 THEN  IF X > DR THEN  IF NA >  = X - DR THEN  PRINT "["A$(X - DR)"]";
 1100  IF X > 0 AND X <  = NR THEN  PRINT "["R$(X)"]";
 1110  GOSUB 89: NEXT : IF MP >  = 6 THEN  PRINT "LIGHT: "R%(R,ND + 1): GOSUB 88
 1120  GOSUB 89: IF  NOT PT THEN L = 99: GOSUB 90
 1130  NEXT : PRINT FF$: PRINT D$"PR#0": GOTO 100
 2000  REM 

LIST ARTIF.

 2010  INPUT "BEGINNING WITH ARTIFACT (1): ";BE$: IF BE$ = "" THEN BE$ = "1": VTAB  PEEK (37): HTAB 30: POKE HT,30: PRINT BE$
 2020  PRINT : PRINT "ENDING WITH ARTIFACT ("NA;: INPUT "): ";EN$: IF EN$ = "" THEN EN$ =  STR$ (NA): VTAB  PEEK (37): HTAB 28: POKE HT,28: PRINT EN$
 2030 BE =  VAL (BE$):EN =  VAL (EN$): IF BE > NA OR BE < 1 THEN BE = 1
 2040  IF EN > NA OR EN < BE THEN EN = NA
 2050  GOSUB 80: GOSUB 75: FOR R = BE TO EN: PRINT "ARTIFACT #"R": "A$(R): GOSUB 88: IF DS THEN  PRINT R$"DESC,R"R + RL%(2): INPUT A$: PRINT D$: GOSUB 9000
 2070  PRINT "  VALUE" LEFT$ (PR$,15)A%(R,1): GOSUB 88: PRINT "  TYPE" LEFT$ (PR$,16)A%(R,2); SPC( 5 - (A%(R,2) > 9))"[";: IF A%(R,2) > AF THEN  PRINT "UNKNOWN]": GOSUB 88: GOTO 2090
 2080  PRINT TY$(A%(R,2))"]": GOSUB 88
 2090  PRINT "  WEIGHT" LEFT$ (PR$,14)A%(R,3): GOSUB 88: PRINT "  ROOM" LEFT$ (PR$,16)A%(R,4);:X =  LEN ( STR$ (A%(R,4))): PRINT  SPC( 5 - (X > 1) - (X > 2) - (X > 3));:A = A%(R,4): IF A > 0 AND A <  = NR THEN  PRINT "["R$(A)"]";
 2100  IF A < 0 AND A =  >  - NM - 1 THEN  PRINT "[CARRIED BY "M$( ABS (A + 1))"]";
 2110  IF A > 200 AND A <  = 200 + NR THEN  PRINT "[EMBEDDED IN ROOM "A - 200"]";
 2120 X = 100 + (400 * (MP >  = 7)): IF A > X AND A < NA + X THEN  PRINT "[INSIDE "A$(A - X)"]";
 2130  IF MP >  = 7 THEN  IF A =  - 999 THEN  PRINT "[WORN BY "M$(0)"]";
 2140  IF MP >  = 6 THEN  IF A > 300 AND A < 300 + NR THEN  PRINT "[HIDDEN IN ROOM "A - 300"]";
 2150  GOSUB 89:X = A%(R,2): FOR A = 5 TO 8: IF X > AF THEN  PRINT "  FIELD #";A; LEFT$ (PR$,12)A%(R,A);: GOTO 2900
 2160 M = A%(R,A): PRINT "  "F$(X,A); LEFT$ (PR$,20 -  LEN (F$(X,A)));M;:B =  LEN ( STR$ (M)): IF X = 2 OR X = 3 THEN  IF A = 5 THEN B = B + 1: PRINT "%";
 2165  PRINT  SPC( 5 - (B > 1) - (B > 2) - (B > 3));: IF X = 2 OR X = 3 THEN  IF A = 6 THEN  PRINT "["W$(M)"]";
 2170  IF X = 10 THEN  IF A = 5 OR A = 7 THEN  IF M > 0 AND M <  = NM THEN  PRINT "["M$(M)"]";
 2180  IF (X = 4 AND A = 5) OR (X = 8 AND A = 6) OR (X = 10 AND A = 6) OR (X = 12 AND A = 5) THEN  IF M <  = NA AND M > 0 THEN  PRINT "["A$(M)"]";
 2190  IF X = 8 THEN  IF A = 5 THEN  IF M > 0 AND M <  = NR THEN  PRINT "["R$(M)"]";
 2200  ON MP < 7 OR X <  > 11 GOTO 2250: IF A = 5 THEN  IF M >  = 0 AND M < 8 THEN  PRINT "["AR$(M)"]";
 2210  IF A = 5 THEN  IF M > 7 THEN  PRINT "[MAGIC/EXOTIC]";
 2220  IF A = 6 THEN  IF  NOT A%(R,5) THEN  IF M >  = 0 AND M < 6 THEN  PRINT "["CT$(M)"]";
 2250  REM ADD MORE SPECIAL STUFF HERE
 2900  GOSUB 89: NEXT : GOSUB 89: IF  NOT PT THEN L = 99: GOSUB 90
 2910  NEXT : PRINT FF$: PRINT D$"PR#0": GOTO 100
 3000  REM 

LIST EFFECTS

 3010  INPUT "BEGINNING WITH # (1): ";BE$: IF BE$ = "" THEN BE$ = "1": VTAB  PEEK (37): HTAB 23: POKE HT,23: PRINT 1
 3020  PRINT : PRINT "ENDING WITH # ("NE;: INPUT "): ";EN$: IF EN$ = "" THEN EN$ =  STR$ (NE): VTAB  PEEK (37): HTAB 21: POKE HT,21: PRINT NE
 3030 BE =  VAL (BE$):EN =  VAL (EN$): IF BE > NE OR BE < 1 THEN BE = 1
 3040  IF EN > NE OR EN < BE THEN EN = NE
 3050  GOSUB 80: GOSUB 75: FOR R = BE TO EN: PRINT "EFFECT #"R":": GOSUB 88: PRINT R$"DESC,R"R + RL%(3): INPUT A$: PRINT D$: GOSUB 9000: GOSUB 89
 3060  IF  NOT PT THEN L = 99: GOSUB 90
 3070  NEXT : PRINT FF$: PRINT D$"PR#0": GOTO 100
 4000  REM 

LIST MONSTERS

 4010  INPUT "BEGINNING WITH # (1): ";BE$: IF BE$ = "" THEN BE$ = "1": VTAB  PEEK (37): HTAB 23: POKE HT,23: PRINT 1
 4020  PRINT : PRINT "ENDING WITH # ("NM;: INPUT "): ";EN$: IF EN$ = "" THEN EN$ =  STR$ (NM): VTAB  PEEK (37): HTAB 21: POKE HT,21: PRINT NM
 4030 BE =  VAL (BE$):EN =  VAL (EN$): IF BE > NM OR BE < 1 THEN BE = 1
 4040  IF EN > NM OR EN < BE THEN EN = NM
 4050  GOSUB 80: GOSUB 75: FOR R = BE TO EN: PRINT "MONSTER #"R": ";: IF MP >  = 7 THEN  IF M%(R,3) > 1 THEN  PRINT M%(R,3)" ";
 4060  PRINT M$(R) MID$ (" S",(MP >  = 7 AND M%(R,3) > 1) * 1 + 1,1): GOSUB 88: IF DS THEN  PRINT R$"DESC,R"R + RL%(4): INPUT A$: PRINT D$: GOSUB 9000
 4070  FOR M = 1 TO MF:X = M%(R,M): IF M = 3 THEN  IF MP >  = 7 THEN 4905
 4075  PRINT " "MF$(M) MID$ (PR$,1,20 -  LEN (MF$(M)))X;:B =  LEN ( STR$ (X)): IF MP < 7 THEN  IF M = 3 OR M = 4 OR M = 7 OR M = 10 THEN B = B + 1: PRINT "%";
 4080  PRINT  SPC( 6 - (B > 1) - (B > 2) - (B > 3));: IF M = 11 AND MP >  = 7 THEN  IF X > 99 THEN  PRINT "["X - 100"%]";
 4090  IF M = 11 THEN  IF MP >  = 7 THEN  IF X > 0 AND X < 4 THEN  PRINT "["FR$(X)"]";
 4100  IF M = 5 THEN  IF X > 0 AND X <  = NR THEN  PRINT "["R$(X)"]";
 4110  ON (MP < 7 AND M <  > 9) OR (MP >  = 7 AND M <  > 8) GOTO 4140: IF X < 0 THEN  PRINT "[WEAPONLESS]";: GOTO 4900
 4120  IF  NOT X THEN  PRINT "[NATURAL WEAPONS]";
 4130  IF X <  = NA AND X > 0 THEN  PRINT "["A$(X)"]";
 4140  REM  ADD MORE HERE  
 4900  GOSUB 89
 4905  NEXT : GOSUB 89: IF  NOT PT THEN L = 99: GOSUB 90
 4910  NEXT : PRINT FF$: PRINT D$"PR#0": GOTO 100
 5000  REM 

TOGGLE PRINTER

 5010 PT =  NOT PT: IF  NOT PT THEN CP = CC: GOTO 100
 5020  PRINT "PRINTER SLOT? (DEFAULT=1):";: GET A$: IF A$ =  CHR$ (13) THEN A$ = "1"
 5030  PRINT A$:PS =  VAL (A$): ON PS < 1 OR PS > 7 GOTO 5000:CP = 80: GOTO 100
 6000  REM 

TOGGLE DESC.

 6010 DS =  NOT DS: GOTO 100
 7000  REM 

40/80 COL.

 7010  IF CP = 40 THEN CP = 80: PRINT D$"PR#3":CC = CP: GOTO 100
 7020 CP = 40:CC = CP: PRINT  CHR$ (26)"1": PRINT  CHR$ (21): GOTO 100
 8000  REM 

QUIT

 8010  PRINT CL$: PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$: PRINT D$"-STARTUP"
 9000  REM 

PRINT DESC

 9010  IF  NOT PT THEN  PRINT A$:L = L +  INT ( LEN (A$) / CP) + 1: RETURN 
 9020  FOR AZ = 1 TO  LEN (A$) STEP CP: PRINT  MID$ (A$,AZ,CP): GOSUB 88: NEXT : GOTO 89
 35000  REM 

SETUP DATA

 35010 FF$ =  CHR$ (12):DS = 1:PR$ = "....................":FR$(1) = "ENEMY":FR$(2) = "NEUTRAL":FR$(3) = "FRIEND":CP = 40:CC = CP:HT = 1403:DR = 500: IF MP < 7 THEN DR = 100
 35020  DIM A$(NA),A%(NA,8),M$(NM),M%(NM,12),R$(NR),R%(NR,ND + 1): READ AF: DIM TY$(AF),F$(AF,8): FOR A = 0 TO AF: READ TY$(A): FOR A2 = 5 TO 8: READ F$(A,A2): NEXT : NEXT :M$(0) = "THE ADVENTURER"
 35025  FOR A = 1 TO AF:F$(A,1) = "VALUE":F$(A,2) = "TYPE":F$(A,3) = "WEIGHT":F$(A,4) = "ROOM": NEXT 
 35030  IF MP < 7 THEN AF = 10: FOR A = 5 TO 7: READ F$(6,A): NEXT :F$(4,7) = "USER #7":F$(7,7) = "READ FLAG":F$(8,8) = "USER #8": IF MP < 6 THEN AF = 3
 35040  IF MP >  = 7 THEN  FOR A = 1 TO 3: READ A$: NEXT 
 35050 MF = 12: DIM MF$(MF): FOR A = 1 TO 12: READ MF$(A): NEXT : IF MP < 7 THEN  FOR A = 1 TO 5: READ A$: NEXT 
 35060  IF MP >  = 7 THEN  FOR A = 7 TO 11: READ MF$(A): NEXT :MF = 11:MF$(3) = "# MEMBERS"
 35070  DIM D$(10): FOR A = 1 TO 10: READ D$(A): NEXT 
 35080  DIM RL%(4): FOR A = 1 TO 4:RL%(A) = A * 100 - 100: NEXT : IF MP >  = 7 THEN  FOR A = 2 TO 4:RL%(A) = RL%(A) * 2: NEXT 
 35090  DIM W$(5): FOR A = 1 TO 5: READ W$(A): NEXT : DIM AR$(7),CT$(5): READ AR$(0),AR$(1): FOR A = 2 TO 6 STEP 2: READ AR$(A):AR$(A + 1) = AR$(A) + " & SHIELD": NEXT : FOR A = 0 TO 5: READ CT$(A): NEXT 
 35100  RETURN 
 36000  REM 

ART. DATA

 36010  DATA 12
 36020  DATA GOLD,USER #5,USER #6,USER #7,USER #8
 36030  DATA TREASURE,USER #5,USER #6,USER #7,USER #8
 36040  DATA WEAPON,ODDS,TYPE,DICE,SIDES
 36050  DATA MAGIC WEAPON,ODDS,TYPE,DICE,SIDES
 36060  DATA CONTAINER,KEY#,STRENGTH,OPEN/CLOSED,USER #8
 36070  DATA LIGHT,COUNTER,USER #6,USER #7,USER #8
 36080  DATA DRINKABLE,HEAL AMOUNT,# OF USES,OPEN/CLOSED,USER #8
 36090  DATA READABLE,1ST EFFECT,# OF EFFECTS,OPEN/CLOSED,USER #8
 36100  DATA DOOR/GATE,ROOM BEYOND,KEY,STRENGTH,HIDDEN?
 36110  DATA KEY,USER #5,USER #6,USER #7,USER #8
 36120  DATA BOUND MONSTER,MONSTER#,KEY#,GUARD#,USER #8
 36130  DATA WEARABLE,ARMOR CLASS,TYPE,USER #7,USER #8
 36140  DATA RE-USEABLE CONTAINER,KEY#,OPEN/CLOSED,#ITEMS INSIDE,#ITEMS CAN HOLD
 36150  REM 

6.0 DRINKABLE

 36160  DATA CHANCES,HEAL AMOUNT,# OF USES
 37000  REM 

MONSTER DATA

 37010  DATA HARDINESS,AGILITY,FRIENDLINESS,COURAGE,ROOM,WEIGHT,DEF. ODDS,ARMOR,WEAPON #,ODDS TO HIT,WEP. DICE,WEP. SIDES
 37040  REM 

MONSTER DATA 7 & >

 37050  DATA ARMOR,WEAPON #,WEP. DICE,WEP. SIDES,FRIEND?
 37060  REM 

DIRECTION NAMES

 37070  DATA NORTH    ,SOUTH    ,EAST     ,WEST     ,UP       ,DOWN     ,NORTHEAST,NORTHWEST,SOUTHEAST,SOUTHWEST
 37080  REM 

WEAPON TYPES

 37090  DATA AXE,BOW,CLUB,SPEAR,SWORD
 37100  REM 

ARMOR & CLOTHING

 37110  DATA CLOTHES,SHIELD,LEATHER,CHAIN,PLATE
 37120  DATA CLOTHING,OVERCLOTHES,SHOES,GLOVES,HEADWEAR,JEWELRY
 40000  REM 
TO ERR IS HUMAN

 40010  PRINT CL$: PRINT "ERROR CODE " PEEK (222)" IN LINE # " PEEK (218) +  PEEK (219) * 256: IF  PEEK (222) = 10 THEN  PRINT "CAN'T SAVE, FILE LOCKED.": END 
 40020  IF  PEEK (222) = 6 THEN  PRINT "NO ART/MON FILES FOR THIS ADVENTURE!": PRINT : PRINT "HIT ANY KEY...";: POKE 49168,0: GET A$: PRINT : PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$: PRINT D$"-STARTUP"
 50000  REM 
SELECT ADVENTURE

 50004  REM (SKIP AHEAD TO ADV DATA)
 50005  TEXT : HOME : PRINT "INITIALIZING...": FOR X = 1 TO 112: READ A$: NEXT 
 50010  READ NA: DIM AN$(NA,2): FOR X = 1 TO NA: READ AN$(X,1),AN$(X,2): NEXT 
 50020 D$ =  CHR$ (4):O$ = D$ + "OPEN":R$ = D$ + "READ":C$ = D$ + "CLOSE"
 50030  TEXT : HOME : PRINT D$"PR#3": HOME : PRINT  SPC( 19)"UNIVERSAL DUNGEON LIST!": PRINT "Please select an adventure, or hit <ESC> to exit."
 50040  VTAB 4: FOR X = 1 TO 17: PRINT AN$(X,1);: HTAB 40: PRINT AN$(X + 17,1): NEXT :L = 4:H = 1:Z = 1
 50050  VTAB L: HTAB H: INVERSE : PRINT AN$(Z,1): NORMAL : POKE 49168,0
 50060 A =  PEEK (49152): ON A < 128 GOTO 50060:A = A - 128: VTAB L: HTAB H: PRINT AN$(Z,1)
 50070  IF A = 8 THEN H = H - 39:Z = Z - 17: ON H = 1 GOTO 50050:H = 40:Z = Z + 34: GOTO 50050
 50080  IF A = 11 THEN L = L - 1:Z = Z - 1: ON L > 3 GOTO 50050:L = 20:Z = Z + 17: GOTO 50050
 50090  IF A = 21 THEN H = H + 39:Z = Z + 17: ON H = 40 GOTO 50050:H = 1:Z = Z - 34: GOTO 50050
 50100  IF A = 10 THEN L = L + 1:Z = Z + 1: ON L < 21 GOTO 50050:L = 4:Z = Z - 17: GOTO 50050
 50110  IF A = 27 THEN  PRINT C$: PRINT : PRINT D$"-STARTUP"
 50120  ON A <  > 13 GOTO 50050: HOME : PRINT  CHR$ (21): HOME 
 50130  POKE 49168,0: PRINT D$"PR#0": TEXT : HOME 
 50140  PRINT D$"PREFIX": INPUT P$: PRINT D$: PRINT D$"PREFIX" + P$ + AN$(Z,2)
 50150  PRINT C$: PRINT : RUN 10
 50210  DATA 34,THE BEGINNER'S CAVE,E001,LAIR OF THE MINOTAUR,E002
 50220  DATA THE CAVE OF THE MIND,E003,THE ZYPHUR RIVERVENTURE,E004,CASTLE OF DOOM,E005,THE DEATH STAR,E006,THE DEVIL'S TOMB,E007
 50230  DATA THE ABDUCTOR'S QUARTERS,E008,ASSAULT ON THE CLONE MASTER,E009,THE MAGIC KINGDOM,E010,THE TOMB OF MOLINAR,E011
 50240  DATA THE BLACK DEATH,E020,PRIESTS OF XIM!,E043,THE SEWERS OF CHICAGO,E060,THE HARPY CLOUD,E061
 50250  DATA DHARMAQUEST,E074,TEMPLE OF THE GUILD,E075,THE PRINCE'S TAVERN,E078,CASTLE OF COUNT FUEY,E079,SEARCH FOR THE KEY/RESCUE MISSION,E080
 50260  DATA CAMP EAMON,E106,ASSAULT ON DOLNI KEEP,E124,JOURNEY TO JOTUNHEIM,E148,WALLED CITY OF DARKNESS,E150
 50270  DATA PATHETIC HIDEOUT OF MR. R.,E157,THE LAIR OF MR. ED,E158,ATTACK OF THE KRETONS,E194
 50280  DATA  EYE OF AGAMON,E217,ADVENTURE IN INTERZONE,E225,BOOKWORM 3-D,E226,FIENDS OF EAMON,E237,THE HEART OF GOLD,E997
 50290  DATA REDEMPTION,E998,EDGAR'S ADVENTURES,E999

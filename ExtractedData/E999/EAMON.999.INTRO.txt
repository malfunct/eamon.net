 1  GOTO 31000: REM 

EDGAR'S ADVENTURES
BY FRANK KUNZE 8/94

 11  & A,A$: GOTO 16
 14  IF  LEFT$ (A$,1) = " " THEN A$ =  MID$ (A$,2): GOTO 14
 16  IF  LEN (A$) <  = CP THEN  PRINT A$: GOTO 52
 17  FOR K = CP TO 1 STEP  - 1: IF  MID$ (A$,K,1) = " " THEN  PRINT  LEFT$ (A$,K - 1):A$ =  RIGHT$ (A$, LEN (A$) - K): GOSUB 52: GOTO 14
 18  NEXT : PRINT "Word too long!": STOP 
 51  PRINT 
 52 L = L + 1: GOTO 58
 54 L = L + 1
 56 L = L + 2: PRINT 
 58  IF L < 23 THEN  RETURN 
 59  POKE 49168,0: PRINT "[MORE] ";: GET Z$: PRINT : ON CP = 40 GOSUB 1170: ON CP = 80 GOSUB 1190:L = 0: RETURN 
 100  REM 

INTRO

 105  PRINT  CHR$ (12): HOME :CP =  PEEK (231): GOSUB 1000:A$ = "EDGAR'S ADVENTURES": GOSUB 1020: GOSUB 1000
 110  PRINT : PRINT : ON CP = 40 GOSUB 1050: ON CP = 80 GOSUB 1110: FOR A = 1 TO 1000: NEXT 
 120  PRINT : PRINT : PRINT "     Press a Key ... ";: GET A$: PRINT : ON CP = 40 GOSUB 1170: IF CP = 80 THEN  GOSUB 1190
 130  PRINT  CHR$ (12): HOME :A$ = "You are about to become Edgar E. Blevins.  One night when Edgar was walking home from his job at a local convienence store he was surrounded by a strange blue light and thick green steam."
 140  GOSUB 11: GOSUB 51:A$ = "   Edgar wakes up to find he is aboard a strange alien spaceship and that several blue-skinned aliens are preparing to dissect him!"
 150  GOSUB 11: GOSUB 51:A$ = "   Your ultimate goal in Edgar's Adventures is to defeat the aliens and escape the spaceship.  What happens next you'll have to find out for yourself.": GOSUB 11
 160  PRINT : GOSUB 59: PRINT "Loading ......": PRINT D$"-MAIN.PGM"
 1000  REM 

INVERSE BAR

 1010  INVERSE : FOR A = 1 TO CP: PRINT " ";:X =  PEEK (49200): NEXT : NORMAL : RETURN 
 1020  REM 

CENTER STRING

 1030  INVERSE : FOR A = 1 TO CP / 2 -  LEN (A$) / 2 - 1: PRINT " ";:X =  PEEK (49200): NEXT : FOR A = 1 TO  LEN (A$): PRINT  MID$ (A$,A,1);:X =  PEEK (49200): NEXT 
 1040  FOR A = 1 TO CP - (CP / 2 -  LEN (A$) / 2) -  LEN (A$) + 1: PRINT " ";:X =  PEEK (49200): NEXT : NORMAL : RETURN 
 1050  REM 

NAME 40-COL

 1060  FOR A = 1 TO 14: HTAB A - 1: PRINT " ";: HTAB A
 1070  PRINT "+ B   r n   u z  +";
 1080  HTAB 23 - A:X =  PEEK (49200)
 1090  PRINT "   y F a k K n e  ";: FOR X = 1 TO 10: NEXT : NEXT 
 1100  HTAB 11: PRINT " + By Frank Kunze +  ": RETURN 
 1110  REM 

NAME 80-COL

 1120  FOR A = 1 TO 28: POKE 1403,A - 1: PRINT " ";: POKE 1403,A
 1130  PRINT "+ B   r n   u z  +      ";
 1140  POKE 1403,53 - A:X =  PEEK (49200)
 1150  PRINT "   y F a k K n e        ";: NEXT 
 1160  POKE 1403,27: PRINT "  + By Frank Kunze +   ": RETURN 
 1170  REM 

WIPE 40-COL

 1180  FOR I = 1 TO 20: POKE 32,20 - I: POKE 33,2 * I: HOME : NEXT : RETURN 
 1190  REM 

WIPE 80-COL

 1200  FOR I = 1 TO 40: POKE 32,40 - I: POKE 33,2 * I: PRINT  CHR$ (12): NEXT : RETURN 
 31000  REM 

INIT/MACHINE CHECK

 31005 D$ =  CHR$ (4): PRINT  CHR$ (21): PRINT D$"PR#0": PRINT D$"IN#0": TEXT : HOME : SPEED= 255: NORMAL : PRINT D$"-EDGAR.ML": POKE 49168,0
 31010  PRINT "80 COLUMN DISPLAY? (Y/N) >";: GET A$: PRINT : IF A$ = "Y" OR A$ = "y" THEN  PRINT D$"PR#3": ROT= 0: SCALE= 80: GOTO 100
 31020  ROT= 0: SCALE= 40: IF  PEEK (64435) = 6 THEN 100
 31030  HOME : PRINT "I SEE YOU ARE USING A II+": PRINT : PRINT "DO YOU HAVE A LOWER-CASE CHIP INSTALLED? (CAN YOU READ THIS: abcdefg?)": PRINT 
 31040  PRINT " (Y/N) >";: GET A$: PRINT : IF A$ <  > "Y" AND A$ <  > "y" THEN  ROT= 1: & P,:D$ =  CHR$ (4)
 31050  GOTO 100

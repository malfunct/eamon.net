 1  REM 


WALLED CITY OF DARKNESS

    - BY -

TOM ZUCHOWSKI
7625 HAWKHAVEN DR.
CLEMMONS, NC 27012
(919)766-7490


 5  REM MP51 12/6/89
 8  GOTO 1000: REM 

EAMON ADVENTURER'S GUILD
7625 HAWKHAVEN DR.
CLEMMONS, NC 27012

 45  GOSUB 51
 47  PRINT D$"FRE": PRINT D$"READ EAMON.DESC,R"R: INPUT A$: PRINT D$:L = L +  INT (( LEN (A$) + CL - 1) / CL): GOSUB 58: IF A$ <  > "BLANK" THEN  PRINT A$
 49  RETURN 
 51  PRINT 
 52 L = L + 1: GOTO 58
 56  PRINT 
 57 L = L + 2
 58  IF L < 21 THEN  RETURN 
 59  PRINT : PRINT "   (PRESS ANY KEY TO CONTINUE)  ";: GET K$:L = 0: PRINT : PRINT : ON K$ <  >  CHR$ (3) GOTO 49: POP : STOP 
 90  PRINT : PRINT "YOU CAN'T "C$(C)" "S$".": GOTO 99
 91  PRINT : PRINT "YOU AREN'T HOLDING IT.": GOTO 99
 93  PRINT : PRINT "THAT'S NOT WHAT YOU DO WITH IT.": GOTO 99
 94  PRINT : PRINT "YOU CAN'T "C$(C)" "S$" HERE.": GOTO 99
 95  PRINT : PRINT "NONE HERE BY THAT NAME!": GOTO 99
 98  PRINT : PRINT "IT'S TOO DARK TO SEE.": GOTO 56
 99  GOSUB 56
 100  REM 

MAIN

 110  GOSUB 51
 120  FOR I = 1 TO 10: IF RO = TP%(2,I) THEN TP = I:A%(7,4) = RO
 122  NEXT 
 123 LU = 0: IF A%(42,4) =  - 99 AND A%(43,4) =  - 99 AND A%(45,4) =  - 99 THEN LU = (M%(0,9) = 40) + (M%(0,9) = 50) * 2: IF LU THEN BT = 1 + (BT > 0)
 124 M%(0,1) = HD: IF LU = 2 THEN M%(0,1) = LH
 125  IF M%(28,5) <  > RO AND  NOT LT THEN  GOSUB 98: GOTO 210
 130  PRINT D$"READ EAMON.ROOM.NAME,R"RO: INPUT A$: PRINT D$: PRINT "YOU ARE ": PRINT " "A$: GOSUB 57: GOSUB 51
 140  IF  NOT V%(RO) THEN R = RO: GOSUB 47: GOSUB 51:V%(RO) = V%(RO) + 1
 142  IF RO = 93 THEN  PRINT "THE WALL COLORS ARE:": FOR I = 1 TO 4: PRINT "   "TP$(MZ%(MZ,I));: NEXT : PRINT :L = L + 3: GOSUB 51: IF MW THEN  PRINT "THE "TP$(MZ%(MZ,MW))" WALL GLOWS!": GOSUB 56
 145 MH = 0: FOR M = 1 TO NM: IF M%(M,5) = RO THEN MH = MH + 1:MH%(MH) = M: IF  NOT M%(M,15) THEN R = M + 300: GOSUB 47:M%(M,15) = 1: GOSUB 51: IF M = 20 THEN R = 250: GOSUB 45: GOSUB 51
 146  NEXT 
 150 AH = 0: FOR A = 1 TO NA: IF A%(A,4) <  > RO THEN  NEXT : GOTO 160
 151 AH = AH + 1:AH%(AH) = A: IF A%(A,9) = 1 THEN  NEXT : GOTO 160
 152 A%(A,9) = 1: IF A < NZ THEN R = A + 100: GOSUB 47
 157  GOSUB 51
 158  NEXT 
 159  GOSUB 51
 160  IF  NOT MH THEN 170
 161  FOR I = 1 TO MH:M = MH%(I)
 162  IF M%(M,6) < 900 THEN  PRINT M$(M)" IS HERE.": GOSUB 52
 165  IF M%(M,6) = 999 THEN  PRINT  INT ((M%(M,1) - M%(M,13) + 4) / 5)" "M$(M)" ARE HERE.": GOSUB 52
 168  NEXT 
 169  GOSUB 51
 170  IF  NOT AH THEN 200
 172  FOR A = 1 TO AH: PRINT "YOU SEE "A$(AH%(A))".": GOSUB 52: NEXT 
 200  GOTO 30000
 210  GOSUB 51: GOSUB 56: INPUT " YOUR COMMAND? ";A$:L = 0
 215  IF A$ =  CHR$ (6) THEN  GOSUB 50000: GOTO 210
 220  IF  LEFT$ (A$,1) = " " THEN A$ =  MID$ (A$,2): GOTO 220
 230  IF A$ = "" THEN A$ = CZ$: VTAB  PEEK (37): HTAB 16: PRINT A$
 235  IF A$ = "" THEN 275
 240 CZ$ = A$: FOR A = 2 TO  LEN (A$): IF  MID$ (A$,A,1) <  > " " THEN  NEXT 
 250 V$ =  LEFT$ (A$,A - 1):S$ =  MID$ (A$,A + 1)
 260  IF  LEFT$ (S$,1) = " " THEN S$ =  MID$ (S$,2): GOTO 260
 270 SL =  LEN (V$): FOR A = 1 TO ND: IF  LEFT$ (C$(A),SL) = V$ THEN C = A:A = 99: NEXT : GOTO 290
 272  NEXT :F = 0: FOR A = 7 TO NC
 273  IF  LEFT$ (C$(A),SL) = V$ THEN F = F + 1:C = A: IF C$(A) = V$ THEN A = 99: NEXT : GOTO 290
 274  NEXT 
 275  IF F <  > 1 THEN  PRINT : PRINT "THESE ARE THE VALID COMMANDS:": FOR C = 1 TO NC: PRINT C$(C) SPC( 20 -  LEN (C$(C)));: NEXT : GOTO 210
 290  ON C < 7 GOTO 3000: ON C - 6 GOTO 4000,5000,6000,6000,7000,8000,9000,10000,14000,14000,14000,14000,15000,16000,23000,17000,18000,19000,20000,21000,22000,24000
 295  ON C - 28 GOTO 31000,32000,33000,34000,35000,36000,37000,38000,29000,39000
 300  REM 

SELECT FOE

 310  IF  NOT NB OR TT THEN 500
 320  FOR M = 1 TO NM: IF M%(M,5) <  > RO THEN 490
 345  IF M%(M,14) = 2 THEN 490
 350  IF M%(M,14) = 3 THEN 390
 360 OF = M: IF TD%(3) = M%(0,1) THEN DF = 0: GOSUB 7500: GOTO 490
 370  FOR M2 = 1 TO NM: IF M%(M2,5) = RO AND M%(M2,14) = 3 AND  RND (1) < .25 THEN DF = M2: GOSUB 7500:M2 = 200: NEXT M2: GOTO 490
 380  NEXT M2:DF = 0: GOSUB 7500: GOTO 490
 390 OF = M
 400  FOR M2 = 1 TO NM: IF M%(M2,5) = RO AND M%(M2,14) = 1 THEN DF = M2: GOSUB 7500:M2 = 200: NEXT M2: GOTO 490
 410  NEXT M2
 490 NB = (FD%(1) < TD%(1)): IF  NOT NB THEN M = 999
 495  NEXT 
 500  REM 

SPECIAL STUFF

 510  IF TT THEN TT = TT - 1
 900  GOTO 100
 1000  REM 

INIT

 1010  IF D$ <  >  CHR$ (4) THEN  PRINT  CHR$ (4)"-STARTUP"
 1800  IF CL = 80 THEN  PRINT D$"PR#3"
 1900 L = 40: GOSUB 58: GOTO 29120
 2000  REM 

DONE

 2010  GOSUB 59
 2020  PRINT D$"CLOSE"
 2030  IF  NOT DI THEN  PRINT D$"-POSTLUDE"
 2500  ONERR  GOTO 2500
 2505  PRINT  CHR$ (26)"1": REM TURN OFF VIDEX 80-COL
 2506  PRINT  CHR$ (26); CHR$ (17): REM TURN OFF IIE 80-COL
 2507  PRINT D$"PR#0"
 2510  HOME : PRINT : PRINT D$"DELETE FRESH.MEAT": PRINT D$"STORE SAVED.GAME": PRINT D$"DELETE SAVED.GAME"
 2520  PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$: PRINT D$"OPEN THE.ADVENTURER": PRINT D$"CLOSE": PRINT D$"DELETE THE.ADVENTURER": PRINT D$"-MAIN.HALL"
 3000  REM 

MOVE

 3010  IF NB THEN  PRINT : PRINT "YOU CAN'T TURN YOUR BACK ON AN ENEMY!": GOSUB 56: GOTO 100
 3020 D = C
 3030 R2 = RD%(D)
 3032  IF RO = 81 THEN  IF D = 6 AND BT THEN R2 = 81:BT = 0
 3035  IF R2 > 0 THEN 3500
 3040  IF RO = 89 THEN  PRINT : PRINT "A SUPERNATURAL WIND FORCES YOU BACK!": GOSUB 57: GOTO 300
 3070  IF D = 5 AND  NOT R2 AND BT = 0 AND A%(45,4) =  - 99 THEN BT = 1:NB = 0: GOSUB 51: PRINT "YOU BOLDLY CLIMB INTO THE AIR!": PRINT "CLAPS OF THUNDER ACCOMPANY EVERY STEP!": GOSUB 57: GOTO 300
 3080  IF D = 6 THEN  IF  NOT R2 AND RO <  > 91 AND BT THEN BT = 0: PRINT : PRINT "YOU DESCEND TO THE GROUND.": GOSUB 57: GOSUB 3600: GOTO 300
 3100  IF  NOT R2 THEN 3250
 3120  IF BT THEN R2 =  ABS (R2)
 3130  IF D = 2 THEN  IF R2 >  - 89 AND R2 <  - 84 AND PR THEN R2 =  ABS (R2)
 3140  IF D = 1 THEN  IF R2 >  - 87 AND R2 <  - 83 AND A%(45,4) <  >  - 99 AND A%(45,4) <  >  - 1 THEN R2 =  ABS (R2)
 3150  IF R2 =  - 82 AND A%(45,4) =  - 99 THEN R2 = 82:BT = 1
 3160  IF R2 =  - 96 AND LU < 2 THEN  PRINT : PRINT "YOU  GET  A  VERY UNCOMFORTABLE FEELING THAT YOU ARE YET UNREADY FOR THIS STEP.":L = L + 3: GOSUB 58
 3170  IF R2 =  - 96 AND A%(45,4) =  - 99 THEN R2 = 96:BT = 1
 3240  IF R2 > 0 THEN 3500
 3250  IF R2 < 0 THEN  IF RO > 84 AND RO < 88 THEN  IF M%(RO - 62,5) = RO THEN M%(RO - 62,15) = 0: PRINT : PRINT "THE GUARDS BLOCK YOUR WAY!":L = L + 2: GOTO 300
 3260  IF D = 5 AND BT THEN  PRINT : PRINT "YOU SINK BACK TO YOUR PREVIOUS HEIGHT.": GOSUB 57: GOTO 100
 3270  PRINT : PRINT "YOU CAN'T GO THAT WAY!": GOSUB 57: GOTO 100
 3500  REM 

GO

 3505  IF BT = 2 THEN  PRINT : PRINT "BOOM! BOOM!": GOSUB 57
 3510  IF BT = 1 THEN BT = 2: PRINT : PRINT "YOU STRIDE THROUGH MID-AIR,  WELL ABOVE THE GROUND! THUNDERCLAPS ACCOMPANY EVERYSTEP!":L = L + 4: GOSUB 58: IF R3 = 95 THEN BT = 0
 3520 TP = 0:A%(7,4) = 0:BY = 0:PR = 0
 3530 R3 = RO:RO = R2: GOSUB 3600
 3540  PRINT D$"READ EAMON.ROOMS,R"RO:NX = 0: FOR X = 1 TO ND: INPUT RD%(X): IF RD%(X) > 0 AND RD%(X) <  = NR THEN NX = NX + 1
 3560  NEXT : INPUT NL: PRINT D$:LT = NL OR LM
 3570  IF RO = 93 AND R3 = 73 THEN MZ = 1
 3590  GOTO 100
 3600  REM 

MONS. REACTIONS

 3605  IF RO <  > 82 AND RO < 96 AND BT THEN NB = 0: RETURN 
 3610 TD%(1) = 0:TD%(3) = M%(0,1):FD%(1) = 0:FD%(3) = M%(0,13)
 3620  FOR M = 1 TO NM
 3625  IF M%(M,2) = 3 THEN  IF M%(M,5) = R3 THEN  IF M%(M,14) = 3 OR (M%(M,14) = 1 AND 200 *  RND (1) < M%(M,4)) THEN M%(M,5) = RO: GOTO 3670
 3630  IF M%(M,5) <  > RO THEN 3900
 3640  IF M%(M,14) THEN 3670
 3650 FR = M%(M,3): IF FR AND FR <  > 100 THEN FR = FR +  INT ((CH - 10) * 2)
 3660 M%(M,14) = 1: IF FR > (100 *  RND (1)) THEN M%(M,14) = 2: IF FR > (100 *  RND (1)) THEN M%(M,14) = 3
 3670 A = M%(M,14):FD%(A) = FD%(A) + M%(M,13):TD%(A) = TD%(A) + M%(M,1)
 3900  NEXT :NB = (TD%(1) > 0): RETURN 
 4000  REM 

GET

 4010  GOSUB 4900: IF  NOT LT THEN  GOSUB 98: GOTO 100
 4020  IF S$ = "ALL" THEN 4160
 4030  GOSUB 4803: IF  NOT F THEN 90
 4035 GT = 1: IF A%(A,2) = 12 OR A = 40 THEN  GOSUB 4500: IF  NOT GT THEN 100
 4037  IF A%(A,4) = EM THEN A%(A,4) = RO:R = 100 + A: GOSUB 45:A%(A,9) = 1: GOSUB 51
 4040  IF A = 50 AND A%(43,4) <  >  - 99 THEN  PRINT : PRINT "IT BURNS YOUR UNPROTECTED HAND!": GOSUB 57:M%(0,13) = M%(0,13) + 3:DF = 0: GOSUB 7650: GOTO 300
 4050  IF A%(A,3) = 999 THEN  PRINT : PRINT "DON'T BE ABSURD!": GOSUB 57: GOTO 100
 4135  IF WT + A%(A,3) > 10 * M%(0,1) OR A%(A,3) = 900 THEN  PRINT : PRINT "YOU CAN'T BUDGE IT!": GOSUB 57: GOTO 100
 4140  PRINT : PRINT "GOT IT.":L = L + 2:A%(A,4) =  - 1:WT = WT + A%(A,3): PRINT : IF (A%(A,2) = 2 OR A%(A,2) = 3) AND M%(0,9) =  - 1 THEN 17100
 4145  IF A = 6 AND  NOT A%(41,4) THEN A%(41,4) = 80
 4146  IF A = 40 THEN FL = 0
 4150  GOTO 300
 4160  PRINT : FOR A = 1 TO NA: IF A%(A,4) <  > RO OR A%(A,2) = 10 THEN 4190
 4175  IF WT + A%(A,3) > 10 * M%(0,1) OR A%(A,3) = 900 THEN  PRINT A$(A)" CAN'T BE MOVED.": GOTO 4190
 4176  IF A = 6 AND  NOT A%(41,4) THEN A%(41,4) = 80
 4177  IF A = 40 THEN FL = 0
 4178  PRINT A$(A)": ";:GT = 1: IF A%(A,2) = 12 THEN  GOSUB 4500: IF  NOT GT THEN 4190
 4180  PRINT "TAKEN.":L = L + 1:A%(A,4) =  - 1:WT = WT + A%(A,3)
 4190  NEXT : PRINT : GOTO 300
 4500  IF A%(19,4) <  >  - 99 AND A%(43,4) <  >  - 99 THEN  PRINT : PRINT "YOUR HAND PASSES RIGHT THOUGH IT!": GOSUB 57:GT = 0
 4510 A2 = 0: FOR I = 1 TO NZ: IF A%(I,2) = 12 THEN  IF A%(I,4) =  - 1 THEN A2 = I:I = 99
 4520  NEXT : IF A2 AND A <  > A2 THEN  PRINT : PRINT "YOUR HAND IS ALREADY FULL!": GOSUB 57:GT = 0
 4530  RETURN 
 4600  REM 

SYNONYMS

 4605  IF RO = 6 THEN  IF S$ = "GLASS" THEN  RETURN 
 4607  IF RO = 43 THEN  IF SL < 5 THEN  IF S$ =  LEFT$ ("WOOD",SL) THEN  POP : RETURN 
 4610  IF RO = 8 THEN SY$ = "MIRROR":SY = 13: GOSUB 4690
 4615  IF RO = 22 THEN  IF S$ = "CHICKENS" THEN S$ = "COCK"
 4620  IF RO = 22 THEN SY$ = "ROOSTERS":SY = 4: GOSUB 4690
 4625  IF RO = 2 THEN SY$ = "GLOVES":SY = 19: GOSUB 4690
 4627  IF RO = 2 THEN SY$ = "GAUNTLETS":SY = 19: GOSUB 4690
 4635  IF RO = 6 THEN SY$ = "SWORD":SY = 11: GOSUB 4690
 4640  IF RO = 9 THEN SY$ = "ADVICE":SY = 28: GOSUB 4690
 4645  IF RO = 9 THEN SY$ = "HOROSCOPE":SY = 28: GOSUB 4690
 4650  IF RO = 9 THEN SY$ = "FORTUNE":SY = 28: GOSUB 4690
 4655  IF RO = 6 THEN SY$ = "PLATE GLASS":SY = 15: GOSUB 4690
 4658  IF RO = 43 THEN SY$ = "MOLD":SY = 25: GOSUB 4690
 4660  IF RO = 40 THEN SY$ = "MOLD":SY = 24: GOSUB 4690
 4665  IF RO = 93 THEN  GOSUB 4695
 4670  IF RO = 93 THEN SY$ = "WALL":SY = 47: GOSUB 4690
 4675  IF RO = 36 THEN SY$ = "HANDLES":SY = 20: GOSUB 4690
 4689  RETURN 
 4690  IF  LEFT$ (SY$,SL) = S$ OR  RIGHT$ (SY$,SL) = S$ THEN S$ = A$(SY):SL =  LEN (S$): POP 
 4692  RETURN 
 4695 A = 0: FOR I = 1 TO  LEN (S$): IF  MID$ (S$,I,1) = " " THEN A = I
 4696  NEXT : IF A > 0 THEN WL$ =  LEFT$ (S$,A - 1):SY$ =  MID$ (S$,A + 1): IF  LEFT$ (SY$,1) <  > "L" THEN S$ = SY$:SL =  LEN (S$)
 4697  IF A = 0 THEN  IF  LEFT$ ("NICHES",SL) = S$ OR  RIGHT$ ("NICHES",SL) = S$ OR  LEFT$ ("WALL",SL) = S$ OR  RIGHT$ ("WALL",SL) = S$ THEN  POP : GOTO 4880
 4698  RETURN 
 4700  REM 

MONS. SEARCH

 4710 F = 0:SL =  LEN (S$)
 4740  FOR X = 1 TO NM: IF M%(X,5) = RO THEN  IF  LEFT$ (M$(X),SL) = S$ OR  RIGHT$ (M$(X),SL) = S$ THEN F = F + 1:M = X: IF S$ = M$(X) THEN X = 999
 4750  NEXT 
 4760  RETURN 
 4800  REM 

ART. SEARCH

 4801 WH =  - 1:HA = WH:EM = WH: GOTO 4810
 4803 WH = RO:HA = RO:EM = RO + 200: GOTO 4810
 4805 WH = RO:HA =  - 1:EM = RO + 200
 4810 F = 0:SL =  LEN (S$): GOSUB 4600: FOR X = 1 TO NA
 4820  IF  LEFT$ (A$(X),SL) = S$ THEN  IF A%(X,4) = WH OR A%(X,4) = HA OR A%(X,4) = EM THEN F = F + 1:A = X: IF S$ = A$(X) THEN X = 99
 4825  NEXT : IF X = 100 THEN  RETURN 
 4830  FOR X = 1 TO NA: IF  RIGHT$ (A$(X),SL) = S$ THEN  IF A%(X,4) = WH OR A%(X,4) = HA OR A%(X,4) = EM THEN F = F + 1:A = X
 4835  NEXT : IF F > 1 AND X < 100 THEN 4880
 4837  IF WH > 400 OR WH =  - 99 OR X = 100 OR C = 34 THEN  RETURN 
 4850  FOR X = 33 TO 39: IF  LEFT$ (A$(X),SL) = S$ OR  RIGHT$ (A$(X),SL) = S$ THEN  IF A%(X,4) = 146 AND A%(46,4) =  - 1 THEN F = F + 1:A = X: IF S$ = A$(X) THEN X = 99
 4860  NEXT : IF F < 2 OR X = 100 THEN  RETURN 
 4880  PRINT : PRINT "WHICH "S$" DO YOU WANT?": GOSUB 57:F = 0: POP : GOTO 100
 4900  REM 

GET S$

 4910  IF S$ = "" THEN  PRINT : PRINT C$(C)" WHAT";: INPUT S$: GOTO 4910
 4920  RETURN 
 4930  IF S$ = "" THEN  PRINT : PRINT C$(C)" WHO";: INPUT S$: GOTO 4930
 4940  RETURN 
 4950  REM 

ANYBODY HOME?

 4960 M = 0: FOR X = 1 TO NM: IF M%(X,5) = RO THEN M = X
 4970  NEXT : IF M THEN  RETURN 
 4980  PRINT : PRINT "THERE'S NOBODY HERE!": GOSUB 56: GOTO 100
 5000  REM 

DROP

 5010  GOSUB 4900: ON S$ = "ALL" GOTO 5100: GOSUB 4801: IF  NOT F THEN 91
 5040 WT = WT - A%(A,3):A%(A,4) = RO: PRINT : PRINT A$(A)" DROPPED.": GOSUB 56
 5050  IF M%(0,9) > 0 THEN  IF A%(M%(0,9),4) <  >  - 1 THEN M%(0,9) =  - 1
 5070  GOTO 300
 5100  FOR A = 1 TO NA: IF A%(A,4) =  - 1 OR A%(A,4) =  - 99 THEN  PRINT A$(A)" DROPPED.":A%(A,4) = RO: GOSUB 52
 5110  NEXT :WT = 0: GOTO 5050
 6000  REM 

LOOK

 6005  IF  NOT LT THEN  GOSUB 98: GOTO 100
 6010  ON S$ = "" GOTO 6250: GOSUB 4805: IF F THEN  IF A%(A,4) = EM THEN A%(A,4) = RO
 6030  IF F THEN  GOSUB 6700: GOTO 300
 6040  GOSUB 4700: IF F THEN R = M + 300: GOSUB 45: GOTO 300
 6250 V%(RO) = 0: GOTO 300
 6700  REM EXAM ART.
 6710  IF A > NZ THEN  PRINT : PRINT "THIS IS YOUR "A$(A)".": GOTO 57
 6770 R = A + 100: GOSUB 45:A%(A,9) = 1: GOSUB 51
 6780  IF RO = 22 THEN  IF (A = 3 OR A = 4) AND  NOT C1 THEN C1 = 1:R = 207: GOSUB 45
 6785  IF A = 5 THEN R = 223: GOSUB 45
 6790  RETURN 
 7000  REM 

ATTACK

 7010  GOSUB 4930
 7020  IF  NOT LT THEN  PRINT : PRINT "YOU SWING WILD --- NOTHING HIT.": GOTO 300
 7050  GOSUB 4700: IF F THEN 7290
 7150 TA = 0: GOTO 20400
 7290  IF M%(0,9) =  - 1 THEN  PRINT : PRINT "YOU HAVE NO WEAPON READY!": PRINT : GOTO 100
 7295  IF M = 19 THEN  PRINT : PRINT "YOU  CANNOT  DEVISE A WAY TO STRIKE THE BEAST WITHOUT EXPOSING YOURSELF  TO ITS GAZE!":L = L + 3: GOSUB 58: GOTO 100
 7297  IF M%(M,2) = 1 OR M%(M,2) = 2 THEN TK = 3:KA = M
 7298 DF = M: IF M%(M,2) = 3 THEN  FOR K = 23 TO 25:M%(K,14) = 1: NEXT :R3 = RO: GOSUB 3600
 7300 OF = 0: GOSUB 7500: IF  NOT HIT THEN 7360
 7310  IF INC THEN W2 = A%(M%(0,9),6):WA%(W2) = WA%(W2) + 2
 7320  IF  - EA <  = AE THEN 300
 7330  IF  INT (100 *  RND (1) + 1) <  = M%(0,10) THEN 7360
 7340 A = 2: IF EA + AE =  - 1 THEN A = 1
 7350 AE = AE + A:M%(0,10) = M%(0,10) + A
 7360  IF M%(DF,14) = 1 OR DF = 0 THEN 300
 7370 M%(DF,3) = M%(DF,3) / 2: FOR M = 1 TO NM: IF M%(M,5) = RO AND M%(M,14) > 1 THEN M%(M,14) = 0
 7380  NEXT :R3 = RO: GOSUB 3600: GOTO 300
 7400  REM 

MONS. P/U WEAP.

 7410  IF M%(OF,10) = 0 THEN  RETURN 
 7420  FOR A = 1 TO NA: IF A%(A,4) <  > RO OR A%(A,2) < 2 OR A%(A,2) > 3 THEN  NEXT : RETURN 
 7430  PRINT M$(OF)" PICKS UP "A$(A)".": PRINT :A%(A,4) = 0:M%(OF,9) = A:M%(OF,10) = M%(OF,10) + A%(A,5):M%(OF,4) = M%(OF,4) * 2: RETURN 
 7500 HI = 0: IF M%(OF,9) =  - 1 THEN 7400
 7503 AK$ = " ATTACKS ": IF M%(OF,2) = 3 OR OF = 27 THEN AK$ = " ATTACK "
 7505  PRINT M$(OF);AK$;M$(DF):L = L + 1: PRINT " --";:RL =  INT (100 *  RND (1) + 1):HI = ((RL < 5 OR RL < M%(OF,10) - M%(DF,7)) AND RL < 96)
 7506  IF OF = 19 THEN  PRINT : PRINT "THE MIRROR TURNS ITS GAZE!": GOTO 57
 7507  IF M%(OF,9) = 10 THEN  IF  RND (1) < .95 THEN R = 222: GOSUB 45:A%(10,4) = 0: GOTO 5050
 7510  IF HIT THEN 7600
 7515  IF M%(OF,2) = 0 OR M%(OF,2) = 3 THEN RL = 1
 7520  IF RL < 97 THEN  PRINT "A MISS.": GOTO 52
 7525  PRINT "A FUMBLE!":FR =  INT (100 *  RND (1) + 1): IF FR <  = 35 + 65 * ( NOT M%(OF,9)) THEN  PRINT "  FUMBLE RECOVERED.": GOTO 56
 7527  IF M%(OF,9) = 50 THEN  PRINT "SPARKS FLY FROM THE BLUE LIGHT!": GOTO 56
 7530  PRINT "  WEAPON DROPPED!":A%(M%(OF,9),4) = RO:M%(OF,10) = M%(OF,10) - A%(M%(OF,9),5):M%(OF,9) =  - 1:M%(OF,4) = M%(OF,4) / 2: GOTO 52
 7600 INC = ( INT (100 *  RND (1) + 1) > M%(OF,10)):M%(OF,10) = M%(OF,10) + 2 * INC
 7601 AA = M%(OF,9): IF A%(AA,2) = 3 THEN  PRINT "A HIT!": PRINT "MAGIC BACKLASH HURTS USER!":D2 = A%(AA,7) * A%(AA,8) / 4:DF = OF: GOSUB 57: GOTO 7640
 7602  IF DF = 20 AND M%(OF,9) = 40 THEN  PRINT "A HIT!": PRINT "BLUE LIGHT PASSES THROUGH GLASS SURFACE WITHOUT RESULT!": GOTO 57
 7603  IF DF = 20 AND M%(OF,9) <  > 12 THEN  PRINT "A HIT!": PRINT " BUT THE BLOW IS TOO LIGHT!": PRINT " IT GLANCES OFF!": GOTO 57
 7604  IF DF <  > 21 THEN 7609
 7605  PRINT "A HIT!": GOSUB 52: IF M%(OF,9) = 10 THEN  PRINT "LIGHTS REFRACT FROM THE SWORD!": GOSUB 52:D = 1: GOTO 7615
 7606  IF M%(OF,9) = 18 THEN DF = 22: GOTO 7609
 7607  IF M%(OF,9) = 11 THEN  PRINT "RAINBOW SHAFTS OF LIGHT REFRACTIONS": PRINT "   BURST FROM THE SWORD!": GOSUB 57:D = D * 2: GOTO 7615
 7608  PRINT "AN ELECTRIC SHOCK RUNS DOWN THE WEAPON": PRINT "  SHOCK HURTS USER!": GOSUB 57:D2 = 3:DF = OF: GOTO 7640
 7609  IF DF = 22 AND M%(OF,9) <  > 40 THEN  PRINT "A HIT - BUT IT HAS NO EFFECT!": GOTO 52
 7610 D = M%(OF,11):S = M%(OF,12):A = 1: IF RL > 5 THEN  PRINT "A HIT!": GOTO 7635
 7612  PRINT "A CRITICAL HIT!":L = L + 1:I =  INT (100 *  RND (1) + 1): IF I < 51 THEN A = 0: GOTO 7635
 7615  IF I < 86 THEN S = 1.5 * S: GOTO 7635
 7620  IF I < 96 THEN D = 2 * D: GOTO 7635
 7625  IF I < 100 THEN D = 3 * D: GOTO 7635
 7630  GOTO 7700
 7635 D2 = 0: FOR D3 = 1 TO D:D2 = D2 +  INT (S *  RND (1) + 1): NEXT :D2 = D2 - A * M%(DF,8): IF D2 < 1 THEN  PRINT "   BLOW BOUNCES OFF ARMOUR.": GOTO 56
 7640 M%(DF,13) = M%(DF,13) + D2: IF M%(DF,13) >  = M%(DF,1) THEN 7700
 7645 A = M%(DF,14):FD%(A) = FD%(A) + D2
 7650  IF DF = 27 OR M%(DF,2) = 3 THEN  PRINT  INT (M%(DF,13) / 5) -  INT ((M%(DF,13) - D2) / 5)" "M$(DF)" KILLED!": GOTO 56
 7651  PRINT : PRINT M$(DF)" IS ";: IF DF > 19 AND DF < 23 THEN  PRINT "STAGGERED!": GOTO 56
 7653  IF  NOT M%(DF,13) THEN  PRINT "IN PERFECT HEALTH.": GOTO 56
 7654  ON  INT (M%(DF,13) * 5 / M%(DF,1) + 1) GOTO 7655,7660,7665,7670,7675
 7655  PRINT "IN GOOD SHAPE.":L = L + 1: GOTO 56
 7660  PRINT "HURT.": GOTO 56
 7665  PRINT "IN PAIN.": GOTO 56
 7670  PRINT "VERY BADLY INJURED.": GOTO 56
 7675  PRINT "NEAR DEATH.":L = L + 1: GOTO 56
 7700  REM 
 7701  IF DF = 20 THEN R = 227: GOSUB 45: GOTO 7710
 7702  IF DF = 21 THEN R = 226: GOSUB 45: GOTO 7710
 7703  IF DF = 22 THEN R = 228: GOSUB 45: GOTO 7710
 7704  IF M%(DF,6) = 999 THEN  PRINT M$(DF)" WIPED OUT!": GOTO 7710
 7705  IF DF = 26 THEN R = 256: GOSUB 45: GOTO 7710
 7706  PRINT M$(DF)" IS DEAD!": IF M%(DF,2) = 1 OR M%(DF,2) = 2 THEN KL = DF
 7710  IF  NOT DF THEN DI = 1: POP : GOTO 2000
 7712  PRINT :A = M%(DF,14):FD%(A) = FD%(A) + M%(DF,1) - M%(DF,13) + D2:M%(DF,5) = 0
 7715  FOR AJ = 1 TO NA: IF A%(AJ,4) =  - DF - 1 THEN A%(AJ,4) = RO
 7720  NEXT : IF M%(DF,9) > 0 THEN A%(M%(DF,9),4) = RO:M%(DF,10) = M%(DF,10) - A%(M%(DF,9),5):M%(DF,9) =  - 1:M%(DF,4) = M%(DF,4) / 2
 7730  IF DF = 26 THEN R2 = 96: GOTO 3530
 7900  GOSUB 51: RETURN 
 8000  REM 

FLEE

 8005  IF RO = 96 THEN R2 = 95: GOTO 3500
 8006  IF RO = 82 THEN R2 = 81: GOTO 3500
 8007  IF RO = 97 THEN M%(26,13) = 0
 8010  IF  NOT NB THEN  PRINT : PRINT "THERE'S NOTHING TO FLEE FROM!": GOSUB 57: GOTO 100
 8015  IF S$ = "" THEN 8500
 8020  FOR I = 1 TO ND: IF S$ =  LEFT$ (C$(I), LEN (S$)) THEN D = I:I = 99: NEXT : GOTO 3030
 8030  NEXT : PRINT : PRINT "THERE'S NO PLACE TO GO!": GOSUB 57: GOTO 300
 8500 NX = 0: FOR I = 1 TO ND: IF RD%(I) THEN NX = 1
 8510  NEXT : IF NX = 0 THEN R2 = RO: PRINT "YOU CAN'T FIND AN EXIT!": GOSUB 57: GOTO 300
 8520 R2 = RD%(ND *  RND (1) + 1): IF R2 = 0 THEN 8520
 8530  GOTO 3500
 9000  REM 

GIVE

 9010  GOSUB 4900: GOSUB 4950
 9070  IF  VAL (A$) > 0 THEN  PRINT : PRINT "USE THE 'PAY' COMMAND TO BUY THINGS": GOSUB 57: GOTO 100
 9090  GOSUB 4801: IF  NOT F THEN 91
 9095  PRINT : PRINT "OKAY.": IF A = M%(0,9) THEN M%(0,9) =  - 1
 9100 A%(A,4) =  - M - 1
 9120  GOTO 5050
 10000  REM 

INVEN

 10010  GOSUB 56: PRINT M$(0)" IS CARRYING--": FOR A = 1 TO NA: IF A%(A,4) =  - 1 THEN  PRINT " "A$(A); LEFT$ (" (READY WEAPON)",1 + 20 * (A = M%(0,9))): GOSUB 52
 10020  IF A%(A,4) =  - 99 THEN  PRINT " "A$(A)" (WORN)": GOSUB 52
 10025  IF A%(A,4) = 146 THEN  IF A%(46,4) =  - 1 THEN  PRINT " "A$(A)" (IN POUCH)": GOSUB 52
 10030  NEXT : PRINT "  "GO" GOLD PIECES.": GOSUB 52
 10040 DF = 0: GOSUB 7650: GOTO 300
 14000  REM 

SPELLS

 14010  PRINT : PRINT "YOUR SPELL IS SNUFFED BY THE DARKNESS!": GOSUB 57: GOTO 100
 15000  REM 

SMILE

 15010  PRINT : FOR M = 1 TO NM: IF M%(M,5) <  > RO THEN  NEXT : GOTO 15110
 15020 A = M%(M,2): IF A = 1 AND TK < 3 THEN  PRINT : PRINT "THE "M$(M)" SMILES BACK."
 15030  IF A = 2 OR A = 1 AND TK = 3 THEN  PRINT : PRINT "THE "M$(M)" PAYS NO ATTENTION."
 15040  IF A <  > 3 THEN 15100
 15050  PRINT : PRINT "THE "M$(M)" ";: IF NB THEN  PRINT : PRINT "GROWL AT YOU.": GOTO 15100
 15060  PRINT "STARE AT YOU";: IF CL = 40 AND  NOT PR AND  NOT BT THEN  PRINT 
 15065  IF  NOT PR AND  NOT BT THEN  PRINT "  WITH DEEP SUSPICION";
 15070  PRINT "."
 15100  NEXT 
 15110  PRINT : GOTO 300
 16000  REM 

SAY

 16010  GOSUB 4900
 16080  IF RO = 64 AND S$ = "BRAAK" THEN TP = 6: PRINT : PRINT "  THE REAR WALL OF THE TEMPLE BEGINS TO SHINE WITH A BLACK 'GLOW' THAT VISIBLY  DARKENS THE ALREADY DIM ROOM!": PRINT :L = L + 5: GOSUB 58:A%(7,4) = RO:A%(7,9) = 1:BK = LU: GOTO 300
 16900  PRINT : PRINT "OKAY, "; CHR$ (34);S$; CHR$ (34): PRINT : GOTO 300
 17000  REM 

READY

 17010  GOSUB 4900
 17020  GOSUB 4801: IF  NOT F THEN 91
 17030  IF A%(A,2) < 2 OR A%(A,2) > 3 THEN  PRINT : PRINT "THAT ISN'T A WEAPON!": GOSUB 56: GOTO 100
 17100 M%(0,10) = (EA + AE) * ( - EA > AE) + A%(A,5) + WA%(A%(A,6)) + 2 * M%(0,2):M%(0,9) = A:M%(0,11) = A%(A,7):M%(0,12) = A%(A,8)
 17190  PRINT : PRINT A$(A)" READIED.": GOSUB 56: GOTO 300
 18000  REM  

SAVE

 18010  PRINT : INPUT "ENTER THE NUMBER OF THE GAME YOU WISH TO SAVE (1-5): ";A$:D =  VAL (A$): ON D < 1 OR D > 5 GOTO 100: PRINT D$"CLOSE"
 18020  PRINT : PRINT D$"OPENSAVE."D: PRINT D$"WRITESAVE."D: PRINT M$(0): FOR A = NZ + 1 TO NA: PRINT A$(A): NEXT : FOR A = 1 TO NR: PRINT V%(A): NEXT : FOR A = 1 TO NA: PRINT A%(A,4): PRINT A%(A,9): NEXT 
 18030  FOR M = 0 TO NM: PRINT M%(M,5): PRINT M%(M,9): PRINT M%(M,13): PRINT M%(M,14): PRINT M%(M,15): NEXT 
 18040  PRINT FL: PRINT LU: PRINT MC: PRINT MF: PRINT MA: PRINT MZ: PRINT TP: PRINT BY: PRINT PR: PRINT BT: PRINT HD: PRINT TK: PRINT CW: PRINT LH: PRINT PC: PRINT SH: PRINT LM: PRINT LT: PRINT CL: PRINT ET: PRINT BK
 18050  PRINT NB: PRINT RO: PRINT R3: PRINT CH: PRINT GO: PRINT M%(0,10): PRINT M%(0,11): PRINT M%(0,12): FOR A = 1 TO 4: PRINT SA%(A): NEXT 
 18060  PRINT D$"CLOSE": GOTO 29120
 19000  REM 

LIGHT

 19010  GOSUB 4900: GOSUB 4801: IF  NOT F THEN 91
 19020  PRINT : PRINT "IT WON'T LIGHT!": GOTO 100
 20000  REM 

OPEN

 20010  GOSUB 4900: GOSUB 4805: IF  NOT F THEN 20200
 20060  IF A%(A,2) <  > 4 THEN 93
 20120  IF A = 22 THEN R = 255: GOSUB 45
 20130  IF A = 44 THEN  IF A%(45,4) = 144 THEN A%(45,4) = RO: PRINT : PRINT "YOU FIND SOMETHING INSIDE."
 20140  GOTO 300
 20200  IF S$ = "DOOR" OR S$ = "GATE" THEN  PRINT : PRINT "IF IT CAN BE OPENED,  IT WILL OPEN WHEN YOU APPROACH.": GOTO 100
 20210  GOTO 90
 20400  REM 

ATT. ART.

 20410  GOSUB 4803: IF  NOT F THEN 20490
 20415  IF A = 40 THEN 30600
 20420  IF A%(A,2) <  > 8 AND A%(A,2) <  > 4 THEN  PRINT : PRINT "WHY WOULD YOU DO THAT?": GOTO 100
 20430  IF M%(0,9) =  - 1 AND TA = 0 THEN  PRINT : PRINT "YOU HAVE NO WEAPON READY!": PRINT : GOTO 100
 20435 D = M%(0,11):S = M%(0,12): IF TA = 1 THEN D = 2:S = 5
 20440 SX = 7:K = 6: IF A%(A,2) = 4 THEN SX = SX - 1:K = K - 1
 20450 D2 = 0: FOR X = 1 TO D:D2 = D2 +  INT ( RND (1) * S + 1): NEXT :A%(A,SX) = A%(A,SX) - D2: IF A%(A,SX) <  = 0 THEN A%(A,SX) = 0:A%(A,K) = 0: PRINT : PRINT "THE "A$(A)" SHATTERS!": GOTO 300
 20470  PRINT : PRINT "WHAM!! YOU " MID$ ("HITBLAST",TA * 3 + 1,3 + TA * 2)" THE "A$(A)"!": GOTO 300
 20490  IF TA = 0 THEN 95
 20495  PRINT : PRINT "YOU CAN'T BLAST ";S$;"!": GOTO 100
 21000  REM 

PUT

 21030  PRINT : INPUT "PUT WHAT?";A$: IF A$ = "" THEN 21030
 21040  INPUT "PUT ON/INTO WHAT?";B$: IF B$ = "" THEN 21040
 21100 S$ = A$: GOSUB 4805: IF  NOT F THEN 91
 21102  IF A%(A,4) = RO AND A%(A,2) <  > 12 THEN 91
 21103  IF A%(A,2) = 12 THEN GT = 1: GOSUB 4500: IF  NOT GT THEN 91
 21105  IF A%(A,4) = 146 AND A%(46,4) <  >  - 1 THEN 91
 21110 Z = A:S$ = B$: GOSUB 4805: IF A = Z OR  NOT F THEN 93
 21120  IF A = 47 THEN 21500
 21130  IF A = 44 AND A%(45,4) = 144 THEN  PRINT : PRINT "IT IS CLOSED.": GOSUB 57: GOTO 300
 21140  IF A = 46 AND A%(Z,2) = 12 THEN A%(Z,4) = 146: PRINT : PRINT "OKAY.": GOSUB 57: GOTO 300
 21150  IF A <  > 48 THEN 93
 21160  PRINT : PRINT "OKAY.": GOSUB 57:A%(Z,4) = RO: IF Z = 40 THEN R = 233: GOSUB 45:FL = 1
 21170  GOTO 300
 21500  REM 
NICHE
 21510  IF A%(Z,2) <  > 12 OR Z = 41 THEN 93
 21520 A2 = 0: FOR I = 1 TO 10: IF LR%(I) = Z THEN A2 = I
 21530  NEXT :MW = 0: FOR I = 1 TO 4: IF A2 = MZ%(MZ,I) THEN MW = I
 21535  NEXT :A2 = 0: FOR A = 1 TO 4: IF WL$ = TP$(MZ%(MZ,A)) THEN A2 = 1
 21540  NEXT : IF  NOT A2 THEN MW = 0: GOTO 95
 21545  IF WL$ <  > TP$(MZ%(MZ,MW)) THEN MW = 0
 21550  PRINT : PRINT "OKAY.": GOSUB 57:A%(Z,4) =  - 100
 21900  GOTO 100
 22000  REM 

DRINK

 22010  GOSUB 4900:WH = RO:HA =  - 1:EM = RO + 400: GOSUB 4810: IF  NOT F THEN 90
 22020  IF A <  > 29 THEN 90
 22030  IF A%(A,4) = EM THEN 91
 22100  PRINT : PRINT "THAT WENT DOWN WELL!": GOSUB 57
 22120 A%(A,4) = 414: IF M%(0,13) > 0 THEN M%(0,13) = M%(0,13) - A%(A,6): PRINT : PRINT "YOU FEEL BETTER NOW!": GOSUB 57: IF M%(0,13) < 0 THEN M%(0,13) = 0
 22130 DF = 0: GOSUB 7650
 22140  GOTO 300
 23000  REM 

READ

 23010  GOSUB 4900: GOSUB 4805: IF  NOT F THEN 90
 23090  IF A%(A,2) <  > 7 THEN  PRINT : PRINT "YOU CAN'T READ IT.": GOTO 300
 23210 R = A%(A,5) + 200: GOSUB 45: GOSUB 56
 23290  GOTO 300
 24000  REM 

FREE

 24010  GOSUB 4930: GOSUB 4805: IF  NOT F THEN 90
 24020  IF A%(A,2) <  > 10 THEN 90
 24030 M = A%(A,5):K = A%(A,6):G = A%(A,7)
 24120  IF M%(G,5) = RO THEN  PRINT : PRINT "THE "M$(G)" WON'T LET YOU DO THAT!": GOTO 300
 24150  PRINT : PRINT "YOU HAVE FREED "M$(M)".":M%(M,5) = RO:A%(A,4) = 0:R3 = RO: GOSUB 3600: GOTO 300
 29000  REM 

RESTORE

 29010  PRINT D$"CLOSE": PRINT : INPUT "ENTER THE NUMBER OF THE GAME YOU WISH TO RESTORE (1-5):";A$:D =  VAL (A$): IF D < 1 OR D > 5 THEN 29120
 29020  ONERR  GOTO 29900
 29030  PRINT D$"OPENSAVE."D: PRINT D$"READSAVE."D: INPUT MS$: FOR A = 1 TO NA - NZ: INPUT AS$(A): NEXT : IF MS$ <  > M$(0) THEN 29900
 29040  FOR J = 1 TO NA - NZ: IF AS$(J) <  > A$(J + NZ) THEN J = 99
 29050  NEXT : IF J > 5 THEN 29900
 29060  FOR A = 1 TO NR: INPUT V%(A): NEXT : FOR A = 1 TO NA: INPUT A%(A,4),A%(A,9): NEXT : FOR M = 0 TO NM: INPUT M%(M,5),M%(M,9),M%(M,13),M%(M,14),M%(M,15): NEXT 
 29070  INPUT FL,LU,MC,MF,MA,MZ,TP,BY,PR,BT,HD,TK,CW,LH,PC,SH,LM,LT,CL,ET,BK,NB,R2,RO,CH,GO,M%(0,10),M%(0,11),M%(0,12): FOR A = 1 TO 4: INPUT SA%(A): NEXT : PRINT D$"CLOSE": POKE 216,0
 29080 A =  PEEK (43604): IF A = 195 AND CL = 80 OR A <  > 195 AND CL = 40 THEN 29120
 29090  PRINT "DO YOU WISH TO USE THE 80-COLUMN SCREEN?": INPUT A$: IF  LEFT$ (A$,1) = "Y" THEN  PRINT D$"PR#3":CL = 80: HOME : PRINT FF$: GOTO 29120
 29100  IF  LEFT$ (A$,1) <  > "N" THEN  PRINT  CHR$ (7): GOTO 29080
 29110  PRINT  CHR$ (26)"1": PRINT  CHR$ (26); CHR$ (17): PRINT D$"PR#0": HOME :CL = 40
 29120 A$ = D$ + "OPENEAMON.": PRINT A$"DESC,L256": PRINT A$"ROOMS,L64": PRINT A$"ROOM.NAME,L64": GOTO 3530
 29900  POKE 216,0: PRINT D$"CLOSE": PRINT  CHR$ (7): PRINT "   -THERE IS NO SAVED GAME #"D"  FOR YOUR CHARACTER.": GOTO 29120
 30000  REM 

SPECIAL ROOMS STUFF

 30010  IF RO = 25 THEN  IF  NOT TK THEN R = 201: GOSUB 45:R2 = 70:M%(5,5) = 70: GOTO 3500
 30020  IF RO = 70 THEN  IF  NOT TK THEN TK = 1: GOSUB 52: FOR R = 202 TO 206: GOSUB 45: NEXT :M%(5,5) = 25: GOTO 100
 30030  IF RO = 25 THEN  IF TK = 1 THEN R = 208: GOSUB 45:TK = 2
 30040  IF RO = 78 AND  NOT CW THEN  IF M%(19,5) AND A%(30,4) <  >  - 1 THEN R = 209: GOSUB 45:CW = 1
 30050  IF RO = 78 THEN  IF M%(19,5) AND A%(30,4) <  >  - 1 THEN  PRINT : PRINT "YOU FEEL WEAKENED.": GOSUB 57:M%(0,13) = M%(0,13) + 1:DF = 0: GOSUB 7650
 30060  IF RO = 79 THEN  IF M%(19,5) AND A%(30,4) <  >  - 1 THEN R = 225: GOSUB 45:M%(0,13) = M%(0,13) + 5:DF = 0: GOSUB 7650
 30070  IF RO = 80 THEN  IF M%(19,5) AND A%(30,4) <  >  - 1 THEN  PRINT "YOU CAN NO LONGER ENDURE ITS GAZE!  YOU HAVE BEEN TURNED TO STONE!":DI = 1: GOTO 2000
 30075  IF MF THEN MC = MC + 1
 30080  IF A%(4,4) = RO OR A%(4,4) =  - 1 AND  RND (1) < .3 THEN  PRINT : PRINT "THE COCK CROWS, 'COCK-A-DOODLE-DOO!": GOSUB 57
 30090  IF RO = 80 THEN  IF A%(4,4) =  - 1 AND M%(19,5) THEN  GOSUB 51: PRINT "THE COCKATRICE STIFFENS AT THE SOUND OF THE COCK'S CROW!": GOSUB 57:M%(19,5) = 0:A%(6,4) = RO:NB = 0: GOTO 100
 30100  IF RO > 84 AND RO < 89 THEN  IF  RND (1) > .6 THEN R =  INT ( RND (1) * 3) + 230: GOSUB 45
 30110  IF LU = 2 THEN  IF RO < 59 AND  RND (1) < .85 THEN LH = LH + 20:R = 236 + (LH > 499 AND  NOT M%(0,13)): GOSUB 45: IF M%(0,13) THEN M%(0,13) = M%(0,13) - 30:M%(0,13) = M%(0,13) * (M%(0,13) > 0):DF = 0: GOSUB 7650
 30120  IF LH > 500 THEN LH = 500
 30130  IF RO = 97 THEN  IF M%(28,5) = RO THEN 2000
 30160  IF RO = 83 THEN  IF A%(45,4) <  >  - 99 THEN  PRINT : PRINT "THE FLAMES BURN!": GOSUB 57:M%(0,13) = M%(0,13) + 4:D2 = 4: GOSUB 7650
 30170  IF RO > 84 AND RO < 89 AND BT THEN  PRINT : PRINT "THE GUARDS GROVEL BENEATH YOU.": GOSUB 57:NB = 0
 30180  IF  NOT LU OR RO = TP%(2,TP) OR RO = 64 AND LU < 2 AND BK = 0 THEN 30210
 30190 TP = 0: FOR I = 1 TO 10: IF RO = TP%(1,I) THEN TP = I
 30200  NEXT : IF TP AND A%(7,4) <  > RO THEN A%(7,4) = RO: GOTO 31030
 30210  IF RO = 67 THEN  IF R3 = 94 THEN  IF  NOT ET THEN  PRINT : PRINT "THE TEMPLE DOESN'T FEEL SO EMPTY NOW.": GOSUB 57:ET = 1: FOR A = 33 TO 39:A%(A,4) = 0: NEXT 
 30499  GOTO 210
 30600  REM 
FORGE LIGHT
 30605  IF A%(42,4) <  >  - 99 OR A%(43,4) <  >  - 99 OR A%(45,4) <  >  - 99 THEN  PRINT : PRINT "THE ENERGIES CANNOT YET FIND RELEASE.": GOSUB 57: GOTO 100
 30610  IF M%(0,9) <  > 41 OR  NOT FL THEN 20420
 30620  IF FL = 1 THEN R = 234: GOSUB 45:FL = 2: GOTO 300
 30630  IF FL = 2 THEN R = 235: GOSUB 45:A%(40,4) = 0:A%(50,4) = 94:MC = 10000: GOTO 300
 30999  GOTO 210
 31000  REM 

PRAY

 31007  IF RO > 84 AND RO < 89 AND A%(45,4) <  > 1 AND A%(45,4) <  >  - 99 THEN PR = 1: GOSUB 51: PRINT "THE GUARDS, SEEING YOUR PRAYERS,  RELAX THEIR VIGILANCE.": GOSUB 57: GOTO 100
 31010 TP = 0: FOR I = 1 TO 10: IF RO = TP%(1,I) AND TP%(2,I) > 0 THEN TP = I
 31013  NEXT : IF TK = 3 THEN  PRINT : PRINT "THE GODS WILL NO LONGER HEAR YOU!": GOSUB 57: GOTO 100
 31020  IF  NOT TP OR  NOT TK OR RO = 64 OR RO = 67 AND  NOT LU AND  NOT ET THEN 31040
 31030  PRINT : PRINT "  THE REAR WALL OF THE TEMPLE BEGINS TO SHINE WITH A TRANSLUCENT "TP$(TP)" GLOW!": PRINT :L = L + 4: GOSUB 58:A%(7,4) = RO:A%(7,9) = 1: GOTO 100
 31040  PRINT : PRINT "NOTHING VISIBLE HAPPENS";
 31050  IF  NOT TP THEN  PRINT " HERE";
 31060  PRINT ".": GOSUB 57: GOTO 300
 32000  REM 

BUY

 32005  IF LU = 2 THEN R = 237: GOSUB 45: GOTO 300
 32010 PC = 0:M = 0: FOR I = 1 TO NM: IF M%(I,5) = RO THEN M = I
 32015  NEXT : IF  NOT M THEN 94
 32016  IF M%(M,2) <  > 1 AND M%(M,2) <  > 2 THEN 15000
 32017  IF TK = 3 THEN  PRINT : PRINT "HE REFUSES TO DEAL WITH YOU.": PRINT "HE SAYS THAT HE HEARD THAT YOU ATTACKED THE "M$(KA);
 32018  IF TK = 3 THEN  IF KL THEN  PRINT " AND KILLED HIM";
 32019  IF TK = 3 THEN  PRINT ".":L = L + 4: GOSUB 58: GOTO 300
 32020  GOSUB 4900:WH = RO + 400:HA = WH:EM = WH: GOSUB 4810
 32022  IF RO = 22 THEN  GOSUB 4803: IF F THEN  IF A = 4 THEN  PRINT : PRINT "'HELP YOURSELF!'": GOSUB 57: GOTO 100
 32025 BY = 0: IF A = 15 AND X < 100 OR  NOT F THEN 32700
 32027  FOR I = 1 TO 24: IF A = SP%(I,0) THEN  IF M = SP%(I,2) THEN BY = I
 32030  NEXT : IF BY = 19 THEN BY = 0: PRINT :R = 217: GOSUB 45: GOTO 100
 32033  IF BY = 16 AND A%(17,4) <  > 444 THEN BY = 0:R = 252: GOSUB 45: GOTO 100
 32035  IF BY = 20 THEN BY = 0: PRINT :R = 219: GOSUB 45: GOTO 100
 32037  IF BY = 21 AND MC > 9999 THEN BY = 0: GOTO 32082
 32038  IF BY = 21 THEN MF = 1: IF MC < 30 +  RND (1) * 70 THEN BY = 0:R = 245: GOSUB 45: GOTO 100
 32040  IF M = 4 OR M = 16 THEN  PRINT : PRINT "THE "M$(M)" NODS HIS HEAD.": PRINT "HE SAYS THAT HE HAS IT FOR SALE.":L = L + 3: GOSUB 58: GOTO 32500
 32045  IF BY THEN 32090
 32050  IF RO = 14 THEN  IF S$ =  LEFT$ ("BEER",SL) OR S$ =  RIGHT$ ("BEER",SL) THEN  GOSUB 51: PRINT "THE BARKEEP POINTS OUT THAT YOU HAVEN'T FINISHED YOUR LAST ONE YET.": GOSUB 57: GOTO 100
 32052  IF M = 3 THEN  IF  LEFT$ (S$,6) = "SILVER" AND  LEN (S$) > 6 THEN BY = 99:PC = 1000: GOTO 32090
 32080  IF M = 13 THEN  PRINT : PRINT "THE BREWER SHOUTS, 'NO! NO! GO AWAY!'": GOSUB 57: GOTO 100
 32082  PRINT : PRINT "THE "M$(M)" LOOKS MOURNFUL.": PRINT "HE REGRETS THAT HE HAS NONE FOR SALE.":L = L + 3: GOSUB 58: GOTO 100
 32090  PRINT : PRINT "THE "M$(M)" NODS HIS HEAD.": PRINT "HE SAYS THAT HE CAN MAKE IT FOR YOU.":L = L + 3: GOSUB 58
 32100 R = 0: IF BY AND BY < 99 THEN R = SP%(BY,6) + 200
 32110  IF R > 200 THEN  GOSUB 45
 32500  IF BY = 16 THEN BY = 0:R = 211: GOSUB 45: GOTO 100
 32505  PRINT : PRINT "HE SAYS THAT IT WILL COST ";: IF PC THEN 32520
 32510 PC = SP%(BY,3): IF  NOT BY THEN BY = 99:PC = A%(A,1):BA = A
 32520  PRINT PC;" GP.": GOSUB 57
 32540  GOTO 100
 32700 F = A:A = 0: FOR X = 0 TO TN: IF  LEFT$ (TA$(X),SL) = S$ OR  RIGHT$ (TA$(X),SL) = S$ THEN A = X: IF TA$(X) = S$ THEN X = 99
 32710  NEXT : IF A = 0 THEN A = F: ON (A = 15) + 1 GOTO 32050,32027
 32720  IF TM%(A) <  > M THEN 32050
 32730 R = 251 + TC%(A): GOSUB 45: GOTO 100
 33000  REM 

PAY

 33010  IF  NOT BY THEN  PRINT : PRINT "THERE IS NO TRANSACTION PENDING.": GOSUB 57: GOTO 100
 33020 M = 0: FOR I = 1 TO NM: IF M%(I,5) = RO THEN M = I
 33030  NEXT : IF M = 0 THEN  PRINT : PRINT "THERE'S NO ONE HERE!": GOSUB 57: GOTO 100
 33040  IF PC > GO THEN  PRINT : PRINT "YOU DON'T HAVE THAT MUCH MONEY!": GOSUB 57: GOTO 100
 33045  IF BY = 99 THEN 33200
 33047  IF SP%(BY,4) = 17 OR SP%(BY,4) = 18 THEN BY = 1 + (RO = 43) + 16 * (A%(18,4) =  - M - 1)
 33048  IF BY = 3 OR BY = 4 THEN BY = 3 + ((A%(24,4) =  - M - 1) OR (A%(25,4) =  - M - 1))
 33050 SH = 0:SH = (A%(SP%(BY,4),4) =  - M - 1)
 33053  IF BY = 7 THEN SH = (A%(SP%(BY,4),4) =  - M - 1) AND (A%(SP%(BY,5),4) =  - M - 1)
 33055  IF BY = 3 OR BY = 4 THEN SH = (A%(SP%(BY,4),4) =  - M - 1) OR (A%(SP%(BY,5),4) =  - M - 1)
 33060  IF  NOT SP%(BY,4) AND  NOT SP%(BY,5) THEN SH = 1
 33070  IF  NOT SH THEN  IF SP%(BY,6) THEN R = 200 + SP%(BY,6): GOSUB 45: GOTO 100
 33200  IF BY = 99 THEN A%(BA,4) = RO
 33210  PRINT : PRINT "THE "M$(M)" TAKES YOUR MONEY.":GO = GO - PC: GOSUB 57: IF M = 4 THEN 33800
 33220  IF BY = 3 AND A%(8,4) <  >  - M - 1 OR BY = 4 AND A%(24,4) <  >  - M - 1 THEN R = 220: GOSUB 45: GOTO 33900
 33225  IF BY = 7 THEN A%(15,4) = 0:A%(16,4) = 0
 33227  IF BY = 13 THEN A%(14,4) = 0:A%(30,4) = RO:A%(20,4) = 0: GOTO 33250
 33230  FOR I = 1 TO NA: IF A%(I,4) =  - M - 1 THEN A%(I,4) = RO
 33240  NEXT : IF BY < 99 THEN A%(SP%(BY,0),4) = RO
 33250  PRINT "HE GIVES YOU YOUR GOODS.": GOSUB 52:BY = 0: GOTO 100
 33800  IF MA = 0 THEN  IF A%(30,4) THEN MA = 1
 33810  IF MA = 1 THEN  IF A%(6,4) THEN MA = 2
 33820  IF MA = 2 THEN  IF M%(20,5) = 0 THEN MA = 3
 33830  IF MA = 3 THEN  IF A%(46,4) <  > 92 THEN MA = 4
 33840  IF MA = 4 THEN  IF M%(21,5) = 0 THEN MA = 5
 33860 R = HO%(MA): GOSUB 45:BY = 0:MF = 0:MC = 0:MA = MA + 1: IF MA > 5 THEN MC = 10000
 33900  IF A%(9,4) =  - M - 1 THEN A%(9,4) = 443
 33910  IF A%(25,4) =  - M - 1 THEN A%(25,4) = 443
 33920  FOR I = 1 TO NA: IF A%(I,4) =  - M - 1 THEN A%(I,4) = RO
 33930  NEXT :BY = 0: GOTO 100
 34000  REM 

ENTER

 34005  IF RO = 93 THEN 34500
 34010  GOSUB 4900: GOSUB 4803: IF  NOT F THEN 94
 34015  IF A = 3 THEN  PRINT : PRINT "THE OPENING IS TOO SMALL FOR YOU.": GOSUB 57: GOTO 100
 34020  IF TP AND A = 7 THEN  IF RO = TP%(1,TP) THEN R2 = TP%(2,TP):BT = 0: GOTO 3500
 34030  IF TP AND A = 7 THEN  IF RO = TP%(2,TP) THEN R2 = TP%(1,TP):BT = 0: GOTO 3500
 34040  IF RO = 73 AND A = 2 THEN R2 = 93: GOTO 3500
 34050  PRINT : PRINT "TRY GOING IN THAT DIRECTION.": GOSUB 57: GOTO 100
 34500  REM 
MAZE
 34510  PRINT : INPUT "ENTER WHICH COLOR WALL?";S$:SL =  LEN (S$):A = 0: FOR I = 1 TO 10: IF  LEFT$ (TP$(I),SL) = S$ OR  RIGHT$ (TP$(I),SL) = S$ THEN A = I
 34520  NEXT : IF A = 0 THEN 94
 34530  IF A = MZ%(MZ,MW) THEN MZ = MZ%(MZ,MW + 4):MW = 0: IF MZ <  > 5 THEN 100
 34540 MW = 0: FOR I = 33 TO 39: IF A%(I,4) =  - 100 THEN A%(I,4) = 146
 34550  NEXT : FOR I = 1 TO NA: IF A%(I,4) = 93 THEN A%(I,4) = 73
 34560  NEXT :A%(47,4) = 93:R2 = 73 + (MZ = 5) * 21: GOTO 3500
 35000  REM 

SELL

 35010  IF M%(16,5) <  > RO THEN 94
 35015  IF TK = 3 THEN 32017
 35020  GOSUB 4900: GOSUB 4801: IF  NOT F THEN 91
 35030  PRINT : PRINT "HE EXAMINES YOUR "A$(A)".": GOSUB 57
 35035  IF A%(A,1) = 0 THEN  PRINT : PRINT "HE GRUNTS, THEN SAYS, 'I HAVE NO MARKET FOR THIS THING.  YOU KEEP IT.'":L = L + 3: GOSUB 58: GOTO 100
 35036  IF A%(A,2) = 12 OR A = 40 THEN  PRINT "HIS HAND PASSES RIGHT THROUGH IT!"
 35037  IF A%(A,1) =  - 1 THEN R = 254: GOSUB 45: GOTO 100
 35038  IF A%(A,1) =  - 2 THEN R = 247: GOSUB 45: GOTO 100
 35039  IF A%(A,1) =  - 3 THEN R = 248: GOSUB 45: GOTO 100
 35040  IF A%(A,2) = 3 THEN  PRINT : PRINT "HE GRUNTS,  'MAGIC, EH? THAT'LL MAKE IT HARD TO SELL IN THESE PARTS.'":L = L + 3: GOSUB 58
 35050 G1 =  INT (.8 * A%(A,1))
 35055  PRINT : PRINT "'I'LL GIVE YOU ";G1;" GOLD PIECES FOR IT.": GOSUB 57
 35060 L = 0: PRINT : INPUT "WILL YOU SELL IT? ";A$: IF  LEFT$ (A$,1) <  > "Y" THEN 100
 35070 A%(A,4) = RO + 400:GO = GO + G1: IF A = M%(0,9) THEN M%(0,9) =  - 1
 35080  PRINT : PRINT "'DONE!'": GOSUB 57
 35100  GOTO 100
 36000  REM 

WEAR

 36010  GOSUB 4900: GOSUB 4805: IF  NOT F THEN 90
 36015  IF A = 54 THEN 4037
 36020  IF A%(A,2) <  > 11 THEN 90
 36030 A%(A,4) =  - 99: PRINT : PRINT "YOU DON THE "A$(A)".": GOSUB 57
 36040  IF A = 42 THEN LM = 1:LT = 1
 36050  IF A = 43 THEN R = 246: GOSUB 45
 36100  GOTO 300
 37000  REM 

REMOVE

 37010  GOSUB 4900:HA =  - 99:WH = HA:EM = HA: GOSUB 4810: IF  NOT F THEN  PRINT : PRINT "YOU AREN'T WEARING IT!": GOSUB 57: GOTO 300
 37020 A%(A,4) =  - 1: PRINT : PRINT "YOU REMOVE THE "A$(A)".": GOSUB 57
 37030  IF A = 42 THEN LM = 0:LT = NL
 37040  IF A = 43 AND A%(50,4) =  - 1 THEN  PRINT : PRINT "THE BLUE LIGHT BURNS YOUR HAND!": GOSUB 57:M%(0,13) = M%(0,13) + 3:DF = 0: GOSUB 7650:A = 50: GOTO 5040
 37100  GOTO 300
 38000  REM 

EARN

 38010 M = 0: FOR I = 1 TO NM: IF M%(I,5) = RO THEN M = I:I = 99
 38020  NEXT : IF  NOT M THEN 94
 38030  IF M%(M,2) = 0 THEN 300
 38040  IF M <  > 12 THEN R = 249: GOSUB 45: GOTO 300
 38050 R = 238: GOSUB 45: PRINT : INPUT "WILL YOU TAKE THE JOB?";A$: IF  LEFT$ (A$,1) <  > "Y" THEN 100
 38060  IF CH > 2 THEN CH = CH - 1: PRINT : PRINT "YOU FINISH THE JOB, BUT YOUR SELF-ESTEEM SUFFERS, AND YOU LOSE CHARISMA.":L = L + 3: GOSUB 58
 38070  IF HD > 4 AND  RND (1) > .3 THEN HD = HD - 1: PRINT : PRINT "YOU  ALSO  STRAIN YOUR BACK SHOVELING,  REDUCING YOUR HARDINESS.": GOSUB 58
 38100 GO = GO + 20: GOTO 300
 39000  REM 

REQUEST

 39030  GOSUB 4900: GOSUB 4950
 39035  IF M%(M,3) <  > 100 THEN 15000
 39040 EM =  - M - 1:WH = RO + 400:HA = EM: GOSUB 4810: IF  NOT F THEN  PRINT : PRINT "HE DOESN'T HAVE YOUR ";S$;".": GOSUB 57: GOTO 300
 39045  IF A%(A,4) = RO + 400 THEN  PRINT : PRINT "HE HAS IT FOR SALE.": GOSUB 57: GOTO 300
 39050  PRINT : PRINT "HE GIVES YOU YOUR "A$(A)"."
 39060 A%(A,4) = RO: GOTO 300
 50000  FOR A = 1 TO NZ: IF A%(A,4) = 200 + RO OR A%(A,4) = 400 + RO THEN  PRINT A": "A$(A)" (" MID$ ("EMB ",(A%(A,4) <  > 200 + RO) * 4 + 1,4); MID$ ("SALE",(A%(A,4) <  > 400 + RO) * 4 + 1,4)")"
 50010  NEXT : RETURN 

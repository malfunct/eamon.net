 1  REM 

ASSAULT ON DOLNI KEEP

    - BY -

TOM ZUCHOWSKI
7625 HAWKHAVEN DR.
CLEMMONS,NC 27012


 5  REM 

EAMON ADVENTURER'S GUILD
7625 HAWKHAVEN DR.
CLEMMONS, NC 27012


 6  GOTO 31000: REM MP86 11/8/89
 40  IF  NOT LT THEN LF = 0: GOTO 63
 41  RETURN 
 44  PRINT :L = L + 1
 45  PRINT D$"FRE": PRINT D$"READ EAMON.DESC,R"R: INPUT A$: PRINT D$
 50 LL =  INT (( LEN (A$) + CL - 1) / CL):L = L + LL: GOSUB 58: PRINT A$:L = L + LL * (L = 0)
 51  PRINT 
 52 L = L + 1: GOTO 58
 54  PRINT 
 56 L = L + 3: PRINT 
 58  IF L < 23 THEN  RETURN 
 59  PRINT "(PRESS ANY KEY TO CONTINUE) ";: NORMAL : GET Q$: PRINT : VTAB  PEEK (37): PRINT  SPC( 30): PRINT : VTAB  PEEK (37):L = 0: ON Q$ <  >  CHR$ (3) GOTO 58: POP : STOP 
 60 B$ = " WHO ": GOTO 63
 61 B$ = " WHO OR WHAT ": GOTO 63
 62 B$ = " WHAT "
 63  IF  NOT LF AND  NOT LT THEN  PRINT : PRINT "YOU CAN'T FIND ANYTHING.": POP : GOTO 305
 64  IF S$ = "" THEN  PRINT : PRINT C$(C);B$;: INPUT S$: IF S$ = "" THEN  POP : GOTO 105
 65  RETURN 
 70  PRINT : PRINT "NONE HERE BY THAT NAME!": GOTO 105
 80  REM 

COMMENTS
 82  VTAB  PEEK (37) - M * 1 + 1:L = L + M
 84  PRINT D$"FRE": PRINT D$"READ COMMENTS,R"R: INPUT A$: PRINT D$: GOTO 50
 100  REM 

YOU SEE:

 105  GOSUB 56
 110  GOSUB 51
 120  IF LP THEN  IF LC < 5 THEN  PRINT : PRINT "YOUR TORCH IS ALMOST OUT.": GOSUB 56: GOTO 126
 125  IF LP THEN  IF LC < 10 THEN  PRINT : PRINT "YOUR TORCH IS GUTTERING.": GOSUB 56
 126 LT = LP OR NL: IF  NOT LT THEN  PRINT "IT'S TOO DARK TO SEE.": GOSUB 52: PRINT : GOTO 210
 130  PRINT "YOU ARE "RN$:L = L + 1: GOSUB 51
 140  IF  NOT R%(R1,0) THEN R%(R1,0) = 1:R = R1: GOSUB 45
 147  FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 150
 148  IF  NOT M%(M,15) THEN M%(M,15) = 1:R = M + 300: GOSUB 45
 149  NEXT 
 150 G1 = (M%(1,5) = R1):G2 = (M%(2,5) = R1):G3 = (M%(3,5) = R1): FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 169
 155  IF M > 9 THEN A =  INT ((M%(M,1) - M%(M,13) + 4) / 5): PRINT A;" ";: IF A = 1 THEN  PRINT "ORC IS HERE.": GOSUB 52: GOTO 168
 160  PRINT M$(M)" "VB$(M > 9)" HERE.": GOSUB 52
 168  NEXT 
 169  GOSUB 51:A%(40,4) = 0: IF DM%(R1) > 0 THEN A%(40 + (DM%(R1) > 1),4) = R1
 170 A2 = 0: FOR A = 1 TO NZ: IF A%(A,4) <  > R1 THEN  NEXT : GOTO 175
 171  IF  NOT A%(A,9) THEN A2 = A2 + 1:AZ%(A2) = A
 172  IF A%(A,9) THEN  PRINT "YOU SEE ";A$(A);".": GOSUB 52
 173  NEXT 
 175  FOR A = A TO N: IF A%(A,4) = R1 THEN  PRINT "YOUR "A$(A)" IS HERE.": GOSUB 52
 185  NEXT : GOSUB 51
 190  IF A2 = 0 THEN 202
 195  FOR A = 1 TO A2:A%(AZ%(A),9) = 1:R = AZ%(A) + 100: GOSUB 45
 200  NEXT 
 202  FOR I = 0 TO 3: IF M%(CD%(R1,I,0),5) = R1 THEN  IF ( NOT T%(1) OR R1 = 38) AND CD%(R1,I,1) = 1 THEN M = 0: FOR R = CD%(R1,I,3) TO CD%(R1,I,4): GOSUB 80:M = 1: NEXT 
 203  NEXT 
 205  REM 

CMD

 210  INPUT " YOUR COMMAND? ";A$:L = 0:LF = 1: IF A$ =  CHR$ (6) THEN 60000
 220  IF  LEFT$ (A$,1) = " " THEN A$ =  MID$ (A$,2): GOTO 220
 230  IF A$ = "" THEN A$ = CZ$: VTAB  PEEK (37): HTAB 16: PRINT A$
 235  PRINT : IF A$ = "" THEN 275
 240 CZ$ = A$: FOR A = 2 TO  LEN (A$): IF  MID$ (A$,A,1) <  > " " THEN  NEXT 
 250 V$ =  LEFT$ (A$,A - 1):S$ =  MID$ (A$,A + 1)
 260  IF  LEFT$ (S$,1) = " " THEN S$ =  MID$ (S$,2): GOTO 260
 270  FOR A = 1 TO 10: IF  LEFT$ (C$(A), LEN (V$)) = V$ THEN C = A:A = 999: NEXT : GOTO 290
 272  NEXT :F = 0: FOR A = 7 TO NC: IF C$(A) = V$ THEN C = A: GOTO 290
 273  IF  LEFT$ (C$(A), LEN (V$)) = V$ THEN F = F + 1:C = A: IF F > 1 THEN A = 999: NEXT : GOTO 275
 274  NEXT : IF F THEN 290
 275  PRINT : PRINT "YOUR PARDON. I KNOW BUT THESE COMMANDS:": PRINT : FOR C = 1 TO NC / 2: PRINT C$(C); SPC( 20 -  LEN (C$(C)));C$(C + NC / 2): NEXT : PRINT C$(NC): PRINT : PRINT : GOTO 210
 280  IF C$(C) = S$ THEN S$ = V$:V$ = C$(C)
 290  ON C GOTO 3000,3000,3000,3000,3000,3000,6000,6000,7000,10000,4000,5000,8000,9000,15000,17000,28000,19000,20000,21000,22000,24000,14000,13000,29000
 300  REM 

PICK FOE

 305  GOSUB 56
 310  IF  NOT T%(1) THEN 500
 320  GOSUB 51: FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 500
 350 OF = M: ON M%(M,14) GOTO 360,490,400
 360  IF OF > 9 THEN MT$ = M$(OF):M$(OF) = "AN ORC": FOR I = 1 TO  INT ((M%(OF,1) - M%(OF,13) + 4) / 5): IF I > 15 THEN I = 999
 370 DF = 0: FOR M2 = 1 TO 3: IF M%(M2,5) = R1 AND  RND (1) < .25 THEN DF = M2:M2 = 999
 380  NEXT : GOSUB 7500: IF OF > 9 THEN  NEXT :M$(OF) = MT$
 390  GOTO 490
 400  FOR M2 = 5 TO NM: IF M%(M2,5) = R1 THEN DF = M2:M2 = 999
 410  NEXT 
 430  ON (DF > 9) + 1 GOSUB 7500,2000: IF  NOT T%(1) THEN M = 999
 490  NEXT 
 500  REM 

SPECIAL PGM

 510 LP = 0: IF  NOT LS THEN 520
 512  IF A%(LS,4) =  - 1 OR A%(LS,4) = R1 THEN LP = 1
 514 LC = LC - 1: IF LC > 0 THEN 520
 516 LP = 0:LS = 0: PRINT : PRINT "THE TORCH WENT OUT.": GOSUB 56:A%(19,4) = A%(22,4):A%(22,4) = 0
 520  IF T%(1) THEN 529
 522  IF A%(2,4) = 0 THEN  PRINT : PRINT "GALEHIR QUICKLY RESTRINGS HIS BOW.": GOSUB 56:A%(2,4) =  - 3:M%(2,10) = M%(2,10) + A%(2,5):A = M%(2,9):M%(2,9) = 2: IF A > 0 THEN M%(2,10) = M%(2,10) - A%(A,5)
 524  FOR A2 = 1 TO 2: IF A%(A2,4) = R1 THEN OF = A2: GOSUB 7430
 526  NEXT : FOR A2 = 4 TO 5: IF A%(A2,4) = R1 THEN OF = A2 - 3: GOSUB 7430
 528  NEXT 
 529  IF AL <  > 1 THEN 535
 530  GOSUB 12300: IF M%(10,5) <  > 0 AND M%(10,5) <  > R1 THEN M%(10,5) = 10
 531  IF R1 < 14 AND R1 <  > 8 AND R1 <  > 11 THEN 536
 532 R = 220: GOSUB 45:M = 0: IF G1 THEN R = 19: GOSUB 80
 533  IF G2 THEN R = 20: GOSUB 80
 534  IF R1 > 33 AND R1 < 37 AND G1 THEN R = 52: GOSUB 80
 535  IF  NOT AL OR AL > 4 THEN 540
 536 AL = AL + 1: IF M%(10,5) > 8 AND M%(10,5) <  > R1 THEN M%(10,5) = M%(10,5) - 1
 537  IF M%(9,5) = 36 AND A%(29,4) = 36 THEN A%(29,4) = 0:A%(34,4) = 36: IF  NOT RF THEN M%(9,5) = 0
 538  FOR M = 15 TO NM: IF M%(M,2) = AL THEN M%(M,5) =  ABS (M%(M,5))
 539  NEXT :M%(5,5) = 0: GOSUB 3600
 540  IF R1 = 31 AND AL THEN 555
 541  IF R1 = 37 OR R1 = 38 THEN 580
 542 OC =  NOT OC: IF R1 < 33 OR AL < 5 THEN 580
 545  IF OC AND BO AND M%(2,5) < 40 THEN R = 50 + ( RND (1) < .75):M = 0: GOSUB 80
 546  IF OC THEN 580
 550  IF  NOT BR AND R1 > 40 OR  NOT BO THEN 560
 552  IF R1 > 39 THEN 580
 555  PRINT : PRINT "SEVERAL ARROWS LAND AMONG THE COMPANY!": GOSUB 56: FOR A = 0 TO 3: IF (A = 0 OR M%(A,5) = R1) AND  RND (1) < .25 THEN M = A:A = 99: IF R1 <  > 31 THEN A%(35,4) = R1
 557  NEXT : IF A > 5 AND  RND (1) < .6 THEN  PRINT M$(M);" IS HIT!": GOSUB 56:R =  - 1: GOSUB 12110
 558  GOTO 580
 560  IF BR AND R1 > 39 THEN  PRINT : PRINT "ARRIVING ORCS DOUSE THE FIRE!": GOSUB 56:A%(33,4) = 0:BR = 0
 565 M%(22,5) = R1:M%(22,1) = M%(22,1) + 25: GOSUB 3600: IF R1 < 41 THEN M%(22,15) = 0
 570  IF R1 > 40 THEN  PRINT : PRINT "MORE ENTER THE CRACK!": GOSUB 56
 580  IF BR THEN BR = BR + 1
 585  IF OC AND A%(35,4) = R1 AND G2 THEN R = 56:M = 0: GOSUB 80
 589  REM 

ROOM ROUTINES

 590  ON R1 GOTO 605,610,615,620,625,630,635,640,645,650,655,660,665,670,675,680,685,690,695,700,705,710,715,720,725,730,735,740,745,750,755,760,765,770,775,780,785,790,795,800,805,810,815,820,825,830,835,840
 605  IF RF = 2 AND G3 AND M%(4,5) = R1 THEN R = 48:M = 0: GOSUB 80
 606  IF LG THEN  IF M%(4,5) = R1 AND  NOT G1 OR  NOT G2 OR  NOT G3 THEN R = 54:M = 0: GOSUB 80
 609  GOTO 110
 610 LG = 1: IF  NOT WG THEN R = 201: GOSUB 45:M%(1,5) = 5:WG = 1
 614  GOTO 110
 615 GL = 0: GOTO 110
 620 OP = 0: GOSUB 870: GOTO 110
 625 GL = 0:OP = 0: IF WG = 1 THEN R = 206: GOSUB 45:A%(7,4) = 0:A%(12,4) = 0:A%(13,4) =  - 2:WG = 2: GOTO 110
 630 OP = 0: GOSUB 870: GOTO 110
 635  GOTO 850
 640  IF  NOT AP AND OP THEN 850
 641  IF GL THEN 860
 642  IF AP AND  NOT AL THEN  GOSUB 12200
 644  GOTO 110
 645  IF GB THEN CD%(R1,2,0) = 0
 646 GB = 1: IF  NOT AP AND OP THEN 850
 647  IF GL OR AP THEN 860
 649  GOTO 110
 650  GOTO 850
 655 GL = 0:OP = 0: GOTO 110
 660 OP = 0:GL = 0: IF  NOT T%(1) OR  NOT MG OR AL THEN MG = 1: GOTO 110
 661 MG =  INT ( RND (1) * 9 + 1): IF MG > 3 THEN 110
 662 R = 234: GOSUB 44:M%(11,13) = M%(11,13) + 5:F%(1) = F%(1) + 5:MM = (M%(11,13) < M%(11,1)):T%(1) = T%(1) * MM:M%(11,5) = M%(11,5) * MM: IF MG < 5 THEN MM = MG - ((MG > 2) * 2): IF M%(MM,5) = R1 THEN R = 234 + MM: GOSUB 45: GOTO 110
 663  IF MG < 6 THEN AL = 1: IF G1 THEN R = 19:M = 0: GOSUB 80
 664  GOTO 110
 665  GOTO 850
 670  IF SS = 1 THEN AR = AR + 1:SS = 2
 671  IF  NOT R%(R1,0) THEN R = 219: GOSUB 45: GOTO 110
 674  GOTO 110
 675  IF  NOT SS AND G2 AND  NOT AL THEN R = 205: GOSUB 45:SS = 1:AR = AR - 1:A%(18,4) = 14
 676  IF  NOT AL AND  NOT SS AND  NOT G2 THEN  GOSUB 12200
 679  IF C <  > 7 THEN 110
 680  IF  NOT R%(R1,0) THEN R = 204: GOSUB 45: GOTO 110
 685  GOTO 110
 690  GOSUB 870: IF AL AND  NOT SB AND R3 = 25 THEN R = 47:M = 0: GOSUB 80:SB = 1
 694  GOTO 110
 695  GOTO 110
 700  GOTO 110
 705  GOTO 110
 710  IF  NOT R%(R1,0) THEN R = 219: GOSUB 45: GOTO 110
 715  GOTO 110
 720  GOTO 110
 725  GOSUB 870: IF R3 <  > 31 OR M%(3,5) <  > R1 THEN 729
 726 M%(3,5) = 0:A%(27,4) = 18: IF G2 THEN R = 31:M = 0: GOSUB 80: GOTO 729
 727  NEXT 
 729  GOTO 110
 730  GOTO 110
 735  IF R3 = 29 THEN CD%(R1,1,0) = 0
 739  GOTO 110
 740  GOTO 110
 745  GOTO 110
 750  GOTO 110
 755  GOSUB 870: GOTO 110
 760  IF AB THEN CD%(R1,0,0) = 0
 761  IF  NOT AB THEN AB = 1
 764  GOTO 110
 765  IF RF AND  NOT BG THEN BG = 1: IF G1 THEN R = 222: GOSUB 45: GOTO 110
 769  GOTO 110
 770  IF RF AND  NOT AL THEN  GOSUB 12200
 774  GOTO 110
 775  IF RF AND  NOT AL THEN FC = FC - 1: IF FC < 0 THEN  GOSUB 12200
 776  IF A%(28,4) = R1 THEN R%(R1,5) = 39
 777  IF BR THEN A%(33,4) = 35
 779  GOTO 110
 780  IF RF THEN CD%(R1,0,0) = 0
 781  IF  NOT RF THEN RF = 1:FC =  RND (1) * 10
 784  GOTO 110
 785  GOTO 110
 790  IF  NOT AL THEN R = 220: GOSUB 45:AL = 2
 791  IF G3 THEN GS = 1
 792  IF GS THEN CD%(R1,0,0) = 0
 793  IF  NOT GS THEN GS = 1
 794  GOTO 110
 795  IF  NOT BR THEN 110
 796  PRINT : PRINT "THE FLAMES GROW HIGHER!": GOSUB 56: IF BR < 3 THEN 799
 797  PRINT : PRINT "YOU ARE BURNED BY THE FLAMES!": GOSUB 56: GOSUB 12100
 799  GOTO 110
 800  IF BR THEN A%(33,4) = 40
 804  GOTO 110
 805  GOTO 110
 810  GOTO 110
 815  GOTO 110
 820  GOTO 110
 825  GOTO 110
 830 A = 44 + (M%(12,5) = 30):CD%(47,0,3) = A:CD%(47,0,4) = A
 834  GOTO 110
 835  GOTO 110
 840  GOSUB 870: GOTO 850
 849  REM 

ROOM SUBR.

 850  IF M%(10,13) =  > M%(10,1) THEN 110
 851 OP = OP + (OP < 4): ON OP GOTO 852,854,856,858
 852 R = 202: GOSUB 45: GOTO 110
 854  PRINT : PRINT "THE PATROL GROWS CLOSER.": GOTO 105
 856 R = 203: GOSUB 45: GOTO 110
 858 AP = 1:M%(10,5) = R1: GOSUB 3600: GOTO 110
 859  GOTO 110
 860 GL = GL + 1: IF GL > 2 THEN AL = AL + 1: IF AL = 1 THEN 530
 861  IF AL > 1 THEN  GOSUB 12300
 863  GOTO 110
 870  FOR A =  - 3 TO  - 1: FOR X = 22 TO 23: IF A%(X,4) = A AND (M%( ABS (A + 1),5) = R1 OR A =  - 1) THEN A%(X,4) = 0:A%(8,4) = A:LS = 0
 872  NEXT : NEXT : RETURN 
 2000  REM 

AN ORC

 2010 MT$ = M$(DF):M$(DF) = "AN ORC": GOSUB 7500:M$(DF) = MT$: RETURN 
 3000  REM 

MOVE

 3010 F = 0: IF T%(1) THEN R2 = 905: GOTO 3060
 3020 B$ =  LEFT$ (V$,1): FOR D = 1 TO 6: IF B$ = C$(D) THEN R2 = R%(R1,D)
 3040  NEXT :CT = 0: FOR I = 0 TO 3: IF M%(CD%(R1,I,0),5) = R1 AND CD%(R1,I,2) = C THEN  IF CD%(R1,I,1) = 2 OR CD%(R1,I,1) = 3 AND (RF < 2 OR A%(34,4) <  > 0) AND  NOT T%(1) THEN M = 0: FOR R = CD%(R1,I,3) TO CD%(R1,I,4): GOSUB 80:M = 1: NEXT :CT = 1
 3045  NEXT 
 3047  IF GB THEN CD%(R1,2,0) = 0
 3050  IF CT THEN  PRINT  SPC( CL / 2 - 7)"CONTINUE ";C$(C);"? ";: GET A$: PRINT A$: GOSUB 56:CT = 0:L = 0: IF A$ <  > "Y" THEN 110
 3055  IF F THEN  PRINT : PRINT "YOU ARE HIT FROM BEHIND AS YOU FLEE!": GOSUB 56: GOSUB 12100
 3058  IF R2 = 901 THEN  IF R1 > 13 AND R1 < 18 THEN  PRINT : PRINT "  (YOU MUST USE 'N' OR 'S' HERE TO SHOW WHICH SIDE OF THE WALL YOU WANT.)":L = L + 1: GOTO 305
 3060  IF R2 > 900 THEN R2 = R2 - 899: PRINT : PRINT "THE ";VB$(R2);" ";VB$(13): GOTO 305
 3065  IF R2 > 700 THEN 3400
 3070  IF  NOT R2 AND A%(28,4) <  > R1 THEN  PRINT : PRINT "YOU CAN'T GO THAT WAY.": GOTO 105
 3075  IF  NOT R2 AND A%(28,4) = R1 THEN  PRINT : PRINT "THE CEILING IS TOO LOW.": GOTO 105
 3080  IF R2 < 0 THEN 3200
 3100  REM 
GO

 3105  IF FT THEN FT = 0: IF C = 5 THEN R2 = 2
 3110 R3 = R1:R1 = R2: GOSUB 3600
 3130  PRINT D$"READEAMON.ROOM.NAME,R"R1: INPUT RN$: PRINT D$:NL = R%(R1,7):LT = NL: IF A%(LS,4) =  - 1 OR A%(LS,4) = R1 THEN LT = NL OR LS
 3140  FOR I = 0 TO 3: IF M%(CD%(R3,I,0),5) = R1 AND CD%(R3,I,2) = C THEN  IF CD%(R3,I,1) = 4 THEN M = 0: FOR R = CD%(R3,I,3) TO CD%(R3,I,4): GOSUB 80:M = 1: NEXT 
 3150  NEXT 
 3190  GOTO 500
 3200  IF R2 =  - 99 THEN  PRINT : PRINT "YOU HAVE RETURNED TO FOREST LOTHLORIEN.": GOTO 32010
 3210  IF R2 =  - 33 AND  NOT BO THEN R2 = 33: GOTO 3100
 3215  IF R2 =  - 33 AND BO THEN R2 = 31: IF G3 THEN R = 26:M = 0: GOSUB 80:CT = 1: GOTO 3050
 3218  IF R2 =  - 40 AND A%(33,4) = R1 THEN  PRINT : PRINT "THE FLAMES ARE TOO HIGH!": GOTO 305
 3220  IF R2 =  - 40 OR R2 =  - 47 THEN  PRINT : PRINT "THE ROOF CRACK IS TOO HIGH!"
 3230  IF R2 =  - 2 AND A%(14,4) = R1 THEN R2 = 2: GOTO 3100
 3240  IF R2 =  - 2 THEN  PRINT : PRINT "WHICH TREE?"
 3290  GOTO 300
 3400  REM 
SPEC. EFF.

 3410  IF R2 = 802 OR R2 = 716 THEN 3460
 3411  IF R2 = 725 THEN R = 221
 3412  IF R2 = 704 THEN R = 218
 3413  IF R2 = 718 THEN R = 218: GOSUB 45
 3414  IF R2 = 703 OR R2 = 708 OR R2 = 720 AND R1 = 15 OR R2 = 711 THEN 3480
 3415  IF R2 = 714 OR R2 > 717 AND R2 < 722 THEN R = 217: IF  NOT AL THEN AL = 1
 3416  IF R2 = 801 THEN 3470
 3417  IF R2 = 740 AND  NOT BR AND G2 THEN R = 225: GOSUB 45:R2 = 40:CT = 1: GOTO 3050
 3418  IF R2 = 740 AND BR AND  NOT BO AND G2 THEN R = 233: GOSUB 45:R2 = 40:CT = 1: GOTO 3050
 3419  IF R2 = 740 THEN 3440
 3420  IF R2 = 803 THEN R = 216
 3421  IF (R2 = 726 OR R2 = 728) AND AL AND G1 THEN R = 46:M = 0: GOSUB 80:R2 = R2 - 700:CT = 1: GOTO 3050
 3422  IF R2 = 726 OR R2 = 728 THEN 3440
 3430  GOSUB 45: GOSUB 56
 3440  IF R2 < 800 THEN R2 = R2 - 700: GOTO 3100
 3450  GOTO 310
 3460  IF A%(6,4) =  - 1 THEN R = 210: GOSUB 45: IF G2 THEN R = 211: GOSUB 45: GOTO 305
 3462  IF A%(6,4) =  - 3 AND G2 THEN R = 211: GOSUB 45: GOTO 305
 3464  IF A%(6,4) <  >  - 2 OR  NOT G1 THEN 3430
 3466  IF R2 = 802 THEN R = 208: GOSUB 45:R = 209: GOSUB 45: GOSUB 56: GOTO 3440
 3468  IF R2 = 716 THEN R = 212: GOTO 3430
 3470  IF G2 THEN R = 215: GOTO 3430
 3472 R2 = 0: GOTO 3070
 3480  IF R2 = 711 AND A%(6,4) < 0 AND A%(6,4) >  - 4 THEN 3440
 3482  PRINT : PRINT "THE LANDING IS HARD!!": GOSUB 56: GOSUB 12100
 3484  IF R2 = 720 THEN 3415
 3486  GOTO 3440
 3600  REM 

ENEMY? SBR.

 3610 T%(1) = 0:T%(3) = M%(0,1):F%(1) = 0:F%(3) = M%(0,13)
 3620  FOR M = 1 TO NM
 3625  IF R1 <  > R3 AND M%(M,5) = R3 AND M <  > 4 THEN  IF M%(M,14) = 3 OR M%(M,14) = 1 AND 300 *  RND (1) < M%(M,1) THEN M%(M,5) = R1
 3630  IF M%(M,5) <  > R1 THEN  NEXT : RETURN 
 3640  IF  NOT M%(M,14) THEN M%(M,14) = 3 - 2 *  NOT M%(M,3)
 3650 A = M%(M,14):F%(A) = F%(A) + M%(M,13):T%(A) = T%(A) + M%(M,1)
 3660  NEXT : RETURN 
 4000  REM 

GET

 4020 LF = 0: GOSUB 62: IF S$ = "ALL" THEN 4160
 4030 HA = R1:D = N: GOSUB 4805: PRINT : IF  NOT F THEN 12010
 4040  IF A = 24 THEN 4200
 4045  IF A = 28 AND RF < 2 AND A%(34,3) > 100 THEN  PRINT : PRINT "YOU MUST FIRST FREE FROMIR!": GOTO 305
 4050  IF A = 29 OR A = 34 AND A%(A,3) > 100 THEN  PRINT : PRINT "HE IS CHAINED TO THE RACK!": GOTO 305
 4060  IF A%(A,3) > 900 THEN  PRINT "DON'T BE ABSURD.": GOTO 305
 4070  IF WT + A%(A,3) > 10 * M%(0,1) THEN  PRINT "IT IS TOO HEAVY FOR YOU.": GOTO 305
 4075  IF A = 26 OR A = 45 THEN  PRINT "IT IS TOO HOT TO TOUCH!": GOTO 305
 4080  PRINT "GOT IT.":A%(A,4) =  - 1:SZ = SZ + A%(A,0):WT = WT + A%(A,3): GOSUB 56: IF (A%(A,2) = 2 OR A%(A,2) = 3) AND M%(0,9) =  - 1 THEN 17100
 4090  IF A = 22 THEN LS = A:LT = 1
 4100  IF  NOT A%(A,2) THEN M = 1: GOTO 4190
 4110  GOTO 310
 4160  GOSUB 51:M = 0: FOR A = 1 TO N: IF A%(A,4) <  > R1 THEN  NEXT : GOTO 310
 4170  IF WT + A%(A,3) > 10 * M%(0,1) THEN  PRINT A$(A);" IS TOO HEAVY": GOSUB 52: GOTO 4185
 4180  PRINT A$(A) + " TAKEN.": GOSUB 52:A%(A,4) =  - 1:WT = WT + A%(A,3): IF  NOT A%(A,2) THEN M = 1
 4185  NEXT 
 4190  GOTO 310
 4200  IF G1 THEN R = 223: GOSUB 45:A%(24,4) = 0:BO = 1:R2 = 34: IF M%(22,5) = R1 THEN M%(22,1) = 10:R = 49:M = 0: GOSUB 80
 4210  GOTO 3100
 4700  REM 

MON SRCH SBR.

 4710 B =  LEN (S$):F = 0: FOR X = 1 TO NM: IF  LEFT$ (M$(X),B) = S$ THEN  GOSUB 4780
 4720  NEXT : IF F = 200 THEN  RETURN 
 4730  FOR X = 1 TO NM: IF  RIGHT$ (M$(X),B) = S$ THEN  GOSUB 4780
 4750  NEXT : RETURN 
 4780  IF M%(X,5) <  > R1 THEN  RETURN 
 4782 F = F + 1: IF F = 1 THEN M = X
 4784  IF S$ = M$(X) THEN M = X:X = 999:F = 200
 4786  RETURN 
 4800  REM 

ART SRCH

 4801 HA =  - 1:WH =  - 1:EM =  - 1: FOR X = 1 TO 4:GI(X) =  - 1: NEXT :D = N: GOSUB 4812: IF  NOT F AND T THEN  PRINT : PRINT "YOU AREN'T CARRYING A ";S$: GOSUB 56
 4802  IF  NOT F THEN  POP : GOTO 110
 4803  RETURN 
 4804 HA =  - 1: FOR X = 1 TO 4:GI(X) =  - X - 1: NEXT 
 4805 WH = R1:EM = R1 + 200
 4810  IF HA <  >  - 1 THEN  FOR X = 1 TO 4:GI(X) = HA: NEXT 
 4812 B =  LEN (S$):F = 0: FOR X = 1 TO D: IF  LEFT$ (A$(X),B) = S$ THEN  GOSUB 4880
 4820  NEXT : IF F = 200 THEN  RETURN 
 4830  FOR X = 1 TO D: IF  RIGHT$ (A$(X),B) = S$ THEN  GOSUB 4880
 4840  NEXT : IF F = 200 THEN  RETURN 
 4850 T = 1: IF F > 1 THEN  PRINT : PRINT "WHICH ";S$;" DO YOU WANT?": GOSUB 56:F = 0:T = 0
 4855  IF  NOT T THEN  POP : GOTO 110
 4860  RETURN 
 4880 T = A%(X,4): IF T = WH OR T = HA OR T = EM THEN 4884
 4881  FOR I = 1 TO 4: IF T = GI(I) THEN I = 99: NEXT : GOTO 4884
 4882  NEXT : RETURN 
 4884 F = F + 1:A = X
 4886  IF S$ = A$(X) THEN X = 999:F = 200
 4888  RETURN 
 5000  REM 

DROP

 5010  GOSUB 62: IF S$ = "ALL" THEN 5100
 5030  GOSUB 4801
 5040 WT = WT - A%(A,3):A%(A,4) = R1: PRINT : PRINT A$(A);" DROPPED.": GOSUB 56
 5050  IF A = M%(0,9) THEN M%(0,9) =  - 1
 5070  GOTO 310
 5100  FOR A = 1 TO N: IF A%(A,4) =  - 1 THEN  PRINT A$(A);" DROPPED.":A%(A,4) = R1: GOSUB 52
 5110  NEXT :WT = 0:M%(0,9) =  - 1: GOTO 310
 6000  REM 

LOOK

 6010  GOSUB 40: IF S$ = "" THEN 6090
 6020 D = NZ: GOSUB 4804: IF  NOT F THEN 6040
 6024  IF A%(A,4) = 200 + R1 THEN A%(A,4) = R1
 6030 A%(A,9) = 1:R = A + 100: GOSUB 45: IF A = 10 THEN  PRINT "THERE ARE ";A%(A,7);" SWALLOWS LEFT.": GOSUB 52
 6033  IF A = 23 THEN  IF G1 THEN R = 57:M = 0: GOSUB 80
 6035  GOTO 310
 6040  GOSUB 4700: IF F THEN R = M + 300: GOSUB 45:DF = M: GOSUB 7650: GOTO 310
 6090 F = 0: FOR A = 1 TO NZ: IF A%(A,4) = R1 + 300 THEN A%(A,4) = R1:F = 1: IF A = 14 THEN FT = 1
 6100  NEXT : IF F THEN  PRINT : PRINT "YOU FOUND SOMETHING.": GOSUB 56:R%(R1,0) = 0
 6200 R%(R1,0) =  NOT R%(R1,0): GOTO 310
 7000  REM 

ATTACK

 7100  IF M%(0,9) =  - 1 THEN  PRINT : PRINT "YOU HAVE NO WEAPON READY!": GOTO 105
 7110 LF = 0: GOSUB 61: GOSUB 4700: IF F THEN 7290
 7150 HA = R1:D = N: GOSUB 4805: IF  NOT F THEN 70
 7170  PRINT : PRINT "WHAM!!  YOU HIT THE ";A$(A);"!": GOSUB 56: IF A = 24 OR A = 25 THEN R = 226: GOSUB 45: GOTO 310
 7180  IF A = 16 THEN R = 213: GOSUB 45: IF G1 THEN R = 214: GOSUB 45
 7190  IF  NOT GL THEN 7220
 7200 AL = AL + 1: IF AL < 2 THEN  GOSUB 12200: GOTO 7220
 7210 R = 228: GOSUB 45: GOSUB 12100
 7220 GL = 1: GOTO 310
 7290 OF = 0:DF = M: IF DF > 9 THEN  GOSUB 2000
 7292  IF DF < 5 AND R1 = 1 THEN  PRINT : PRINT "   LAHALOS KNOCKS YOUR WEAPON FROM YOUR HAND!":A%(M%(0,9),4) = R1:M%(0,9) =  - 1:R = 55:M = 0: GOSUB 80: GOTO 305
 7295  IF DF < 10 THEN  GOSUB 7500
 7300  IF  NOT HIT THEN 7360
 7310  IF INC THEN W2 = A%(M%(0,9),6):WA%(W2) = WA%(W2) + 2
 7320  IF  - EA <  = AE THEN 300
 7330  IF  INT (100 *  RND (1) + 1) <  = M%(0,10) THEN 7360
 7340 A = 2: IF EA + AE =  - 1 THEN A = 1
 7350 AE = AE + A:M%(0,10) = M%(0,10) + A
 7360  IF M%(DF,14) = 1 OR DF = 0 THEN 300
 7370 M%(DF,3) = M%(DF,3) / 2: FOR M = 1 TO NM: IF M%(M,5) = R1 AND M%(M,14) > 1 THEN M%(M,14) = 0
 7380  NEXT :R3 = R1: GOSUB 3600: GOTO 310
 7400  REM 

P/U WEAP SBR.

 7403  IF OF > 2 THEN 7406
 7404  IF A%(OF,4) = R1 THEN A2 = OF: GOTO 7430
 7405  IF A%(OF + 3,4) =  - OF - 1 THEN A = OF + 3: PRINT : PRINT M$(OF);" READIES HIS ";A$(A);"!": GOSUB 56: GOSUB 12400: GOTO 7440
 7406  IF OF = 3 AND A%(11,4) = R1 THEN A2 = 11: GOTO 7430
 7407  IF OF = 3 THEN 7440
 7410 A2 = 0: FOR A = 1 TO N: IF A%(A,4) = R1 THEN  IF A%(A,2) = 2 OR A%(A,2) = 3 THEN  IF A%(A,7) * A%(A,8) > A%(A2,7) * A%(A2,8) THEN A2 = A
 7425  NEXT 
 7430  IF A2 > 0 THEN  PRINT M$(OF);" PICKS UP ";A$(A2);".": GOSUB 54:A%(A2,4) =  - OF - 1:M = OF:A = A2: GOSUB 12400
 7440  RETURN 
 7500  REM 

DO BATTLE SBR.

 7505 HIT = 0: IF M%(OF,9) =  - 1 THEN 7400
 7508 A = A%(M%(OF,9),6): PRINT M$(OF);: IF OF = 1 THEN BK =  INT ( RND (1) * 10): IF BK < 5 THEN  PRINT " CRIES, '";BK$(BK);"'": GOSUB 52: PRINT " HE";
 7510  PRINT " ";BV$(A, INT ( RND (1) * 3));" AT ";M$(DF): GOSUB 52:RL =  INT (100 *  RND (1) + 1): IF (RL < 5 OR RL < M%(OF,10) - M%(DF,7)) AND RL < 96 THEN HIT = 1: GOTO 7600
 7520  IF RL < 97 THEN  PRINT " --";RB$(A, INT ( RND (1) * 2));"!": GOTO 52
 7525 A = M%(OF,9): PRINT " --";A$(A);" FUMBLED--";:FR =  INT (100 *  RND (1) + 1): IF FR <  = 50 THEN  PRINT "-BUT RECOVERED!": GOTO 52
 7530  IF FR < 76 THEN  PRINT "AND DROPPED!": IF OF < 4 THEN A%(A,4) = R1:M%(OF,10) = M%(OF,10) - A%(A,5):M%(OF,9) =  - 1:M%(OF,4) = M%(OF,4) / 2
 7531  IF FR < 76 THEN 52
 7532  IF A > 0 THEN  IF A%(A,2) = 3 THEN  PRINT : PRINT "SPARKS FLY FROM ";M$(OF);"'S ";A$(A);"!": GOTO 56
 7535  IF FR > 95 THEN  PRINT "-";M$(OF);" INJURED!": GOTO 7550
 7540  PRINT "-AND BROKEN!": IF OF < 4 THEN A%(A,4) = 0:M%(OF,10) = M%(OF,10) - A%(A,5):M%(OF,9) =  - 1:M%(OF,4) = M%(OF,4) / 2
 7542  IF  RND (1) > .5 THEN 52
 7545  PRINT : PRINT " --BROKEN ";A$(A);" HURTS ";M$(OF);"!": GOSUB 56
 7550 D = M%(OF,11):S = M%(OF,12):DT = DF:DF = OF:A = 1: IF FR = 100 THEN D = D * 2:A = 0
 7560  GOSUB 7635:DF = DT: RETURN 
 7600 INC = ( INT (100 *  RND (1) + 1) > M%(OF,10)):M%(OF,10) = M%(OF,10) + 2 * INC:D = M%(OF,11):S = M%(OF,12)
 7605 A = 1: PRINT  MID$ (" ** ++",(DF > 0) * 3 + 1,3);: IF RL > 5 THEN  PRINT "A HIT!": GOTO 7635
 7610  PRINT "WELL STRUCK!":R2 =  INT (100 *  RND (1) + 1): IF R2 < 51 THEN A = 0: GOTO 7635
 7615  IF R2 < 86 THEN S = 1.5 * S: GOTO 7635
 7620  IF R2 < 96 THEN D = 2 * D: GOTO 7635
 7625  IF R2 < 100 THEN D = 3 * D: GOTO 7635
 7628  IF DF > 9 THEN D2 = 5: GOTO 7639
 7630  GOTO 7700
 7635 D2 = 0: FOR D3 = 1 TO D:D2 = D2 +  INT (S *  RND (1) + 1): NEXT :D2 = D2 - A * M%(DF,8): IF D2 < 1 THEN  PRINT " --REBOUNDS OFF ARMOUR!": GOTO 56
 7638  IF DF > 9 AND D2 > 5 THEN D2 = 5
 7639  IF DF > 9 AND M%(DF,13) + D2 =  > M%(DF,1) THEN D2 = 5
 7640 A = M%(DF,14):M%(DF,13) = M%(DF,13) + D2: IF M%(DF,13) >  = M%(DF,1) AND DF < 10 THEN 7700
 7645 F%(A) = F%(A) + D2
 7647  IF DF > 9 THEN  PRINT " --AN ORC IS ";: ON D2 GOTO 7660,7665,7670,7675: PRINT "DEAD!": GOSUB 56:M%(DF,5) = M%(DF,5) * (M%(DF,13) < M%(DF,1)): GOTO 7750
 7650  PRINT " --";M$(DF);" IS ";: ON  INT (M%(DF,13) * 5 / M%(DF,1) + 2 - (M%(DF,13) = 0)) GOTO 7652,7655,7660,7665,7670,7675
 7652  PRINT "IN PERFECT HEALTH.": GOTO 56
 7655  PRINT "OK.": GOTO 56
 7660  PRINT "HURT.": GOTO 56
 7665  PRINT "BADLY HURT.": GOTO 56
 7670  PRINT "GRAVELY INJURED.": GOTO 56
 7675  PRINT "AT DEATH'S DOOR.": GOTO 56
 7700  PRINT " --";M$(DF);" IS DEAD!": GOSUB 56:F%(A) = F%(A) + M%(DF,1) - M%(DF,13) + D2:M%(DF,5) = 0
 7710  FOR AJ = 1 TO N: IF A%(AJ,4) =  - DF - 1 THEN A%(AJ,4) = R1
 7720  NEXT : IF M%(DF,9) > 0 THEN A%(M%(DF,9),4) = R1:M%(DF,10) = M%(DF,10) - A%(M%(DF,9),5):M%(DF,9) =  - 1:M%(DF,4) = M%(DF,4) / 2
 7740  IF DF = 0 THEN DI = 1: GOTO 32000
 7750  IF F%(1) =  > T%(1) THEN T%(1) = 0
 7770 A2 = DF + 35: IF A2 > 39 THEN DM%(R1) = DM%(R1) + 1
 7780  IF A2 < 40 THEN A%(A2,4) = R1:A%(A2,9) = 1:R = A2 + 100: GOTO 45
 7790  RETURN 
 8000  REM 

FLEE

 8010  IF  NOT T%(1) THEN  PRINT : PRINT "THERE'S NOTHING TO FLEE FROM!": GOTO 305
 8020  IF T%(1) - F%(1) > 49 THEN R2 = 905: GOTO 3060
 8030  IF S$ = "" THEN 8060
 8040  IF S$ <  > "" THEN  FOR D = 1 TO 6: IF S$ = C$(D) THEN R2 = R%(R1,D):D = 6
 8050  NEXT : GOTO 8070
 8060 R2 = R%(R1, INT (6 *  RND (1) + 1)): IF R2 < 1 OR R2 > 900 THEN 8060
 8070 F = 1: IF G2 THEN R = 32:M = 0: GOSUB 80:CT = 1: GOTO 3050
 8080  GOTO 3055
 9000  REM 

GIVE

 9010  GOSUB 40: IF S$ = "" THEN 9040
 9020  FOR X = 1 TO  LEN (S$): IF  MID$ (S$,X,4) = " TO " AND X > 2 THEN B$ =  MID$ (S$,X + 4):A$ =  LEFT$ (S$,X - 1):X = 999: NEXT : GOTO 9050
 9030  NEXT 
 9040  GOSUB 62:A$ = S$: INPUT "TO WHOM?";B$: GOSUB 56: IF B$ = "" THEN 110
 9050 S$ = B$: GOSUB 4700: IF  NOT F THEN 70
 9060 S$ = A$: IF  VAL (A$) > 0 THEN 9500
 9070  GOSUB 4801
 9090  PRINT : PRINT "OKAY.": GOSUB 56: IF A = M%(0,9) THEN M%(0,9) =  - 1
 9095  IF A = 10 AND M%(M,14) = 3 THEN 9600
 9100 A%(A,4) =  - M - 1: IF M = 3 AND A <  > 11 AND (A%(A,2) = 3 OR A%(A,2) = 2) THEN R = 33:M = 0: GOSUB 80: GOTO 9120
 9110  IF M%(M,10) <  > 0 AND M%(M,9) =  - 1 THEN  GOSUB 12400
 9120 R3 = R1: GOSUB 3600: GOTO 5050
 9500  IF  VAL (S$) > GO THEN  PRINT : PRINT "YOU AREN'T CARRYING THAT MUCH GOLD!": GOTO 305
 9540 GO =  INT (GO -  VAL (S$)):M%(M,0) = M%(M,0) +  INT ( VAL (S$)):M%(M,14) = 0: IF M%(M,3) < 100 THEN M%(M,3) = M%(M,3) * (1 +  VAL (S$) / 100)
 9550  GOTO 9120
 9600  REM 

MIRUVOR TO FRIEND
 9610  PRINT M$(M);" THANKS YOU.": IF  NOT A%(A,7) THEN  PRINT "BUT THE BOTTLE IS EMPTY.": GOSUB 56: GOTO 9100
 9620  IF M%(M,13) = 0 THEN 9100
 9630  IF M%(M,13) > 0 THEN  PRINT "HE SIPS THE MIRUVOR. HIS WOUNDS HEAL!": GOSUB 56:GF = GF + 1:A%(A,7) = A%(A,7) - 1:M%(M,13) = M%(M,13) - A%(A,6): IF M%(M,13) < 0 THEN M%(M,13) = 0
 9640 DF = M: GOSUB 7650
 9650  PRINT : PRINT "HE RETURNS THE BOTTLE TO YOU.": GOTO 305
 10000  REM 

INVEN

 10005  IF  NOT LT THEN LF = 0: GOSUB 63
 10010  IF S$ = "" THEN M = 0:X =  - 1:W =  - 99: GOTO 10110
 10020  GOSUB 4700: IF  NOT F THEN 70
 10030  IF M%(M,14) <  > 3 THEN  FOR M = M TO M: GOTO 15030
 10040 X =  - M - 1:W = X
 10110  GOSUB 51: PRINT M$(M);" IS CARRYING THE FOLLOWING:": FOR A = 1 TO N: IF A%(A,4) <  > X AND A%(A,4) <  > W THEN  NEXT : GOTO 10150
 10120  PRINT " ";A$(A);: IF A = M%(M,9) THEN  PRINT " (READY WEAPON)";
 10130  IF A%(A,4) =  - 99 THEN  PRINT " (BEING WORN)";
 10140  GOSUB 51: NEXT 
 10150  IF  NOT M THEN  PRINT "  ";GO;" GOLD PIECES.": GOSUB 52
 10155  IF  NOT M THEN  GOSUB 51:DF = M: GOSUB 7650
 10160  GOTO 310
 12000  REM 

MISC. SUBR.

 12010  PRINT "YOU CAN'T ";C$(C);" ";S$: GOTO 105
 12020  PRINT "YOU CAN'T ";C$(C);" ";A$(A): GOTO 305
 12100  FOR M = 0 TO 3: IF M%(M,5) <  > R1 AND M <  > 0 THEN 12130
 12110 A = M%(M,13) = M%(M,1) - 1:M%(M,13) = M%(M,13) +  INT ( RND (1) * 10): IF  NOT A AND C <  > 13 AND M%(M,13) >  = M%(M,1) THEN M%(M,13) = M%(M,1) - 1
 12115 DF = M: IF M%(M,13) >  = M%(M,1) THEN  GOSUB 7700: GOTO 12125
 12120  GOSUB 7650
 12125  IF C = 18 OR R < 0 THEN  RETURN 
 12130  NEXT : RETURN 
 12200 R = 217: GOSUB 45: IF  NOT AL THEN AL = 1
 12210  RETURN 
 12300  IF R1 <  > 12 AND M%(11,5) = 12 THEN M%(11,5) = 9: GOSUB 3600
 12310  RETURN 
 12400  IF (A%(A,2) = 2 OR A%(A,2) = 3) THEN M%(M,9) = A:M%(M,10) = M%(M,10) + A%(A,5):M%(M,4) = 2 * M%(M,4): IF M = 3 THEN M%(3,10) = 95
 12405  IF A <  > 11 THEN  RETURN 
 12410  PRINT : PRINT "THE RING ";: IF M <  > 3 THEN  PRINT "GLOWS DIMLY."
 12420  IF M = 3 THEN  PRINT "FLASHES BRIGHTLY!"
 12430  GOSUB 56: RETURN 
 13000  REM 

INFO

 13010 R = 229: GOSUB 45:R = 232: GOSUB 45: GOTO 105: REM R#230-231 ARE DOS 3.3 ONLY
 14000  REM 

REQUEST

 14010  GOSUB 40: IF S$ = "" THEN 14040
 14020  FOR X = 1 TO  LEN (S$): IF  MID$ (S$,X,6) = " FROM " AND X > 2 THEN A$ =  LEFT$ (S$,X - 1):B$ =  MID$ (S$,X + 6):X = 999: NEXT : GOTO 14050
 14030  NEXT 
 14040  GOSUB 62:A$ = S$: INPUT "FROM WHOM?";B$: GOSUB 56: IF B$ = "" THEN 110
 14050 S$ = B$: GOSUB 4700: IF  NOT F THEN 70
 14060  IF M%(M,14) <  > 3 THEN  FOR M = M TO M: GOTO 15030
 14070 S$ = A$:HA =  - M - 1:WH = HA:EM = HA:D = N: GOSUB 4810: IF  NOT F THEN  PRINT : PRINT M$(M);" DOESN'T HAVE IT.": GOTO 105
 14080  IF A < 6 THEN  PRINT : PRINT M$(M);" WILL NOT GIVE UP HIS WEAPONS.": GOSUB 56:R = 26 + M: GOSUB 84: GOTO 310
 14085  IF A = 7 OR A = 12 THEN R = 207: GOSUB 45: GOTO 310
 14090  PRINT : PRINT "OKAY.": GOSUB 56:A%(A,4) =  - 1: GOTO 5050
 15000  REM 

SMILE

 15010  GOSUB 40: GOSUB 51: FOR M = 1 TO NM: IF M%(M,5) <  > R1 THEN  NEXT : GOTO 15040
 15020  PRINT M$(M);: ON M%(M,14) GOSUB 15050,15060,15070
 15030  GOSUB 52: NEXT 
 15040  GOSUB 51: GOTO 310
 15050  PRINT " GROWL";: IF M < 10 OR (M%(M,1) - M%(M,13) < 6) THEN  PRINT "S";
 15055  PRINT " AT YOU!": RETURN 
 15060  PRINT " IGNORES YOU.": RETURN 
 15070  PRINT " SMILES BACK.": RETURN 
 17000  REM 

READY

 17020  GOSUB 62: GOSUB 4801
 17030  IF A%(A,2) < 2 OR A%(A,2) > 3 THEN  PRINT : PRINT "THAT ISN'T A WEAPON!": GOTO 305
 17100 M%(0,10) = (EA + AE) * ( - EA > AE) + A%(A,5) + WA%(A%(A,6)) + 2 * M%(0,2):M%(0,9) = A:M%(0,11) = A%(A,7):M%(0,12) = A%(A,8)
 17110  PRINT : PRINT "READIED.": GOSUB 56: IF A = 11 THEN  GOSUB 12410
 17120  GOTO 310
 19000  REM 

LIGHT

 19010 LF = 0: GOSUB 62:D = N: GOSUB 4804: PRINT : IF  NOT F THEN 12010
 19012  IF (R1 <  > 30 AND R1 <  > 35 OR A <  > 23 AND A <  > 30) AND  NOT LS THEN  PRINT : PRINT "YOU HAVE NOTHING TO LIGHT IT WITH.": GOTO 305
 19015  IF (A = 28 OR A = 9) AND LS THEN  PRINT : PRINT "THE WOOD OF THE RACK DIDN'T IGNITE.": GOTO 305
 19025  IF A = 22 THEN  PRINT : PRINT "IT IS ALREADY LIT!": GOTO 305
 19035  IF A%(26,4) = R1 + 200 THEN A%(26,4) = R1:A%(26,9) = 1
 19042  IF A <  > 30 THEN 19050
 19045  PRINT : PRINT "THE STRAW IGNITES!": GOSUB 56: IF R1 = 39 AND A%(A,4) = R1 THEN R = 224: GOSUB 45:BR = 1:A%(28,4) = 0
 19047  IF A%(A,4) =  - 1 THEN R = 227: GOSUB 45:M = 0: GOSUB 12110
 19048 A%(A,4) = 0: GOTO 310
 19050  PRINT : PRINT "THE TORCH IGNITES!":L = L + 1
 19060 A%(22,4) = A%(23,4):A%(23,4) = 0:LC = A%(22,5):LS = 22: GOTO 305
 20000  REM 

OPEN

 20010 LF = 0: GOSUB 62: IF  RIGHT$ (S$,4) = "GATE" THEN  PRINT : PRINT "IT IS BARRED FROM WITHIN.": GOTO 305
 20020  GOSUB 62: GOSUB 4801: IF A = 13 THEN R = 113: GOSUB 45: GOTO 310
 20090  PRINT : PRINT "IT DOESN'T OPEN.": GOTO 305
 21000  REM 

PUT

 21010  PRINT : INPUT "PUT WHAT? ";A$: IF A$ = "" THEN 305
 21020  INPUT "INTO WHAT? ";B$: IF B$ = "" THEN 305
 21150  PRINT : PRINT "YOU CAN'T PUT ";A$;" INTO ";B$: GOTO 305
 22000  REM 

DRINK

 22010  GOSUB 40: GOSUB 62: GOSUB 4801: PRINT : IF  NOT F THEN 12000
 22020  IF A%(A,2) <  > 6 THEN 12020
 22110  IF A%(A,7) < 1 THEN  PRINT "THERE IS NO ";A$(A);" LEFT.": GOTO 305
 22130 A%(A,7) = A%(A,7) - 1:M%(0,13) = M%(0,13) - A%(A,6): PRINT "YOUR WOUNDS SEEM TO HEAL!": IF M%(0,13) < 0 THEN M%(0,13) = 0
 22150  GOSUB 56:DF = 0: GOSUB 7650: GOTO 310
 24000  REM 

FREE

 24010 LF = 0: GOSUB 62:HA = R1:D = N: GOSUB 4805: IF  NOT F THEN 70
 24020  IF A <  > 29 AND A <  > 34 THEN 12010
 24025  IF A = 34 THEN A%(A,3) = 100
 24030 M = 0: IF M%(9,5) = R1 THEN R = 29: GOSUB 80: GOTO 310
 24040 R = 30:M = 0: GOSUB 80: IF A = 29 THEN M%(3,5) = R1:M%(3,13) = 10:A%(29,4) = 0:M%(3,14) = 3:RF = 2
 24050  GOTO 310
 28000  REM 

SAVE

 28010  PRINT : PRINT "DO YOU WANT TO SAVE THIS GAME? (Y/N) :";: GET A$: PRINT A$: ON A$ <  > "Y" GOTO 100: PRINT D$"FRE": PRINT D$"STORESAVED.GAME": GOTO 100
 29000  REM 

RESUME GAME

 29010  POKE 216,0: PRINT : PRINT "RESUME THE SAVED GAME? (Y/N): ";: GET A$: PRINT A$: ON A$ <  > "Y" GOTO 100: ROT= CL: PRINT D$"RESTORESAVED.GAME":CL =  PEEK (249): GOTO 100
 31000  REM 

INIT

 31010  PRINT  CHR$ (4)"RESTOREFAST.START"
 31020  PRINT D$"OPEN FRESH.MEAT": PRINT D$"READ FRESH.MEAT": INPUT RE,M$(0),M%(0,1),M%(0,2),CH: FOR A = 1 TO 4: INPUT SA%(A): NEXT : FOR A = 1 TO 5: INPUT WA%(A): NEXT : INPUT AE,SEX$,GO,BA,AC
 31030  DIM W$(4),W%(4,4): FOR A = 1 TO 4: INPUT W$(A): FOR M = 1 TO 4: INPUT W%(A,M): NEXT : NEXT : PRINT D$"CLOSE"
 31040  FOR M = 1 TO 4: IF W$(M) <  > "NONE" THEN N = N + 1:A$(N) = W$(M):A%(N,2) = 2:A%(N,3) = 2:A%(N,4) =  - 1:A%(N,5) = W%(M,2):A%(N,6) = W%(M,1):A%(N,7) = W%(M,3):A%(N,8) = W%(M,4): NEXT 
 31050  FOR A2 = 1 TO N - NZ: FOR A = 1 TO NZ: IF A$(A) = W$(A2) THEN A$(A) = A$(A) + "#":A = 999: NEXT :A2 = 999: NEXT : GOTO 31050
 31060  NEXT : NEXT :EA = 0:M%(0,8) = 0:A2 =  INT (AC / 2): IF A2 * 2 <  > AC THEN M%(0,8) = 1:EA =  - 5
 31070  IF A2 THEN M%(0,8) = M%(0,8) + A2:EA = EA - A2 * 10: IF A2 = 3 THEN M%(0,8) = M%(0,8) + 2:EA = EA - 30
 31080 M%(0,14) = 3: IF N > NZ THEN M%(0,10) = (EA + AE) * ( - EA > AE) + W%(1,2) + WA%(W%(1,1)) + 2 * M%(0,2):M%(0,9) = NZ + 1:M%(0,11) = W%(1,3):M%(0,12) = W%(1,4)
 31090  PRINT D$"OPENEAMON.DESC,L256": PRINT D$"OPENEAMON.ROOM.NAME,L64": PRINT D$"OPENCOMMENTS,L128": IF  PEEK (43604) = 195 THEN CL = 80
 31100  GOSUB 58: GOTO 3100
 32000  REM 

DONE

 32010  POKE  - 16368,0: PRINT : PRINT : GOSUB 59: PRINT : PRINT D$"CLOSE"
 32030  IF DI THEN  PRINT : PRINT "YOU HAVE DIED!": FOR I = 1 TO 2000: NEXT : GOTO 32500
 32040  FOR A = 1 TO 4:W$(A) = "NONE": NEXT 
 32050 M = 0: FOR A = 1 TO N: IF A%(A,4) =  - 1 THEN  IF A%(A,2) > 1 AND A%(A,2) < 4 THEN M = M + 1:A%(A,4) =  - 100:R%(M,0) = A
 32060  NEXT :R%(M + 1,0) = 0: IF M < 5 THEN 32120
 32070  PRINT FF$: HOME : VTAB 5: PRINT "AS YOU ENTER LOTHLORIEN, YOU ARE STOPPED AND TOLD THAT YOU HAVE TOO MANY WEAPONS--4 IS THE LEGAL LIMIT.": PRINT : PRINT "YOUR WEAPONS ARE--"
 32080  FOR A = 1 TO N: PRINT  SPC( 4)A"--"A$(R%(A,0)): IF  NOT R%(A + 1,0) THEN A = 999
 32090  NEXT 
 32100  PRINT : PRINT "ENTER THE NUMBER OF THE WEAPON TO SELL     (1-";M;: INPUT ") ";A$:X =  INT ( VAL (A$)): IF X < 1 OR X > M THEN  VTAB  PEEK (37) - 80 / CL: GOTO 32100
 32110 A%(R%(X,0),4) =  - 1:R%(X,0) = R%(M,0):R%(M,0) = 0:M = M - 1: IF M > 4 THEN 32070
 32120  FOR W = 1 TO 4:K = R%(W,0): IF K THEN W$(W) = A$(K):W%(W,1) = A%(K,6):W%(W,2) = A%(K,5):W%(W,3) = A%(K,7):W%(W,4) = A%(K,8)
 32130  NEXT : PRINT FF$: HOME : VTAB 5: PRINT "YOU PRESENT YOUR TREASURES TO THE LOCAL BUYER OF TREASURES (UNDER THE SIGN OF 3 BALLS). HE EXAMINES YOUR GOODS AND PAYS YOU ";
 32140 TP = 0: FOR A = 1 TO NZ: IF A%(A,4) <  >  - 1 THEN  NEXT : GOTO 32180
 32150  IF A%(A,2) <  > 0 THEN A%(A,1) = A%(A,1) * CH / 10
 32160 TP = TP + A%(A,1)
 32170  NEXT 
 32180  PRINT TP;" GOLD PIECES.": PRINT 
 32190 GO = GO + TP: IF GO < 0 THEN GO = 0
 32200  POKE  - 16368,0: VTAB 23: GOSUB 59: PRINT 
 32210  PRINT D$"OPEN XFER": PRINT D$"WRITE XFER": PRINT M$(0): PRINT M%(0,1): PRINT M%(0,2): PRINT CH: FOR A = 1 TO 4: PRINT SA%(A): NEXT 
 32230  FOR A = 1 TO 5: PRINT WA%(A): NEXT : PRINT AE: PRINT SEX$: PRINT GO: PRINT BANK: PRINT AC: FOR A = 1 TO 4: PRINT W$(A): FOR M = 1 TO 4: PRINT W%(A,M): NEXT : NEXT 
 32240  PRINT RE: PRINT G1: PRINT G2: PRINT G3: PRINT RF: PRINT D$"CLOSE": PRINT D$"-POSTLUDE"
 32500  REM 

DEAD

 32510  PRINT  CHR$ (26)"1": REM TURNS OFF VIDEX VIDEOTERM
 32520  PRINT  CHR$ (21): REM TURNS OFF IIE 80-COL
 32540  HOME : PRINT : PRINT D$"DELETE FRESH.MEAT": PRINT D$"STORE SAVED.GAME": PRINT D$"DELETE SAVED.GAME"
 32550  PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT S$: PRINT D$"PREFIX"S$: PRINT D$"VERIFYMAIN.HALL": POKE 216,0
 32560  PRINT A$
 32570  PRINT D$"OPENTHE.ADVENTURER": PRINT D$"CLOSE": PRINT D$"DELETETHE.ADVENTURER": PRINT D$"-MAIN.HALL"
 60000  PRINT : PRINT "EMB: ";: FOR A = 1 TO NZ: IF A%(A,4) = 200 + R1 THEN  PRINT A$(A),;
 60010  NEXT : PRINT : PRINT : GOTO 210

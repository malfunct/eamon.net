 1  REM DOLNI KEEP POSTLUDE
 2  REM PL3  3/9/86 
 10 D$ =  CHR$ (4):FF$ =  CHR$ (12): DIM M$(40)
 20  PRINT D$"OPEN XFER": PRINT D$"READ XFER": FOR I = 1 TO 38: INPUT M$(I): NEXT : INPUT R,G1,G2,G3,RF: PRINT D$"CLOSE": PRINT D$"DELETE XFER"
 30 GG = G1 + G2:RF = RF + G3 + 1
 40  HOME : PRINT FF$: PRINT : PRINT "ONCE AGAIN YOU STAND BEFORE THE LADY OF LOTHLORIEN.": PRINT 
 50  ON RF GOTO 60,60,60,110
 60  PRINT : PRINT "'IT IS WITH GREAT SORROW THAT I SEE THE QUEST WAS NOT FULFILLED.": ON RF GOTO 70,70,100
 70  PRINT : PRINT "PERHAPS WE HAD MISJUDGED THE DIFFICULTY OF REMAINING UNSEEN.": ON RF GOTO 90,80
 80  PRINT : PRINT "NOW WE KNOW HIS TORMENT IS AT AN END.": GOTO 120
 90  PRINT : PRINT "NOW IT IS HOPELESS;  EVEN IF FROMIR WAS NOT SLAIN, THE ORCS WILL NOW BE ON THEIRGUARD.": GOTO 120
 100  PRINT : PRINT "AH!  TO ACHIEVE THE RESCUE, BUT FAIL IN THE ESCAPE!": GOTO 80
 110  PRINT : PRINT "'IT IS WITH GREAT JOY THAT ONCE AGAIN I LAY MINE EYES ON GOOD FROMIR!": PRINT 
 120  PRINT : PRINT : IF GG < 2 THEN 160
 130  PRINT : IF RF = 4 THEN  PRINT "AND";
 140  IF RF < 4 THEN  PRINT "AT LEAST";
 150  PRINT " THE COMPANIONS ARE SAFE!": GOTO 220
 160  IF RF = 4 THEN  PRINT "BUT";
 170  IF RF < 4 THEN  PRINT "AND";
 180  PRINT " AT SUCH A COST!": PRINT : IF  NOT G1 THEN  PRINT "GORIM ";
 190  IF  NOT GG THEN  PRINT "AND ";
 200  IF  NOT G2 THEN  PRINT "GALEHIR ";
 210  PRINT "WILL BE SORELY MISSED!"
 220  GOSUB 9000
 230  HOME : PRINT FF$: PRINT "YOU FOUND LITTLE ENOUGH TREASURE ON THE QUEST; IT IS NOW YOU SHALL BE REWARDED  ACCORDING TO YOUR DESERTS.":GL = GG * 500 - 1000:GL = GL + RF * 500 - 1000:GL = GL + 4000 * (RF = 4)
 240  PRINT : PRINT : PRINT "FOR YOUR EFFORTS, YOU ARE ";: IF GL < 0 THEN  PRINT "FINED "; ABS (GL): GOTO 260
 250  PRINT "AWARDED ";GL
 260  PRINT " GOLD PIECES."
 270 M$(16) =  STR$ ( VAL (M$(16)) + GL)
 1000  GOSUB 9000
 1010  PRINT  CHR$ (26);"1": REM TURN OFF VIDEX VIDEOTERM
 1020  PRINT  CHR$ (26); CHR$ (17): REM TURN OFF IIE 80-COL
 1030  HOME : PRINT D$"STORESAVED.GAME": PRINT D$"DELETESAVED.GAME": PRINT D$"DELETE FRESH.MEAT"
 1040  HOME 
 1050  PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$: PRINT D$"VERIFYCHARACTERS": POKE 216,0
 1060  PRINT D$"OPEN CHARACTERS,L150": PRINT D$"WRITE CHARACTERS,R"R: FOR I = 1 TO 39: PRINT M$(I): NEXT 
 1070  PRINT D$"OPEN THE.ADVENTURER": PRINT D$"WRITE THE.ADVENTURER": PRINT M$(1): PRINT R: PRINT D$"CLOSE"
 1080  PRINT D$"-MAIN.HALL"
 9000  VTAB 21: POKE  - 16368,0: PRINT : PRINT : PRINT "(HIT ANY KEY TO CONTINUE...)": GET A$: PRINT A$: RETURN 

 1  REM COMMENT EDITOR
 2  REM VER.A
 3  REM  2/3/86
 4  REM 


 5  HOME : INPUT "INSERT DUNGEON DISK & HIT RETURN";A$
 10 D$ =  CHR$ (4):DQ$ =  CHR$ (34)
 20  PRINT D$;"OPEN COMMENTS,L128": REM  PRINT D$;"OPEN COMDATA,L16"
 30  PRINT D$;"READ COMMENTS,R0"
 40  INPUT NC
 50  PRINT D$
 100  HOME : VTAB 5
 105 I$ = "":I2$ = ""
 110  PRINT "INPUT YOUR CHOICE:"
 120  PRINT : PRINT : PRINT "1. ADD COMMENT": PRINT : PRINT "2. EDIT COMMENT": PRINT : PRINT "3. LIST": PRINT : PRINT "4. EXIT"
 130  GET A$: PRINT A$:A =  VAL (A$): IF (A < 1 OR A > 4) AND A$ <  >  CHR$ (3) THEN 100
 140  IF A$ =  CHR$ (3) THEN  END 
 150  ON A GOTO 1000,2000,4000,3000
 600  REM ///ROUTINE TO INPUT I$, HAVING BEEN PASSED I2$
 605 I$ = "":IS = 0
 610  PRINT I2$;: CALL  - 958: IF  LEN (I2$) THEN  FOR A = 1 TO  LEN (I2$): PRINT  CHR$ (8);: NEXT 
 615  GET A$: IF A$ =  CHR$ (13) THEN  CALL  - 958: PRINT : GOTO 700
 618  IF A$ =  CHR$ (17) AND LK THEN  POP : POP : GOTO 900
 620  IF (A$ >  CHR$ (31) OR A$ =  CHR$ (9) OR (A$ =  CHR$ (21) AND  NOT  LEN (I2$))) AND  LEN (I$) +  LEN (I2$) > 120 THEN  PRINT  CHR$ (7);: GOTO 615
 622  IF A$ >  CHR$ (31) AND IS THEN  PRINT A$;:I$ = I$ + A$: GOTO 610
 625  IF A$ >  CHR$ (31) THEN  PRINT A$;:I$ = I$ + A$:I2$ =  MID$ (I2$,2): GOTO 615
 630  IF A$ =  CHR$ (5) THEN  PRINT I2$;:I$ = I$ + I2$:I2$ = "": GOTO 615
 635  IF A$ =  CHR$ (2) AND  LEN (I$) THEN  FOR A = 1 TO  LEN (I$): PRINT  CHR$ (8);: NEXT :I2$ = I$ + I2$:I$ = "": GOTO 615
 640  IF A$ =  CHR$ (4) THEN I2$ =  MID$ (I2$,2): GOTO 610
 642  IF A$ =  CHR$ (9) AND CI THEN IS =  NOT IS:H =  PEEK (36):V =  PEEK (37): HTAB 39: VTAB 1: INVERSE : PRINT  MID$ (" I",IS + 1,1): NORMAL : HTAB H + 1: VTAB V + 1: GOTO 615
 645  IF A$ =  CHR$ (9) THEN I2$ = " " + I2$: GOTO 610
 650  IF A$ =  CHR$ (21) AND  LEN (I2$) THEN A$ =  LEFT$ (I2$,1): GOTO 625
 655  IF A$ =  CHR$ (21) THEN A$ = " ": GOTO 625
 660  IF A$ =  CHR$ (8) AND  LEN (I$) > 1 THEN I2$ =  RIGHT$ (I$,1) + I2$:I$ =  LEFT$ (I$, LEN (I$) - 1): PRINT A$;: GOTO 615
 665  IF A$ =  CHR$ (8) AND  LEN (I$) THEN I2$ = I$ + I2$: PRINT A$;: GOTO 600
 670  IF A$ =  CHR$ (8) THEN 600
 675  IF A$ =  CHR$ (27) THEN  PRINT I2$:I$ = I$ + I2$: GOTO 700
 680  GOTO 615
 700  IF  LEN (I$) > 125 THEN I$ =  LEFT$ (I$,125)
 710  RETURN 
 1000  REM ADD COMMENT
 1010 NC = NC + 1
 1020  HOME : VTAB 5: PRINT "ENTER COMMENT # ";NC: PRINT 
 1030  GOSUB 600
 1060  PRINT D$;"WRITE COMMENTS,R";NC
 1070  PRINT DQ$;I$
 1080  PRINT D$
 1090  GOTO 100
 2000  REM EDIT COMMENT
 2010  HOME : VTAB 5: PRINT "ENTER REC# TO EDIT: (1 - ";NC;")"
 2020  INPUT EC
 2030  PRINT D$;"READ COMMENTS,R"EC"
 2040  INPUT I2$
 2045  PRINT D$
 2050  GOSUB 600
 2060  PRINT D$;"WRITE COMMENTS, R"EC"
 2070  PRINT DQ$;I$
 2080  PRINT D$
 2090  GOTO 100
 3000  REM EXIT
 3010  PRINT D$;"WRITE COMMENTS, R0"
 3020  PRINT NC
 3030  PRINT D$;"CLOSE"
 3100  END 
 4000  REM LIST
 4010  HOME : VTAB 5
 4020  PRINT "OUTPUT TO PRINTER?": INPUT P$
 4030  PRINT : PRINT "STARTING #: ";: INPUT BE$:BE =  VAL (BE$): IF BE$ = "" THEN BE = 1
 4040  PRINT : PRINT "ENDING # (1-";NC;"): ";: INPUT EN$:EN =  VAL (EN$): IF EN$ = "" THEN EN = NC
 4050  IF P$ = "Y" THEN  PRINT D$;"PR#1"
 4060  FOR I = 1 TO NC: PRINT : PRINT "COMMENT #";I;":"
 4070  PRINT D$;"READ COMMENTS, R";I
 4080  INPUT A$
 4090  PRINT D$
 4100  PRINT A$
 4110  NEXT 
 4120  PRINT D$;"PR#0"
 4130  PRINT : INPUT "HIT RETURN TO CONTINUE";A$
 4140  GOTO 100

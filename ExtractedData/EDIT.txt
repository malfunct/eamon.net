 1  REM 

EAMON UNIVERSAL DUNGEON EDIT
BY FRANK KUNZE 1994

 2  REM REV 10 NOV 94/23 DEC 02
 5  REM 

EAMON ADVENTURER'S GUILD
7625 HAWKHAVEN DR.
CLEMMONS, NC 27012

 6  GOTO 50000
 10 D$ =  CHR$ (4):CL$ = D$ + "CLOSE":O$ = D$ + "OPENEAMON.":W$ = D$ + "WRITEEAMON.":R$ = D$ + "READEAMON.": PRINT  CHR$ (21): TEXT : HOME : VTAB 5: HTAB 11: PRINT "EAMON DUNGEON EDIT": HTAB 15: PRINT "UNIVERSAL": VTAB 10
 15  FOR X = 1 TO 7000: NEXT :B$ = D$ + "PREFIX"
 25  ONERR  GOTO 45
 30 A$ = D$ + "UNLOCKEAMON.": PRINT A$"DESC": PRINT A$"ROOMS": PRINT A$"ARTIFACTS": PRINT A$"MONSTERS"
 35 DR = 1: PRINT O$"NAME": PRINT R$"NAME": INPUT AN$: INPUT ND: INPUT MP: PRINT CL$: POKE 216,0: IF MP < 7 THEN 50
 40 MP = 7:LD = 242: GOTO 55
 45  PRINT CL$: IF  PEEK (222) <  > 5 THEN 40000
 50  POKE 216,0:MP = 6:LD = 256:LA = 128:LM = 128:LR = 64:LN = 64: IF ND < 6 THEN ND = 6:MP = 5
 55  PRINT O$"DESC,L"LD: PRINT R$"DESC,R0": INPUT N%(1),N%(2),N%(3),N%(4): IF MP >  = 7 THEN  PRINT : INPUT LR,LM,LA
 60  PRINT D$: GOSUB 35000: ON MP >  = 7 GOTO 90: PRINT A$"ROOM.NAME": GOTO 90
 65  REM 

OPEN ROOM FILES

 70  IF  NOT RM THEN RM = 1:AM = 0: PRINT CL$"EAMON.ARTIFACTS": PRINT CL$"EAMON.MONSTERS": PRINT O$"ROOMS,L"LR: IF MP < 7 THEN  PRINT O$"ROOM.NAME,L"LN
 72  RETURN 
 75  REM 

OPEN ART. & MON.

 77  ON AM GOTO 72: PRINT CL$"EAMON.ROOMS": IF MP < 7 THEN  PRINT CL$"EAMON.ROOM.NAME"
 80 RM = 0:AM = 1: PRINT O$"ARTIFACTS,L"LA: PRINT O$"MONSTERS,L"LM: RETURN 
 90  REM ONERR GOTO 40000  
 100  REM 

MENU

 110 NW = 0: HOME : FOR T = 1 TO 4: PRINT T$(T)"-"N%(T)"  ";: NEXT : PRINT : VTAB 3: PRINT  TAB( 4)"EDITING ";: INVERSE : PRINT AN$: PRINT : PRINT  MID$ ("LOCK",1,LK * 4);: NORMAL 
 120  PRINT "YOUR CHOICES ARE--": PRINT : PRINT "  1. ADD NEW ROOM,ARTIFACT,EFFECT OR": PRINT  TAB( 6)"MONSTER": PRINT : PRINT "  2. EDIT OLD ROOM,ARTIFACT, EFFECT": PRINT  TAB( 6)"MONSTER"
 130  PRINT : PRINT "  3. LIST NAMES OF ROOMS, ARTIFACT,ETC": PRINT : PRINT "  4. DUPLICATE A ROOM, ARTIFACT": PRINT  TAB( 6)"OR MONSTER"
 140  PRINT : PRINT "  5. SPECIAL FUNCTIONS": PRINT : PRINT "  6. QUIT": PRINT : PRINT "ENTER KEY FOR YOUR CHOICE (1-6) ";
 150  GET A$: IF A$ =  CHR$ (12) THEN LK =  NOT LK: GOTO 100
 160 C =  VAL (A$): ON C < 1 OR C > 6 GOTO 150: PRINT C: ON C GOTO 1000,2000,3000,4000,5000,6000
 500  REM 

SELECT WHAT TO WORK ON

 510  VTAB 2: CALL  - 958: PRINT "DO YOU WANT TO "C$" A ROOM, ARTIFACT,": PRINT "  EFFECT, OR MONSTER (HIT KEY, RAEM)  ";
 520  GET A$: FOR A = 1 TO 8: IF A$ <  >  MID$ ("RAEMraem",A,1) THEN  NEXT : GOTO 520
 530  IF A > 4 THEN A = A - 4
 540  PRINT A$:TY = A: FOR A = A TO A: NEXT : RETURN 
 600  REM 

INPUT I$

 605  IF  NOT TL THEN TL = 238
 610  CALL  - 958: IF TL > 5 THEN  HTAB 1:V =  PEEK (37) + 1: FOR X = 1 TO TL: PRINT U$;: NEXT : PRINT : VTAB V
 615 I$ = ""
 620  PRINT I2$;: IF TL > 5 THEN  PRINT U$;: PRINT  CHR$ (8);
 625  IF  LEN (I2$) THEN  FOR X = 1 TO  LEN (I2$): PRINT  CHR$ (8);: NEXT 
 630  GET A$: IF A$ =  CHR$ (4) THEN I2$ =  MID$ (I2$,2): GOTO 620
 635 X =  LEN (I$) +  LEN (I2$) = TL:SL =  LEN (I2$): IF A$ >  CHR$ (31) THEN  ON X GOTO 685: PRINT A$;:I$ = I$ + A$:I2$ =  MID$ (I2$,2): GOTO 630
 640  IF A$ =  CHR$ (9) THEN  ON X GOTO 685:I2$ = " " + I2$: GOTO 620
 645  IF A$ =  CHR$ (5) THEN  PRINT I2$;:I$ = I$ + I2$:I2$ = "": GOTO 630
 650  IF A$ =  CHR$ (2) AND  LEN (I$) THEN  FOR A = 1 TO  LEN (I$): PRINT  CHR$ (8);: NEXT :I2$ = I$ + I2$:I$ = "": GOTO 630
 655  IF A$ =  CHR$ (21) THEN  ON  NOT SL GOTO 685:A$ =  LEFT$ (I2$,1): GOTO 635
 660  IF A$ =  CHR$ (8) AND  LEN (I$) > 1 THEN I2$ =  RIGHT$ (I$,1) + I2$:I$ =  LEFT$ (I$, LEN (I$) - 1): PRINT A$;: GOTO 630
 665  IF A$ =  CHR$ (8) AND  LEN (I$) THEN I2$ = I$ + I2$: PRINT A$;: GOTO 615
 670  IF A$ =  CHR$ (8) THEN 615
 675  IF A$ =  CHR$ (27) THEN I$ = I$ + I2$: PRINT : ON TL < 10 GOTO 72: VTAB V: CALL  - 958: VTAB V: PRINT I$: PRINT : RETURN 
 680  IF A$ =  CHR$ (13) THEN  ON I$ = "" GOTO 685: CALL  - 958: PRINT : ON TL < 10 GOTO 72: PRINT : RETURN 
 685  PRINT  CHR$ (7);: GOTO 630
 700  REM 

GET NUMBER

 710  VTAB 6: CALL  - 958: PRINT "INPUT NUMBER OF "T$(TY)" TO "C$
 720  PRINT "   (1-"N%(TY);: INPUT ")  ";A$:NO =  VAL (A$):NO =  INT (NO): IF NO < 1 OR NO > N%(TY) + (C$ = "CREATE") THEN  POP : GOTO 100
 750  RETURN 
 1000  REM 

ADD ITEM

 1010 C$ = "ADD": GOSUB 500:N%(TY) = N%(TY) + 1:NO = N%(TY):RE = NO + RL%(TY):UP = 1: ON TY GOTO 1100,1200,1300,1400
 1100  REM 

ADD ROOM

 1120 NA$ = "":DE$ = "": FOR A = 1 TO ND:RD%(A) = 0: NEXT :LT = 1
 1130  GOSUB 11000: GOSUB 31100: GOSUB 18000: GOTO 100
 1200  REM 

ADD ARTIFACT

 1220 NA$ = "":DE$ = "":NW = 1
 1230  GOSUB 12000: GOSUB 31200: GOSUB 18000: GOTO 100
 1300  REM 

ADD EFFECT

 1320 DE$ = ""
 1330  GOSUB 13000: GOSUB 18000: GOTO 100
 1400  REM 

ADD MONSTER

 1420 NA$ = "":DE$ = "": FOR A = 1 TO MF:M%(A) = MD(A): NEXT 
 1430  GOSUB 14000: GOSUB 18000: GOSUB 31400: GOTO 100
 2000  REM 

EDIT OLD ITEM

 2010 C$ = "EDIT": GOSUB 500: GOSUB 700:RE = NO + RL%(TY):NW = 0: GOSUB 20000: ON TYPE GOTO 2100,2200,1330,2400
 2100  REM 

EDIT ROOM

 2110  GOSUB 21000: GOTO 1130
 2200  REM 

EDIT ART.

 2220  GOSUB 22000: GOTO 1230
 2400  REM 

EDIT MON.

 2410  GOSUB 24000: GOTO 1430
 3000  REM 

LIST THINGS

 3010 C$ = "LIST": GOSUB 500: IF  NOT N%(TY) THEN  PRINT "NONE OF THAT TYPE TO PRINT!": FOR A = 1 TO 2000: NEXT : GOTO 100
 3030  ON TYPE GOTO 3100,3200,3300,3400
 3100 A$ = "ROOMS":F$ = A$: GOSUB 65: ON MP >  = 7 GOTO 3500:F$ = "ROOM.NAME": GOTO 3500
 3200 A$ = "ARTIFACTS":F$ = A$: GOSUB 75: GOTO 3500
 3300  IF PT THEN  PRINT D$"PR#1"
 3305  TEXT : HOME :L = 0: PRINT "LISTING EFFECTS": PRINT : POKE 34,2: FOR A = 1 TO N%(TY):RE = A + RL%(TY): GOSUB 20000: PRINT "EFFECT #"A:L = L + 1
 3310  IF PT THEN  FOR X = 1 TO  LEN (DE$) STEP 80: PRINT  MID$ (DE$,X,40):L = L + 1: NEXT 
 3320  IF  NOT PT THEN  PRINT DE$:L = L +  LEN (DE$) / 39: IF  INT (L) <  > L THEN L =  INT (L) + 1
 3330  PRINT :L = L + 1: IF L > 19 + 30 * PT THEN  GOSUB 3600
 3370  NEXT : PRINT D$"PR#0": GOTO 3570
 3400 A$ = "MONSTERS":F$ = A$: GOSUB 75: GOTO 3500
 3500  REM 

LIST STUFF

 3502  HOME : PRINT "# OF SPACES TO INDENT?": INPUT "(USUALLY 0) : ";ID
 3507  IF PT THEN  PRINT D$"PR#1"
 3510  TEXT : HOME :L = 0: PRINT  SPC( ID)"LISTING "A$: PRINT : POKE 34,2: FOR A = 1 TO N%(TY): PRINT R$;F$",R"A: INPUT NA$: PRINT D$: PRINT  SPC( ID)"  "A": "NA$
 3520 L = L + 1: IF L > 19 + 30 * PT THEN  GOSUB 3600
 3530  NEXT : PRINT D$"PR#0"
 3570  PRINT : PRINT "PRESS ANY KEY TO RETURN TO MENU";: GET A$: PRINT : TEXT : GOTO 100
 3600  REM 

PAGE BREAK

 3610 L = 0: IF PT THEN  PRINT  CHR$ (12): RETURN 
 3620  PRINT : PRINT "      (PRESS ANY KEY TO CONTINUE)     ";: POKE 49168,0: GET B$: PRINT : VTAB  PEEK (37) - 1: CALL  - 958: RETURN 
 4000  REM 

DUPLICATE

 4010 C$ = "DUPLIC.": GOSUB 500:C$ = "DUP. FROM": GOSUB 700:FR = NO:C$ = "CREATE": GOSUB 700:NC = NO
 4030  IF NO > N%(TY) THEN  PRINT "CREATING A "T$(TY)" FROM ANOTHER.":N%(TY) = N%(TY) + 1:UP = 1
 4040 NO = FR:RE = NO + RL%(TY): GOSUB 20000: ON TYPE GOTO 4100,4200,4300,4400
 4100  REM 

DUP. ROOM

 4110  GOSUB 21000:RE = NC:NO = NC: GOTO 1130
 4200  REM 

DUP. ART.

 4210  GOSUB 22000:RE = NC + RL%(2):NO = NC: GOTO 1230
 4300  REM 

DUP. EFFECT

 4310 RE = NC + RL%(3): GOSUB 18000: GOTO 100
 4400  REM 

DUP. MONSTER

 4410  GOSUB 24000:RE = NC + RL%(4):NO = NC: GOTO 1430
 5000  REM 

SPECIAL FUNCTIONS

 5010  HOME : INVERSE : PRINT "   SELECT FUNCTION OR PRESS <RETURN>   ": NORMAL : PRINT 
 5020  PRINT "  1. CHANGE FILE LENGTHS": PRINT : PRINT "  2. DELETE LAST R,A,E,M": PRINT : PRINT "  3. ADD TO NBR OF R,A,E,M": PRINT 
 5030  PRINT "  4. TOGGLE LOCK MODE (NOW " MID$ ("OFFON",LK * 3 + 1,3)")": PRINT : PRINT "  5. GENERATE DUMMY ARTIFACTS": PRINT : PRINT "  6. TOGGLE PRINTER (NOW " MID$ ("OFFON",PT * 3 + 1,3)")"
 5040  PRINT : PRINT "  7. GENERATE BODIES": PRINT : PRINT "  8. (NOT USED)": PRINT : PRINT "  9. (NOT USED)"
 5050  PRINT : PRINT "INPUT YOUR CHOICE (1-9):";
 5060  GET S$: IF S$ =  CHR$ (13) THEN  PRINT : GOTO 100
 5070 C =  VAL (S$): IF C < 1 OR C > 9 THEN 5060
 5080  HOME : ON C GOTO 5100,5200,5300,5400,5500,5600,5700,5000,5000
 5100  REM 

FILE LENGTHS

 5110  IF MP >  = 7 THEN  PRINT "INVALID OPTION!" CHR$ (7): FOR A = 1 TO 2000: NEXT : GOTO 5000
 5120  PRINT "ROOMS="LR: PRINT "ROOM.NAME="LN: PRINT "ARTIFACTS="LA: PRINT "MONSTERS="LM: PRINT "DESC="LD: PRINT 
 5130  INPUT "ROOMS:";LR: INPUT "ROOM.NAME:";LN: INPUT "ARTIFACTS:";LA: INPUT "MONSTERS:";LM: INPUT "DESC:";LD: GOTO 5000
 5200  REM 

DELETE LAST ITEM

 5210 C$ = "DELETE": GOSUB 500:N%(TY) = N%(TY) - 1:UP = 1: GOTO 5000
 5300  REM 

ADD TO # OF ITEM

 5310 C$ = "INCRMNT": GOSUB 500:N%(TY) = N%(TY) + 1:UP = 1: GOTO 5000
 5400  REM 

TOGGLE LOCK

 5410 LK =  NOT LK: GOTO 5000
 5500  REM 

GEN. DUMMY ART.

 5510 UP = 1: GOSUB 75: INPUT "NBR ART. TO GEN:";NA: FOR A = 1 TO NA:N%(2) = N%(2) + 1: PRINT W$"DESC,R"N%(2) + RL%(2): PRINT "YOU SEE ARTIFACT #"N%(2)"."
 5520  PRINT W$"ARTIFACTS,R"N%(2): PRINT "ARTIFACT "N%(2): FOR X = 1 TO 8: PRINT 0: NEXT : NEXT : PRINT D$: GOTO 100
 5600  REM 

TOGGLE PRINTER

 5610 PT =  NOT PT: GOTO 5000
 5700  REM 

GEN. BODIES

 5710 UP = 1: PRINT "# OF MONSTER TO START WITH": PRINT : INPUT "(DEFAULT=1) :";A$: IF A$ = "" THEN A$ = "1"
 5720 SW =  VAL (A$): IF SW > N%(4) THEN  GOTO 5000
 5730  GOSUB 75: FOR A = SW TO N%(4): PRINT R$"MONSTERS,R"A: INPUT M$: FOR A2 = 1 TO 6: INPUT WT: NEXT : PRINT D$
 5740  HOME : PRINT "MONSTER #"A: PRINT : PRINT " 1. THE DEAD BODY OF "M$: PRINT : PRINT " 2. THE DEAD BODY OF THE "M$: PRINT : PRINT "CHOOSE THE CORRECT PHRASE (1-2):";
 5750  GET A$: IF A$ < "1" OR A$ > "2" THEN 5750
 5760  PRINT :A$ =  MID$ ("THE ",1,(A$ = "2") * 4):NA$ = "DEAD " + M$:DE$ = "YOU SEE THE DEAD BODY OF " + A$ + M$ + "."
 5770  FOR X = 1 TO 8:A%(X) = 0: NEXT :A%(2) = 1:A%(3) = WT:X = N%(2) + 1:RE = X + RL%(2):NO = X:N%(2) = X: GOSUB 18000: GOSUB 31200: NEXT : GOTO 100
 6000  REM 

QUIT

 6010  PRINT CL$: IF UP THEN  PRINT O$"DESC": PRINT W$"DESC": FOR A = 1 TO 4: PRINT N%(A): NEXT : PRINT LR: PRINT LM: PRINT LA: PRINT CL$
 6020  IF LK THEN A$ = D$ + "LOCKEAMON.": PRINT A$"DESC": PRINT A$"ROOMS": PRINT A$"ARTIFACTS": PRINT A$"MONSTERS": IF MP < 7 THEN  PRINT A$"ROOM.NAME"
 6030  POKE 216,0: PRINT : PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$: PRINT D$"-STARTUP"
 11000  REM 

EDIT ROOM

 11010  HOME : PRINT "ENTERING ROOM "NO: PRINT :I2$ = NA$:TL = RL: PRINT "ENTER ROOM NAME:": GOSUB 600: ON I$ = "" GOTO 11010:NA$ = I$:TL = 238:I2$ = DE$
 11040  PRINT "ENTER ROOM DESCRIPTION:": GOSUB 600:DE$ = I$
 11050  CALL  - 958: PRINT "FOR EACH DIRECTION ENTER THE ROOM # THATTHIS ROOM CONNECTS TO:": PRINT : PRINT "  NO CONNECTION:  0": PRINT "  EXIT TO MAIN HALL:  -99": IF MP < 6 THEN 11055
 11052  PRINT "  DOORS:  ARTIFACT # OF DOOR + "100 + (400 * (MP >  = 7)): IF MP >  = 7 THEN  PRINT "  NEGATIVE NUMBER FOR UNUSUAL THINGS": GOTO 11055
 11053  PRINT "  NEGATIVE #-1 TO -98 FOR PASSAGES"
 11055  PRINT : FOR D = 1 TO ND
 11060  PRINT "MOVE "D$(D)"...";:I2$ =  STR$ (RD%(D)):TL = 5: GOSUB 600:RD%(D) =  VAL (I$): IF RD%(D) <  >  INT (RD%(D)) OR  STR$ (RD%(D)) <  > I$ THEN  VTAB  PEEK (37): GOTO 11060
 11080  NEXT : ON MP < 6 GOTO 72: PRINT "LIGHT (0 = NONE; 1 = NORMAL) ";:I2$ =  STR$ (LT): GOSUB 600:LT =  VAL (I$): RETURN 
 12000  REM 

EDIT ARTIFACT

 12010  HOME : PRINT "ENTERING ARTIFACT "NO:I2$ = NA$:TL = AL: PRINT : PRINT "ENTER ARTIFACT NAME:": GOSUB 600: IF I$ = "" THEN 12010
 12020 NA$ = I$:I2$ = DE$:TL = 238: PRINT "ARTIFACT DESCRIPTION:": GOSUB 600:DE$ = I$
 12030 TL = 5: IF NW THEN A%(1) = 50:A%(2) = 1:A%(3) = 3:A%(4) = 0
 12040 A$ = "VALUE":A = 1: GOSUB 12500:A$ = "TYPE":A = 2: PRINT : FOR X = 0 TO NF STEP 2: PRINT " "X" = "TY$(X);: IF X < NF THEN  PRINT  TAB( 23);X + 1" = "TY$(X + 1);
 12050  PRINT : NEXT : PRINT : GOSUB 12500:A$ = "WEIGHT":A = 3: GOSUB 12500:A$ = "ROOM":A = 4: PRINT : PRINT "-1.................CARRIED BY PLAYER": IF MP >  = 6 THEN  PRINT "(- MONSTER# - 1)...MONSTER# CARRYING IT"
 12060  IF MP < 7 AND MP >  = 6 THEN  PRINT "ARTIFACT# + 100....INSIDE ARTIFACT#": PRINT "ROOM# + 200........EMBEDDED IN ROOM#": PRINT "ROOM# + 300........HIDDEN IN ROOM#"
 12070  IF MP >  = 7 THEN  PRINT "ARTIFACT# + 500....INSIDE ARTIFACT#": PRINT "ROOM# + 200........EMBEDDED IN ROOM#": PRINT "-999...............WORN BY PLAYER"
 12080  PRINT : GOSUB 12500:TP = A%(2): FOR AR = 5 TO 8: IF AR = 8 THEN  IF TP = 8 THEN  IF MP >  = 7 THEN  PRINT : PRINT " 1 = HIDDEN DOOR": PRINT 
 12090  IF NW THEN A%(AR) = DF(TP,AR - 5)
 12100  IF MP >  = 7 THEN  IF (AR = 7 AND (TP = 4 OR TP = 6 OR TP = 7)) OR AR = 6 AND TP = 12 THEN  PRINT : PRINT "OPEN = 1;  CLOSED = 0": PRINT 
 12110  IF AR = 6 THEN  IF TP = 2 OR TP = 3 THEN  PRINT : PRINT "1=AXE, 2=BOW, 3=MACE, 4=SPEAR, 5=SWORD": PRINT 
 12115  IF TP > NF THEN  PRINT "FIELD #"AR" : ";: GOTO 12120
 12117  PRINT F$(TP,AR - 5)" : ";
 12120 I2$ =  STR$ (A%(AR)): GOSUB 600:A%(AR) =  VAL (I$): NEXT : RETURN 
 12500  PRINT A$" : ";:I2$ =  STR$ (A%(A)):TL = 5: GOSUB 600:A%(A) =  VAL (I$): RETURN 
 13000  REM 

EDIT EFFECT

 13010  PRINT : PRINT "ENTER EFFECT #"NO:I2$ = DE$:TL = 238: GOSUB 600:DE$ = I$: PRINT : RETURN 
 14000  REM 

EDIT MONSTER

 14010  HOME : PRINT "ENTERING MONSTER # "NO: PRINT : IF MP >  = 7 THEN  PRINT "** BE SURE TO ENTER *ALL* MONSTER NAMES IN A SINGULAR FORM **": PRINT 
 14015  PRINT :I2$ = NA$:TL = ML: PRINT "ENTER NAME:": GOSUB 600: IF I$ = "" THEN 14010
 14020 NA$ = I$: PRINT "ENTER DESC--":I2$ = DE$:TL = 238: GOSUB 600:DE$ = I$: HOME : PRINT "MONSER #"NO"  "NA$: PRINT 
 14040  FOR M = 1 TO MF
 14100  IF M = 1 OR M = 6 THEN  IF MP >  = 7 THEN  PRINT : PRINT "   IF GROUP MONSTER ENTER VALUE FOR JUST   ONE MEMBER OF GROUP": PRINT 
 14110  IF M = 3 THEN  IF MP >  = 7 THEN  PRINT : PRINT "   SINGLE MONSTERS - ENTER 1": PRINT "   GROUP MONSTERS - ENTER # OF MEMBERS": PRINT 
 14120  IF M = 11 THEN  IF MP >  = 7 THEN  PRINT : PRINT "   1=ENEMY   2=NEUTRAL   3=FRIEND": PRINT "   FOR RANDOM FRIENDLINESS,": PRINT "  ENTER PERCENT LIKELY FRIENDLY + 100": PRINT 
 14130  IF (M = 8 AND MP >  = 7) OR (M = 9 AND MP < 7) THEN  PRINT : PRINT "  ART.#...WEAPON #": PRINT "  0.......NATURAL WEAPONS": PRINT "  - 1.....WEAPONLESS": PRINT 
 14150  PRINT "ENTER "MF$(M)": ";:I2$ =  STR$ (M%(M)):TL = 5: GOSUB 600:M%(M) =  VAL (I$): IF  STR$ (M%(M)) <  > I$ THEN  PRINT  CHR$ (7): VTAB  PEEK (37): GOTO 14150
 14160  NEXT : RETURN 
 18000  REM 

WRITE DESC

 18010  PRINT W$"DESC,R"RE: PRINT  CHR$ (34);DE$; CHR$ (34): PRINT D$: RETURN 
 20000  REM 

READ DESC

 20010  PRINT R$"DESC,R"RE: INPUT DE$: PRINT D$: RETURN 
 21000  REM 

READ ROOM

 21010  GOSUB 65: PRINT R$"ROOMS,R"NO: IF MP >  = 7 THEN  INPUT NA$: FOR A = 1 TO ND: INPUT RD%(A): NEXT : INPUT LT: PRINT D$: RETURN 
 21020  FOR A = 1 TO ND + (MP >  = 6): INPUT RD%(A): NEXT :LT = 1: IF MP >  = 6 THEN LT = RD%(ND + 1)
 21030  PRINT D$: PRINT R$"ROOM.NAME,R"NO: INPUT NA$: PRINT D$: RETURN 
 22000  REM 

READ ART.

 22010  GOSUB 75: PRINT R$"ARTIFACTS,R"NO: INPUT NA$: FOR A = 1 TO 4: INPUT A%(A): NEXT : IF MP >  = 6 OR A%(2) = 2 OR A%(2) = 3 THEN  FOR A = 5 TO 8: INPUT A%(A): NEXT : PRINT D$: RETURN 
 22020  FOR A = 5 TO 8:A%(A) = 0: NEXT : PRINT D$: RETURN 
 24000  REM 

READ MON.

 24010  GOSUB 75: PRINT R$"MONSTERS,R"NO: INPUT NA$: FOR D = 1 TO MF: INPUT M%(D): NEXT : PRINT D$: RETURN 
 31100  REM 

WRITE ROOM

 31110  GOSUB 65: PRINT W$"ROOMS,R"NO: IF MP >  = 7 THEN  PRINT  CHR$ (34);NA$: FOR D = 1 TO ND: PRINT RD%(D): NEXT : PRINT LT: PRINT D$: RETURN 
 31130  FOR A = 1 TO ND: PRINT RD%(A): NEXT : PRINT LT: PRINT D$: PRINT W$"ROOM.NAME,R"NO: PRINT  CHR$ (34);NA$: PRINT D$: RETURN 
 31200  REM 

WRITE ART.

 31210  GOSUB 75: PRINT W$"ARTIFACTS,R"NO: PRINT  CHR$ (34);NA$; CHR$ (34): FOR D = 1 TO 8: PRINT A%(D): NEXT : PRINT D$: RETURN 
 31400  REM 

WRITE MONSTER

 31410  GOSUB 75: PRINT W$"MONSTERS,R"NO: PRINT  CHR$ (34);NA$; CHR$ (34): FOR M = 1 TO MF: PRINT M%(M): NEXT : PRINT D$: RETURN 
 35000  REM 

SETUP DATA

 35010 RL = LR - 34:ML = LM - 62:AL = LA - 42:U$ =  CHR$ (95): IF MP < 7 THEN RL = 38:ML = 30:AL = ML
 35020  DIM A%(8),RD%(ND + 1),T$(4): FOR A = 1 TO 4: READ T$(A): NEXT : READ NF: DIM TY$(NF),F$(NF,3),DF(NF,3): FOR A = 0 TO NF: READ TY$(A): FOR A2 = 0 TO 3: READ F$(A,A2),DF(A,A2): NEXT : NEXT 
 35030  IF MP < 7 THEN NF = 10: FOR A = 0 TO 2: READ F$(6,A),DF(6,A): NEXT :F$(4,2) = "USER #7":F$(7,2) = "READ FLAG":F$(8,3) = "USER #8": IF MP < 6 THEN NF = 3
 35040  IF MP >  = 7 THEN  FOR A = 1 TO 6: READ A$: NEXT 
 35050 MF = 12: DIM M%(MF),MF$(MF),MD(MF): FOR A = 1 TO 12: READ MF$(A),MD(A): NEXT : IF MP < 7 THEN  FOR A = 1 TO 10: READ A$: NEXT 
 35060  IF MP >  = 7 THEN  FOR A = 7 TO 11: READ MF$(A),MD(A): NEXT :MF = 11:MF$(3) = "# MEMBERS":MD(3) = 1
 35070  DIM D$(10): FOR A = 1 TO 10: READ D$(A): NEXT 
 35080  DIM RL%(4): FOR A = 1 TO 4:RL%(A) = A * 100 - 100: NEXT : IF MP >  = 7 THEN  FOR A = 2 TO 4:RL%(A) = RL%(A) * 2: NEXT 
 35090  RETURN 
 35100  DATA ROOM,ART.,EFF.,MONS.
 36000  REM 

ART. DATA

 36010  DATA 12
 36020  DATA GOLD,USER #5,0,USER #6,0,USER #7,0,USER #8,0
 36030  DATA TREASURE,USER #5,0,USER #6,0,USER #7,0,USER #8,0
 36040  DATA WEAPON,ODDS,10,TYPE,5,DICE,1,SIDES,4
 36050  DATA MAGIC WEAPON,ODDS,10,TYPE,5,DICE,1,SIDES,4
 36060  DATA CONTAINER,KEY#,99,STRENGTH,100,OPEN/CLOSED,0,USER #8,0
 36070  DATA LIGHT,COUNTER,999,USER #6,0,USER #7,0,USER #8,0
 36080  DATA DRINKABLE,HEAL AMOUNT,10,# OF USES,5,OPEN/CLOSED,0,USER #8,0
 36090  DATA READABLE,1ST EFFECT,1,# OF EFFECTS,1,OPEN/CLOSED,1,USER #8,0
 36100  DATA DOOR/GATE,ROOM BEYOND,1,KEY #,99,STRENGTH,999,HIDDEN?,0
 36110  DATA KEY,USER #5,0,USER #6,0,USER #7,0,USER #8,0
 36120  DATA BOUND MONSTER,MONSTER#,0,KEY#,0,GUARD#,0,USER #8,0
 36130  DATA WEARABLE,ARMOR CLASS,0,TYPE,0,USER #7,0,USER #8,0
 36140  DATA RE-USEABLE CONT.,KEY#,0,OPEN/CLOSED,0,#ITEMS INSIDE,0,#ITEMS CAN HOLD,5
 36150  REM 

6.0 DRINKABLE

 36160  DATA CHANCES,50,HEAL AMOUNT,5,# OF USES,10
 37000  REM 

MONSTER DATA

 37010  DATA HARDINESS,16,AGILITY,16,FRIENDLINESS,65
 37020  DATA COURAGE,100,ROOM,0,WEIGHT,150,DEF. ODDS,0
 37030  DATA ARMOR,0,WEAPON #,0,ODDS TO HIT,60,WEP DICE,1,WEP SIDES,4
 37040  REM 

MONSTER DATA 7 & >

 37050  DATA ARMOR,0,WEAPON #,0,WEP. DICE,1,WEP. SIDES,4,FRIEND,3
 37060  REM 

DIRECTION NAMES

 37070  DATA N ,S ,E ,W ,U ,D ,NE,NW,SE,SW
 40000  REM 

ERROR TRAP

 40005  PRINT CL$: PRINT "ERROR CODE " PEEK (222)" IN LINE # " PEEK (218) +  PEEK (219) * 256: IF  PEEK (222) = 10 THEN  PRINT "CAN'T SAVE, FILE LOCKED.": END 
 40008  IF  PEEK (222) = 6 THEN  PRINT "NO ART/MON FILES FOR THIS ADVENTURE!": PRINT : PRINT "HIT ANY KEY...";: POKE 49168,0: GET A$: PRINT : PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$: PRINT D$"-STARTUP"
 40010  GOTO 6020
 50000  REM 
SELECT ADVENTURE

 50004  REM (SKIP AHEAD TO ADV DATA)
 50005  TEXT : HOME : PRINT "INITIALIZING...": FOR X = 1 TO 172: READ A$: NEXT 
 50010  READ NA: DIM AN$(NA,2): FOR X = 1 TO NA: READ AN$(X,1),AN$(X,2): NEXT 
 50020 D$ =  CHR$ (4):O$ = D$ + "OPEN":R$ = D$ + "READ":C$ = D$ + "CLOSE"
 50030  TEXT : HOME : PRINT D$"PR#3": HOME : PRINT  SPC( 19)"UNIVERSAL DUNGEON EDIT!": PRINT "Please select an adventure, or hit <ESC> to exit."
 50040  VTAB 4: FOR X = 1 TO 17: PRINT AN$(X,1);: HTAB 40: PRINT AN$(X + 17,1): NEXT :L = 4:H = 1:Z = 1
 50050  VTAB L: HTAB H: INVERSE : PRINT AN$(Z,1): NORMAL : POKE 49168,0
 50060 A =  PEEK (49152): ON A < 128 GOTO 50060:A = A - 128: VTAB L: HTAB H: PRINT AN$(Z,1)
 50070  IF A = 8 THEN H = H - 39:Z = Z - 17: ON H = 1 GOTO 50050:H = 40:Z = Z + 34: GOTO 50050
 50080  IF A = 11 THEN L = L - 1:Z = Z - 1: ON L > 3 GOTO 50050:L = 20:Z = Z + 17: GOTO 50050
 50090  IF A = 21 THEN H = H + 39:Z = Z + 17: ON H = 40 GOTO 50050:H = 1:Z = Z - 34: GOTO 50050
 50100  IF A = 10 THEN L = L + 1:Z = Z + 1: ON L < 21 GOTO 50050:L = 4:Z = Z - 17: GOTO 50050
 50110  IF A = 27 THEN  PRINT C$: PRINT : PRINT D$"-STARTUP"
 50120  ON A <  > 13 GOTO 50050: HOME : PRINT  CHR$ (21): HOME 
 50130  POKE 49168,0: PRINT D$"PR#0": TEXT : HOME 
 50140  PRINT D$"PREFIX": INPUT P$: PRINT D$: PRINT D$"PREFIX" + P$ + AN$(Z,2)
 50150  PRINT C$: PRINT : RUN 10
 50210  DATA 34,THE BEGINNER'S CAVE,E001,LAIR OF THE MINOTAUR,E002
 50220  DATA THE CAVE OF THE MIND,E003,THE ZYPHUR RIVERVENTURE,E004,CASTLE OF DOOM,E005,THE DEATH STAR,E006,THE DEVIL'S TOMB,E007
 50230  DATA THE ABDUCTOR'S QUARTERS,E008,ASSAULT ON THE CLONE MASTER,E009,THE MAGIC KINGDOM,E010,THE TOMB OF MOLINAR,E011
 50240  DATA THE BLACK DEATH,E020,PRIESTS OF XIM!,E043,THE SEWERS OF CHICAGO,E060,THE HARPY CLOUD,E061
 50250  DATA DHARMAQUEST,E074,TEMPLE OF THE GUILD,E075,THE PRINCE'S TAVERN,E078,CASTLE OF COUNT FUEY,E079,SEARCH FOR THE KEY/RESCUE MISSION,E080
 50260  DATA CAMP EAMON,E106,ASSAULT ON DOLNI KEEP,E124,JOURNEY TO JOTUNHEIM,E148,WALLED CITY OF DARKNESS,E150
 50270  DATA PATHETIC HIDEOUT OF MR. R.,E157,THE LAIR OF MR. ED,E158,ATTACK OF THE KRETONS,E194
 50280  DATA  EYE OF AGAMON,E217,ADVENTURE IN INTERZONE,E225,BOOKWORM 3-D,E226,FIENDS OF EAMON,E237,THE HEART OF GOLD,E997
 50290  DATA REDEMPTION,E998,EDGAR'S ADVENTURES,E999

 1  REM 

EAMON ADVENTURE #75
TEMPLE OF THE GUILD
BY JOHN NELSON &
DON DOUMAKES

 5  REM MP6 SUB V4
 6  REM REV. 1 OCT 94

 7  GOTO 31000: REM 

EAMON ADVENTURER'S GUILD
7625 HAWKHAVEN DR.
CLEMMONS, NC 27012

 8  PRINT D$"FRE": & A,A$: GOTO 10
 9  IF  LEFT$ (A$,1) = " " THEN A$ =  MID$ (A$,2): GOTO 9
 10  IF  LEN (A$) <  = CP THEN  PRINT A$: FOR K = K TO K: NEXT : GOTO 52
 11  FOR K = CP TO 1 STEP  - 1: IF  MID$ (A$,K,1) = " " THEN  PRINT  LEFT$ (A$,K - 1):A$ =  RIGHT$ (A$, LEN (A$) - K): GOSUB 52: GOTO 9
 12  NEXT : PRINT "WORD TOO LONG": STOP 
 15  RETURN 
 45  PRINT D$"FRE": PRINT D$"READEAMON.DESC,R"R: INPUT A$: PRINT D$: GOSUB 8
 51  PRINT 
 52 L = L + 1: GOTO 58
 54 L = L + 1
 56 L = L + 2: PRINT 
 58  IF L < 23 THEN  RETURN 
 59  POKE 49168,0: PRINT "[MORE] ";: GET Z$: PRINT : VTAB  PEEK (37): PRINT  SPC( 6): PRINT : VTAB  PEEK (37):L = 0: ON Z$ <  >  CHR$ (3) GOTO 15: POP : STOP 
 60  PRINT  CHR$ (12): HOME :L = 0: RETURN 
 62  GOSUB 45:L = L - 1: VTAB  PEEK (37): RETURN 
 64  GOSUB 56: GOTO 300
 65  GOSUB 8: GOTO 100
 66  GOSUB 8: GOTO 300
 67  GOSUB 8: GOTO 51
 69 WT = WT - A%(A,3) * (A%(A,4) =  - 1): RETURN 
 70 M%(0,9) = A:M%(0,10) = (EA + AE) * ( - EA > AE) + WA%(A%(A,6)) + A%(A,5) + 2 * M%(0,2): RETURN 
 91  PRINT "YOU AREN'T CARRYING IT.": GOTO 99
 94 A$ = "YOU CAN'T " + C$(C) + " " + S$ + ".": GOTO 65
 96  PRINT : PRINT "NOBODY HERE BY THAT NAME!": GOTO 99
 99  GOSUB 56
 100  REM 

YOU SEE

 120  GOSUB 51: IF SP THEN SP = SP - 1: IF  NOT SP THEN M%(0,2) = M%(0,2) / 2: PRINT "YOUR SPEED SPELL HAS JUST EXPIRED!": GOSUB 56
 122  FOR A = 0 TO 3: IF S2%(A) < SA%(A) THEN S2%(A) = S2%(A) * 1.1: IF S2%(A) > SA%(A) THEN S2%(A) = SA%(A)
 125  NEXT 
 130  PRINT "YOU ARE": PRINT " "R$(RO): GOSUB 56: IF  NOT R%(RO,0) THEN R%(RO,0) = 1:R = RO: GOSUB 45
 135  IF RO = 6 THEN  IF P% THEN  PRINT "YOU SEE A SECRET PASSAGE EAST!": GOSUB 56
 140  FOR M = 1 TO NM: IF M%(M,5) <  > RO THEN  NEXT : GOTO 160
 145  IF  NOT M%(M,0) THEN M%(M,0) = 1:R = M + 300: GOSUB 62: PRINT "("M$(M)" IS HERE.)": GOSUB 56: NEXT : GOTO 160
 150  PRINT M$(M)" IS HERE.": GOSUB 52: NEXT 
 160  FOR A = 1 TO NA: IF A%(A,4) <  > RO THEN  NEXT : GOTO 210
 162  IF A > NZ THEN  PRINT "YOUR "A$(A)" IS HERE.": GOSUB 52: NEXT : GOTO 210
 165  IF  NOT A%(A,0) THEN A%(A,0) = 1:R = A + 100: GOSUB 62: PRINT "(YOU SEE "A$(A)".)": GOSUB 56: NEXT : GOTO 210
 170  PRINT "YOU SEE "A$(A)".": GOSUB 52: NEXT 
 210  GOSUB 51: INPUT "YOUR COMMAND? ";A$:L = 0: GOSUB 51: IF A$ <  > "" THEN  ROT= 1: & A,A$: ROT= ID
 215  IF  LEFT$ (A$,1) = " " THEN A$ =  MID$ (A$,2): GOTO 215
 220  IF A$ = "" THEN A$ = CZ$: VTAB  PEEK (37) - 1: HTAB 15: POKE 1403,15: PRINT A$: PRINT 
 225  ON A$ = "" GOTO 275:CZ$ = A$: FOR A = 2 TO  LEN (A$): IF  MID$ (A$,A,1) <  > " " THEN  NEXT 
 230 V$ =  LEFT$ (A$,A - 1):S$ =  MID$ (A$,A + 1)
 235  IF  LEFT$ (S$,1) = " " THEN S$ =  MID$ (S$,2): GOTO 235
 240 S2$ = "": FOR A = 1 TO  LEN (S$): IF  MID$ (S$,A,1) <  > " " THEN  NEXT : GOTO 265
 245 S2$ =  MID$ (S$,A + 1):S$ =  LEFT$ (S$,A - 1)
 250  IF  LEFT$ (S2$,1) = " " THEN S2$ =  MID$ (S2$,2): GOTO 250
 255 Z$ =  LEFT$ (S2$,3): IF Z$ = "TO " OR Z$ = "ON " OR Z$ = "IN " THEN S2$ =  MID$ (S2$,4): GOTO 250
 260 Z$ =  LEFT$ (S2$,5): IF Z$ = "FROM " OR Z$ = "WITH " THEN S2$ =  MID$ (S2$,6): GOTO 250
 265 K =  LEN (V$): FOR A = 1 TO ND: IF  LEFT$ (C$(A),K) = V$ THEN C = A:A = 999: NEXT : GOTO 290
 270  NEXT :SL =  LEN (V$): FOR A = 1 TO NC: IF V$ =  LEFT$ (C$(A),SL) THEN C = A:A = 99: NEXT : GOTO 290
 275  NEXT : PRINT "VALID COMMANDS:": PRINT : FOR C = 1 TO NC: PRINT C$(C),;: NEXT : PRINT : GOTO 210
 290  ON C < 5 GOTO 3000: ON C - 4 GOTO 4000,5000,6500,6000,17000,7000,10000,8000,11000,12000,13000,14000,15000
 294  PRINT "ATTACK NON-ENEMY?"
 295  INPUT "  (Y/N)?";S$: ON S$ = "" GOTO 295:L = 1: PRINT : GOTO 4920
 300  REM 

PICK FOE

 310  ON  NOT T(1) GOTO 500: GOSUB 51:D% = RO: FOR M = 1 TO NM: IF M%(M,5) <  > RO THEN  NEXT : GOTO 500
 330  IF M%(M,14) = 2 THEN  NEXT : GOTO 500
 333  ON M%(M,13) / M%(M,1) *  RND (1) * 100 <  = M%(M,4) GOTO 350: PRINT M$(M)" FLEES!": GOSUB 56: IF  NOT NX THEN  PRINT "BUT CAN'T FIND AN EXIT!": GOSUB 56: NEXT : GOTO 500
 340  GOSUB 8510:M%(M,5) = R2:R2 = RO: GOSUB 3600: ON T(1) > 0 GOTO 495: FOR M = M TO M: NEXT : GOTO 500
 350 OF = M:DF = 0: IF M%(M,14) = 3 THEN 400
 360  IF T(3) = M%(0,1) THEN 480
 370  FOR M2 = 1 TO NM: IF M%(M2,5) <  > RO THEN  NEXT : GOTO 480
 375 M2 = C%: IF M%(M2,14) = 3 AND  RND (1) < .25 THEN DF = M2:M2 = 999
 380  NEXT : GOTO 480
 400 C% = 1
 405  IF M%(C%,5) <  > RO THEN C% = C% + 1: GOTO 405
 410  IF M%(C%,14) <  > 1 THEN C% = C% + 1: GOTO 405
 415 DF = C%: IF DF = NM THEN 480
 420  FOR M2 = DF + 1 TO NM: IF M%(M2,5) <  > RO THEN  NEXT : GOTO 480
 425 M2 = C%: IF M%(M2,14) = 1 AND  RND (1) < .25 THEN DF = M2:M2 = 999
 430  NEXT 
 480  GOSUB 7500
 490  IF  NOT T(1) THEN M = 999
 495  NEXT 
 500  REM 

SPECIAL STUFF

 510  IF GL THEN  IF  NOT M%(GL,5) THEN  PRINT "THE DOOR OPENS AND YOU LEAVE THE ARENA.":DI = 0: GOTO 32000
 900  GOTO 100
 3000  REM 

MOVE

 3010 R2 = R%(RO,C): IF T(1) THEN A$ = "YOU CAN'T DO THAT WITH UNFRIENDLIES ABOUT!": GOTO 65
 3040  IF R2 > 0 AND R2 <  = NR THEN 3500
 3050  REM 

SPECIAL MOVES

 3060  IF R2 =  - 99 THEN  PRINT "YOU SUCCESSFULLY RIDE OFF": PRINT "  INTO THE SUNSET.":DI = 0: GOTO 32000
 3390  PRINT "YOU CAN'T GO THAT WAY!"G$: GOTO 99
 3500  REM 

GO

 3530 R3 = RO:RO = R2: GOSUB 3600: GOTO 100
 3600  REM 

ENEMY?

 3610 T(1) = 0:T(3) = M%(0,1):F(1) = 0:F(3) = M%(0,13): FOR K = 1 TO NM: IF M%(K,5) <  > R3 THEN  NEXT : GOTO 3640
 3630  IF (M%(K,14) = 3 OR (M%(K,14) = 1) AND  RND (1) * 100 < M%(K,4)) THEN M%(K,5) = RO
 3635  NEXT 
 3640  FOR K = 1 TO NM: IF M%(K,5) <  > RO THEN  NEXT : RETURN 
 3650  IF  NOT M%(K,14) THEN FR = M%(K,3) +  INT (CH - 10 / 2) * (M%(K,3) > 0):M%(K,14) = 1: IF FR >  RND (1) * 100 THEN M%(K,14) = 2: IF FR >  RND (1) * 100 THEN M%(K,14) = 3
 3670 A = M%(K,14):F(A) = F(A) + M%(K,13):T(A) = T(A) + M%(K,1): NEXT : RETURN 
 4000  REM 

GET

 4010  GOSUB 4900: ON S$ = "ALL" GOTO 4200:HA = RO: GOSUB 4805: IF  NOT F THEN 94
 4020  GOSUB 4300: GOTO 64
 4200  FOR A = 1 TO NA: IF A%(A,4) <  > RO THEN  NEXT : GOTO 300
 4210  IF A%(A,3) > 900 THEN  PRINT A$(A)": ";
 4220  GOSUB 4300: NEXT : GOTO 64
 4300  IF A%(A,3) > 900 THEN  PRINT "DON'T BE ABSURD.": GOTO 52
 4320  REM 

SPECIAL GET EFFECTS

 4600  PRINT A$(A)" ";: IF WT + A%(A,3) > 10 * M%(0,1) THEN  PRINT "IS TOO HEAVY.": GOTO 52
 4620  PRINT "TAKEN.":A%(A,4) =  - 1:WT = WT + A%(A,3): IF M%(0,9) < 1 THEN  IF A%(A,2) = 2 OR A%(A,2) = 3 THEN  GOSUB 70: PRINT : PRINT A$(A)" READIED.":L = L + 1: GOTO 54
 4630  GOTO 52
 4700  REM 

MONS SEARCH

 4710 F = 0:SL =  LEN (S$): FOR X = 1 TO NM: IF  LEFT$ (M$(X),SL) <  > S$ AND  RIGHT$ (M$(X),SL) <  > S$ THEN  NEXT : RETURN 
 4720  IF M%(X,5) = RO THEN M = X:F = 1:X = 99
 4730  NEXT : RETURN 
 4800  REM  

ARTIF SEARCH

 4801 HA =  - 1:WH = HA: GOTO 4810
 4804 HA =  - 1
 4805 WH = RO
 4810 F = 0:SL =  LEN (S$): FOR X = 1 TO NA: IF  LEFT$ (A$(X),SL) <  > S$ AND  RIGHT$ (A$(X),SL) <  > S$ THEN  NEXT : GOTO 4830
 4820  IF A%(X,4) = HA OR A%(X,4) = WH THEN F = F + 1:A = X
 4825  NEXT 
 4830  IF F > 1 THEN A$ = "WHICH " + Q$ + S$ + Q$ + " DO YOU WANT?": POP : GOTO 65
 4840  RETURN 
 4900  REM 

GET S$

 4910  IF S$ = "" THEN  PRINT : PRINT C$(C)" WHO OR WHAT";: INPUT S$: PRINT :L = 0: GOTO 4910
 4920  & A,S$: RETURN 
 5000  REM 

DROP

 5010  GOSUB 51: GOSUB 4900: ON S$ = "ALL" GOTO 5100: GOSUB 4801: ON  NOT F GOTO 91: GOSUB 5200: GOTO 300
 5100  FOR A = 1 TO NA: IF A%(A,4) <  >  - 1 THEN  NEXT : GOTO 5120
 5110  GOSUB 5200: NEXT 
 5120 WT = 0:M%(0,9) =  - 1: GOTO 64
 5200  IF A = M%(0,9) THEN M%(0,9) =  - 1
 5210 WT = WT - A%(A,3):A%(A,4) = RO: PRINT A$(A)" DROPPED.": GOTO 52
 6000  REM 

EXAMINE

 6010  GOSUB 4900: GOSUB 4804: ON  NOT F GOTO 6050: IF A%(A,4) = EM THEN A%(A,4) = RO
 6020  IF A > NZ THEN  PRINT "THIS IS YOUR "A$(A)".": GOTO 64
 6040 R = A + 100: GOSUB 45:A%(A,0) = 1: GOTO 300
 6050  GOSUB 4700: IF  NOT F THEN  PRINT "YOU SEE NOTHING SPECIAL.": GOTO 64
 6060 R = M + 300: GOSUB 45
 6070 DF = M: GOSUB 7650: GOTO 300
 6500  REM 

LOOK

 6510  IF RO = 6 THEN  IF  NOT P% THEN P% = 1: GOTO 100
 6530 R = RO: GOSUB 45: GOTO 300
 7000  REM 

ATTACK

 7010  IF M%(0,9) < 1 THEN  PRINT "YOU HAVE NO WEAPON READY!": GOTO 100
 7020  GOSUB 4900: GOSUB 4700: IF  NOT F THEN 96
 7300  IF M%(M,14) <  > 1 THEN  GOSUB 294: IF S$ <  > "Y" THEN 100
 7305 OF = 0:DF = M: GOSUB 7500: IF  NOT HI THEN 7360
 7310  IF UP THEN W2 = A%(M%(0,9),6):WA%(W2) = WA%(W2) + 2:A$ = M$(0) + "'S " + W$(W2) + " ABILITY INCREASED!": GOSUB 67
 7320  ON  - EA <  = AE GOTO 7360: ON  INT ( RND (1) * 100 + 1) <  = M%(0,10) GOTO 7360:A = (EA + AE <  >  - 1) + 1:AE = AE + A:M%(0,10) = M%(0,10) + A:A$ = M$(0) + "'S ARMOUR EXPERTISE INCREASED BY " +  STR$ (A) + "!": GOSUB 67
 7360  ON M%(DF,14) = 1 OR  NOT DF GOTO 300:M%(DF,3) = 1 + (M%(DF,14) = 3) * 25:R3 = RO:M%(DF,14) = 0: GOSUB 3600: GOTO 300
 7400  REM 

MONS P/U WEAP

 7410 K = D%:X =  ABS (M%(OF,9)) - 1: IF X > 0 AND X <  = NZ THEN  IF A%(X,4) = RO THEN 7460
 7420 D% = RO:X = 0: FOR M2 = 1 TO NA:C% = M2: & S,A,D%,A%(C%,4),C%,A%:M2 = C%: IF  NOT M2 THEN M2 = 999: NEXT : GOTO 7460
 7430  IF A%(M2,2) < 2 OR A%(M2,2) > 3 THEN  NEXT : GOTO 7460
 7440  IF X THEN  IF A%(M2,7) * A%(M2,8) < A%(X,7) * A%(X,8) THEN  NEXT : GOTO 7460
 7450 X = M2: NEXT 
 7460 D% = K: IF X THEN A$ = M$(OF) + " PICKS UP " + A$(X) + ".": GOSUB 8:A%(X,4) =  - OF - 1:M%(OF,9) = X
 7470  GOTO 51
 7500  REM 

DO BATTLE

 7505 HI = 0:W = M%(OF,9): IF OF = 18 THEN W = M%(0,9):M%(OF,10) = M%(0,10): REM OTHER SELF
 7506  ON W < 0 GOTO 7400:A%(0,7) = M%(OF,11):A%(0,8) = M%(OF,12)
 7507 A$ = M$(OF) + " " + BV$(A%(W,6), RND (1) * 3) + "S AT " + M$(DF) + "...": GOSUB 8: PRINT " --- ";:RL =  INT ( RND (1) * 100 + 1):HI = 0: IF (RL < 5 OR RL < M%(OF,10) - M%(DF,7)) AND RL < 96 THEN HI = 1
 7520  ON HI GOTO 7600: IF RL < 97 THEN  PRINT "MISSED!": GOTO 56
 7525  PRINT "FUMBLED!": GOSUB 52:FR =  INT ( RND (1) * 100 + 1): IF FR < 36 OR  NOT W OR OF = 18 THEN  PRINT "...FUMBLE RECOVERED.": GOTO 56
 7530  IF FR < 61 THEN A$ = M$(OF) + " DROPS " + A$(W) + "!":A%(W,4) = RO:M%(OF,10) = M%(OF,10) - A%(W,5):M%(OF,9) =  - W - 1:WT = WT - A%(W,3) * ( NOT DF): GOTO 67
 7532  IF A%(W,2) = 3 THEN A$ = "SPARKS FLY FROM " + A$(W) + "!": GOTO 67
 7535  IF FR > 95 THEN 7550
 7540  PRINT "...WEAPON BROKEN!"G$: GOSUB 52:A%(W,4) = 0:M%(OF,10) = M%(OF,10) - A%(W,5):M%(OF,9) =  - 1:WT = WT - A%(W,3) * ( NOT DF): IF FR > 10 THEN 51
 7545  PRINT "BROKEN WEAPON HITS USER!"G$: GOSUB 56
 7550  IF FR = 100 THEN 7560
 7555 D = A%(W,7):S = A%(W,8):DF = OF:A = 1: GOTO 7635
 7560 D = A%(W,7) * 2:S = A%(W,8):DF = OF:A = 0: GOTO 7635
 7600 UP = ( INT ( RND (1) * 100 + 1) > M%(OF,10)):M%(OF,10) = M%(OF,10) + 2 * UP
 7605 D = A%(W,7):S = A%(W,8):A = 1: IF RL > 5 THEN  PRINT "A HIT! ---": GOSUB 56: GOTO 7635
 7610  PRINT "A CRITICAL HIT!": GOSUB 56:R2 =  INT ( RND (1) * 100 + 1): IF R2 < 51 THEN A = 0: GOTO 7635
 7615  IF R2 < 86 THEN S = 1.5 * S: GOTO 7635
 7620  IF R2 < 96 THEN D = 2 * D: GOTO 7635
 7625 D = 3 * D: GOTO 7635
 7635 D2 = 0: FOR K = 1 TO D:D2 = D2 +  INT (S *  RND (1) + 1): NEXT :D2 = D2 - A * M%(DF,8): IF D2 < 1 THEN  IF  NOT M%(DF,8) THEN  PRINT "*** BLOW TURNED! ***": GOTO 56
 7638  IF D2 < 1 THEN  PRINT "**BLOW BOUNCES OFF ARMOUR!": GOTO 56
 7640 M%(DF,13) = M%(DF,13) + D2: IF M%(DF,13) >  = M%(DF,1) THEN 7700
 7645 A = M%(DF,14):F(A) = F(A) + D2
 7650  PRINT M$(DF)" IS ";: ON  INT (M%(DF,13) * 5 / M%(DF,1)) + 2 - ( NOT M%(DF,13)) GOTO 7652,7655,7660,7665,7670,7675
 7652  PRINT "IN PERFECT HEALTH.": GOTO 56
 7655  PRINT "IN GOOD SHAPE.": GOTO 56
 7660  PRINT "HURTING.": GOTO 56
 7665  PRINT "IN PAIN.": GOTO 56
 7670  PRINT "BADLY INJURED.": GOTO 56
 7675  PRINT "AT DEATH'S DOOR,": PRINT "  KNOCKING LOUDLY.": GOTO 54
 7700  REM 

MONSTER DIES

 7710  PRINT M$(DF)" IS DEAD!":A = M%(DF,14):F(A) = F(A) + M%(DF,1) - M%(DF,13): IF  NOT DF THEN  POP :DI = 1: GOTO 32000
 7730 M%(DF,5) = 0:M%(DF,13) = 0: IF M%(DF,9) > 0 AND NA >  = M%(DF,9) THEN M%(DF,10) = M%(DF,10) - A%(M%(DF,9),5)
 7735 M%(DF,9) =  - 1: FOR A2 = 1 TO NA: IF A%(A2,4) <  >  - DF - 1 THEN  NEXT : GOTO 7900
 7740 A%(A2,4) = RO: NEXT 
 7900 T(A) = T(A) - M%(DF,1): GOTO 56
 8000  REM 

FLEE

 8010  IF  NOT T(1) THEN  PRINT "CALM DOWN.": GOTO 99
 8020  IF  NOT NX THEN  PRINT "THERE'S NO PLACE TO RUN!"G$: GOTO 99
 8040  ON S$ = "" GOTO 8500: FOR D = 1 TO ND: IF  LEFT$ (C$(D), LEN (S$)) = S$ THEN R2 = R%(RO,D):D = 99: NEXT : GOTO 3500
 8050  NEXT 
 8500  GOSUB 8510: GOTO 3500
 8510 R2 = R%(RO, RND (1) * ND + 1): ON R2 < 1 OR R2 > NR GOTO 8510: RETURN 
 10000  REM 

INVEN

 10010 M = 0: ON S$ = "" GOTO 10100: GOSUB 4900: GOSUB 4700: ON  NOT F GOTO 96: ON M%(M,14) > 1 GOTO 10100: FOR M = M TO M: GOTO 15030
 10100  PRINT : PRINT M$(M)" IS CARRYING:": GOSUB 54:W = 0:C% =  - M - 1: FOR A = 1 TO NA: IF A%(A,4) <  > C% THEN  NEXT : FOR A = A TO A: GOTO 10130
 10120 W = 1: PRINT " "A$(A);: IF A = M%(M,9) THEN  PRINT " [READY WEAPON]";
 10130  GOSUB 51: NEXT : IF  NOT W THEN  PRINT " (NOTHING)": GOSUB 56
 10140  IF  NOT M THEN  PRINT GO" GOLD PIECES.": GOSUB 56
 10150 DF = M: GOSUB 7650: GOTO 300
 11000  REM 

BLAST

 11010 S = 0:TA = 1: GOSUB 11500: GOSUB 4900: GOSUB 4700: IF  NOT F THEN 96
 11020  PRINT "DIRECT HIT!": GOSUB 56:S = 5:D = 2:DF = M:A = 0: GOSUB 7635: GOTO 7360
 11500 W = 0: IF SA%(S) THEN RL = 100 *  RND (1):W = (((RL < S2%(S)) OR (RL < 5)) AND (RL < 95))
 11510  IF RL = 100 THEN A$ = "SPELL BACKLASH!!  YOUR ABILITY TO CAST THIS SPELL TEMPORARILY DIMINISHES!":S2%(S) = S2%(S) / 10: POP : GOTO 66
 11520  IF  NOT W THEN  PRINT "NOTHING HAPPENED.": POP : GOTO 64
 11530 RL = 100 *  RND (1): IF RL > S2%(S) THEN S2%(S) = S2%(S) + 2:SA%(S) = SA%(S) + 2
 11540 S2%(S) = S2%(S) * .8: RETURN 
 12000  REM 

HEAL

 12010 S = 1:M = 0: GOSUB 11500: ON S$ <  > "" GOSUB 4700: IF M%(M,13) THEN  PRINT M$(M)"'S HEALTH IMPROVES!": GOSUB 56
 12050 M%(M,13) = M%(M,13) - 3 - 10 *  RND (1):M%(M,13) = M%(M,13) * (M%(M,13) >  - 1): GOTO 10150
 13000  REM 

POWER

 13010 A$ = "YOU HEAR A LOUD SONIC BOOM WHICH ECHOES ALL AROUND YOU.": GOTO 66
 14000  REM 

SPEED

 14010 S = 2: GOSUB 11500: IF SP THEN  PRINT "NOTHING HAPPENED.": GOTO 64
 14040 M%(0,2) = M%(0,2) * 2:SP =  INT ( RND (1) * 25 + 10): PRINT "YOUR MOVEMENTS QUICKEN!": GOTO 64
 15000  REM 

SMILE

 15010  FOR M = 1 TO NM: IF M%(M,5) <  > RO THEN  NEXT : GOTO 300
 15030  PRINT M$(M)" "SM$(M%(M,14) - 1)" YOU.": GOSUB 52: NEXT : GOTO 300
 17000  REM 

READY

 17010  GOSUB 4900: GOSUB 4801: ON  NOT F GOTO 91: IF A%(A,2) < 2 OR A%(A,2) > 3 THEN  PRINT "THAT ISN'T A WEAPON!": GOTO 99
 17100  GOSUB 70: PRINT A$(A)" READIED.": GOTO 64
 31000  REM 

INIT

 31010  POKE 216,0:D$ =  CHR$ (4): PRINT  CHR$ (26)"1": PRINT  CHR$ (21): TEXT : HOME : PRINT D$"RESTOREFAST.START": PRINT D$"-EAMON.UTILS": ONERR  GOTO 31030
 31020  PRINT D$"VERIFYBADGUY": POKE 216,0: PRINT D$"OPENBADGUY": PRINT D$"READBADGUY": INPUT GL: PRINT D$"CLOSE":M%(GL,5) = 1:R2 = 0
 31030  POKE 216,0:R2 = R2 + 1
 31070  PRINT D$"OPENFRESH.MEAT": PRINT D$"READFRESH.MEAT": INPUT RE,M$(0),M%(0,1),M%(0,2),CH: FOR A = 0 TO 3: INPUT SA%(A):S2%(A) = SA%(A): NEXT : FOR A = 1 TO 5: INPUT WA%(A): NEXT : INPUT AE,SE$,GO,BA,AC
 31080 NA = NZ: FOR A = NZ + 1 TO NZ + 4: INPUT A$(A),A%(A,6),A%(A,5),A%(A,7),A%(A,8)
 31090  IF A$(A) <  > "" AND A$(A) <  > "NONE" THEN NA = NA + 1:A%(A,2) = 2 + (A%(A,7) * A%(A,8) > 20):A%(A,3) = 2:A%(A,4) =  - 1:WT = WT + 2
 31100  NEXT : PRINT D$"CLOSE":CP = 40:ID = 1: ROT= ID
 31110  FOR W2 = NZ + 1 TO NA
 31120  FOR W = 1 TO NZ: IF A$(W) = A$(W2) THEN A$(W) = A$(W) + "#": GOTO 31110
 31140  NEXT :M =  INT (AC / 2): IF M * 2 <  > AC THEN M%(0,8) = 1:EA =  - 5
 31150  IF M THEN M%(0,8) = M%(0,8) + M:EA = EA - M * 10: IF M = 3 THEN M%(0,8) = M%(0,8) + 2:EA = EA - 30
 31160  IF NA > NZ THEN A = NZ + 1: GOSUB 70
 31170  REM 

"OTHER SELF" MONSTER

 31200 M = 18: FOR A = 1 TO 12:M%(M,A) = M%(0,A): NEXT :M%(M,3) = 0:M%(M,4) = 100:M%(M,5) = 19:M%(M,9) = 0
 31900  GOSUB 60: VTAB 24: GOSUB 31960: GOTO 3500
 31960  PRINT D$"OPENEAMON.DESC,L256": RETURN 
 32000  REM 

DONE

 32010  PRINT : GOSUB 59: IF SP THEN M%(0,2) = M%(0,2) / 2
 32020  ON DI <  > 0 GOTO 32500: FOR A = 1 TO 4:V%(A) = 0: NEXT :A$(0) = "NONE": FOR A = 5 TO 8:A%(0,A) = 0: NEXT 
 32060 W = 0: FOR A = 1 TO NA: IF A%(A,4) <  >  - 1 THEN  NEXT : GOTO 32090
 32070  IF A%(A,2) = 2 OR A%(A,2) = 3 THEN W = W + 1:V%(W) = A:A%(A,4) = 0
 32080  NEXT 
 32090  ON W < 5 GOTO 32340: GOSUB 60:A$ = "AS YOU ENTER THE LOBBY, YOU ARE TOLD THAT " + "YOU HAVE TOO MANY WEAPONS -- 4 IS THE LEGAL LIMIT.": GOSUB 8: GOSUB 59
 32100  GOSUB 60: PRINT "YOUR WEAPONS ARE:": GOSUB 56: FOR A = 1 TO W: PRINT  SPC( 4)A" - "A$(V%(A)): GOSUB 52: NEXT : GOSUB 51: PRINT "ENTER # OF WEAPON TO SELL (1-"W")": GOSUB 52
 32110  INPUT "    >";A$:M =  VAL (A$): IF M < 1 OR M > W OR M <  >  INT (M) THEN  VTAB  PEEK (37): GOTO 32110
 32120 A%(V%(M),4) =  - 1:W = W - 1: ON M = W + 1 AND W > 4 GOTO 32100:V%(M) = V%(W + 1): ON W > 4 GOTO 32100
 32340  PRINT D$"CLOSE": IF  NOT GL THEN  PRINT D$"OPENPRIZE": PRINT D$"READPRIZE": FOR A = 1 TO 3: INPUT M:M%(0,A) = M%(0,A) + M: NEXT : PRINT D$"CLOSE": PRINT D$"DELETEPRIZE"
 32350 S$ = "FRESH.MEAT":A$ = S$: GOSUB 36000: IF GL THEN  PRINT D$"-GLADIATOR.POOL"
 32360  PRINT D$"-LOBBY"
 32500  PRINT : PRINT D$"DELETE FRESH.MEAT": PRINT D$"STORE SAVED.GAME": PRINT D$"DELETE SAVED.GAME": PRINT "HIT ANY KEY...": GET A$: PRINT 
 32510  REM 
 32515  PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT S$: PRINT D$"PREFIX"S$: PRINT D$"VERIFYCHARACTERS": POKE 216,0
 32520 A$ = "THE.ADVENTURER": IF DI THEN  PRINT D$"OPEN"A$: PRINT D$"CLOSE": PRINT D$"DELETE"A$: GOTO 32900
 32530 S$ = "CHARACTERS":A$ = S$ + ",R" +  STR$ (RE):S$ = S$ + ",L150": GOSUB 36000
 32900  PRINT D$"-MAIN.HALL"
 36000  REM 

WRITE CHAR. DATA

 36010  PRINT D$"OPEN"S$: PRINT D$"WRITE"A$: PRINT RE: PRINT M$(0): PRINT M%(0,1): PRINT M%(0,2): PRINT CH: FOR A = 0 TO 3: PRINT SA%(A): NEXT : FOR A = 1 TO 5: PRINT WA%(A): NEXT : PRINT AE
 36020  PRINT SE$: PRINT GO: PRINT BA: PRINT AC: FOR A = 1 TO 4:W = V%(A): PRINT A$(W): PRINT A%(W,6): PRINT A%(W,5): PRINT A%(W,7): PRINT A%(W,8): NEXT : RETURN 

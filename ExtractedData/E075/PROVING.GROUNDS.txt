 1  REM 

TEMPLE OF THE GUILD
PROVING GROUNDS

 10 D$ =  CHR$ (4): PRINT  CHR$ (21): TEXT : HOME : HTAB 6: INVERSE : PRINT "ADVENTURERS' PROVING GROUNDS": NORMAL : PRINT : PRINT 
 20  PRINT "     YOU HAVE COME TO THE INNERMOST AND MOST EXCLUSIVE OF THE ROOMS IN THE      TEMPLE OF THE GUILD.  THIS IS THE PLACE WHERE ADVENTURERS COME FOR CHANGES IN   THE BASIC STUFF OF THEMSELVES.  IF YOU"
 30  PRINT "HAVE A DESIRE TO BE A BETTER ADVENTURER,A DESIRE SO STRONG THAT YOU ARE WILLING TO SPEND YOUR FORTUNE AND RISK YOUR LIFEAS WELL, THIS MAY BE THE PLACE WHERE YOUREACH THAT GOAL.": PRINT 
 40  PRINT "     TO INCREASE ANY OF YOUR BASIC": PRINT "CHARACTERISTICS (HARDINESS, AGILITY, OR CHARISMA), YOU MUST PAY A FEE AND MEET ATEST.  THOSE WHO CANNOT MEET THE TEST   LOSE THEIR LIVES."
 50  PRINT : GOSUB 1000: PRINT "(PRESS ANY KEY TO CONTINUE) ";: GET R$: PRINT : HOME 
 60  PRINT "     TO RAISE YOUR CHARISMA ONE POINT   WILL COST YOU A MERE 5,000 GOLD PIECES. A ONE-POINT INCREASE IN EITHER HARDINESSOR AGILITY WILL COST YOU 10,000 GOLD    PIECES.": PRINT : IF GOLD >  = 5000 GOTO 80
 70  PRINT "     SINCE YOU DON'T HAVE ENOUGH GOLD ONYOU, YOU OF COURSE WON'T HAVE THE CHANCETO PROVE YOURSELF HERE.  BE GONE.": PRINT : PRINT "(PRESS ANY KEY TO CONTINUE...)": GET R$: PRINT : PRINT D$"RUN LOBBY"
 80  PRINT "     WOULD YOU BE INTERESTED IN RAISING YOUR HARDINESS, YOUR AGILITY, OR YOUR   CHARISMA?": PRINT : PRINT "     OR WOULD YOU PREFER TO LEAVE?": PRINT : PRINT : PRINT : PRINT "(PRESS H, A, C, OR L)";
 90  GET R$: IF R$ <  > "H" AND R$ <  > "A" AND R$ <  > "C" AND R$ <  > "L" GOTO 90
 100  PRINT : IF R$ = "L" THEN : PRINT : PRINT "WELL, GOOD DAY THEN.": PRINT D$"-LOBBY"
 110  IF R$ = "H" THEN P = 1: GOTO 140
 120  IF R$ = "A" THEN P = 2: GOTO 140
 130 P = 3
 140 P$(1) = "HARDINESS":P$(2) = "AGILITY":P$(3) = "CHARISMA": IF P < 3 AND GOLD < 10000 THEN : PRINT "YOU DON'T HAVE ENOUGH GOLD TO GO        CHANGING YOUR ";P$(P);"!": PRINT : GOTO 80
 150  PRINT : PRINT "ARE YOU QUITE SURE YOU WANT TO CHANGE   YOUR ";P$(P);"?": PRINT : PRINT "(PRESS Y OR N) ";
 160  GET R$: IF R$ <  > "Y" AND R$ <  > "N" GOTO 160
 170  PRINT : IF R$ = "N" GOTO 80
 180  PRINT : PRINT "VERY WELL THEN...":P%(P) = 1: PRINT D$"OPENPRIZE": PRINT D$"WRITEPRIZE": FOR I = 1 TO 3: PRINT P%(I): NEXT : PRINT D$"CLOSE":GO = GO - 5000: IF P < 3 THEN GO = GO - 5000
 190  GOSUB 2000: PRINT D$"-MAIN.PGM"
 1000  REM 

READ CHARACTER

 1010  PRINT D$"OPENFRESH.MEAT": PRINT D$"READFRESH.MEAT": INPUT RE,NA$,HD,AG,CH: FOR I = 1 TO 4: INPUT SA%(I): NEXT : FOR I = 1 TO 5: INPUT WA%(I): NEXT 
 1020  INPUT AE,SE$,GO,BA,AC: FOR I = 1 TO 4: INPUT WN$(I): FOR J = 1 TO 4: INPUT WE%(I,J): NEXT : NEXT : PRINT D$"CLOSE": RETURN 
 2000  REM 

WRITE CHARACTER

 2010  PRINT D$"OPENFRESH.MEAT": PRINT D$"WRITEFRESH.MEAT": PRINT RE: PRINT NA$: PRINT HD: PRINT AG: PRINT CH: FOR I = 1 TO 4: PRINT SA%(I): NEXT : FOR I = 1 TO 5: PRINT WA%(I): NEXT 
 2020  PRINT AE: PRINT SE$: PRINT GO: PRINT BA: PRINT AC: FOR I = 1 TO 4: PRINT WN$(I): FOR J = 1 TO 4: PRINT WE%(I,J): NEXT : NEXT : PRINT D$"CLOSE": RETURN 

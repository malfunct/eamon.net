 10 D$ =  CHR$ (4)
 12  DIM X(16),Y(16),FM$(39)
 15  PRINT D$"OPENFRESH.MEAT": PRINT D$"READFRESH.MEAT": FOR I = 1 TO 39: INPUT FM$(I): NEXT : PRINT D$"CLOSE"
 20 GOLD =  VAL (FM$(17))
 30  DATA  20,1,25,2,30,4,33,7,35,11,33,15,30,18,25,20,20,21,15,20,10,18,7,15,5,11,7,7,10,4,15,2
 40  FOR I = 1 TO 16
 50  READ X(I),Y(I)
 60  NEXT 
 61  DIM PRIME(20),WR$(6),WR(6),ODDS(6,2),SC(7),B(6,2)
 62  FOR I = 1 TO 20: READ PRIME(I): NEXT 
 63  DATA  2,3,5,7,11,13,17,19,23,29,31,37,41,43,47,53,59,61,67,71
 64  FOR I = 1 TO 6: READ WR$(I),WR(I): NEXT 
 65  DATA   SPARTUS,.6
 66  DATA   CYRO,.5
 67  DATA   CONAN,.7
 68  DATA   CLAUDIUS,.4
 69  DATA   ATHENIUS,.6
 70  DATA   ZELANO,.7
 100  HOME 
 105  VTAB 20: PRINT "YOU HAVE ";GOLD;" GOLD PIECES.": VTAB 1
 110  INVERSE : HTAB 11: PRINT "SHEEPSKINNER CASINO": NORMAL 
 120  PRINT : PRINT "AS YOU ENTER THE CASINO, YOU SEE THAT   YOU CAN:"
 130  PRINT 
 140  PRINT "1.  PLAY AT THE WHEEL OF FORTUNE."
 150  PRINT "2.  BET ON THE FIGHTS."
 170  PRINT "3.  LEAVE."
 180  PRINT : PRINT "HIT A KEY FOR YOUR CHOICE (1-3)  ";
 190  GET R$: IF R$ < "1" OR R$ > "3" GOTO 190
 200  ON  VAL (R$) GOTO 500,710,5000
 500  HOME : INVERSE : PRINT "WHEEL OF": PRINT "FORTUNE": NORMAL 
 505  FOR I = 1 TO 16: HTAB X(I): VTAB Y(I): PRINT I: NEXT 
 510  VTAB 23: CALL  - 958
 515  INPUT "HOW MUCH GOLD WILL YOU BET?  ";BET
 520  IF BET = 0 GOTO 100
 525  IF BET > GOLD OR BET <  >  INT (BET) GOTO 510
 530  VTAB 23: CALL  - 958
 535  INPUT "BETTING ON WHICH NUMBER (1-16)?  ";NUM
 540  IF NUM < 1 OR NUM > 16 OR NUM <  >  INT (NUM) GOTO 530
 545  REM  SELECT FINAL NUMBER
 550 F = 40 -  INT ( RND (1) * 16 + 1)
 555  REM  SPIN WHEEL
 560 K = 1
 565  FOR I = 1 TO F
 570  INVERSE : HTAB X(K): VTAB Y(K): PRINT K: NORMAL 
 575 Z1 =  - 16336:Z2 =  PEEK (Z1):Z2 =  PEEK (Z1)
 580  FOR J = 1 TO I * 25 * I / F: NEXT 
 585  IF I = F GOTO 600
 590  HTAB X(K): VTAB Y(K): PRINT K
 595 K = K + 1: IF K = 17 THEN K = 1
 600  NEXT 
 605  REM  WIN OR LOSE?
 610  IF K = NUM THEN : VTAB 23: CALL  - 958: PRINT "YOU WIN ";BET * 15;" GOLD PIECES!":GOLD = GOLD + BET * 15: GOTO 620
 615  VTAB 23: CALL  - 958: PRINT "YOU LOSE!":GOLD = GOLD - BET
 620  PRINT "(HIT ANY KEY TO CONTINUE)";: GET R$: VTAB 23: PRINT : GOTO 505
 697  REM  BETTING ON THE FIGHTS
 700  REM  ASSIGN FIGHTERS RANDOMLY TO EACH OTHER.
 710  FOR I = 1 TO 5: FOR J = I + 1 TO 6
 720  IF  RND (1) > .5 THEN :H$ = WR$(I):WR$(I) = WR$(J):WR$(J) = H$:H = WR(I):WR(I) = WR(J):WR(J) = H
 730  NEXT : NEXT 
 740  REM  CALCULATE INITIAL ODDS
 750  FOR I = 1 TO 5 STEP 2
 760 A =  INT (WR(I) * 10):B =  INT (WR(I + 1) * 10)
 770  GOSUB 1600
 780 ODDS(I,1) = B:ODDS(I,2) = A
 790 ODDS(I + 1,1) = A:ODDS(I + 1,2) = B
 800  NEXT 
 810  REM  CLEAR OLD BETS
 820  FOR I = 1 TO 6:B(I,1) = 0:B(I,2) = 0: NEXT 
 825  REM  CLEAR OLD SCORES
 827  FOR I = 1 TO 7:SC(I) = 0: NEXT 
 830  REM  PRINT THE DISPLAY
 840  HOME 
 850  PRINT "SEVERAL CLERICS ARE MADLY SCRIBBLING AT A BLACKBOARD OUTSIDE THE WRESTLING ROOM.AT THIS MOMENT IT READS:"
 860  PRINT 
 870 T1 = 20:T2 = 35
 880  PRINT "WRESTLERS"; TAB( T1);"ODDS"; TAB( T2 - 2);"SCORE"
 890  PRINT 
 900  FOR I = 1 TO 5 STEP 2
 910  PRINT I;".  ";WR$(I); TAB( T1);ODDS(I,1);" TO ";ODDS(I,2); TAB( T2);0
 920  PRINT I + 1;".  ";WR$(I + 1); TAB( T1);ODDS(I + 1,1);" TO ";ODDS(I + 1,2); TAB( T2);0
 930  PRINT 
 940  NEXT 
 950  PRINT "THE WRESTLERS ARE FIGHTING NOW!"
 960  PRINT 
 970  PRINT "(HIT ANY KEY TO PLACE A BET.)"
 980  POKE  - 16368,0
 990  REM  READ THE KEYBOARD.
 1000 I = 1
 1010  IF  PEEK ( - 16384) > 127 GOTO 1340
 1020 I = I + 1: IF I <  = 100 GOTO 1010
 1030  REM  FIGHTERS FIGHT.
 1040 Z9 = 0: VTAB 7
 1050  FOR I = 1 TO 5 STEP 2
 1060  IF SC(I) = 10 OR SC(I + 1) = 10 THEN : PRINT : PRINT : PRINT : GOTO 1140
 1070 Z9 = 1: REM  THE FIGHTS ARE STILL IN PROGRESS
 1080  IF  RND (1) <  = WR(I) THEN :SC(I) = SC(I) + 1
 1090  IF  RND (1) <  = WR(I + 1) THEN :SC(I + 1) = SC(I + 1) + 1
 1100  IF SC(I) = 10 AND SC(I + 1) = 10 THEN :SC(I) = 9:SC(I + 1) = 9: GOTO 1080: REM  NO TIE SCORES ALLOWED.
 1110  HTAB T2: PRINT SC(I)
 1120  HTAB T2: PRINT SC(I + 1)
 1130  PRINT 
 1140  NEXT 
 1150  REM  RE-CALCULATE ODDS.
 1160 Z9 = 0
 1170  VTAB 7
 1180  FOR I = 1 TO 5 STEP 2
 1190  IF SC(I) = 10 OR SC(I + 1) = 10 THEN : HTAB T1: PRINT "        ": HTAB T1: PRINT "        ": GOTO 1280
 1200 Z9 = 1
 1210 A =  INT (WR(I + 1) * (10 - SC(I)) * 10)
 1220 B =  INT (WR(I) * (10 - SC(I + 1)) * 10)
 1230  GOSUB 1600
 1240 ODDS(I,1) = A:ODDS(I,2) = B
 1250 ODDS(I + 1,1) = B:ODDS(I + 1,2) = A
 1260  HTAB T1: PRINT A;" TO ";B;"   "
 1270  HTAB T1: PRINT B;" TO ";A;"   "
 1280  PRINT 
 1290  NEXT 
 1300  IF  NOT Z9 GOTO 1510: REM  END OF FIGHTS
 1310  REM  GO BACK TO READ KEYBOARD
 1320  GOTO 1000
 1330  REM  RECEIVE A BET
 1340  POKE  - 16368,0
 1350  VTAB 20
 1360  INPUT "HOW MUCH TO BET?  ";BET
 1370  IF BET <  >  INT (BET) GOTO 1350
 1380  IF BET > GOLD THEN : PRINT "YOU DON'T HAVE THAT MUCH GOLD!": PRINT "(HIT ANY KEY TO CONTINUE.)": GET R$: PRINT : GOTO 1480
 1390  IF BET = 0 GOTO 1480
 1400 GOLD = GOLD - BET
 1410  VTAB 20: CALL  - 958
 1420  INPUT "BETTING ON WHICH WRESTLER (1-6)?  ";NUM
 1430  IF NUM < 1 OR NUM > 6 OR NUM <  >  INT (NUM) GOTO 1410
 1440  IF SC(NUM) = 10 OR (NUM / 2 =  INT (NUM / 2) AND SC(NUM - 1) = 10) OR (NUM / 2 <  >  INT (NUM / 2) AND SC(NUM + 1) = 10) THEN : PRINT "THAT MATCH IS OVER!": PRINT "(HIT ANY KEY TO CONTINUE.)": GET R$: VTAB 22: PRINT : GOTO 1480
 1445  VTAB 6: FOR I = 1 TO NUM: PRINT : NEXT : IF NUM > 2 THEN  PRINT : IF NUM > 4 THEN  PRINT 
 1447  INVERSE : HTAB 5: PRINT WR$(NUM): NORMAL 
 1450 B(NUM,1) = B(NUM,1) +  INT (BET * ODDS(NUM,1) / ODDS(NUM,2)) + BET
 1460 B(NUM,2) = BET: REM  COL. 1 IS WIN, COL. 2 IS LOSE.
 1470  POKE  - 16368,0
 1480  VTAB 20: CALL  - 958
 1490  GOTO 1000
 1500  REM  END OF FIGHTS
 1510  VTAB 16: CALL  - 958
 1520  FOR I = 1 TO 6
 1530  IF B(I,1) = 0 GOTO 1560
 1540  IF SC(I) = 10 THEN : PRINT "YOU WON ";B(I,1) - B(I,2);" GOLD PIECES BETTING": PRINT "ON ";WR$(I);".":GOLD = GOLD + B(I,1): GOTO 1560
 1550  PRINT "YOU LOST ";B(I,2);" GOLD PIECES BETTING": PRINT "ON ";WR$(I);"."
 1560  NEXT 
 1570  PRINT "(HIT ANY KEY TO CONTINUE)";: GET R$: PRINT 
 1580  GOTO 100
 1590  REM  SUBROUTINE TO REDUCE ODDS
 1600 C = 1
 1610 P = PRIME(C)
 1620  IF P > A OR P > B THEN : RETURN 
 1630  IF A / P =  INT (A / P) AND B / P =  INT (B / P) THEN :A = A / P:B = B / P: GOTO 1630
 1640 C = C + 1: IF C > 20 THEN  RETURN 
 1650  GOTO 1610
 5000 FM$(17) =  STR$ (GOLD)
 5005  PRINT 
 5010  PRINT D$"OPENFRESH.MEAT"
 5020  PRINT D$"WRITEFRESH.MEAT"
 5030  FOR I = 1 TO 39: PRINT FM$(I): NEXT 
 5040  PRINT D$"CLOSE"
 5050  PRINT D$"-LOBBY"

 1  REM 

TEMPLE OF THE GUILD
BUREAU OF NAMES

 10 D$ =  CHR$ (4): PRINT  CHR$ (21): TEXT : HOME : HTAB 12: INVERSE : PRINT "BUREAU OF NAMES": NORMAL 
 20  PRINT : PRINT "    YOU ARE STANDING BEFORE A HIGH-     RANKING CLERIC EMPLOYED BY THE GUILD.   HE ASKS IF YOU WISH TO:"
 30  PRINT : PRINT "1.  CHANGE YOUR NAME?": PRINT "2.  CHANGE THE NAME OF A WEAPON?": PRINT "3.  CHANGE YOUR MIND (AND LEAVE)?": PRINT "HIT KEY FOR CHOICE (1-3): ";
 40  GET R$:I =  VAL (R$): ON I < 1 OR I > 3 GOTO 40: PRINT : ON I GOTO 100,500,700
 100  REM 

CHANGE NAME

 110  PRINT : PRINT "'SO, YOU WISH TO CHANGE YOUR LEGAL NAME,EH?  WELL, THIS KIND OF THING IS NOT TO BE DONE LIGHTLY.  THERE WILL BE A FEE OF100 GOLD PIECES, FOR STARTERS, JUST TO  MAKE SURE YOU'RE IN EARNEST.  AND THEN"
 120  PRINT "YOUR NEW NAME HAS TO BE APPROVED BY THE BOARD OF JUDGES.  STILL INTERESTED?'    (HIT Y OR N) "
 130  GET R$: ON R$ <  > "Y" AND R$ <  > "N" GOTO 130: PRINT R$: IF R$ = "N" THEN  PRINT "WELL, GOOD DAY THEN.": PRINT D$"-LOBBY"
 140  GOSUB 1000: IF GO + BA < 100 THEN  PRINT "'YOU DON'T HAVE ENOUGH MONEY!  BEGONE AT ONCE!'": PRINT D$"-LOBBY"
 150  IF GO >  = 100 THEN GO = GO - 100: GOTO 190
 160  PRINT "'WE SHALL HAVE TO TRANSFER A LITTLE GOLDFROM YOUR BANK ACCOUNT TO PAY FOR THIS,'HE SAYS.  'NO PROBLEM.'  BEFORE YOU CAN"
 170  PRINT "OBJECT, HE STAMPS AN IMPORTANT-LOOKING  FORM WITH AN IMPORTANT-LOOKING RUBBER   STAMP AND HANDS THE FORM TO A PAGE.  THEPAGE LEAVES IMMEDIATELY FOR THE BANK."
 180 BA = BA - 100 + GO:GO = 0
 190  PRINT : PRINT "'SO, YOU DON'T WANT TO BE CALLED BY THE NAME OF "NA$" ANYMORE.'": INPUT "'WHAT WOULD YOU RATHER BE CALLED?'      ";NA$
 200  PRINT "'VERY WELL, I'LL HAVE TO CHECK WITH THE BOARD OF JUDGES.  PLEASE WAIT HERE.'  HELEAVES THE ROOM.": PRINT 
 210  PRINT "(INSERT EAMON MASTER DISK AND HIT A KEY)": ONERR  GOTO 210
 220  GET A$: PRINT : PRINT D$"PREFIX/": PRINT D$"PREFIX": INPUT P$: PRINT D$"PREFIX"P$: PRINT D$"VERIFYCHARACTERS": POKE 216,0
 230 NO = 0: PRINT D$"OPENCHARACTERS,L150":I = 1: ONERR  GOTO 290
 240  IF I = RE THEN 280
 250  PRINT D$"READCHARACTERS,R"I: INPUT N2$: IF NA$ = N2$ THEN NO = 1: GOTO 300
 280 I = I + 1: GOTO 240
 290  IF  PEEK (222) <  > 5 AND  PEEK (222) <  > 2 THEN  PRINT "ERROR CODE " PEEK (222)" IN " PEEK (218) +  PEEK (219) * 256: END 
 300  POKE 216,0: PRINT D$"CLOSE"
 310  IF (NO) THEN  PRINT "HE RETURNS AND SAYS, 'THE BOARD REFUSES TO APPROVE THIS NAME CHANGE BECAUSE SOMEOTHER ADVENTURER IS USING THE NEW NAME.'": GOTO 190
 320  PRINT "HE RETURNS AND SAYS, 'THE BOARD HAS     APPROVED THIS NAME CHANGE.  GOOD LUCK,  "NA$".": GOTO 2000
 500  REM 

CHANGE WEAPON NAME

 510  GOSUB 1000: IF WN$(1) = "NONE" THEN  PRINT "'YOU AREN'T CARRYING ANY WEAPONS!'": GOTO 40
 520  PRINT "'THIS IS A SMALL MATTER.  WE CAN TAKE   CARE OF IT FOR FREE.  WHICH OF YOUR     WEAPONS DO YOU WANT TO RENAME?'"
 530 I = 2
 540  IF WN$(I) <  > "NONE" THEN I = I + 1: IF I <  = 4 GOTO 540
 550 NW = I - 1
 560  INPUT "(TYPE THE WEAPON NAME NOW)  ";Z$
 570 I = 1
 580  IF WN$(I) = Z$ GOTO 610
 590 I = I + 1: IF I <  = 4 GOTO 580
 600  PRINT "'YOU AREN'T CARRYING A WEAPON BY THAT   NAME!  YOUR WEAPONS ARE:": FOR I = 1 TO NW: HTAB 10: PRINT WN$(I): NEXT : PRINT "TRY AGAIN!'": GOTO 560
 610  PRINT : INPUT "'AND WHAT WOULD YOU RATHER CALL THIS    WEAPON?' ";Z$
 620 J = 1
 630  IF WN$(J) = Z$ THEN  PRINT "'BUT YOU ALREADY HAVE ANOTHER WEAPON BY THAT NAME!  YOU CANNOT USE THIS AS A NEWNAME.": GOTO 610
 640 J = J + 1: IF J <  = 4 GOTO 630
 650 WN$(I) = Z$
 666  PRINT "'VERY WELL.  IT'S DONE.  FROM NOW ON ITSNAME IS "WN$(I)".": GOTO 2000
 700  REM 

LEAVE

 710  PRINT D$"-LOBBY"
 1000  REM 

READ CHARACTER

 1010  PRINT D$"OPENFRESH.MEAT": PRINT D$"READFRESH.MEAT": INPUT RE,NA$,HD,AG,CH: FOR I = 1 TO 4: INPUT SA%(I): NEXT : FOR I = 1 TO 5: INPUT WA%(I): NEXT 
 1020  INPUT AE,SE$,GO,BA,AC: FOR I = 1 TO 4: INPUT WN$(I): FOR J = 1 TO 4: INPUT WE%(I,J): NEXT : NEXT : PRINT D$"CLOSE": RETURN 
 2000  REM 

WRITE CHARACTER

 2010  PRINT : PRINT "(INSERT DISK FOR 'TEMPLE OF THE GUILD'  AND PRESS ANY KEY) ";: GET R$: PRINT : ONERR  GOTO 2010
 2020  PRINT D$"PREFIX/EAMON.075": POKE 216,0
 2030  PRINT D$"OPENFRESH.MEAT": PRINT D$"WRITEFRESH.MEAT": PRINT RE: PRINT NA$: PRINT HD: PRINT AG: PRINT CH: FOR I = 1 TO 4: PRINT SA%(I): NEXT 
 2040  FOR I = 1 TO 5: PRINT WA%(I): NEXT : PRINT AE: PRINT SE$: PRINT GO: PRINT BA: PRINT AC: FOR I = 1 TO 4: PRINT WN$(I): FOR J = 1 TO 4: PRINT WE%(I,J): NEXT : NEXT : PRINT D$"CLOSE"
 2050  PRINT "(HIT ANY KEY TO CONTINUE.) ";: GET R$: PRINT : GOTO 10

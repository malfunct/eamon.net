 1  REM THE GLADIATOR POOL
 10  DIM RN$(7),RB(7):D$ =  CHR$ (4): PRINT  CHR$ (21): TEXT : HOME : FOR I = 1 TO 7: READ RN$(I): NEXT : FOR I = 1 TO 7: READ RB(I): NEXT : INVERSE : HTAB 13: PRINT "GLADIATOR POOL": NORMAL : PRINT : PRINT : ONERR  GOTO 30
 20  PRINT D$"VERIFYBADGUY": POKE 216,0: GOTO 160
 30  POKE 216,0: IF  PEEK (222) <  > 6 THEN  STOP 
 40  PRINT "     A FEW OF THE WEALTHIER AND OLDER   ADVENTURERS HAVE, WELL, SOMEWHAT        GROTESQUE TASTE IN ENTERTAINMENT.  THEY LIKE TO WATCH FIGHTS.  NOT WRESTLING,   WHICH IS DONE FOR SPORT AND EXERCISE,"
 50  PRINT "BUT REAL FIGHTING WITH WEAPONS AND      BLOOD.  THEY HAVE INVENTED A SPECTATOR  SPORT IN WHICH A ROBOT FIGHTER IS SET   AGAINST A HUMAN ADVENTURER.  THE HUMAN  FIGHTERS ARE, APPROPRIATELY ENOUGH,"
 60  PRINT "CALLED GLADIATORS.": PRINT "     THE ROBOTS ARE QUITE DANGEROUS, ANDMANY GLADIATORS HAVE BEEN KILLED OR     INJURED IN THE GAME.  THERE IS NO CHANCETO FLEE FROM THE ARENA DURING COMBAT."
 70  PRINT : PRINT "(HIT ANY KEY TO CONTINUE)";: GET R$: PRINT : HOME 
 80  PRINT "     WHY, YOU MAY ASK, WOULD AN": PRINT "ADVENTURER DELIBERATELY RISK DEATH TO   FIGHT A MACHINE?  FOR MONEY, OF COURSE. THOSE ADVENTURERS WHO SURVIVE ARE PAID  A HANDSOME BOUNTY, LARGE ENOUGH TO"
 90  PRINT "ATTRACT A LARGE POOL OF FIGHTERS.  THOSEWHO DON'T SURVIVE...WELL, THEY COULDN'T HAVE TAKEN IT WITH THEM ANYWAY.": PRINT 
 100  PRINT "     IF YOU ARE IN NEED OF A SIZEABLE   AMOUNT OF CASH, YOU MAY BE ABLE TO WIN  IT WORKING IN THE GLADIATOR POOL."
 110  PRINT : PRINT "(HIT ANY KEY TO CONTINUE)";: GET R$: PRINT : HOME 
 120  PRINT "     AS YOU WALK INTO THE OFFICE OF     GLADIATOR RECRUITMENT, YOU SEE THAT YOU CAN CHOOSE YOUR OPPONENT FROM THIS LIST:": PRINT : PRINT  TAB( 5);"NAME"; TAB( 28);"BOUNTY": FOR I = 1 TO 7: PRINT : PRINT I;"  ";RN$(I); TAB( 23);RB(I);" GOLD PIECES": NEXT : PRINT 
 130  PRINT "ENTER NUMBER FOR YOUR CHOICE (1-7), OR  ENTER '0' TO LEAVE.";: INPUT "  ";C: IF C < 0 OR C > 7 OR C <  >  INT (C) THEN : HOME : GOTO 120
 140  IF C = 0 THEN : PRINT D$"OPENBADGUY": PRINT D$"CLOSE": PRINT D$"DELETEBADGUY": PRINT D$"-LOBBY"
 150  PRINT D$"OPENBADGUY": PRINT D$"WRITEBADGUY": PRINT C: PRINT D$"CLOSE": HOME : PRINT D$"-MAIN.PGM": REM RETURN AFTER SUCCESSFUL FIGHT 
 160  PRINT D$"OPENBADGUY": PRINT D$"READBADGUY": INPUT C: PRINT D$"CLOSE": PRINT "YOU HAVE WON "RB(C)" GOLD PIECES": PRINT "FOR THE DEFEAT OF "RN$(C)"!"
 170  GOSUB 1000: PRINT D$"DELETEBADGUY": PRINT : PRINT "CONGRATULATIONS!":GO = GO + RB(C):ER = 0: GOSUB 2000: GOTO 110
 1000  REM 

READ CHARACTER

 1010  PRINT D$"OPENFRESH.MEAT": PRINT D$"READFRESH.MEAT": INPUT RE,NA$,HD,AG,CH: FOR I = 1 TO 4: INPUT SA%(I): NEXT : FOR I = 1 TO 5: INPUT WA%(I): NEXT 
 1020  INPUT AE,SE$,GO,BA,AC: FOR I = 1 TO 4: INPUT WN$(I): FOR J = 1 TO 4: INPUT WE%(I,J): NEXT : NEXT : PRINT D$"CLOSE": RETURN 
 2000  REM 

WRITE CHARACTER

 2010  PRINT D$"OPENFRESH.MEAT": PRINT D$"WRITEFRESH.MEAT": PRINT RE: PRINT NA$: PRINT HD: PRINT AG: PRINT CH: FOR I = 1 TO 4: PRINT SA%(I): NEXT : FOR I = 1 TO 5: PRINT WA%(I): NEXT 
 2020  PRINT AE: PRINT SE$: PRINT GO: PRINT BA: PRINT AC: FOR I = 1 TO 4: PRINT WN$(I): FOR J = 1 TO 4: PRINT WE%(I,J): NEXT : NEXT : PRINT D$"CLOSE": RETURN 
 9000  DATA COLOSSUS,GORGON II,GORGON I,MAYHEM 3,SARGON,MEDUSA,OTIS,10000,1000,700,400,300,200,75
